/-
Copyright (c) 2022 Apurva Nakade All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Apurva Nakade
-/
import Mathlib.Analysis.Convex.Cone.Dual
import Mathlib.Analysis.InnerProductSpace.Adjoint

#align_import analysis.convex.cone.proper from "leanprover-community/mathlib"@"147b294346843885f952c5171e9606616a8fd869"

/-!
# Proper cones

We define a proper cone as a nonempty, closed, convex cone. Proper cones are used in defining conic
programs which generalize linear programs. A linear program is a conic program for the positive
cone. We then prove Farkas' lemma for conic programs following the proof in the reference below.
Farkas' lemma is equivalent to strong duality. So, once have the definitions of conic programs and
linear programs, the results from this file can be used to prove duality theorems.

## TODO

The next steps are:
- Add convex_cone_class that extends set_like and replace the below instance
- Define primal and dual cone programs and prove weak duality.
- Prove regular and strong duality for cone programs using Farkas' lemma (see reference).
- Define linear programs and prove LP duality as a special case of cone duality.
- Find a better reference (textbook instead of lecture notes).
- Show submodules are (proper) cones.

## References

- [B. Gartner and J. Matousek, Cone Programming][gartnerMatousek]

-/

open ContinuousLinearMap Filter Set

namespace ConvexCone

variable {ğ•œ : Type*} [OrderedSemiring ğ•œ]

variable {E : Type*} [AddCommMonoid E] [TopologicalSpace E] [ContinuousAdd E] [SMul ğ•œ E]
  [ContinuousConstSMul ğ•œ E]

/-- The closure of a convex cone inside a topological space as a convex cone. This
construction is mainly used for defining maps between proper cones. -/
protected def closure (K : ConvexCone ğ•œ E) : ConvexCone ğ•œ E where
  carrier := closure â†‘K
  smul_mem' c hc _ hâ‚ :=
    map_mem_closure (continuous_id'.const_smul c) hâ‚ fun _ hâ‚‚ => K.smul_mem hc hâ‚‚
  add_mem' _ hâ‚ _ hâ‚‚ := map_mem_closureâ‚‚ continuous_add hâ‚ hâ‚‚ K.add_mem
#align convex_cone.closure ConvexCone.closure

@[simp, norm_cast]
theorem coe_closure (K : ConvexCone ğ•œ E) : (K.closure : Set E) = closure K :=
  rfl
#align convex_cone.coe_closure ConvexCone.coe_closure

@[simp]
protected theorem mem_closure {K : ConvexCone ğ•œ E} {a : E} :
    a âˆˆ K.closure â†” a âˆˆ closure (K : Set E) :=
  Iff.rfl
#align convex_cone.mem_closure ConvexCone.mem_closure

@[simp]
theorem closure_eq {K L : ConvexCone ğ•œ E} : K.closure = L â†” closure (K : Set E) = L :=
  SetLike.ext'_iff
#align convex_cone.closure_eq ConvexCone.closure_eq

end ConvexCone

/-- A proper cone is a convex cone `K` that is nonempty and closed. Proper cones have the nice
property that the dual of the dual of a proper cone is itself. This makes them useful for defining
cone programs and proving duality theorems. -/
structure ProperCone (ğ•œ : Type*) (E : Type*) [OrderedSemiring ğ•œ] [AddCommMonoid E]
    [TopologicalSpace E] [SMul ğ•œ E] extends ConvexCone ğ•œ E where
  nonempty' : (carrier : Set E).Nonempty
  isClosed' : IsClosed (carrier : Set E)
#align proper_cone ProperCone

namespace ProperCone

section SMul

variable {ğ•œ : Type*} [OrderedSemiring ğ•œ]

variable {E : Type*} [AddCommMonoid E] [TopologicalSpace E] [SMul ğ•œ E]

attribute [coe] toConvexCone

instance : Coe (ProperCone ğ•œ E) (ConvexCone ğ•œ E) :=
  âŸ¨toConvexConeâŸ©

-- Porting note: now a syntactic tautology
-- @[simp]
-- theorem toConvexCone_eq_coe (K : ProperCone ğ•œ E) : K.toConvexCone = K :=
--   rfl
#noalign proper_cone.to_convex_cone_eq_coe

theorem ext' : Function.Injective ((â†‘) : ProperCone ğ•œ E â†’ ConvexCone ğ•œ E) := fun S T h => by
  cases S; cases T; congr
#align proper_cone.ext' ProperCone.ext'

-- TODO: add `ConvexConeClass` that extends `SetLike` and replace the below instance
instance : SetLike (ProperCone ğ•œ E) E where
  coe K := K.carrier
  coe_injective' _ _ h := ProperCone.ext' (SetLike.coe_injective h)

@[ext]
theorem ext {S T : ProperCone ğ•œ E} (h : âˆ€ x, x âˆˆ S â†” x âˆˆ T) : S = T :=
  SetLike.ext h
#align proper_cone.ext ProperCone.ext

@[simp]
theorem mem_coe {x : E} {K : ProperCone ğ•œ E} : x âˆˆ (K : ConvexCone ğ•œ E) â†” x âˆˆ K :=
  Iff.rfl
#align proper_cone.mem_coe ProperCone.mem_coe

protected theorem nonempty (K : ProperCone ğ•œ E) : (K : Set E).Nonempty :=
  K.nonempty'
#align proper_cone.nonempty ProperCone.nonempty

protected theorem isClosed (K : ProperCone ğ•œ E) : IsClosed (K : Set E) :=
  K.isClosed'
#align proper_cone.is_closed ProperCone.isClosed

end SMul

section PositiveCone

variable (ğ•œ E)
variable [OrderedSemiring ğ•œ] [OrderedAddCommGroup E] [Module ğ•œ E] [OrderedSMul ğ•œ E]
  [TopologicalSpace E] [OrderClosedTopology E]

/-- The positive cone is the proper cone formed by the set of nonnegative elements in an ordered
module. -/
def positive : ProperCone ğ•œ E where
  toConvexCone := ConvexCone.positive ğ•œ E
  nonempty' := âŸ¨0, ConvexCone.pointed_positive _ _âŸ©
  isClosed' := isClosed_Ici

@[simp]
theorem mem_positive {x : E} : x âˆˆ positive ğ•œ E â†” 0 â‰¤ x :=
  Iff.rfl

@[simp]
theorem coe_positive : â†‘(positive ğ•œ E) = ConvexCone.positive ğ•œ E :=
  rfl

end PositiveCone

section Module

variable {ğ•œ : Type*} [OrderedSemiring ğ•œ]

variable {E : Type*} [AddCommMonoid E] [TopologicalSpace E] [T1Space E] [Module ğ•œ E]

instance : Zero (ProperCone ğ•œ E) :=
  âŸ¨{  toConvexCone := 0
      nonempty' := âŸ¨0, rflâŸ©
      isClosed' := isClosed_singleton }âŸ©

instance : Inhabited (ProperCone ğ•œ E) :=
  âŸ¨0âŸ©

@[simp]
theorem mem_zero (x : E) : x âˆˆ (0 : ProperCone ğ•œ E) â†” x = 0 :=
  Iff.rfl
#align proper_cone.mem_zero ProperCone.mem_zero

@[simp, norm_cast]
theorem coe_zero : â†‘(0 : ProperCone ğ•œ E) = (0 : ConvexCone ğ•œ E) :=
  rfl
#align proper_cone.coe_zero ProperCone.coe_zero

theorem pointed_zero : (0 : ProperCone ğ•œ E).Pointed := by simp [ConvexCone.pointed_zero]
#align proper_cone.pointed_zero ProperCone.pointed_zero

end Module

section InnerProductSpace

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]

variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]

variable {G : Type*} [NormedAddCommGroup G] [InnerProductSpace â„ G]

protected theorem pointed (K : ProperCone â„ E) : (K : ConvexCone â„ E).Pointed :=
  (K : ConvexCone â„ E).pointed_of_nonempty_of_isClosed K.nonempty' K.isClosed
#align proper_cone.pointed ProperCone.pointed

/-- The closure of image of a proper cone under a continuous `â„`-linear map is a proper cone. We
use continuous maps here so that the comap of f is also a map between proper cones. -/
noncomputable def map (f : E â†’L[â„] F) (K : ProperCone â„ E) : ProperCone â„ F where
  toConvexCone := ConvexCone.closure (ConvexCone.map (f : E â†’â‚—[â„] F) â†‘K)
  nonempty' :=
    âŸ¨0, subset_closure <| SetLike.mem_coe.2 <| ConvexCone.mem_map.2 âŸ¨0, K.pointed, map_zero _âŸ©âŸ©
  isClosed' := isClosed_closure
#align proper_cone.map ProperCone.map

@[simp, norm_cast]
theorem coe_map (f : E â†’L[â„] F) (K : ProperCone â„ E) :
    â†‘(K.map f) = (ConvexCone.map (f : E â†’â‚—[â„] F) â†‘K).closure :=
  rfl
#align proper_cone.coe_map ProperCone.coe_map

@[simp]
theorem mem_map {f : E â†’L[â„] F} {K : ProperCone â„ E} {y : F} :
    y âˆˆ K.map f â†” y âˆˆ (ConvexCone.map (f : E â†’â‚—[â„] F) â†‘K).closure :=
  Iff.rfl
#align proper_cone.mem_map ProperCone.mem_map

@[simp]
theorem map_id (K : ProperCone â„ E) : K.map (ContinuousLinearMap.id â„ E) = K :=
  ProperCone.ext' <| by simpa using IsClosed.closure_eq K.isClosed
#align proper_cone.map_id ProperCone.map_id

/-- The inner dual cone of a proper cone is a proper cone. -/
def dual (K : ProperCone â„ E) : ProperCone â„ E where
  toConvexCone := (K : Set E).innerDualCone
  nonempty' := âŸ¨0, pointed_innerDualCone _âŸ©
  isClosed' := isClosed_innerDualCone _
#align proper_cone.dual ProperCone.dual

@[simp, norm_cast]
theorem coe_dual (K : ProperCone â„ E) : â†‘(dual K) = (K : Set E).innerDualCone :=
  rfl
#align proper_cone.coe_dual ProperCone.coe_dual

@[simp]
theorem mem_dual {K : ProperCone â„ E} {y : E} : y âˆˆ dual K â†” âˆ€ â¦ƒxâ¦„, x âˆˆ K â†’ 0 â‰¤ âŸªx, yâŸ«_â„ := by
  rw [â† mem_coe, coe_dual, mem_innerDualCone _ _]; rfl
#align proper_cone.mem_dual ProperCone.mem_dual

/-- The preimage of a proper cone under a continuous `â„`-linear map is a proper cone. -/
noncomputable def comap (f : E â†’L[â„] F) (S : ProperCone â„ F) : ProperCone â„ E where
  toConvexCone := ConvexCone.comap (f : E â†’â‚—[â„] F) S
  nonempty' :=
    âŸ¨0, by
      simp only [ConvexCone.comap, mem_preimage, map_zero, SetLike.mem_coe, mem_coe]
      apply ProperCone.pointedâŸ©
  isClosed' := by
    simp only [ConvexCone.comap, ContinuousLinearMap.coe_coe]
    apply IsClosed.preimage f.2 S.isClosed
#align proper_cone.comap ProperCone.comap

@[simp]
theorem coe_comap (f : E â†’L[â„] F) (S : ProperCone â„ F) : (S.comap f : Set E) = f â»Â¹' S :=
  rfl
#align proper_cone.coe_comap ProperCone.coe_comap

@[simp]
theorem comap_id (S : ConvexCone â„ E) : S.comap LinearMap.id = S :=
  SetLike.coe_injective preimage_id
#align proper_cone.comap_id ProperCone.comap_id

theorem comap_comap (g : F â†’L[â„] G) (f : E â†’L[â„] F) (S : ProperCone â„ G) :
    (S.comap g).comap f = S.comap (g.comp f) :=
  SetLike.coe_injective <| by congr
#align proper_cone.comap_comap ProperCone.comap_comap

@[simp]
theorem mem_comap {f : E â†’L[â„] F} {S : ProperCone â„ F} {x : E} : x âˆˆ S.comap f â†” f x âˆˆ S :=
  Iff.rfl
#align proper_cone.mem_comap ProperCone.mem_comap

end InnerProductSpace

section CompleteSpace

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E] [CompleteSpace E]

variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F] [CompleteSpace F]

/-- The dual of the dual of a proper cone is itself. -/
@[simp]
theorem dual_dual (K : ProperCone â„ E) : K.dual.dual = K :=
  ProperCone.ext' <|
    (K : ConvexCone â„ E).innerDualCone_of_innerDualCone_eq_self K.nonempty' K.isClosed
#align proper_cone.dual_dual ProperCone.dual_dual

/-- This is a relative version of
`ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_nmem`, which we recover by setting
`f` to be the identity map. This is a geometric interpretation of the Farkas' lemma
stated using proper cones. -/
theorem hyperplane_separation (K : ProperCone â„ E) {f : E â†’L[â„] F} {b : F} :
    b âˆˆ K.map f â†” âˆ€ y : F, adjoint f y âˆˆ K.dual â†’ 0 â‰¤ âŸªy, bâŸ«_â„ :=
  Iff.intro
    (by
      -- suppose `b âˆˆ K.map f`
      simp only [ProperCone.mem_map, ProperCone.mem_dual, adjoint_inner_right,
        ConvexCone.mem_closure, mem_closure_iff_seq_limit]
      -- there is a sequence `seq : â„• â†’ F` in the image of `f` that converges to `b`
      rintro âŸ¨seq, hmem, htendsâŸ© y hinner
      suffices h : âˆ€ n, 0 â‰¤ âŸªy, seq nâŸ«_â„ from
        ge_of_tendsto'
          (Continuous.seqContinuous (Continuous.inner (@continuous_const _ _ _ _ y) continuous_id)
            htends)
          h
      intro n
      obtain âŸ¨_, h, hseqâŸ© := hmem n
      simpa only [â† hseq, real_inner_comm] using hinner h)
    (by
      -- proof by contradiction
      -- suppose `b âˆ‰ K.map f`
      intro h
      contrapose! h
      -- as `b âˆ‰ K.map f`, there is a hyperplane `y` separating `b` from `K.map f`
      obtain âŸ¨y, hxy, hybâŸ© :=
        ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_nmem _ (K.map f).nonempty
          (K.map f).isClosed h
      -- the rest of the proof is a straightforward algebraic manipulation
      refine' âŸ¨y, _, hybâŸ©
      simp_rw [ProperCone.mem_dual, adjoint_inner_right]
      intro x hxK
      apply hxy (f x)
      rw [ProperCone.coe_map]
      apply subset_closure
      rw [SetLike.mem_coe, ConvexCone.mem_map]
      refine' âŸ¨x, hxK, by rw [coe_coe]âŸ©)
#align proper_cone.hyperplane_separation ProperCone.hyperplane_separation

theorem hyperplane_separation_of_nmem (K : ProperCone â„ E) {f : E â†’L[â„] F} {b : F}
    (disj : b âˆ‰ K.map f) : âˆƒ y : F, adjoint f y âˆˆ K.dual âˆ§ âŸªy, bâŸ«_â„ < 0 := by
  contrapose! disj; rwa [K.hyperplane_separation]
#align proper_cone.hyperplane_separation_of_nmem ProperCone.hyperplane_separation_of_nmem

end CompleteSpace

end ProperCone
