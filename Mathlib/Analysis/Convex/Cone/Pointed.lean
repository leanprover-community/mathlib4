/-
Copyright (c) 2023 Apurva Nakade. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Apurva Nakade
-/
import Mathlib.Analysis.Convex.Cone.InnerDual
import Mathlib.Algebra.Order.Nonneg.Module
import Mathlib.Algebra.Module.Submodule.Basic

/-!
# Pointed cones

A *pointed cone* is defined to be a submodule of a module where the scalars are restricted to be
nonnegative. This is equivalent to saying that as a set a pointed cone is convex cone which
contains `0`. This is a bundled version of `ConvexCone.Pointed`. We choose the submodule definition
as it allows us to use the `Module` API to work with convex cones.

-/

variable {ğ•œ E F G : Type*}

local notation3 "ğ•œâ‰¥0" => {c : ğ•œ // 0 â‰¤ c}

/-- A pointed cone is a submodule of a module with scalars restricted to being nonnegative. -/
abbrev PointedCone (ğ•œ E) [OrderedSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] :=
  Submodule {c : ğ•œ // 0 â‰¤ c} E

namespace PointedCone

open Function

section Definitions

variable [OrderedSemiring ğ•œ]
variable [AddCommMonoid E] [Module ğ•œ E]

/-- Every pointed cone is a convex cone. -/
@[coe]
def toConvexCone (S : PointedCone ğ•œ E) : ConvexCone ğ•œ E where
  carrier := S
  smul_mem' c hc _ hx := S.smul_mem âŸ¨c, le_of_lt hcâŸ© hx
  add_mem' _ hx _ hy := S.add_mem hx hy

instance : Coe (PointedCone ğ•œ E) (ConvexCone ğ•œ E) where
  coe := toConvexCone

theorem toConvexCone_injective : Injective ((â†‘) : PointedCone ğ•œ E â†’ ConvexCone ğ•œ E) :=
  fun _ _ => by simp [toConvexCone]

@[simp]
theorem toConvexCone_pointed (S : PointedCone ğ•œ E) : (S : ConvexCone ğ•œ E).Pointed := by
  simp [toConvexCone, ConvexCone.Pointed]

@[ext]
theorem ext {S T : PointedCone ğ•œ E} (h : âˆ€ x, x âˆˆ S â†” x âˆˆ T) : S = T :=
  SetLike.ext h

instance instZero (S : PointedCone ğ•œ E) : Zero S :=
  âŸ¨0, S.zero_memâŸ©

/-- The `PointedCone` constructed from a pointed `ConvexCone`. -/
def _root_.ConvexCone.toPointedCone {S : ConvexCone ğ•œ E} (hS : S.Pointed) : PointedCone ğ•œ E where
  carrier := S
  add_mem' hx hy := S.add_mem hx hy
  zero_mem' := hS
  smul_mem' := fun âŸ¨c, hcâŸ© x hx => by
    simp_rw [SetLike.mem_coe]
    cases' eq_or_lt_of_le hc with hzero hpos
    Â· unfold ConvexCone.Pointed at hS
      convert hS
      simp [â† hzero]
    Â· apply ConvexCone.smul_mem
      Â· convert hpos
      Â· exact hx

@[simp]
lemma _root_.ConvexCone.mem_toPointedCone {S : ConvexCone ğ•œ E} (hS : S.Pointed) (x : E) :
    x âˆˆ S.toPointedCone hS â†” x âˆˆ S :=
  Iff.rfl

@[simp, norm_cast]
lemma _root_.ConvexCone.coe_toPointedCone {S : ConvexCone ğ•œ E} (hS : S.Pointed) :
    S.toPointedCone hS = S :=
  rfl

instance canLift : CanLift (ConvexCone ğ•œ E) (PointedCone ğ•œ E) (â†‘) ConvexCone.Pointed where
  prf S hS := âŸ¨S.toPointedCone hS, rflâŸ©

end Definitions

section Maps

variable [OrderedSemiring ğ•œ]
variable [AddCommMonoid E] [Module ğ•œ E]
variable [AddCommMonoid F] [Module ğ•œ F]
variable [AddCommMonoid G] [Module ğ•œ G]

/-!

## Maps between pointed cones

There is already a definition of maps between submodules, `Submodule.map`. In our case, these maps
are induced from linear maps between the ambient modules that are linear over nonnegative scalars.
Such maps are unlikely to be of any use in practice. So, we construct some API to define maps
between pointed cones induced from linear maps between the ambient modules that are linear over
*all* scalars.

-/

/-- The image of a pointed cone under a `ğ•œ`-linear map is a pointed cone. -/
def map (f : E â†’â‚—[ğ•œ] F) (S : PointedCone ğ•œ E) : PointedCone ğ•œ F :=
  Submodule.map (f : E â†’â‚—[ğ•œâ‰¥0] F) S

@[simp, norm_cast]
theorem toConvexCone_map (S : PointedCone ğ•œ E) (f : E â†’â‚—[ğ•œ] F) :
    (S.map f : ConvexCone ğ•œ F) = (S : ConvexCone ğ•œ E).map f :=
  rfl

@[simp, norm_cast]
theorem coe_map (S : PointedCone ğ•œ E) (f : E â†’â‚—[ğ•œ] F) : (S.map f : Set F) = f '' S :=
  rfl

@[simp]
theorem mem_map {f : E â†’â‚—[ğ•œ] F} {S : PointedCone ğ•œ E} {y : F} : y âˆˆ S.map f â†” âˆƒ x âˆˆ S, f x = y :=
  Iff.rfl

theorem map_map (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) (S : PointedCone ğ•œ E) :
    (S.map f).map g = S.map (g.comp f) :=
  SetLike.coe_injective <| Set.image_image g f S

@[simp]
theorem map_id (S : PointedCone ğ•œ E) : S.map LinearMap.id = S :=
  SetLike.coe_injective <| Set.image_id _

/-- The preimage of a convex cone under a `ğ•œ`-linear map is a convex cone. -/
def comap (f : E â†’â‚—[ğ•œ] F) (S : PointedCone ğ•œ F) : PointedCone ğ•œ E :=
  Submodule.comap (f : E â†’â‚—[ğ•œâ‰¥0] F) S

@[simp, norm_cast]
theorem coe_comap (f : E â†’â‚—[ğ•œ] F) (S : PointedCone ğ•œ F) : (S.comap f : Set E) = f â»Â¹' S :=
  rfl

@[simp]
theorem comap_id (S : PointedCone ğ•œ E) : S.comap LinearMap.id = S :=
  rfl

theorem comap_comap (g : F â†’â‚—[ğ•œ] G) (f : E â†’â‚—[ğ•œ] F) (S : PointedCone ğ•œ G) :
    (S.comap g).comap f = S.comap (g.comp f) :=
  rfl

@[simp]
theorem mem_comap {f : E â†’â‚—[ğ•œ] F} {S : PointedCone ğ•œ F} {x : E} : x âˆˆ S.comap f â†” f x âˆˆ S :=
  Iff.rfl

end Maps

section PositiveCone

variable (ğ•œ E)
variable [OrderedSemiring ğ•œ]
variable [OrderedAddCommGroup E] [Module ğ•œ E] [OrderedSMul ğ•œ E]

/-- The positive cone is the pointed cone formed by the set of nonnegative elements in an ordered
module. -/
def positive : PointedCone ğ•œ E :=
  (ConvexCone.positive ğ•œ E).toPointedCone <| ConvexCone.pointed_positive ğ•œ E

@[simp]
theorem mem_positive {x : E} : x âˆˆ positive ğ•œ E â†” 0 â‰¤ x :=
  Iff.rfl

@[simp, norm_cast]
theorem toConvexCone_positive : â†‘(positive ğ•œ E) = ConvexCone.positive ğ•œ E :=
  rfl

end PositiveCone
section Dual

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]

/-- The inner dual cone of a pointed cone is a pointed cone. -/
def dual (S : PointedCone â„ E) : PointedCone â„ E :=
  ((S : Set E).innerDualCone).toPointedCone <| pointed_innerDualCone (S : Set E)

@[simp, norm_cast]
theorem toConvexCone_dual (S : PointedCone â„ E) : â†‘(dual S) = (S : Set E).innerDualCone :=
  rfl

@[simp]
theorem mem_dual {S : PointedCone â„ E} {y : E} : y âˆˆ dual S â†” âˆ€ â¦ƒxâ¦„, x âˆˆ S â†’ 0 â‰¤ âŸªx, yâŸ«_â„ := by
  rfl

end Dual

end PointedCone
