/-
Copyright (c) 2022 Apurva Nakade. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Apurva Nakade, Ya√´l Dillies
-/
import Mathlib.Analysis.Convex.Cone.Closure
import Mathlib.Geometry.Convex.Cone.Pointed
import Mathlib.Topology.Algebra.Module.ClosedSubmodule
import Mathlib.Topology.Algebra.Order.Module
import Mathlib.Topology.Order.DenselyOrdered

/-!
# Proper cones

We define a *proper cone* as a closed, pointed cone. Proper cones are used in defining conic
programs which generalize linear programs. A linear program is a conic program for the positive
cone. We then prove Farkas' lemma for conic programs following the proof in the reference below.
Farkas' lemma is equivalent to strong duality. So, once we have the definitions of conic and
linear programs, the results from this file can be used to prove duality theorems.

One can turn `C : PointedCone R E` + `hC : IsClosed C` into `C : ProperCone R E` in a tactic block
by doing `lift C to ProperCone R E using hC`.

One can also turn `C : ConvexCone ùïú E` + `hC : Set.Nonempty C ‚àß IsClosed C` into
`C : ProperCone ùïú E` in a tactic block by doing `lift C to ProperCone ùïú E using hC`,
assuming `ùïú` is a dense topological field.

## TODO

The next steps are:
- Add `ConvexConeClass` that extends `SetLike` and replace the below instance
- Define primal and dual cone programs and prove weak duality.
- Prove regular and strong duality for cone programs using Farkas' lemma (see reference).
- Define linear programs and prove LP duality as a special case of cone duality.
- Find a better reference (textbook instead of lecture notes).

## References

- [B. Gartner and J. Matousek, Cone Programming][gartnerMatousek]

-/

open ContinuousLinearMap Filter Function Set

variable {ùïú R E F G : Type*} [Semiring R] [PartialOrder R] [IsOrderedRing R]
variable [AddCommMonoid E] [TopologicalSpace E] [Module R E]
variable [AddCommMonoid F] [TopologicalSpace F] [Module R F]
variable [AddCommMonoid G] [TopologicalSpace G] [Module R G]

local notation "R‚â•0" => {r : R // 0 ‚â§ r}

variable (R E) in
/-- A proper cone is a pointed cone `C` that is closed. Proper cones have the nice property that
they are equal to their double dual, see `ProperCone.dual_dual`.
This makes them useful for defining cone programs and proving duality theorems. -/
abbrev ProperCone := ClosedSubmodule R‚â•0 E

namespace ProperCone
section Module
variable {C C‚ÇÅ C‚ÇÇ : ProperCone R E} {r : R} {x : E}

/-- Any proper cone can be seen as a pointed cone.

This is an alias of `ClosedSubmodule.toSubmodule` for convenience and discoverability. -/
@[coe] abbrev toPointedCone (C : ProperCone R E) : PointedCone R E := C.toSubmodule

instance : Coe (ProperCone R E) (PointedCone R E) := ‚ü®toPointedCone‚ü©

lemma toPointedCone_injective : Injective ((‚Üë) : ProperCone R E ‚Üí PointedCone R E) :=
  ClosedSubmodule.toSubmodule_injective

-- TODO: add `ConvexConeClass` that extends `SetLike` and replace the below instance
instance : SetLike (ProperCone R E) E where
  coe C := C.carrier
  coe_injective' _ _ h := ProperCone.toPointedCone_injective <| SetLike.coe_injective h

@[ext] lemma ext (h : ‚àÄ x, x ‚àà C‚ÇÅ ‚Üî x ‚àà C‚ÇÇ) : C‚ÇÅ = C‚ÇÇ := SetLike.ext h

@[simp] lemma mem_toPointedCone : x ‚àà C.toPointedCone ‚Üî x ‚àà C := .rfl

@[deprecated (since := "2025-06-11")] alias mem_coe := mem_toPointedCone

lemma pointed_toConvexCone (C : ProperCone R E) : (C : ConvexCone R E).Pointed :=
  C.toPointedCone.pointed_toConvexCone

@[deprecated (since := "2025-06-11")] protected alias pointed := pointed_toConvexCone

protected lemma nonempty (C : ProperCone R E) : (C : Set E).Nonempty := C.toSubmodule.nonempty
protected lemma isClosed (C : ProperCone R E) : IsClosed (C : Set E) := C.isClosed'
protected lemma convex (C : ProperCone R E) : Convex R (C : Set E) := C.toPointedCone.convex

protected nonrec lemma smul_mem (C : ProperCone R E) (hx : x ‚àà C) (hr : 0 ‚â§ r) : r ‚Ä¢ x ‚àà C :=
  C.smul_mem ‚ü®r, hr‚ü© hx

section T1Space
variable [T1Space E]

lemma mem_bot : x ‚àà (‚ä• : ProperCone R E) ‚Üî x = 0 := .rfl

@[simp, norm_cast] lemma coe_bot : (‚ä• : ProperCone R E) = ({0} : Set E) := rfl
@[simp, norm_cast] lemma toPointedCone_bot : (‚ä• : ProperCone R E).toPointedCone = ‚ä• := rfl

@[deprecated (since := "2025-06-11")] alias mem_zero := mem_bot
@[deprecated (since := "2025-06-11")] alias coe_zero := coe_bot
@[deprecated (since := "2025-06-11")] alias pointed_zero := pointed_toConvexCone

end T1Space

/-- The closure of image of a proper cone under a `R`-linear map is a proper cone. We
use continuous maps here so that the comap of f is also a map between proper cones. -/
abbrev comap (f : E ‚ÜíL[R] F) (C : ProperCone R F) : ProperCone R E :=
  ClosedSubmodule.comap (f.restrictScalars R‚â•0) C

@[simp] lemma comap_id (C : ProperCone R F) : C.comap (.id _ _) = C := rfl

@[simp] lemma coe_comap (f : E ‚ÜíL[R] F) (C : ProperCone R F) : (C.comap f : Set E) = f ‚Åª¬π' C := rfl

lemma comap_comap (g : F ‚ÜíL[R] G) (f : E ‚ÜíL[R] F) (C : ProperCone R G) :
    (C.comap g).comap f = C.comap (g.comp f) := rfl

lemma mem_comap {C : ProperCone R F} {f : E ‚ÜíL[R] F} : x ‚àà C.comap f ‚Üî f x ‚àà C := .rfl

variable [ContinuousAdd F] [ContinuousConstSMul R F]

/-- The closure of image of a proper cone under a linear map is a proper cone.

We use continuous maps here to match `ProperCone.comap`. -/
abbrev map (f : E ‚ÜíL[R] F) (C : ProperCone R E) : ProperCone R F :=
  ClosedSubmodule.map (f.restrictScalars R‚â•0) C

@[simp] lemma map_id (C : ProperCone R F) : C.map (.id _ _) = C := ClosedSubmodule.map_id _

@[simp, norm_cast]
lemma coe_map (f : E ‚ÜíL[R] F) (C : ProperCone R E) :
    C.map f = (C.toPointedCone.map (f : E ‚Üí‚Çó[R] F)).closure := rfl

@[simp]
lemma mem_map {f : E ‚ÜíL[R] F} {C : ProperCone R E} {y : F} :
    y ‚àà C.map f ‚Üî y ‚àà (C.toPointedCone.map (f : E ‚Üí‚Çó[R] F)).closure := .rfl

end Module

section PositiveCone
variable [PartialOrder E] [IsOrderedAddMonoid E] [PosSMulMono R E] [OrderClosedTopology E] {x : E}

variable (R E) in
/-- The positive cone is the proper cone formed by the set of nonnegative elements in an ordered
module. -/
@[simps!]
def positive : ProperCone R E where
  toSubmodule := PointedCone.positive R E
  isClosed' := isClosed_Ici

@[simp] lemma mem_positive : x ‚àà positive R E ‚Üî 0 ‚â§ x := .rfl
@[simp] lemma toPointedCone_positive : (positive R E).toPointedCone = .positive R E := rfl

end PositiveCone
end ProperCone

/-!
### Topological properties of convex cones

This section proves topological results about convex cones.

#### TODO

This result generalises to G-submodules.
-/

namespace ConvexCone
variable [Semifield ùïú] [LinearOrder ùïú] [Module ùïú E] {s : Set E}

-- FIXME: This is necessary for the proof below but triggers the `unusedSectionVars` linter.
-- variable [IsStrictOrderedRing ùïú] [IsTopologicalAddGroup M] in
/-- This is true essentially by `Submodule.span_eq_iUnion_nat`, except that `Submodule` currently
doesn't support that use case. See
https://leanprover.zulipchat.com/#narrow/channel/116395-maths/topic/G-submodules/with/514426583 -/
proof_wanted isOpen_hull (hs : IsOpen s) : IsOpen (hull ùïú s : Set E)

variable [TopologicalSpace ùïú] [OrderTopology ùïú] [DenselyOrdered ùïú] [NoMaxOrder ùïú]
  [ContinuousSMul ùïú E] {C : ConvexCone ùïú E}

lemma Pointed.of_nonempty_of_isClosed (hC : (C : Set E).Nonempty) (hSclos : IsClosed (C : Set E)) :
    C.Pointed := by
  obtain ‚ü®x, hx‚ü© := hC
  let f : ùïú ‚Üí E := (¬∑ ‚Ä¢ x)
  -- The closure of `f (0, ‚àû)` is a subset of `C`
  have hfS : closure (f '' Set.Ioi 0) ‚äÜ C :=
    hSclos.closure_subset_iff.2 <| by rintro _ ‚ü®_, h, rfl‚ü©; exact C.smul_mem h hx
  -- `f` is continuous at `0` from the right
  have fc : ContinuousWithinAt f (Set.Ioi (0 : ùïú)) 0 :=
    (continuous_id.smul continuous_const).continuousWithinAt
  -- `0 ‚àà closure f (0, ‚àû) ‚äÜ C, 0 ‚àà C`
  simpa [f, Pointed, ‚Üê SetLike.mem_coe] using hfS <| fc.mem_closure_image <| by simp

@[deprecated (since := "2025-04-18")]
alias pointed_of_nonempty_of_isClosed := Pointed.of_nonempty_of_isClosed

variable [IsOrderedRing ùïú]

instance canLift : CanLift (ConvexCone ùïú E) (ProperCone ùïú E) (‚Üë)
    fun C ‚Ü¶ (C : Set E).Nonempty ‚àß IsClosed (C : Set E) where
  prf C hC := ‚ü®‚ü®C.toPointedCone <| .of_nonempty_of_isClosed hC.1 hC.2, hC.2‚ü©, rfl‚ü©

end ConvexCone
