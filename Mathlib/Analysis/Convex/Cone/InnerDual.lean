/-
Copyright (c) 2021 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.Analysis.Convex.Cone.Basic
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.InnerProductSpace.Projection

/-!
# Convex cones in inner product spaces

We define `Set.innerDualCone` to be the cone consisting of all points `y` such that for
all points `x` in a given set `0 ‚â§ ‚ü™ x, y ‚ü´`.

## Main statements

We prove the following theorems:
* `ConvexCone.innerDualCone_of_innerDualCone_eq_self`:
  The `innerDualCone` of the `innerDualCone` of a nonempty, closed, convex cone is itself.
* `ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_notMem`:
  This variant of the
  [hyperplane separation theorem](https://en.wikipedia.org/wiki/Hyperplane_separation_theorem)
  states that given a nonempty, closed, convex cone `K` in a complete, real inner product space `H`
  and a point `b` disjoint from it, there is a vector `y` which separates `b` from `K` in the sense
  that for all points `x` in `K`, `0 ‚â§ ‚ü™x, y‚ü´_‚Ñù` and `‚ü™y, b‚ü´_‚Ñù < 0`. This is also a geometric
  interpretation of the
  [Farkas lemma](https://en.wikipedia.org/wiki/Farkas%27_lemma#Geometric_interpretation).
-/

open Set LinearMap Pointwise

/-! ### The dual cone -/


section Dual

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace ‚Ñù H] (s t : Set H)

open RealInnerProductSpace

/-- The dual cone is the cone consisting of all points `y` such that for
all points `x` in a given set `0 ‚â§ ‚ü™ x, y ‚ü´`. -/
def Set.innerDualCone (s : Set H) : ConvexCone ‚Ñù H where
  carrier := { y | ‚àÄ x ‚àà s, 0 ‚â§ ‚ü™x, y‚ü´ }
  smul_mem' c hc y hy x hx := by
    rw [real_inner_smul_right]
    exact mul_nonneg hc.le (hy x hx)
  add_mem' u hu v hv x hx := by
    rw [inner_add_right]
    exact add_nonneg (hu x hx) (hv x hx)

@[simp]
theorem mem_innerDualCone (y : H) (s : Set H) : y ‚àà s.innerDualCone ‚Üî ‚àÄ x ‚àà s, 0 ‚â§ ‚ü™x, y‚ü´ :=
  Iff.rfl

@[simp]
theorem innerDualCone_empty : (‚àÖ : Set H).innerDualCone = ‚ä§ :=
  eq_top_iff.mpr fun _ _ _ => False.elim

/-- Dual cone of the convex cone {0} is the total space. -/
@[simp]
theorem innerDualCone_zero : (0 : Set H).innerDualCone = ‚ä§ :=
  eq_top_iff.mpr fun _ _ y (hy : y = 0) => hy.symm ‚ñ∏ (inner_zero_left _).ge

/-- Dual cone of the total space is the convex cone {0}. -/
@[simp]
theorem innerDualCone_univ : (univ : Set H).innerDualCone = 0 := by
  suffices ‚àÄ x : H, x ‚àà (univ : Set H).innerDualCone ‚Üí x = 0 by
    apply SetLike.coe_injective
    exact eq_singleton_iff_unique_mem.mpr ‚ü®fun x _ => (inner_zero_right _).ge, this‚ü©
  exact fun x hx => by simpa [‚Üê real_inner_self_nonpos] using hx (-x) (mem_univ _)

variable {s t} in
@[gcongr]
theorem innerDualCone_le_innerDualCone (h : t ‚äÜ s) : s.innerDualCone ‚â§ t.innerDualCone :=
  fun _ hy x hx => hy x (h hx)

theorem pointed_innerDualCone : s.innerDualCone.Pointed := fun x _ => by rw [inner_zero_right]

/-- The inner dual cone of a singleton is given by the preimage of the positive cone under the
linear map `fun y ‚Ü¶ ‚ü™x, y‚ü´`. -/
theorem innerDualCone_singleton (x : H) :
    ({x} : Set H).innerDualCone = (ConvexCone.positive ‚Ñù ‚Ñù).comap (inner‚Çõ‚Çó ‚Ñù x) :=
  ConvexCone.ext fun _ => forall_eq

theorem innerDualCone_union (s t : Set H) :
    (s ‚à™ t).innerDualCone = s.innerDualCone ‚äì t.innerDualCone :=
  le_antisymm (le_inf (fun _ hx _ hy => hx _ <| Or.inl hy) fun _ hx _ hy => hx _ <| Or.inr hy)
    fun _ hx _ => Or.rec (hx.1 _) (hx.2 _)

theorem innerDualCone_insert (x : H) (s : Set H) :
    (insert x s).innerDualCone = Set.innerDualCone {x} ‚äì s.innerDualCone := by
  rw [insert_eq, innerDualCone_union]

theorem innerDualCone_iUnion {Œπ : Sort*} (f : Œπ ‚Üí Set H) :
    (‚ãÉ i, f i).innerDualCone = ‚®Ö i, (f i).innerDualCone := by
  refine le_antisymm (le_iInf fun i x hx y hy => hx _ <| mem_iUnion_of_mem _ hy) ?_
  intro x hx y hy
  rw [ConvexCone.mem_iInf] at hx
  obtain ‚ü®j, hj‚ü© := mem_iUnion.mp hy
  exact hx _ _ hj

theorem innerDualCone_sUnion (S : Set (Set H)) :
    (‚ãÉ‚ÇÄ S).innerDualCone = sInf (Set.innerDualCone '' S) := by
  simp_rw [sInf_image, sUnion_eq_biUnion, innerDualCone_iUnion]

/-- The dual cone of `s` equals the intersection of dual cones of the points in `s`. -/
theorem innerDualCone_eq_iInter_innerDualCone_singleton :
    (s.innerDualCone : Set H) = ‚ãÇ i : s, (({‚Üëi} : Set H).innerDualCone : Set H) := by
  rw [‚Üê ConvexCone.coe_iInf, ‚Üê innerDualCone_iUnion, iUnion_of_singleton_coe]

theorem isClosed_innerDualCone : IsClosed (s.innerDualCone : Set H) := by
  -- reduce the problem to showing that dual cone of a singleton `{x}` is closed
  rw [innerDualCone_eq_iInter_innerDualCone_singleton]
  apply isClosed_iInter
  intro x
  -- the dual cone of a singleton `{x}` is the preimage of `[0, ‚àû)` under `inner x`
  have h : ({‚Üëx} : Set H).innerDualCone = (inner ‚Ñù (x : H)) ‚Åª¬π' Set.Ici 0 := by
    rw [innerDualCone_singleton, ConvexCone.coe_comap, ConvexCone.coe_positive, inner‚Çõ‚Çó_apply_coe]
  -- the preimage is closed as `inner x` is continuous and `[0, ‚àû)` is closed
  rw [h]
  exact isClosed_Ici.preimage (continuous_const.inner continuous_id')

theorem ConvexCone.pointed_of_nonempty_of_isClosed (K : ConvexCone ‚Ñù H) (ne : (K : Set H).Nonempty)
    (hc : IsClosed (K : Set H)) : K.Pointed := by
  obtain ‚ü®x, hx‚ü© := ne
  let f : ‚Ñù ‚Üí H := (¬∑ ‚Ä¢ x)
  -- f (0, ‚àû) is a subset of K
  have fI : f '' Set.Ioi 0 ‚äÜ (K : Set H) := by
    rintro _ ‚ü®_, h, rfl‚ü©
    exact K.smul_mem (Set.mem_Ioi.1 h) hx
  -- closure of f (0, ‚àû) is a subset of K
  have clf : closure (f '' Set.Ioi 0) ‚äÜ (K : Set H) := hc.closure_subset_iff.2 fI
  -- f is continuous at 0 from the right
  have fc : ContinuousWithinAt f (Set.Ioi (0 : ‚Ñù)) 0 :=
    (continuous_id.smul continuous_const).continuousWithinAt
  -- 0 belongs to the closure of the f (0, ‚àû)
  have mem‚ÇÄ := fc.mem_closure_image (by rw [closure_Ioi (0 : ‚Ñù), mem_Ici])
  -- as 0 ‚àà closure f (0, ‚àû) and closure f (0, ‚àû) ‚äÜ K, 0 ‚àà K.
  have f‚ÇÄ : f 0 = 0 := zero_smul ‚Ñù x
  simpa only [f‚ÇÄ, ConvexCone.Pointed, ‚Üê SetLike.mem_coe] using mem_of_subset_of_mem clf mem‚ÇÄ

namespace PointedCone

/-- The inner dual cone of a pointed cone is a pointed cone. -/
def dual (C : PointedCone ‚Ñù H) : PointedCone ‚Ñù H :=
  ((C : Set H).innerDualCone).toPointedCone <| pointed_innerDualCone (C : Set H)

@[simp, norm_cast]
lemma toConvexCone_dual (C : PointedCone ‚Ñù H) : ‚Üë(dual C) = (C : Set H).innerDualCone := rfl

open InnerProductSpace in
@[simp]
lemma mem_dual {C : PointedCone ‚Ñù H} {y : H} : y ‚àà dual C ‚Üî ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà C ‚Üí 0 ‚â§ ‚ü™x, y‚ü´_‚Ñù := .rfl

end PointedCone

namespace ProperCone

/-- The inner dual cone of a proper cone is a proper cone. -/
def dual (C : ProperCone ‚Ñù H) : ProperCone ‚Ñù H where
  toSubmodule := PointedCone.dual (C : PointedCone ‚Ñù H)
  isClosed' := isClosed_innerDualCone _

@[simp, norm_cast]
lemma coe_dual (C : ProperCone ‚Ñù H) : C.dual = (C : Set H).innerDualCone := rfl

open scoped InnerProductSpace in
@[simp]
lemma mem_dual {C : ProperCone ‚Ñù H} {y : H} : y ‚àà dual C ‚Üî ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà C ‚Üí 0 ‚â§ ‚ü™x, y‚ü´_‚Ñù := .rfl

end ProperCone

section CompleteSpace

variable [CompleteSpace H]

open scoped InnerProductSpace in
/-- This is a stronger version of the Hahn-Banach separation theorem for closed convex cones. This
is also the geometric interpretation of Farkas' lemma. -/
theorem ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_notMem (K : ConvexCone ‚Ñù H)
    (ne : (K : Set H).Nonempty) (hc : IsClosed (K : Set H)) {b : H} (disj : b ‚àâ K) :
    ‚àÉ y : H, (‚àÄ x : H, x ‚àà K ‚Üí 0 ‚â§ ‚ü™x, y‚ü´_‚Ñù) ‚àß ‚ü™y, b‚ü´_‚Ñù < 0 := by
  -- let `z` be the point in `K` closest to `b`
  obtain ‚ü®z, hzK, infi‚ü© := exists_norm_eq_iInf_of_complete_convex ne hc.isComplete K.convex b
  -- for any `w` in `K`, we have `‚ü™b - z, w - z‚ü´_‚Ñù ‚â§ 0`
  have hinner := (norm_eq_iInf_iff_real_inner_le_zero K.convex hzK).1 infi
  -- set `y := z - b`
  use z - b
  constructor
  ¬∑ -- the rest of the proof is a straightforward calculation
    rintro x hxK
    specialize hinner _ (K.add_mem hxK hzK)
    rwa [add_sub_cancel_right, real_inner_comm, ‚Üê neg_nonneg, neg_eq_neg_one_mul,
      ‚Üê real_inner_smul_right, neg_smul, one_smul, neg_sub] at hinner
  ¬∑ -- as `K` is closed and non-empty, it is pointed
    have hinner‚ÇÄ := hinner 0 (K.pointed_of_nonempty_of_isClosed ne hc)
    -- the rest of the proof is a straightforward calculation
    rw [zero_sub, inner_neg_right, Right.neg_nonpos_iff] at hinner‚ÇÄ
    have hbz : b - z ‚â† 0 := by
      rw [sub_ne_zero]
      contrapose! hzK
      rwa [‚Üê hzK]
    rw [‚Üê neg_zero, lt_neg, ‚Üê neg_one_mul, ‚Üê real_inner_smul_left, smul_sub, neg_smul, one_smul,
      neg_smul, neg_sub_neg, one_smul]
    calc
      0 < ‚ü™b - z, b - z‚ü´_‚Ñù := lt_of_not_ge ((Iff.not real_inner_self_nonpos).2 hbz)
      _ = ‚ü™b - z, b - z‚ü´_‚Ñù + 0 := (add_zero _).symm
      _ ‚â§ ‚ü™b - z, b - z‚ü´_‚Ñù + ‚ü™b - z, z‚ü´_‚Ñù := add_le_add rfl.ge hinner‚ÇÄ
      _ = ‚ü™b - z, b - z + z‚ü´_‚Ñù := (inner_add_right _ _ _).symm
      _ = ‚ü™b - z, b‚ü´_‚Ñù := by rw [sub_add_cancel]

@[deprecated (since := "2025-05-24")]
alias ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_nmem :=
  ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_notMem

/-- The inner dual of inner dual of a non-empty, closed convex cone is itself. -/
theorem ConvexCone.innerDualCone_of_innerDualCone_eq_self (K : ConvexCone ‚Ñù H)
    (ne : (K : Set H).Nonempty) (hc : IsClosed (K : Set H)) :
    ((K : Set H).innerDualCone : Set H).innerDualCone = K := by
  ext x
  constructor
  ¬∑ rw [mem_innerDualCone, ‚Üê SetLike.mem_coe]
    contrapose!
    exact K.hyperplane_separation_of_nonempty_of_isClosed_of_notMem ne hc
  ¬∑ rintro hxK y h
    specialize h x hxK
    rwa [real_inner_comm]

namespace ProperCone
variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace ‚Ñù F]

/-- The dual of the dual of a proper cone is itself. -/
@[simp]
theorem dual_dual (K : ProperCone ‚Ñù H) : K.dual.dual = K :=
  ProperCone.toPointedCone_injective <| PointedCone.toConvexCone_injective <|
    (K : ConvexCone ‚Ñù H).innerDualCone_of_innerDualCone_eq_self K.nonempty K.isClosed

variable [CompleteSpace F]

open scoped InnerProductSpace

/-- This is a relative version of
`ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_notMem`, which we recover by setting
`f` to be the identity map. This is also a geometric interpretation of the Farkas' lemma
stated using proper cones. -/
theorem hyperplane_separation (K : ProperCone ‚Ñù H) {f : H ‚ÜíL[‚Ñù] F} {b : F} :
    b ‚àà K.map f ‚Üî ‚àÄ y : F, f.adjoint y ‚àà K.dual ‚Üí 0 ‚â§ ‚ü™y, b‚ü´_‚Ñù where
  mp := by
    -- suppose `b ‚àà K.map f`
    simp only [mem_map, PointedCone.mem_closure, PointedCone.coe_map, ContinuousLinearMap.coe_coe,
      mem_closure_iff_seq_limit, mem_image, SetLike.mem_coe, mem_dual,
      ContinuousLinearMap.adjoint_inner_right, forall_exists_index, and_imp]
    -- there is a sequence `seq : ‚Ñï ‚Üí F` in the image of `f` that converges to `b`
    rintro seq hmem htends y hinner
    suffices h : ‚àÄ n, 0 ‚â§ ‚ü™y, seq n‚ü´_‚Ñù from
      ge_of_tendsto' (Continuous.seqContinuous (Continuous.inner (@continuous_const _ _ _ _ y)
        continuous_id) htends) h
    intro n
    obtain ‚ü®_, h, hseq‚ü© := hmem n
    simpa only [‚Üê hseq, real_inner_comm] using hinner h
  mpr := by
    -- proof by contradiction
    -- suppose `b ‚àâ K.map f`
    intro h
    contrapose! h
    -- as `b ‚àâ K.map f`, there is a hyperplane `y` separating `b` from `K.map f`
    let C := PointedCone.toConvexCone (ùïú := ‚Ñù) (E := F) (K.map f)
    obtain ‚ü®y, hxy, hyb‚ü© :=
      @ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_notMem
      _ _ _ _ C (K.map f).nonempty (K.map f).isClosed b h
    -- the rest of the proof is a straightforward algebraic manipulation
    refine ‚ü®y, ?_, hyb‚ü©
    simp only [mem_dual, ContinuousLinearMap.adjoint_inner_right]
    intro x hxK
    exact hxy (f x) <| subset_closure <| Set.mem_image_of_mem _ hxK

theorem hyperplane_separation_of_notMem (K : ProperCone ‚Ñù H) {f : H ‚ÜíL[‚Ñù] F} {b : F}
    (disj : b ‚àâ K.map f) : ‚àÉ y : F, ContinuousLinearMap.adjoint f y ‚àà K.dual ‚àß ‚ü™y, b‚ü´_‚Ñù < 0 := by
  contrapose! disj; rwa [K.hyperplane_separation]

@[deprecated (since := "2025-05-24")]
alias hyperplane_separation_of_nmem := hyperplane_separation_of_notMem

end ProperCone

end CompleteSpace

end Dual
