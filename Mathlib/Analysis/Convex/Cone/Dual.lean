/-
Copyright (c) 2021 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.Analysis.Convex.Cone.Basic
import Mathlib.Analysis.InnerProductSpace.Projection

#align_import analysis.convex.cone.dual from "leanprover-community/mathlib"@"915591b2bb3ea303648db07284a161a7f2a9e3d4"

/-!
# Convex cones in inner product spaces

We define `Set.innerDualCone` to be the cone consisting of all points `y` such that for
all points `x` in a given set `0 â‰¤ âŸª x, y âŸ«`.

## Main statements

We prove the following theorems:
* `ConvexCone.innerDualCone_of_innerDualCone_eq_self`:
  The `innerDualCone` of the `innerDualCone` of a nonempty, closed, convex cone is itself.

-/


open Set LinearMap

open Classical Pointwise

variable {ð•œ E F G : Type*}

/-! ### The dual cone -/


section Dual

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„ H] (s t : Set H)

open RealInnerProductSpace

/-- The dual cone is the cone consisting of all points `y` such that for
all points `x` in a given set `0 â‰¤ âŸª x, y âŸ«`. -/
def Set.innerDualCone (s : Set H) : ConvexCone â„ H where
  carrier := { y | âˆ€ x âˆˆ s, 0 â‰¤ âŸªx, yâŸ« }
  smul_mem' c hc y hy x hx := by
    rw [real_inner_smul_right]
    exact mul_nonneg hc.le (hy x hx)
  add_mem' u hu v hv x hx := by
    rw [inner_add_right]
    exact add_nonneg (hu x hx) (hv x hx)
#align set.inner_dual_cone Set.innerDualCone

@[simp]
theorem mem_innerDualCone (y : H) (s : Set H) : y âˆˆ s.innerDualCone â†” âˆ€ x âˆˆ s, 0 â‰¤ âŸªx, yâŸ« :=
  Iff.rfl
#align mem_inner_dual_cone mem_innerDualCone

@[simp]
theorem innerDualCone_empty : (âˆ… : Set H).innerDualCone = âŠ¤ :=
  eq_top_iff.mpr fun _ _ _ => False.elim
#align inner_dual_cone_empty innerDualCone_empty

/-- Dual cone of the convex cone {0} is the total space. -/
@[simp]
theorem innerDualCone_zero : (0 : Set H).innerDualCone = âŠ¤ :=
  eq_top_iff.mpr fun _ _ y (hy : y = 0) => hy.symm â–¸ (inner_zero_left _).ge
#align inner_dual_cone_zero innerDualCone_zero

/-- Dual cone of the total space is the convex cone {0}. -/
@[simp]
theorem innerDualCone_univ : (univ : Set H).innerDualCone = 0 := by
  suffices âˆ€ x : H, x âˆˆ (univ : Set H).innerDualCone â†’ x = 0 by
    apply SetLike.coe_injective
    exact eq_singleton_iff_unique_mem.mpr âŸ¨fun x _ => (inner_zero_right _).ge, thisâŸ©
  exact fun x hx => by simpa [â† real_inner_self_nonpos] using hx (-x) (mem_univ _)
#align inner_dual_cone_univ innerDualCone_univ

theorem innerDualCone_le_innerDualCone (h : t âŠ† s) : s.innerDualCone â‰¤ t.innerDualCone :=
  fun _ hy x hx => hy x (h hx)
#align inner_dual_cone_le_inner_dual_cone innerDualCone_le_innerDualCone

theorem pointed_innerDualCone : s.innerDualCone.Pointed := fun x _ => by rw [inner_zero_right]
#align pointed_inner_dual_cone pointed_innerDualCone

/-- The inner dual cone of a singleton is given by the preimage of the positive cone under the
linear map `fun y â†¦ âŸªx, yâŸ«`. -/
theorem innerDualCone_singleton (x : H) :
    ({x} : Set H).innerDualCone = (ConvexCone.positive â„ â„).comap (innerâ‚›â‚— â„ x) :=
  ConvexCone.ext fun _ => forall_eq
#align inner_dual_cone_singleton innerDualCone_singleton

theorem innerDualCone_union (s t : Set H) :
    (s âˆª t).innerDualCone = s.innerDualCone âŠ“ t.innerDualCone :=
  le_antisymm (le_inf (fun _ hx _ hy => hx _ <| Or.inl hy) fun _ hx _ hy => hx _ <| Or.inr hy)
    fun _ hx _ => Or.rec (hx.1 _) (hx.2 _)
#align inner_dual_cone_union innerDualCone_union

theorem innerDualCone_insert (x : H) (s : Set H) :
    (insert x s).innerDualCone = Set.innerDualCone {x} âŠ“ s.innerDualCone := by
  rw [insert_eq, innerDualCone_union]
#align inner_dual_cone_insert innerDualCone_insert

theorem innerDualCone_iUnion {Î¹ : Sort*} (f : Î¹ â†’ Set H) :
    (â‹ƒ i, f i).innerDualCone = â¨… i, (f i).innerDualCone := by
  refine' le_antisymm (le_iInf fun i x hx y hy => hx _ <| mem_iUnion_of_mem _ hy) _
  intro x hx y hy
  rw [ConvexCone.mem_iInf] at hx
  obtain âŸ¨j, hjâŸ© := mem_iUnion.mp hy
  exact hx _ _ hj
#align inner_dual_cone_Union innerDualCone_iUnion

theorem innerDualCone_sUnion (S : Set (Set H)) :
    (â‹ƒâ‚€ S).innerDualCone = sInf (Set.innerDualCone '' S) := by
  simp_rw [sInf_image, sUnion_eq_biUnion, innerDualCone_iUnion]
#align inner_dual_cone_sUnion innerDualCone_sUnion

/-- The dual cone of `s` equals the intersection of dual cones of the points in `s`. -/
theorem innerDualCone_eq_iInter_innerDualCone_singleton :
    (s.innerDualCone : Set H) = â‹‚ i : s, (({â†‘i} : Set H).innerDualCone : Set H) := by
  rw [â† ConvexCone.coe_iInf, â† innerDualCone_iUnion, iUnion_of_singleton_coe]
#align inner_dual_cone_eq_Inter_inner_dual_cone_singleton innerDualCone_eq_iInter_innerDualCone_singleton

theorem isClosed_innerDualCone : IsClosed (s.innerDualCone : Set H) := by
  -- reduce the problem to showing that dual cone of a singleton `{x}` is closed
  rw [innerDualCone_eq_iInter_innerDualCone_singleton]
  apply isClosed_iInter
  intro x
  -- the dual cone of a singleton `{x}` is the preimage of `[0, âˆž)` under `inner x`
  have h : ({â†‘x} : Set H).innerDualCone = (inner x : H â†’ â„) â»Â¹' Set.Ici 0 := by
    rw [innerDualCone_singleton, ConvexCone.coe_comap, ConvexCone.coe_positive, innerâ‚›â‚—_apply_coe]
  -- the preimage is closed as `inner x` is continuous and `[0, âˆž)` is closed
  rw [h]
  exact isClosed_Ici.preimage (continuous_const.inner continuous_id')

#align is_closed_inner_dual_cone isClosed_innerDualCone

theorem ConvexCone.pointed_of_nonempty_of_isClosed (K : ConvexCone â„ H) (ne : (K : Set H).Nonempty)
    (hc : IsClosed (K : Set H)) : K.Pointed := by
  obtain âŸ¨x, hxâŸ© := ne
  let f : â„ â†’ H := (Â· â€¢ x)
  -- f (0, âˆž) is a subset of K
  have fI : f '' Set.Ioi 0 âŠ† (K : Set H) := by
    rintro _ âŸ¨_, h, rflâŸ©
    exact K.smul_mem (Set.mem_Ioi.1 h) hx
  -- closure of f (0, âˆž) is a subset of K
  have clf : closure (f '' Set.Ioi 0) âŠ† (K : Set H) := hc.closure_subset_iff.2 fI
  -- f is continuous at 0 from the right
  have fc : ContinuousWithinAt f (Set.Ioi (0 : â„)) 0 :=
    (continuous_id.smul continuous_const).continuousWithinAt
  -- 0 belongs to the closure of the f (0, âˆž)
  have memâ‚€ := fc.mem_closure_image (by rw [closure_Ioi (0 : â„), mem_Ici])
  -- as 0 âˆˆ closure f (0, âˆž) and closure f (0, âˆž) âŠ† K, 0 âˆˆ K.
  have fâ‚€ : f 0 = 0 := zero_smul â„ x
  simpa only [fâ‚€, ConvexCone.Pointed, â† SetLike.mem_coe] using mem_of_subset_of_mem clf memâ‚€
#align convex_cone.pointed_of_nonempty_of_is_closed ConvexCone.pointed_of_nonempty_of_isClosed

section CompleteSpace

variable [CompleteSpace H]

/-- This is a stronger version of the Hahn-Banach separation theorem for closed convex cones. This
is also the geometric interpretation of Farkas' lemma. -/
theorem ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_nmem (K : ConvexCone â„ H)
    (ne : (K : Set H).Nonempty) (hc : IsClosed (K : Set H)) {b : H} (disj : b âˆ‰ K) :
    âˆƒ y : H, (âˆ€ x : H, x âˆˆ K â†’ 0 â‰¤ âŸªx, yâŸ«_â„) âˆ§ âŸªy, bâŸ«_â„ < 0 := by
  -- let `z` be the point in `K` closest to `b`
  obtain âŸ¨z, hzK, infiâŸ© := exists_norm_eq_iInf_of_complete_convex ne hc.isComplete K.convex b
  -- for any `w` in `K`, we have `âŸªb - z, w - zâŸ«_â„ â‰¤ 0`
  have hinner := (norm_eq_iInf_iff_real_inner_le_zero K.convex hzK).1 infi
  -- set `y := z - b`
  use z - b
  constructor
  Â· -- the rest of the proof is a straightforward calculation
    rintro x hxK
    specialize hinner _ (K.add_mem hxK hzK)
    rwa [add_sub_cancel, real_inner_comm, â† neg_nonneg, neg_eq_neg_one_mul, â† real_inner_smul_right,
      neg_smul, one_smul, neg_sub] at hinner
  Â· -- as `K` is closed and non-empty, it is pointed
    have hinnerâ‚€ := hinner 0 (K.pointed_of_nonempty_of_isClosed ne hc)
    -- the rest of the proof is a straightforward calculation
    rw [zero_sub, inner_neg_right, Right.neg_nonpos_iff] at hinnerâ‚€
    have hbz : b - z â‰  0 := by
      rw [sub_ne_zero]
      contrapose! hzK
      rwa [â† hzK]
    rw [â† neg_zero, lt_neg, â† neg_one_mul, â† real_inner_smul_left, smul_sub, neg_smul, one_smul,
      neg_smul, neg_sub_neg, one_smul]
    calc
      0 < âŸªb - z, b - zâŸ«_â„ := lt_of_not_le ((Iff.not real_inner_self_nonpos).2 hbz)
      _ = âŸªb - z, b - zâŸ«_â„ + 0 := (add_zero _).symm
      _ â‰¤ âŸªb - z, b - zâŸ«_â„ + âŸªb - z, zâŸ«_â„ := (add_le_add rfl.ge hinnerâ‚€)
      _ = âŸªb - z, b - z + zâŸ«_â„ := (inner_add_right _ _ _).symm
      _ = âŸªb - z, bâŸ«_â„ := by rw [sub_add_cancel]

#align convex_cone.hyperplane_separation_of_nonempty_of_is_closed_of_nmem ConvexCone.hyperplane_separation_of_nonempty_of_isClosed_of_nmem

/-- The inner dual of inner dual of a non-empty, closed convex cone is itself.  -/
theorem ConvexCone.innerDualCone_of_innerDualCone_eq_self (K : ConvexCone â„ H)
    (ne : (K : Set H).Nonempty) (hc : IsClosed (K : Set H)) :
    ((K : Set H).innerDualCone : Set H).innerDualCone = K := by
  ext x
  constructor
  Â· rw [mem_innerDualCone, â† SetLike.mem_coe]
    contrapose!
    exact K.hyperplane_separation_of_nonempty_of_isClosed_of_nmem ne hc
  Â· rintro hxK y h
    specialize h x hxK
    rwa [real_inner_comm]
#align convex_cone.inner_dual_cone_of_inner_dual_cone_eq_self ConvexCone.innerDualCone_of_innerDualCone_eq_self

end CompleteSpace

end Dual
