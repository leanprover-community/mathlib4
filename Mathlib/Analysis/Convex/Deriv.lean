/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov, David Loeffler
-/
import Mathlib.Analysis.Convex.Slope
import Mathlib.Analysis.Calculus.Deriv.MeanValue

/-!
# Convexity of functions and derivatives

Here we relate convexity of functions `‚Ñù ‚Üí ‚Ñù` to properties of their derivatives.

## Main results

* `MonotoneOn.convexOn_of_deriv`, `convexOn_of_deriv2_nonneg` : if the derivative of a function
  is increasing or its second derivative is nonnegative, then the original function is convex.
* `ConvexOn.monotoneOn_deriv`: if a function is convex and differentiable, then its derivative is
  monotone.
-/

open Metric Set Asymptotics ContinuousLinearMap Filter
open scoped Topology NNReal

/-!
## Monotonicity of `f'` implies convexity of `f`
-/

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is differentiable on its interior,
and `f'` is monotone on the interior, then `f` is convex on `D`. -/
theorem MonotoneOn.convexOn_of_deriv {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D))
    (hf'_mono : MonotoneOn (deriv f) (interior D)) : ConvexOn ‚Ñù D f :=
  convexOn_of_slope_mono_adjacent hD
    (by
      intro x y z hx hz hxy hyz
      -- First we prove some trivial inclusions
      have hxzD : Icc x z ‚äÜ D := hD.ordConnected.out hx hz
      have hxyD : Icc x y ‚äÜ D := (Icc_subset_Icc_right hyz.le).trans hxzD
      have hxyD' : Ioo x y ‚äÜ interior D :=
        subset_sUnion_of_mem ‚ü®isOpen_Ioo, Ioo_subset_Icc_self.trans hxyD‚ü©
      have hyzD : Icc y z ‚äÜ D := (Icc_subset_Icc_left hxy.le).trans hxzD
      have hyzD' : Ioo y z ‚äÜ interior D :=
        subset_sUnion_of_mem ‚ü®isOpen_Ioo, Ioo_subset_Icc_self.trans hyzD‚ü©
      -- Then we apply MVT to both `[x, y]` and `[y, z]`
      obtain ‚ü®a, ‚ü®hxa, hay‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x) :=
        exists_deriv_eq_slope f hxy (hf.mono hxyD) (hf'.mono hxyD')
      obtain ‚ü®b, ‚ü®hyb, hbz‚ü©, hb‚ü© : ‚àÉ b ‚àà Ioo y z, deriv f b = (f z - f y) / (z - y) :=
        exists_deriv_eq_slope f hyz (hf.mono hyzD) (hf'.mono hyzD')
      rw [‚Üê ha, ‚Üê hb]
      exact hf'_mono (hxyD' ‚ü®hxa, hay‚ü©) (hyzD' ‚ü®hyb, hbz‚ü©) (hay.trans hyb).le)

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is differentiable on its interior,
and `f'` is antitone on the interior, then `f` is concave on `D`. -/
theorem AntitoneOn.concaveOn_of_deriv {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D))
    (h_anti : AntitoneOn (deriv f) (interior D)) : ConcaveOn ‚Ñù D f :=
  haveI : MonotoneOn (deriv (-f)) (interior D) := by
    simpa only [‚Üê deriv.neg] using h_anti.neg
  neg_convexOn_iff.mp (this.convexOn_of_deriv hD hf.neg hf'.neg)

theorem StrictMonoOn.exists_slope_lt_deriv_aux {x y : ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f (Icc x y))
    (hxy : x < y) (hf'_mono : StrictMonoOn (deriv f) (Ioo x y)) (h : ‚àÄ w ‚àà Ioo x y, deriv f w ‚â† 0) :
    ‚àÉ a ‚àà Ioo x y, (f y - f x) / (y - x) < deriv f a := by
  have A : DifferentiableOn ‚Ñù f (Ioo x y) := fun w wmem =>
    (differentiableAt_of_deriv_ne_zero (h w wmem)).differentiableWithinAt
  obtain ‚ü®a, ‚ü®hxa, hay‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x) :=
    exists_deriv_eq_slope f hxy hf A
  rcases nonempty_Ioo.2 hay with ‚ü®b, ‚ü®hab, hby‚ü©‚ü©
  refine ‚ü®b, ‚ü®hxa.trans hab, hby‚ü©, ?_‚ü©
  rw [‚Üê ha]
  exact hf'_mono ‚ü®hxa, hay‚ü© ‚ü®hxa.trans hab, hby‚ü© hab

theorem StrictMonoOn.exists_slope_lt_deriv {x y : ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f (Icc x y))
    (hxy : x < y) (hf'_mono : StrictMonoOn (deriv f) (Ioo x y)) :
    ‚àÉ a ‚àà Ioo x y, (f y - f x) / (y - x) < deriv f a := by
  by_cases h : ‚àÄ w ‚àà Ioo x y, deriv f w ‚â† 0
  ¬∑ apply StrictMonoOn.exists_slope_lt_deriv_aux hf hxy hf'_mono h
  ¬∑ push_neg at h
    rcases h with ‚ü®w, ‚ü®hxw, hwy‚ü©, hw‚ü©
    obtain ‚ü®a, ‚ü®hxa, haw‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x w, (f w - f x) / (w - x) < deriv f a := by
      apply StrictMonoOn.exists_slope_lt_deriv_aux _ hxw _ _
      ¬∑ exact hf.mono (Icc_subset_Icc le_rfl hwy.le)
      ¬∑ exact hf'_mono.mono (Ioo_subset_Ioo le_rfl hwy.le)
      ¬∑ intro z hz
        rw [‚Üê hw]
        apply ne_of_lt
        exact hf'_mono ‚ü®hz.1, hz.2.trans hwy‚ü© ‚ü®hxw, hwy‚ü© hz.2
    obtain ‚ü®b, ‚ü®hwb, hby‚ü©, hb‚ü© : ‚àÉ b ‚àà Ioo w y, (f y - f w) / (y - w) < deriv f b := by
      apply StrictMonoOn.exists_slope_lt_deriv_aux _ hwy _ _
      ¬∑ refine hf.mono (Icc_subset_Icc hxw.le le_rfl)
      ¬∑ exact hf'_mono.mono (Ioo_subset_Ioo hxw.le le_rfl)
      ¬∑ intro z hz
        rw [‚Üê hw]
        apply ne_of_gt
        exact hf'_mono ‚ü®hxw, hwy‚ü© ‚ü®hxw.trans hz.1, hz.2‚ü© hz.1
    refine ‚ü®b, ‚ü®hxw.trans hwb, hby‚ü©, ?_‚ü©
    simp only [div_lt_iff‚ÇÄ, hxy, hxw, hwy, sub_pos] at ha hb ‚ä¢
    have : deriv f a * (w - x) < deriv f b * (w - x) := by
      apply mul_lt_mul _ le_rfl (sub_pos.2 hxw) _
      ¬∑ exact hf'_mono ‚ü®hxa, haw.trans hwy‚ü© ‚ü®hxw.trans hwb, hby‚ü© (haw.trans hwb)
      ¬∑ rw [‚Üê hw]
        exact (hf'_mono ‚ü®hxw, hwy‚ü© ‚ü®hxw.trans hwb, hby‚ü© hwb).le
    linarith

theorem StrictMonoOn.exists_deriv_lt_slope_aux {x y : ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f (Icc x y))
    (hxy : x < y) (hf'_mono : StrictMonoOn (deriv f) (Ioo x y)) (h : ‚àÄ w ‚àà Ioo x y, deriv f w ‚â† 0) :
    ‚àÉ a ‚àà Ioo x y, deriv f a < (f y - f x) / (y - x) := by
  have A : DifferentiableOn ‚Ñù f (Ioo x y) := fun w wmem =>
    (differentiableAt_of_deriv_ne_zero (h w wmem)).differentiableWithinAt
  obtain ‚ü®a, ‚ü®hxa, hay‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x) :=
    exists_deriv_eq_slope f hxy hf A
  rcases nonempty_Ioo.2 hxa with ‚ü®b, ‚ü®hxb, hba‚ü©‚ü©
  refine ‚ü®b, ‚ü®hxb, hba.trans hay‚ü©, ?_‚ü©
  rw [‚Üê ha]
  exact hf'_mono ‚ü®hxb, hba.trans hay‚ü© ‚ü®hxa, hay‚ü© hba

theorem StrictMonoOn.exists_deriv_lt_slope {x y : ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f (Icc x y))
    (hxy : x < y) (hf'_mono : StrictMonoOn (deriv f) (Ioo x y)) :
    ‚àÉ a ‚àà Ioo x y, deriv f a < (f y - f x) / (y - x) := by
  by_cases h : ‚àÄ w ‚àà Ioo x y, deriv f w ‚â† 0
  ¬∑ apply StrictMonoOn.exists_deriv_lt_slope_aux hf hxy hf'_mono h
  ¬∑ push_neg at h
    rcases h with ‚ü®w, ‚ü®hxw, hwy‚ü©, hw‚ü©
    obtain ‚ü®a, ‚ü®hxa, haw‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x w, deriv f a < (f w - f x) / (w - x) := by
      apply StrictMonoOn.exists_deriv_lt_slope_aux _ hxw _ _
      ¬∑ exact hf.mono (Icc_subset_Icc le_rfl hwy.le)
      ¬∑ exact hf'_mono.mono (Ioo_subset_Ioo le_rfl hwy.le)
      ¬∑ intro z hz
        rw [‚Üê hw]
        apply ne_of_lt
        exact hf'_mono ‚ü®hz.1, hz.2.trans hwy‚ü© ‚ü®hxw, hwy‚ü© hz.2
    obtain ‚ü®b, ‚ü®hwb, hby‚ü©, hb‚ü© : ‚àÉ b ‚àà Ioo w y, deriv f b < (f y - f w) / (y - w) := by
      apply StrictMonoOn.exists_deriv_lt_slope_aux _ hwy _ _
      ¬∑ refine hf.mono (Icc_subset_Icc hxw.le le_rfl)
      ¬∑ exact hf'_mono.mono (Ioo_subset_Ioo hxw.le le_rfl)
      ¬∑ intro z hz
        rw [‚Üê hw]
        apply ne_of_gt
        exact hf'_mono ‚ü®hxw, hwy‚ü© ‚ü®hxw.trans hz.1, hz.2‚ü© hz.1
    refine ‚ü®a, ‚ü®hxa, haw.trans hwy‚ü©, ?_‚ü©
    simp only [lt_div_iff‚ÇÄ, hxy, hxw, hwy, sub_pos] at ha hb ‚ä¢
    have : deriv f a * (y - w) < deriv f b * (y - w) := by
      apply mul_lt_mul _ le_rfl (sub_pos.2 hwy) _
      ¬∑ exact hf'_mono ‚ü®hxa, haw.trans hwy‚ü© ‚ü®hxw.trans hwb, hby‚ü© (haw.trans hwb)
      ¬∑ rw [‚Üê hw]
        exact (hf'_mono ‚ü®hxw, hwy‚ü© ‚ü®hxw.trans hwb, hby‚ü© hwb).le
    linarith

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, and `f'` is strictly monotone on the
interior, then `f` is strictly convex on `D`.
Note that we don't require differentiability, since it is guaranteed at all but at most
one point by the strict monotonicity of `f'`. -/
theorem StrictMonoOn.strictConvexOn_of_deriv {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : StrictMonoOn (deriv f) (interior D)) : StrictConvexOn ‚Ñù D f :=
  strictConvexOn_of_slope_strict_mono_adjacent hD fun {x y z} hx hz hxy hyz => by
    -- First we prove some trivial inclusions
    have hxzD : Icc x z ‚äÜ D := hD.ordConnected.out hx hz
    have hxyD : Icc x y ‚äÜ D := (Icc_subset_Icc_right hyz.le).trans hxzD
    have hxyD' : Ioo x y ‚äÜ interior D :=
      subset_sUnion_of_mem ‚ü®isOpen_Ioo, Ioo_subset_Icc_self.trans hxyD‚ü©
    have hyzD : Icc y z ‚äÜ D := (Icc_subset_Icc_left hxy.le).trans hxzD
    have hyzD' : Ioo y z ‚äÜ interior D :=
      subset_sUnion_of_mem ‚ü®isOpen_Ioo, Ioo_subset_Icc_self.trans hyzD‚ü©
    -- Then we get points `a` and `b` in each interval `[x, y]` and `[y, z]` where the derivatives
    -- can be compared to the slopes between `x, y` and `y, z` respectively.
    obtain ‚ü®a, ‚ü®hxa, hay‚ü©, ha‚ü© : ‚àÉ a ‚àà Ioo x y, (f y - f x) / (y - x) < deriv f a :=
      StrictMonoOn.exists_slope_lt_deriv (hf.mono hxyD) hxy (hf'.mono hxyD')
    obtain ‚ü®b, ‚ü®hyb, hbz‚ü©, hb‚ü© : ‚àÉ b ‚àà Ioo y z, deriv f b < (f z - f y) / (z - y) :=
      StrictMonoOn.exists_deriv_lt_slope (hf.mono hyzD) hyz (hf'.mono hyzD')
    apply ha.trans (lt_trans _ hb)
    exact hf' (hxyD' ‚ü®hxa, hay‚ü©) (hyzD' ‚ü®hyb, hbz‚ü©) (hay.trans hyb)

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù` and `f'` is strictly antitone on the
interior, then `f` is strictly concave on `D`.
Note that we don't require differentiability, since it is guaranteed at all but at most
one point by the strict antitonicity of `f'`. -/
theorem StrictAntiOn.strictConcaveOn_of_deriv {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (h_anti : StrictAntiOn (deriv f) (interior D)) :
    StrictConcaveOn ‚Ñù D f :=
  have : StrictMonoOn (deriv (-f)) (interior D) := by simpa only [‚Üê deriv.neg] using h_anti.neg
  neg_neg f ‚ñ∏ (this.strictConvexOn_of_deriv hD hf.neg).neg

/-- If a function `f` is differentiable and `f'` is monotone on `‚Ñù` then `f` is convex. -/
theorem Monotone.convexOn_univ_of_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f)
    (hf'_mono : Monotone (deriv f)) : ConvexOn ‚Ñù univ f :=
  (hf'_mono.monotoneOn _).convexOn_of_deriv convex_univ hf.continuous.continuousOn
    hf.differentiableOn

/-- If a function `f` is differentiable and `f'` is antitone on `‚Ñù` then `f` is concave. -/
theorem Antitone.concaveOn_univ_of_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f)
    (hf'_anti : Antitone (deriv f)) : ConcaveOn ‚Ñù univ f :=
  (hf'_anti.antitoneOn _).concaveOn_of_deriv convex_univ hf.continuous.continuousOn
    hf.differentiableOn

/-- If a function `f` is continuous and `f'` is strictly monotone on `‚Ñù` then `f` is strictly
convex. Note that we don't require differentiability, since it is guaranteed at all but at most
one point by the strict monotonicity of `f'`. -/
theorem StrictMono.strictConvexOn_univ_of_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : Continuous f)
    (hf'_mono : StrictMono (deriv f)) : StrictConvexOn ‚Ñù univ f :=
  (hf'_mono.strictMonoOn _).strictConvexOn_of_deriv convex_univ hf.continuousOn

/-- If a function `f` is continuous and `f'` is strictly antitone on `‚Ñù` then `f` is strictly
concave. Note that we don't require differentiability, since it is guaranteed at all but at most
one point by the strict antitonicity of `f'`. -/
theorem StrictAnti.strictConcaveOn_univ_of_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : Continuous f)
    (hf'_anti : StrictAnti (deriv f)) : StrictConcaveOn ‚Ñù univ f :=
  (hf'_anti.strictAntiOn _).strictConcaveOn_of_deriv convex_univ hf.continuousOn

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is twice differentiable on its
interior, and `f''` is nonnegative on the interior, then `f` is convex on `D`. -/
theorem convexOn_of_deriv2_nonneg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f D)
    (hf' : DifferentiableOn ‚Ñù f (interior D)) (hf'' : DifferentiableOn ‚Ñù (deriv f) (interior D))
    (hf''_nonneg : ‚àÄ x ‚àà interior D, 0 ‚â§ deriv^[2] f x) : ConvexOn ‚Ñù D f :=
  (monotoneOn_of_deriv_nonneg hD.interior hf''.continuousOn (by rwa [interior_interior]) <| by
        rwa [interior_interior]).convexOn_of_deriv
    hD hf hf'

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is twice differentiable on its
interior, and `f''` is nonpositive on the interior, then `f` is concave on `D`. -/
theorem concaveOn_of_deriv2_nonpos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f D)
    (hf' : DifferentiableOn ‚Ñù f (interior D)) (hf'' : DifferentiableOn ‚Ñù (deriv f) (interior D))
    (hf''_nonpos : ‚àÄ x ‚àà interior D, deriv^[2] f x ‚â§ 0) : ConcaveOn ‚Ñù D f :=
  (antitoneOn_of_deriv_nonpos hD.interior hf''.continuousOn (by rwa [interior_interior]) <| by
        rwa [interior_interior]).concaveOn_of_deriv
    hD hf hf'

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is twice differentiable on its
interior, and `f''` is nonnegative on the interior, then `f` is convex on `D`. -/
lemma convexOn_of_hasDerivWithinAt2_nonneg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f f' f'' : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f' (f'' x) (interior D) x)
    (hf''‚ÇÄ : ‚àÄ x ‚àà interior D, 0 ‚â§ f'' x) : ConvexOn ‚Ñù D f := by
  have : (interior D).EqOn (deriv f) f' := deriv_eqOn isOpen_interior hf'
  refine convexOn_of_deriv2_nonneg hD hf (fun x hx ‚Ü¶ (hf' _ hx).differentiableWithinAt) ?_ ?_
  ¬∑ rw [differentiableOn_congr this]
    exact fun x hx ‚Ü¶ (hf'' _ hx).differentiableWithinAt
  ¬∑ rintro x hx
    convert hf''‚ÇÄ _ hx using 1
    dsimp
    rw [deriv_eqOn isOpen_interior (fun y hy ‚Ü¶ ?_) hx]
    exact (hf'' _ hy).congr this <| by rw [this hy]

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù`, is twice differentiable on its
interior, and `f''` is nonpositive on the interior, then `f` is concave on `D`. -/
lemma concaveOn_of_hasDerivWithinAt2_nonpos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f f' f'' : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f' (f'' x) (interior D) x)
    (hf''‚ÇÄ : ‚àÄ x ‚àà interior D, f'' x ‚â§ 0) : ConcaveOn ‚Ñù D f := by
  have : (interior D).EqOn (deriv f) f' := deriv_eqOn isOpen_interior hf'
  refine concaveOn_of_deriv2_nonpos hD hf (fun x hx ‚Ü¶ (hf' _ hx).differentiableWithinAt) ?_ ?_
  ¬∑ rw [differentiableOn_congr this]
    exact fun x hx ‚Ü¶ (hf'' _ hx).differentiableWithinAt
  ¬∑ rintro x hx
    convert hf''‚ÇÄ _ hx using 1
    dsimp
    rw [deriv_eqOn isOpen_interior (fun y hy ‚Ü¶ ?_) hx]
    exact (hf'' _ hy).congr this <| by rw [this hy]

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù` and `f''` is strictly positive on the
interior, then `f` is strictly convex on `D`.
Note that we don't require twice differentiability explicitly as it is already implied by the second
derivative being strictly positive, except at at most one point. -/
theorem strictConvexOn_of_deriv2_pos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf'' : ‚àÄ x ‚àà interior D, 0 < (deriv^[2] f) x) :
    StrictConvexOn ‚Ñù D f :=
  ((strictMonoOn_of_deriv_pos hD.interior fun z hz =>
          (differentiableAt_of_deriv_ne_zero
                (hf'' z hz).ne').differentiableWithinAt.continuousWithinAt) <|
        by rwa [interior_interior]).strictConvexOn_of_deriv
    hD hf

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù` and `f''` is strictly negative on the
interior, then `f` is strictly concave on `D`.
Note that we don't require twice differentiability explicitly as it already implied by the second
derivative being strictly negative, except at at most one point. -/
theorem strictConcaveOn_of_deriv2_neg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf'' : ‚àÄ x ‚àà interior D, deriv^[2] f x < 0) :
    StrictConcaveOn ‚Ñù D f :=
  ((strictAntiOn_of_deriv_neg hD.interior fun z hz =>
          (differentiableAt_of_deriv_ne_zero
                (hf'' z hz).ne).differentiableWithinAt.continuousWithinAt) <|
        by rwa [interior_interior]).strictConcaveOn_of_deriv
    hD hf

/-- If a function `f` is twice differentiable on an open convex set `D ‚äÜ ‚Ñù` and
`f''` is nonnegative on `D`, then `f` is convex on `D`. -/
theorem convexOn_of_deriv2_nonneg' {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf' : DifferentiableOn ‚Ñù f D) (hf'' : DifferentiableOn ‚Ñù (deriv f) D)
    (hf''_nonneg : ‚àÄ x ‚àà D, 0 ‚â§ (deriv^[2] f) x) : ConvexOn ‚Ñù D f :=
  convexOn_of_deriv2_nonneg hD hf'.continuousOn (hf'.mono interior_subset)
    (hf''.mono interior_subset) fun x hx => hf''_nonneg x (interior_subset hx)

/-- If a function `f` is twice differentiable on an open convex set `D ‚äÜ ‚Ñù` and
`f''` is nonpositive on `D`, then `f` is concave on `D`. -/
theorem concaveOn_of_deriv2_nonpos' {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf' : DifferentiableOn ‚Ñù f D) (hf'' : DifferentiableOn ‚Ñù (deriv f) D)
    (hf''_nonpos : ‚àÄ x ‚àà D, deriv^[2] f x ‚â§ 0) : ConcaveOn ‚Ñù D f :=
  concaveOn_of_deriv2_nonpos hD hf'.continuousOn (hf'.mono interior_subset)
    (hf''.mono interior_subset) fun x hx => hf''_nonpos x (interior_subset hx)

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù` and `f''` is strictly positive on `D`,
then `f` is strictly convex on `D`.
Note that we don't require twice differentiability explicitly as it is already implied by the second
derivative being strictly positive, except at at most one point. -/
theorem strictConvexOn_of_deriv2_pos' {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf'' : ‚àÄ x ‚àà D, 0 < (deriv^[2] f) x) : StrictConvexOn ‚Ñù D f :=
  strictConvexOn_of_deriv2_pos hD hf fun x hx => hf'' x (interior_subset hx)

/-- If a function `f` is continuous on a convex set `D ‚äÜ ‚Ñù` and `f''` is strictly negative on `D`,
then `f` is strictly concave on `D`.
Note that we don't require twice differentiability explicitly as it is already implied by the second
derivative being strictly negative, except at at most one point. -/
theorem strictConcaveOn_of_deriv2_neg' {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf'' : ‚àÄ x ‚àà D, deriv^[2] f x < 0) : StrictConcaveOn ‚Ñù D f :=
  strictConcaveOn_of_deriv2_neg hD hf fun x hx => hf'' x (interior_subset hx)

/-- If a function `f` is twice differentiable on `‚Ñù`, and `f''` is nonnegative on `‚Ñù`,
then `f` is convex on `‚Ñù`. -/
theorem convexOn_univ_of_deriv2_nonneg {f : ‚Ñù ‚Üí ‚Ñù} (hf' : Differentiable ‚Ñù f)
    (hf'' : Differentiable ‚Ñù (deriv f)) (hf''_nonneg : ‚àÄ x, 0 ‚â§ (deriv^[2] f) x) :
    ConvexOn ‚Ñù univ f :=
  convexOn_of_deriv2_nonneg' convex_univ hf'.differentiableOn hf''.differentiableOn fun x _ =>
    hf''_nonneg x

/-- If a function `f` is twice differentiable on `‚Ñù`, and `f''` is nonpositive on `‚Ñù`,
then `f` is concave on `‚Ñù`. -/
theorem concaveOn_univ_of_deriv2_nonpos {f : ‚Ñù ‚Üí ‚Ñù} (hf' : Differentiable ‚Ñù f)
    (hf'' : Differentiable ‚Ñù (deriv f)) (hf''_nonpos : ‚àÄ x, deriv^[2] f x ‚â§ 0) :
    ConcaveOn ‚Ñù univ f :=
  concaveOn_of_deriv2_nonpos' convex_univ hf'.differentiableOn hf''.differentiableOn fun x _ =>
    hf''_nonpos x

/-- If a function `f` is continuous on `‚Ñù`, and `f''` is strictly positive on `‚Ñù`,
then `f` is strictly convex on `‚Ñù`.
Note that we don't require twice differentiability explicitly as it is already implied by the second
derivative being strictly positive, except at at most one point. -/
theorem strictConvexOn_univ_of_deriv2_pos {f : ‚Ñù ‚Üí ‚Ñù} (hf : Continuous f)
    (hf'' : ‚àÄ x, 0 < (deriv^[2] f) x) : StrictConvexOn ‚Ñù univ f :=
  strictConvexOn_of_deriv2_pos' convex_univ hf.continuousOn fun x _ => hf'' x

/-- If a function `f` is continuous on `‚Ñù`, and `f''` is strictly negative on `‚Ñù`,
then `f` is strictly concave on `‚Ñù`.
Note that we don't require twice differentiability explicitly as it is already implied by the second
derivative being strictly negative, except at at most one point. -/
theorem strictConcaveOn_univ_of_deriv2_neg {f : ‚Ñù ‚Üí ‚Ñù} (hf : Continuous f)
    (hf'' : ‚àÄ x, deriv^[2] f x < 0) : StrictConcaveOn ‚Ñù univ f :=
  strictConcaveOn_of_deriv2_neg' convex_univ hf.continuousOn fun x _ => hf'' x

/-!
## Convexity of `f` implies monotonicity of `f'`

In this section we prove inequalities relating derivatives of convex functions to slopes of secant
lines, and deduce that if `f` is convex then its derivative is monotone (and similarly for strict
convexity / strict monotonicity).
-/

section slope

variable {ùïú : Type*} [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú]
  {s : Set ùïú} {f : ùïú ‚Üí ùïú} {x : ùïú}

/-- If `f : ùïú ‚Üí ùïú` is convex on `s`, then for any point `x ‚àà s` the slope of the secant line of `f`
through `x` is monotone on `s \ {x}`. -/
lemma ConvexOn.slope_mono (hfc : ConvexOn ùïú s f) (hx : x ‚àà s) : MonotoneOn (slope f x) (s \ {x}) :=
  (slope_fun_def_field f _).symm ‚ñ∏ fun _ hy _ hz hz' ‚Ü¶ hfc.secant_mono hx (mem_of_mem_diff hy)
    (mem_of_mem_diff hz) (notMem_of_mem_diff hy :) (notMem_of_mem_diff hz :) hz'

lemma ConvexOn.monotoneOn_slope_gt (hfc : ConvexOn ùïú s f) (hxs : x ‚àà s) :
    MonotoneOn (slope f x) {y ‚àà s | x < y} :=
  (hfc.slope_mono hxs).mono fun _ ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®h1, h2.ne'‚ü©

lemma ConvexOn.monotoneOn_slope_lt (hfc : ConvexOn ùïú s f) (hxs : x ‚àà s) :
    MonotoneOn (slope f x) {y ‚àà s | y < x} :=
  (hfc.slope_mono hxs).mono fun _ ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®h1, h2.ne‚ü©

/-- If `f : ùïú ‚Üí ùïú` is concave on `s`, then for any point `x ‚àà s` the slope of the secant line of `f`
through `x` is antitone on `s \ {x}`. -/
lemma ConcaveOn.slope_anti (hfc : ConcaveOn ùïú s f) (hx : x ‚àà s) :
    AntitoneOn (slope f x) (s \ {x}) := by
  rw [‚Üê neg_neg f, slope_neg_fun]
  exact (ConvexOn.slope_mono hfc.neg hx).neg

lemma ConcaveOn.antitoneOn_slope_gt (hfc : ConcaveOn ùïú s f) (hxs : x ‚àà s) :
    AntitoneOn (slope f x) {y ‚àà s | x < y} :=
  (hfc.slope_anti hxs).mono fun _ ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®h1, h2.ne'‚ü©

lemma ConcaveOn.antitoneOn_slope_lt (hfc : ConcaveOn ùïú s f) (hxs : x ‚àà s) :
    AntitoneOn (slope f x) {y ‚àà s | y < x} :=
  (hfc.slope_anti hxs).mono fun _ ‚ü®h1, h2‚ü© ‚Ü¶ ‚ü®h1, h2.ne‚ü©

variable [TopologicalSpace ùïú] [OrderTopology ùïú]

lemma bddBelow_slope_lt_of_mem_interior (hfc : ConvexOn ùïú s f) (hxs : x ‚àà interior s) :
    BddBelow (slope f x '' {y ‚àà s | x < y}) := by
  obtain ‚ü®y, hyx, hys‚ü© : ‚àÉ y, y < x ‚àß y ‚àà s :=
    Eventually.exists_lt (mem_interior_iff_mem_nhds.mp hxs)
  refine bddBelow_iff_subset_Ici.mpr ‚ü®slope f x y, fun y' ‚ü®z, hz, hz'‚ü© ‚Ü¶ ?_‚ü©
  simp_rw [mem_Ici, ‚Üê hz']
  refine hfc.slope_mono (interior_subset hxs) ?_ ?_ (hyx.trans hz.2).le
  ¬∑ simp [hys, hyx.ne]
  ¬∑ simp [hz.2.ne', hz.1]

lemma bddAbove_slope_gt_of_mem_interior (hfc : ConvexOn ùïú s f) (hxs : x ‚àà interior s) :
    BddAbove (slope f x '' {y ‚àà s | y < x}) := by
  obtain ‚ü®y, hyx, hys‚ü© : ‚àÉ y, x < y ‚àß y ‚àà s :=
    Eventually.exists_gt (mem_interior_iff_mem_nhds.mp hxs)
  refine bddAbove_iff_subset_Iic.mpr ‚ü®slope f x y, fun y' ‚ü®z, hz, hz'‚ü© ‚Ü¶ ?_‚ü©
  simp_rw [mem_Iic, ‚Üê hz']
  refine hfc.slope_mono (interior_subset hxs) ?_ ?_ (hz.2.trans hyx).le
  ¬∑ simp [hz.2.ne, hz.1]
  ¬∑ simp [hys, hyx.ne']

end slope

namespace ConvexOn

variable {S : Set ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {x y f' : ‚Ñù}

section Interior

/-!
### Left and right derivative of a convex function in the interior of the set
-/

lemma hasDerivWithinAt_sInf_slope_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    HasDerivWithinAt f (sInf (slope f x '' {y ‚àà S | x < y})) (Ioi x) x := by
  have hxs' := hxs
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hxs'
  obtain ‚ü®a, b, hxab, habs‚ü© := hxs'
  simp_rw [hasDerivWithinAt_iff_tendsto_slope]
  simp only [mem_Ioi, lt_self_iff_false, not_false_eq_true, diff_singleton_eq_self]
  have h : Ioo x b ‚äÜ {y | y ‚àà S ‚àß x < y} := fun z hz ‚Ü¶ ‚ü®habs ‚ü®hxab.1.trans hz.1, hz.2‚ü©, hz.1‚ü©
  have h_Ioo : Tendsto (slope f x) (ùìù[>] x) (ùìù (sInf (slope f x '' Ioo x b))) :=
    ((monotoneOn_slope_gt hfc (habs hxab)).mono h).tendsto_nhdsWithin_Ioo_right
      (by simpa using hxab.2) ((bddBelow_slope_lt_of_mem_interior hfc hxs).mono (image_mono h))
  suffices sInf (slope f x '' Ioo x b) = sInf (slope f x '' {y ‚àà S | x < y}) by rwa [‚Üê this]
  apply (monotoneOn_slope_gt hfc (habs hxab)).csInf_eq_of_subset_of_forall_exists_le
    (bddBelow_slope_lt_of_mem_interior hfc hxs) h ?_
  rintro y ‚ü®hyS, hxy‚ü©
  obtain ‚ü®z, hxz, hzy‚ü© := exists_between (lt_min hxab.2 hxy)
  exact ‚ü®z, ‚ü®hxz, hzy.trans_le (min_le_left _ _)‚ü©, hzy.le.trans (min_le_right _ _)‚ü©

lemma hasDerivWithinAt_sSup_slope_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    HasDerivWithinAt f (sSup (slope f x '' {y ‚àà S | y < x})) (Iio x) x := by
  have hxs' := hxs
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hxs'
  obtain ‚ü®a, b, hxab, habs‚ü© := hxs'
  simp_rw [hasDerivWithinAt_iff_tendsto_slope]
  simp only [mem_Iio, lt_self_iff_false, not_false_eq_true, diff_singleton_eq_self]
  have h : Ioo a x ‚äÜ {y | y ‚àà S ‚àß y < x} := fun z hz ‚Ü¶ ‚ü®habs ‚ü®hz.1, hz.2.trans hxab.2‚ü©, hz.2‚ü©
  have h_Ioo : Tendsto (slope f x) (ùìù[<] x) (ùìù (sSup (slope f x '' Ioo a x))) :=
    ((monotoneOn_slope_lt hfc (habs hxab)).mono h).tendsto_nhdsWithin_Ioo_left
      (by simpa using hxab.1) ((bddAbove_slope_gt_of_mem_interior hfc hxs).mono (image_mono h))
  suffices sSup (slope f x '' Ioo a x) = sSup (slope f x '' {y ‚àà S | y < x}) by rwa [‚Üê this]
  apply (monotoneOn_slope_lt hfc (habs hxab)).csSup_eq_of_subset_of_forall_exists_le
    (bddAbove_slope_gt_of_mem_interior hfc hxs) h ?_
  rintro y ‚ü®hyS, hyx‚ü©
  obtain ‚ü®z, hyz, hzx‚ü© := exists_between (max_lt hxab.1 hyx)
  exact ‚ü®z, ‚ü®(le_max_left _ _).trans_lt hyz, hzx‚ü©, (le_max_right _ _).trans hyz.le‚ü©

lemma differentiableWithinAt_Ioi_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    DifferentiableWithinAt ‚Ñù f (Ioi x) x :=
  (hfc.hasDerivWithinAt_sInf_slope_of_mem_interior hxs).differentiableWithinAt

lemma differentiableWithinAt_Iio_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    DifferentiableWithinAt ‚Ñù f (Iio x) x :=
  (hfc.hasDerivWithinAt_sSup_slope_of_mem_interior hxs).differentiableWithinAt

lemma hasDerivWithinAt_rightDeriv_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    HasDerivWithinAt f (derivWithin f (Ioi x) x) (Ioi x) x :=
  (hfc.differentiableWithinAt_Ioi_of_mem_interior hxs).hasDerivWithinAt

lemma hasDerivWithinAt_leftDeriv_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    HasDerivWithinAt f (derivWithin f (Iio x) x) (Iio x) x :=
  (hfc.differentiableWithinAt_Iio_of_mem_interior hxs).hasDerivWithinAt

lemma rightDeriv_eq_sInf_slope_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    derivWithin f (Ioi x) x = sInf (slope f x '' {y | y ‚àà S ‚àß x < y}) :=
  (hfc.hasDerivWithinAt_sInf_slope_of_mem_interior hxs).derivWithin (uniqueDiffWithinAt_Ioi x)

lemma leftDeriv_eq_sSup_slope_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    derivWithin f (Iio x) x = sSup (slope f x '' {y | y ‚àà S ‚àß y < x}) :=
  (hfc.hasDerivWithinAt_sSup_slope_of_mem_interior hxs).derivWithin (uniqueDiffWithinAt_Iio x)

lemma monotoneOn_rightDeriv (hfc : ConvexOn ‚Ñù S f) :
    MonotoneOn (fun x ‚Ü¶ derivWithin f (Ioi x) x) (interior S) := by
  intro x hxs y hys hxy
  rcases eq_or_lt_of_le hxy with rfl | hxy; ¬∑ rfl
  simp_rw [hfc.rightDeriv_eq_sInf_slope_of_mem_interior hxs,
    hfc.rightDeriv_eq_sInf_slope_of_mem_interior hys]
  refine csInf_le_of_le (b := slope f x y) (bddBelow_slope_lt_of_mem_interior hfc hxs)
    ‚ü®y, by simp only [mem_setOf_eq, hxy, and_true]; exact interior_subset hys‚ü©
    (le_csInf ?_ ?_)
  ¬∑ have hys' := hys
    rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hys'
    obtain ‚ü®a, b, hxab, habs‚ü© := hys'
    rw [image_nonempty]
    obtain ‚ü®z, hxz, hzb‚ü© := exists_between hxab.2
    exact ‚ü®z, habs ‚ü®hxab.1.trans hxz, hzb‚ü©, hxz‚ü©
  ¬∑ rintro _ ‚ü®z, ‚ü®hzs, hyz : y < z‚ü©, rfl‚ü©
    rw [slope_comm]
    exact slope_mono hfc (interior_subset hys) ‚ü®interior_subset hxs, hxy.ne‚ü© ‚ü®hzs, hyz.ne'‚ü©
      (hxy.trans hyz).le

lemma monotoneOn_leftDeriv (hfc : ConvexOn ‚Ñù S f) :
    MonotoneOn (fun x ‚Ü¶ derivWithin f (Iio x) x) (interior S) := by
  intro x hxs y hys hxy
  rcases eq_or_lt_of_le hxy with rfl | hxy; ¬∑ rfl
  simp_rw [hfc.leftDeriv_eq_sSup_slope_of_mem_interior hxs,
    hfc.leftDeriv_eq_sSup_slope_of_mem_interior hys]
  refine le_csSup_of_le (b := slope f x y) (bddAbove_slope_gt_of_mem_interior hfc hys)
    ‚ü®x, by simp only [slope_comm, mem_setOf_eq, hxy, and_true]; exact interior_subset hxs‚ü©
    (csSup_le ?_ ?_)
  ¬∑ have hxs' := hxs
    rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hxs'
    obtain ‚ü®a, b, hxab, habs‚ü© := hxs'
    rw [image_nonempty]
    obtain ‚ü®z, hxz, hzb‚ü© := exists_between hxab.1
    exact ‚ü®z, habs ‚ü®hxz, hzb.trans hxab.2‚ü©, hzb‚ü©
  ¬∑ rintro _ ‚ü®z, ‚ü®hzs, hyz : z < x‚ü©, rfl‚ü©
    exact slope_mono hfc (interior_subset hxs) ‚ü®hzs, hyz.ne‚ü© ‚ü®interior_subset hys, hxy.ne'‚ü©
      (hyz.trans hxy).le

lemma leftDeriv_le_rightDeriv_of_mem_interior (hfc : ConvexOn ‚Ñù S f) (hxs : x ‚àà interior S) :
    derivWithin f (Iio x) x ‚â§ derivWithin f (Ioi x) x := by
  have hxs' := hxs
  rw [mem_interior_iff_mem_nhds, mem_nhds_iff_exists_Ioo_subset] at hxs'
  obtain ‚ü®a, b, hxab, habs‚ü© := hxs'
  rw [hfc.rightDeriv_eq_sInf_slope_of_mem_interior hxs,
    hfc.leftDeriv_eq_sSup_slope_of_mem_interior hxs]
  refine csSup_le ?_ ?_
  ¬∑ rw [image_nonempty]
    obtain ‚ü®z, haz, hzx‚ü© := exists_between hxab.1
    exact ‚ü®z, habs ‚ü®haz, hzx.trans hxab.2‚ü©, hzx‚ü©
  rintro _ ‚ü®z, ‚ü®hzs, hzx‚ü©, rfl‚ü©
  refine le_csInf ?_ ?_
  ¬∑ rw [image_nonempty]
    obtain ‚ü®z, hxz, hzb‚ü© := exists_between hxab.2
    exact ‚ü®z, habs ‚ü®hxab.1.trans hxz, hzb‚ü©, hxz‚ü©
  rintro _ ‚ü®y, ‚ü®hys, hxy‚ü©, rfl‚ü©
  exact slope_mono hfc (interior_subset hxs) ‚ü®hzs, hzx.ne‚ü© ‚ü®hys, hxy.ne'‚ü© (hzx.trans hxy).le

end Interior

section left
/-!
### Convex functions, derivative at left endpoint of secant
-/

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is convex on `S` and right-differentiable at `x ‚àà S`, then the slope of any
secant line with left endpoint at `x` is bounded below by the right derivative of `f` at `x`. -/
lemma le_slope_of_hasDerivWithinAt_Ioi (hfc : ConvexOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Ioi x) x) :
    f' ‚â§ slope f x y := by
  apply le_of_tendsto <| (hasDerivWithinAt_iff_tendsto_slope' notMem_Ioi_self).mp hf'
  simp_rw [eventually_nhdsWithin_iff, slope_def_field]
  filter_upwards [eventually_lt_nhds hxy] with t ht (ht' : x < t)
  refine hfc.secant_mono hx (?_ : t ‚àà S) hy ht'.ne' hxy.ne' ht.le
  exact hfc.1.ordConnected.out hx hy ‚ü®ht'.le, ht.le‚ü©

/-- Reformulation of `ConvexOn.le_slope_of_hasDerivWithinAt_Ioi` using `derivWithin`. -/
lemma rightDeriv_le_slope (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Ioi x) x) :
    derivWithin f (Ioi x) x ‚â§ slope f x y :=
  le_slope_of_hasDerivWithinAt_Ioi hfc hx hy hxy hfd.hasDerivWithinAt

lemma rightDeriv_le_slope_of_mem_interior (hfc : ConvexOn ‚Ñù S f)
    {y : ‚Ñù} (hxs : x ‚àà interior S) (hys : y ‚àà S) (hxy : x < y) :
    derivWithin f (Ioi x) x ‚â§ slope f x y :=
  rightDeriv_le_slope hfc (interior_subset hxs) hys hxy
    (differentiableWithinAt_Ioi_of_mem_interior hfc hxs)

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is convex on `S` and differentiable within `S` at `x`, then the slope of any
secant line with left endpoint at `x` is bounded below by the derivative of `f` within `S` at `x`.

This is fractionally weaker than `ConvexOn.le_slope_of_hasDerivWithinAt_Ioi` but simpler to apply
under a `DifferentiableOn S` hypothesis. -/
lemma le_slope_of_hasDerivWithinAt (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivWithinAt f f' S x) :
    f' ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivWithinAt_Ioi hx hy hxy <|
    hf'.mono_of_mem_nhdsWithin <| hfc.1.ordConnected.mem_nhdsGT hx hy hxy

/-- Reformulation of `ConvexOn.le_slope_of_hasDerivWithinAt` using `derivWithin`. -/
lemma derivWithin_le_slope (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S x) :
    derivWithin f S x ‚â§ slope f x y :=
  le_slope_of_hasDerivWithinAt hfc hx hy hxy hfd.hasDerivWithinAt

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is convex on `S` and differentiable at `x ‚àà S`, then the slope of any secant
line with left endpoint at `x` is bounded below by the derivative of `f` at `x`. -/
lemma le_slope_of_hasDerivAt (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (ha : HasDerivAt f f' x) :
    f' ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivWithinAt_Ioi hx hy hxy ha.hasDerivWithinAt

/-- Reformulation of `ConvexOn.le_slope_of_hasDerivAt` using `deriv` -/
lemma deriv_le_slope (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f x) :
    deriv f x ‚â§ slope f x y :=
  le_slope_of_hasDerivAt hfc hx hy hxy hfd.hasDerivAt

end left

section right
/-!
### Convex functions, derivative at right endpoint of secant
-/

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is convex on `S` and left-differentiable at `y ‚àà S`, then the slope of any secant
line with right endpoint at `y` is bounded above by the left derivative of `f` at `y`. -/
lemma slope_le_of_hasDerivWithinAt_Iio (hfc : ConvexOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Iio y) y) :
    slope f x y ‚â§ f' := by
  apply ge_of_tendsto <| (hasDerivWithinAt_iff_tendsto_slope' notMem_Iio_self).mp hf'
  simp_rw [eventually_nhdsWithin_iff, slope_comm f x y, slope_def_field]
  filter_upwards [eventually_gt_nhds hxy] with t ht (ht' : t < y)
  refine hfc.secant_mono hy hx (?_ : t ‚àà S) hxy.ne ht'.ne ht.le
  exact hfc.1.ordConnected.out hx hy ‚ü®ht.le, ht'.le‚ü©

/-- Reformulation of `ConvexOn.slope_le_of_hasDerivWithinAt_Iio` using `derivWithin`. -/
lemma slope_le_leftDeriv (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Iio y) y) :
    slope f x y ‚â§ derivWithin f (Iio y) y :=
  hfc.slope_le_of_hasDerivWithinAt_Iio hx hy hxy hfd.hasDerivWithinAt

lemma slope_le_leftDeriv_of_mem_interior (hfc : ConvexOn ‚Ñù S f)
    (hys : x ‚àà S) (hxs : y ‚àà interior S) (hxy : x < y) :
    slope f x y ‚â§ derivWithin f (Iio y) y :=
  slope_le_leftDeriv hfc hys (interior_subset hxs) hxy
    (differentiableWithinAt_Iio_of_mem_interior hfc hxs)

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is convex on `S` and differentiable within `S` at `y`, then the slope of any
secant line with right endpoint at `y` is bounded above by the derivative of `f` within `S` at `y`.

This is fractionally weaker than `ConvexOn.slope_le_of_hasDerivWithinAt_Iio` but simpler to apply
under a `DifferentiableOn S` hypothesis. -/
lemma slope_le_of_hasDerivWithinAt (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivWithinAt f f' S y) :
    slope f x y ‚â§ f' :=
  hfc.slope_le_of_hasDerivWithinAt_Iio hx hy hxy <|
    hf'.mono_of_mem_nhdsWithin <| hfc.1.ordConnected.mem_nhdsLT hx hy hxy

/-- Reformulation of `ConvexOn.slope_le_of_hasDerivWithinAt` using `derivWithin`. -/
lemma slope_le_derivWithin (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S y) :
    slope f x y ‚â§ derivWithin f S y :=
  hfc.slope_le_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is convex on `S` and differentiable at `y ‚àà S`, then the slope of any secant
line with right endpoint at `y` is bounded above by the derivative of `f` at `y`. -/
lemma slope_le_of_hasDerivAt (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivAt f f' y) :
    slope f x y ‚â§ f' :=
  hfc.slope_le_of_hasDerivWithinAt_Iio hx hy hxy hf'.hasDerivWithinAt

/-- Reformulation of `ConvexOn.slope_le_of_hasDerivAt` using `deriv`. -/
lemma slope_le_deriv (hfc : ConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f y) :
    slope f x y ‚â§ deriv f y :=
  hfc.slope_le_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end right
/-!
### Convex functions, monotonicity of derivative
-/

/-- If `f` is convex on `S` and differentiable on `S`, then its derivative within `S` is monotone
on `S`. -/
lemma monotoneOn_derivWithin (hfc : ConvexOn ‚Ñù S f) (hfd : DifferentiableOn ‚Ñù f S) :
    MonotoneOn (derivWithin f S) S := by
  intro x hx y hy hxy
  rcases eq_or_lt_of_le hxy with rfl | hxy'
  ¬∑ rfl
  exact (hfc.derivWithin_le_slope hx hy hxy' (hfd x hx)).trans
    (hfc.slope_le_derivWithin hx hy hxy' (hfd y hy))

/-- If `f` is convex on `S` and differentiable at all points of `S`, then its derivative is monotone
on `S`. -/
theorem monotoneOn_deriv (hfc : ConvexOn ‚Ñù S f) (hfd : ‚àÄ x ‚àà S, DifferentiableAt ‚Ñù f x) :
    MonotoneOn (deriv f) S := by
  intro x hx y hy hxy
  rcases eq_or_lt_of_le hxy with rfl | hxy'
  ¬∑ rfl
  exact (hfc.deriv_le_slope hx hy hxy' (hfd x hx)).trans (hfc.slope_le_deriv hx hy hxy' (hfd y hy))

lemma isMinOn_of_leftDeriv_nonpos_of_rightDeriv_nonneg (hf : ConvexOn ‚Ñù S f) (hx : x ‚àà interior S)
    (hf_ld : derivWithin f (Iio x) x ‚â§ 0) (hf_rd : 0 ‚â§ derivWithin f (Ioi x) x) :
    IsMinOn f S x := by
  intro y hy
  rcases lt_trichotomy x y with hxy | h_eq | hyx
  ¬∑ suffices 0 ‚â§ slope f x y by
      simp only [slope_def_field, div_nonneg_iff, sub_nonneg, tsub_le_iff_right, zero_add,
        not_le.mpr hxy, and_false, or_false] at this
      exact this.1
    exact hf_rd.trans <| rightDeriv_le_slope_of_mem_interior hf hx hy hxy
  ¬∑ simp [h_eq]
  ¬∑ suffices slope f x y ‚â§ 0 by
      simp only [slope_def_field, div_nonpos_iff, sub_nonneg, tsub_le_iff_right, zero_add,
        not_le.mpr hyx, and_false, or_false] at this
      exact this.1
    rw [slope_comm]
    exact (slope_le_leftDeriv_of_mem_interior hf hy hx hyx).trans hf_ld

lemma isMinOn_of_rightDeriv_eq_zero (hf : ConvexOn ‚Ñù S f) (hx : x ‚àà interior S)
    (hf_rd : derivWithin f (Ioi x) x = 0) :
    IsMinOn f S x := by
  refine hf.isMinOn_of_leftDeriv_nonpos_of_rightDeriv_nonneg hx ?_ hf_rd.symm.le
  exact (hf.leftDeriv_le_rightDeriv_of_mem_interior hx).trans_eq hf_rd

lemma isMinOn_of_leftDeriv_eq_zero (hf : ConvexOn ‚Ñù S f) (hx : x ‚àà interior S)
    (hf_ld : derivWithin f (Iio x) x = 0) :
    IsMinOn f S x := by
  refine hf.isMinOn_of_leftDeriv_nonpos_of_rightDeriv_nonneg hx hf_ld.le ?_
  exact hf_ld.symm.le.trans (hf.leftDeriv_le_rightDeriv_of_mem_interior hx)

end ConvexOn

namespace StrictConvexOn

variable {S : Set ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {x y f' : ‚Ñù}

section left
/-!
### Strict convex functions, derivative at left endpoint of secant
-/

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is strictly convex on `S` and right-differentiable at `x ‚àà S`, then the slope of
any secant line with left endpoint at `x` is strictly greater than the right derivative of `f` at
`x`. -/
lemma lt_slope_of_hasDerivWithinAt_Ioi (hfc : StrictConvexOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Ioi x) x) :
    f' < slope f x y := by
  obtain ‚ü®u, hxu, huy‚ü© := exists_between hxy
  have hu : u ‚àà S := hfc.1.ordConnected.out hx hy ‚ü®hxu.le, huy.le‚ü©
  have := hfc.secant_strict_mono hx hu hy hxu.ne' hxy.ne' huy
  simp only [‚Üê slope_def_field] at this
  exact (hfc.convexOn.le_slope_of_hasDerivWithinAt_Ioi hx hu hxu hf').trans_lt this

lemma rightDeriv_lt_slope (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Ioi x) x) :
    derivWithin f (Ioi x) x < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt_Ioi hx hy hxy hfd.hasDerivWithinAt

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is strictly convex on `S` and differentiable within `S` at `x ‚àà S`, then the
slope of any secant line with left endpoint at `x` is strictly greater than the derivative of `f`
within `S` at `x`.

This is fractionally weaker than `StrictConvexOn.lt_slope_of_hasDerivWithinAt_Ioi` but simpler to
apply under a `DifferentiableOn S` hypothesis. -/
lemma lt_slope_of_hasDerivWithinAt (hfc : StrictConvexOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' S x) :
    f' < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt_Ioi hx hy hxy <|
    hf'.mono_of_mem_nhdsWithin <| hfc.1.ordConnected.mem_nhdsGT hx hy hxy

lemma derivWithin_lt_slope (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S x) :
    derivWithin f S x < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is strictly convex on `S` and differentiable at `x ‚àà S`, then the slope of any
secant line with left endpoint at `x` is strictly greater than the derivative of `f` at `x`. -/
lemma lt_slope_of_hasDerivAt (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivAt f f' x) :
    f' < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt_Ioi hx hy hxy hf'.hasDerivWithinAt

lemma deriv_lt_slope (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f x) :
    deriv f x < slope f x y :=
  hfc.lt_slope_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end left

section right
/-!
### Strict convex functions, derivative at right endpoint of secant
-/

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is strictly convex on `S` and differentiable at `y ‚àà S`, then the slope of any
secant line with right endpoint at `y` is strictly less than the left derivative at `y`. -/
lemma slope_lt_of_hasDerivWithinAt_Iio (hfc : StrictConvexOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Iio y) y) :
    slope f x y < f' := by
  obtain ‚ü®u, hxu, huy‚ü© := exists_between hxy
  have hu : u ‚àà S := hfc.1.ordConnected.out hx hy ‚ü®hxu.le, huy.le‚ü©
  have := hfc.secant_strict_mono hy hx hu hxy.ne huy.ne hxu
  simp_rw [‚Üê slope_def_field, slope_comm _ y] at this
  exact this.trans_le <| hfc.convexOn.slope_le_of_hasDerivWithinAt_Iio hu hy huy hf'

lemma slope_lt_leftDeriv (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Iio y) y) :
    slope f x y < derivWithin f (Iio y) y :=
  hfc.slope_lt_of_hasDerivWithinAt_Iio hx hy hxy hfd.hasDerivWithinAt

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is strictly convex on `S` and differentiable within `S` at `y ‚àà S`, then the
slope of any secant line with right endpoint at `y` is strictly less than the derivative of `f`
within `S` at `y`.

This is fractionally weaker than `StrictConvexOn.slope_lt_of_hasDerivWithinAt_Iio` but simpler to
apply under a `DifferentiableOn S` hypothesis. -/
lemma slope_lt_of_hasDerivWithinAt (hfc : StrictConvexOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' S y) :
    slope f x y < f' :=
  hfc.slope_lt_of_hasDerivWithinAt_Iio hx hy hxy <|
    hf'.mono_of_mem_nhdsWithin <| hfc.1.ordConnected.mem_nhdsLT hx hy hxy

lemma slope_lt_derivWithin (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S y) :
    slope f x y < derivWithin f S y :=
  hfc.slope_lt_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

/-- If `f : ‚Ñù ‚Üí ‚Ñù` is strictly convex on `S` and differentiable at `y ‚àà S`, then the slope of any
secant line with right endpoint at `y` is strictly less than the derivative of `f` at `y`. -/
lemma slope_lt_of_hasDerivAt (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivAt f f' y) :
    slope f x y < f' :=
  hfc.slope_lt_of_hasDerivWithinAt_Iio hx hy hxy hf'.hasDerivWithinAt

lemma slope_lt_deriv (hfc : StrictConvexOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f y) :
    slope f x y < deriv f y :=
  hfc.slope_lt_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end right

/-!
### Strict convex functions, strict monotonicity of derivative
-/

/-- If `f` is convex on `S` and differentiable on `S`, then its derivative within `S` is monotone
on `S`. -/
lemma strictMonoOn_derivWithin (hfc : StrictConvexOn ‚Ñù S f) (hfd : DifferentiableOn ‚Ñù f S) :
    StrictMonoOn (derivWithin f S) S := by
  intro x hx y hy hxy
  exact (hfc.derivWithin_lt_slope hx hy hxy (hfd x hx)).trans
    (hfc.slope_lt_derivWithin hx hy hxy (hfd y hy))

/-- If `f` is convex on `S` and differentiable at all points of `S`, then its derivative is monotone
on `S`. -/
lemma strictMonoOn_deriv (hfc : StrictConvexOn ‚Ñù S f) (hfd : ‚àÄ x ‚àà S, DifferentiableAt ‚Ñù f x) :
    StrictMonoOn (deriv f) S := by
  intro x hx y hy hxy
  exact (hfc.deriv_lt_slope hx hy hxy (hfd x hx)).trans (hfc.slope_lt_deriv hx hy hxy (hfd y hy))

end StrictConvexOn

section MirrorImage

variable {S : Set ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {x y f' : ‚Ñù}

namespace ConcaveOn

section left
/-!
### Concave functions, derivative at left endpoint of secant
-/

lemma slope_le_of_hasDerivWithinAt_Ioi (hfc : ConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Ioi x) x) :
    slope f x y ‚â§ f' := by
  simpa only [Pi.neg_def, slope_neg, neg_neg] using
    neg_le_neg (hfc.neg.le_slope_of_hasDerivWithinAt_Ioi hx hy hxy hf'.neg)

lemma slope_le_rightDeriv (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Ioi x) x) :
    slope f x y ‚â§ derivWithin f (Ioi x) x :=
  hfc.slope_le_of_hasDerivWithinAt_Ioi hx hy hxy hfd.hasDerivWithinAt

lemma slope_le_of_hasDerivWithinAt (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : HasDerivWithinAt f f' S x) :
    slope f x y ‚â§ f' :=
  hfc.slope_le_of_hasDerivWithinAt_Ioi hx hy hxy <|
    hfd.mono_of_mem_nhdsWithin <| hfc.1.ordConnected.mem_nhdsGT hx hy hxy

lemma slope_le_derivWithin (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S x) :
    slope f x y ‚â§ derivWithin f S x :=
  hfc.slope_le_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

lemma slope_le_of_hasDerivAt (hfc : ConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivAt f f' x) :
    slope f x y ‚â§ f' :=
  hfc.slope_le_of_hasDerivWithinAt_Ioi hx hy hxy hf'.hasDerivWithinAt

lemma slope_le_deriv (hfc : ConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hfd : DifferentiableAt ‚Ñù f x) :
    slope f x y ‚â§ deriv f x :=
  hfc.slope_le_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end left

section right
/-!
### Concave functions, derivative at right endpoint of secant
-/

lemma le_slope_of_hasDerivWithinAt_Iio (hfc : ConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Iio y) y) :
    f' ‚â§ slope f x y := by
  simpa only [neg_neg, Pi.neg_def, slope_neg] using
    neg_le_neg (hfc.neg.slope_le_of_hasDerivWithinAt_Iio hx hy hxy hf'.neg)

lemma leftDeriv_le_slope (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Iio y) y) :
    derivWithin f (Iio y) y ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivWithinAt_Iio hx hy hxy hfd.hasDerivWithinAt

lemma le_slope_of_hasDerivWithinAt (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivWithinAt f f' S y) :
    f' ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivWithinAt_Iio hx hy hxy <|
    hf'.mono_of_mem_nhdsWithin <| hfc.1.ordConnected.mem_nhdsLT hx hy hxy

lemma derivWithin_le_slope (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S y) :
    derivWithin f S y ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

lemma le_slope_of_hasDerivAt (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivAt f f' y) :
    f' ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivWithinAt_Iio hx hy hxy hf'.hasDerivWithinAt

lemma deriv_le_slope (hfc : ConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f y) :
    deriv f y ‚â§ slope f x y :=
  hfc.le_slope_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end right
/-!
### Concave functions, anti-monotonicity of derivative
-/

lemma antitoneOn_derivWithin (hfc : ConcaveOn ‚Ñù S f) (hfd : DifferentiableOn ‚Ñù f S) :
    AntitoneOn (derivWithin f S) S := by
  intro x hx y hy hxy
  rcases eq_or_lt_of_le hxy with rfl | hxy'
  ¬∑ rfl
  exact (hfc.derivWithin_le_slope hx hy hxy' (hfd y hy)).trans
    (hfc.slope_le_derivWithin hx hy hxy' (hfd x hx))

/-- If `f` is concave on `S` and differentiable at all points of `S`, then its derivative is
antitone (monotone decreasing) on `S`. -/
theorem antitoneOn_deriv (hfc : ConcaveOn ‚Ñù S f) (hfd : ‚àÄ x ‚àà S, DifferentiableAt ‚Ñù f x) :
    AntitoneOn (deriv f) S := by
  simpa using (hfc.neg.monotoneOn_deriv (fun x hx ‚Ü¶ (hfd x hx).neg)).neg

end ConcaveOn

namespace StrictConcaveOn

section left
/-!
### Strict concave functions, derivative at left endpoint of secant
-/

lemma slope_lt_of_hasDerivWithinAt_Ioi (hfc : StrictConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Ioi x) x) :
    slope f x y < f' := by
  simpa only [Pi.neg_def, slope_neg, neg_neg] using
    neg_lt_neg (hfc.neg.lt_slope_of_hasDerivWithinAt_Ioi hx hy hxy hf'.neg)

lemma slope_lt_rightDeriv (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Ioi x) x) :
    slope f x y < derivWithin f (Ioi x) x :=
  hfc.slope_lt_of_hasDerivWithinAt_Ioi hx hy hxy hfd.hasDerivWithinAt

lemma slope_lt_of_hasDerivWithinAt (hfc : StrictConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hfd : HasDerivWithinAt f f' S x) :
    slope f x y < f' := by
  simpa only [Pi.neg_def, slope_neg, neg_neg] using
    neg_lt_neg (hfc.neg.lt_slope_of_hasDerivWithinAt hx hy hxy hfd.neg)

lemma slope_lt_derivWithin (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S x) :
    slope f x y < derivWithin f S x :=
  hfc.slope_lt_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

lemma slope_lt_of_hasDerivAt (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : HasDerivAt f f' x) :
    slope f x y < f' := by
  simpa only [Pi.neg_def, slope_neg, neg_neg] using
    neg_lt_neg (hfc.neg.lt_slope_of_hasDerivAt hx hy hxy hfd.neg)

lemma slope_lt_deriv (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f x) :
    slope f x y < deriv f x :=
  hfc.slope_lt_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end left

section right
/-!
### Strict concave functions, derivative at right endpoint of secant
-/

lemma lt_slope_of_hasDerivWithinAt_Iio (hfc : StrictConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' (Iio y) y) :
    f' < slope f x y := by
  simpa only [Pi.neg_def, slope_neg, neg_neg] using
    neg_lt_neg (hfc.neg.slope_lt_of_hasDerivWithinAt_Iio hx hy hxy hf'.neg)

lemma leftDeriv_lt_slope (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f (Iio y) y) :
    derivWithin f (Iio y) y < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt_Iio hx hy hxy hfd.hasDerivWithinAt

lemma lt_slope_of_hasDerivWithinAt (hfc : StrictConcaveOn ‚Ñù S f)
    (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y) (hf' : HasDerivWithinAt f f' S y) :
    f' < slope f x y := by
  simpa only [neg_neg, Pi.neg_def, slope_neg] using
    neg_lt_neg (hfc.neg.slope_lt_of_hasDerivWithinAt hx hy hxy hf'.neg)

lemma derivWithin_lt_slope (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableWithinAt ‚Ñù f S y) :
    derivWithin f S y < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt hx hy hxy hfd.hasDerivWithinAt

lemma lt_slope_of_hasDerivAt (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hf' : HasDerivAt f f' y) :
    f' < slope f x y :=
  hfc.lt_slope_of_hasDerivWithinAt_Iio hx hy hxy hf'.hasDerivWithinAt

lemma deriv_lt_slope (hfc : StrictConcaveOn ‚Ñù S f) (hx : x ‚àà S) (hy : y ‚àà S) (hxy : x < y)
    (hfd : DifferentiableAt ‚Ñù f y) :
    deriv f y < slope f x y :=
  hfc.lt_slope_of_hasDerivAt hx hy hxy hfd.hasDerivAt

end right
/-!
### Strict concave functions, anti-monotonicity of derivative
-/

lemma strictAntiOn_derivWithin (hfc : StrictConcaveOn ‚Ñù S f) (hfd : DifferentiableOn ‚Ñù f S) :
    StrictAntiOn (derivWithin f S) S := by
  intro x hx y hy hxy
  exact (hfc.derivWithin_lt_slope hx hy hxy (hfd y hy)).trans
    (hfc.slope_lt_derivWithin hx hy hxy (hfd x hx))

theorem strictAntiOn_deriv (hfc : StrictConcaveOn ‚Ñù S f) (hfd : ‚àÄ x ‚àà S, DifferentiableAt ‚Ñù f x) :
    StrictAntiOn (deriv f) S := by
  simpa using (hfc.neg.strictMonoOn_deriv (fun x hx ‚Ü¶ (hfd x hx).neg)).neg

end StrictConcaveOn

end MirrorImage
