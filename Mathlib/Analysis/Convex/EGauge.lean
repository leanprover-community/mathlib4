/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Seminorm
import Mathlib.GroupTheory.GroupAction.Pointwise

/-!
# The Minkowski functional, normed field version

In this file we define `(egauge ğ•œ s Â·)`
to be the Minkowski functional (gauge) of the set `s`
in a topological vector space `E` over a normed field `ğ•œ`,
as a function `E â†’ â„â‰¥0âˆ`.

It is defined as the infimum of the norms of `c : ğ•œ` such that `x âˆˆ c â€¢ s`.
In particular, for `ğ•œ = â„â‰¥0` this definition gives an `â„â‰¥0âˆ`-valued version of `gauge`
defined in `Mathlib/Analysis/Convex/Gauge.lean`.

This definition can be used to generalize the notion of FrÃ©chet derivative
to maps between topological vector spaces without norms.

Currently, we can't reuse results about `egauge` for `gauge`,
because we lack a theory of normed semifields.
-/

open Function Set Filter Metric
open scoped Topology Pointwise ENNReal NNReal

section SMul

/-- The Minkowski functional for vector spaces over normed fields.
Given a set `s` in a vector space over a normed field `ğ•œ`,
`egauge s` is the functional which sends `x : E`
to the infimum of `â€–câ€–â‚‘` over `c` such that `x` belongs to `s` scaled by `c`.

The definition only requires `ğ•œ` to have a `ENorm` instance
and `(Â· â€¢ Â·) : ğ•œ â†’ E â†’ E` to be defined.
This way the definition applies, e.g., to `ğ•œ = â„â‰¥0`.
For `ğ•œ = â„â‰¥0`, the function is equal (up to conversion to `â„`)
to the usual Minkowski functional defined in `gauge`. -/
noncomputable def egauge (ğ•œ : Type*) [ENorm ğ•œ] {E : Type*} [SMul ğ•œ E] (s : Set E) (x : E) : â„â‰¥0âˆ :=
  â¨… (c : ğ•œ) (_ : x âˆˆ c â€¢ s), â€–câ€–â‚‘

variable (ğ•œ : Type*) [NNNorm ğ•œ] {E : Type*} [SMul ğ•œ E] {c : ğ•œ} {s t : Set E} {x : E} {r : â„â‰¥0âˆ}

lemma Set.MapsTo.egauge_le {E' F : Type*} [SMul ğ•œ E'] [FunLike F E E'] [MulActionHomClass F ğ•œ E E']
    (f : F) {t : Set E'} (h : MapsTo f s t) (x : E) : egauge ğ•œ t (f x) â‰¤ egauge ğ•œ s x :=
  iInf_mono fun c â†¦ iInf_mono' fun hc â†¦ âŸ¨h.smul_set c hc, le_rflâŸ©

@[mono, gcongr]
lemma egauge_anti (h : s âŠ† t) (x : E) : egauge ğ•œ t x â‰¤ egauge ğ•œ s x :=
  MapsTo.egauge_le _ (MulActionHom.id ..) h _

@[simp] lemma egauge_empty (x : E) : egauge ğ•œ âˆ… x = âˆ := by simp [egauge]

variable {ğ•œ}

lemma egauge_le_of_mem_smul (h : x âˆˆ c â€¢ s) : egauge ğ•œ s x â‰¤ â€–câ€–â‚‘ := iInfâ‚‚_le c h

lemma le_egauge_iff : r â‰¤ egauge ğ•œ s x â†” âˆ€ c : ğ•œ, x âˆˆ c â€¢ s â†’ r â‰¤ â€–câ€–â‚‘ := le_iInfâ‚‚_iff

lemma egauge_eq_top : egauge ğ•œ s x = âˆ â†” âˆ€ c : ğ•œ, x âˆ‰ c â€¢ s := by simp [egauge]

lemma egauge_lt_iff : egauge ğ•œ s x < r â†” âˆƒ c : ğ•œ, x âˆˆ c â€¢ s âˆ§ â€–câ€–â‚‘ < r := by
  simp [egauge, iInf_lt_iff]

lemma egauge_union (s t : Set E) (x : E) : egauge ğ•œ (s âˆª t) x = egauge ğ•œ s x âŠ“ egauge ğ•œ t x := by
  unfold egauge
  simp [smul_set_union, iInf_or, iInf_inf_eq]

lemma le_egauge_inter (s t : Set E) (x : E) :
    egauge ğ•œ s x âŠ” egauge ğ•œ t x â‰¤ egauge ğ•œ (s âˆ© t) x :=
  max_le (egauge_anti _ inter_subset_left _) (egauge_anti _ inter_subset_right _)

lemma le_egauge_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, SMul ğ•œ (E i)] {I : Set Î¹} {i : Î¹}
    (hi : i âˆˆ I) (s : âˆ€ i, Set (E i)) (x : âˆ€ i, E i) :
    egauge ğ•œ (s i) (x i) â‰¤ egauge ğ•œ (I.pi s) x :=
  MapsTo.egauge_le _ (Pi.evalMulActionHom i) (fun x hx â†¦ by exact hx i hi) _

variable {F : Type*} [SMul ğ•œ F]

lemma le_egauge_prod (s : Set E) (t : Set F) (a : E) (b : F) :
    max (egauge ğ•œ s a) (egauge ğ•œ t b) â‰¤ egauge ğ•œ (s Ã—Ë¢ t) (a, b) :=
  max_le (mapsTo_fst_prod.egauge_le ğ•œ (MulActionHom.fst ğ•œ E F) (a, b))
    (MapsTo.egauge_le ğ•œ (MulActionHom.snd ğ•œ E F) mapsTo_snd_prod (a, b))

end SMul

section SMulZero

variable (ğ•œ : Type*) [NNNorm ğ•œ] [Nonempty ğ•œ] {E : Type*} [Zero E] [SMulZeroClass ğ•œ E] {x : E}

@[simp] lemma egauge_zero_left_eq_top : egauge ğ•œ 0 x = âˆ â†” x â‰  0 := by
  simp [egauge_eq_top]

@[simp] alias âŸ¨_, egauge_zero_leftâŸ© := egauge_zero_left_eq_top

end SMulZero

section NormedDivisionRing

variable {ğ•œ : Type*} [NormedDivisionRing ğ•œ] {E : Type*} [AddCommGroup E] [Module ğ•œ E]
    {c : ğ•œ} {s : Set E} {x : E}

/-- If `c â€¢ x âˆˆ s` and `c â‰  0`, then `egauge ğ•œ s x` is at most `(â€–câ€–â‚Šâ»Â¹ : â„â‰¥0)`.

See also `egauge_le_of_smul_mem`. -/
lemma egauge_le_of_smul_mem_of_ne (h : c â€¢ x âˆˆ s) (hc : c â‰  0) : egauge ğ•œ s x â‰¤ (â€–câ€–â‚Šâ»Â¹ : â„â‰¥0) := by
  rw [â† nnnorm_inv]
  exact egauge_le_of_mem_smul <| (mem_inv_smul_set_iffâ‚€ hc _ _).2 h

/-- If `c â€¢ x âˆˆ s`, then `egauge ğ•œ s x` is at most `â€–câ€–â‚‘â»Â¹`.

See also `egauge_le_of_smul_mem_of_ne`. -/
lemma egauge_le_of_smul_mem (h : c â€¢ x âˆˆ s) : egauge ğ•œ s x â‰¤ â€–câ€–â‚‘â»Â¹ := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· exact (egauge_le_of_smul_mem_of_ne h hc).trans ENNReal.coe_inv_le

lemma mem_smul_of_egauge_lt (hs : Balanced ğ•œ s) (hc : egauge ğ•œ s x < â€–câ€–â‚‘) : x âˆˆ c â€¢ s :=
  let âŸ¨a, hxa, haâŸ© := egauge_lt_iff.1 hc
  hs.smul_mono (by simpa [enorm] using ha.le) hxa

lemma mem_of_egauge_lt_one (hs : Balanced ğ•œ s) (hx : egauge ğ•œ s x < 1) : x âˆˆ s :=
  one_smul ğ•œ s â–¸ mem_smul_of_egauge_lt hs (by simpa)

lemma egauge_eq_zero_iff : egauge ğ•œ s x = 0 â†” âˆƒá¶  c : ğ•œ in ğ“ 0, x âˆˆ c â€¢ s := by
  refine (iInfâ‚‚_eq_bot _).trans ?_
  rw [(nhds_basis_uniformity uniformity_basis_edist).frequently_iff]
  simp [and_comm]

@[simp]
lemma egauge_univ [(ğ“[â‰ ] (0 : ğ•œ)).NeBot] : egauge ğ•œ univ x = 0 := by
  rw [egauge_eq_zero_iff]
  refine (frequently_iff_neBot.2 â€¹_â€º).mono fun c hc â†¦ ?_
  simp_all [smul_set_univâ‚€]

variable (ğ•œ)

@[simp]
lemma egauge_zero_right (hs : s.Nonempty) : egauge ğ•œ s 0 = 0 := by
  have : 0 âˆˆ (0 : ğ•œ) â€¢ s := by simp [zero_smul_set hs]
  simpa using egauge_le_of_mem_smul this

lemma egauge_zero_zero : egauge ğ•œ (0 : Set E) 0 = 0 := by simp

lemma egauge_le_one (h : x âˆˆ s) : egauge ğ•œ s x â‰¤ 1 := by
  rw [â† one_smul ğ•œ s] at h
  simpa using egauge_le_of_mem_smul h

variable {ğ•œ}

lemma le_egauge_smul_left (c : ğ•œ) (s : Set E) (x : E) :
    egauge ğ•œ s x / â€–câ€–â‚‘ â‰¤ egauge ğ•œ (c â€¢ s) x := by
  simp_rw [le_egauge_iff, smul_smul]
  rintro a âŸ¨x, hx, rflâŸ©
  apply ENNReal.div_le_of_le_mul
  rw [â† enorm_mul]
  exact egauge_le_of_mem_smul <| smul_mem_smul_set hx

lemma egauge_smul_left (hc : c â‰  0) (s : Set E) (x : E) :
    egauge ğ•œ (c â€¢ s) x = egauge ğ•œ s x / â€–câ€–â‚‘ := by
  refine le_antisymm ?_ (le_egauge_smul_left _ _ _)
  rw [ENNReal.le_div_iff_mul_le (by simp [*]) (by simp)]
  calc
    egauge ğ•œ (c â€¢ s) x * â€–câ€–â‚‘ = egauge ğ•œ (c â€¢ s) x / â€–câ»Â¹â€–â‚‘ := by
      rw [enorm_inv (by simpa), div_eq_mul_inv, inv_inv]
    _ â‰¤ egauge ğ•œ (câ»Â¹ â€¢ c â€¢ s) x := le_egauge_smul_left _ _ _
    _ = egauge ğ•œ s x := by rw [inv_smul_smulâ‚€ hc]

lemma le_egauge_smul_right (c : ğ•œ) (s : Set E) (x : E) :
    â€–câ€–â‚‘ * egauge ğ•œ s x â‰¤ egauge ğ•œ s (c â€¢ x) := by
  rw [le_egauge_iff]
  rintro a âŸ¨y, hy, hxyâŸ©
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· refine ENNReal.mul_le_of_le_div' <| le_trans ?_ ENNReal.coe_div_le
    rw [div_eq_inv_mul, â† nnnorm_inv, â† nnnorm_mul]
    refine egauge_le_of_mem_smul âŸ¨y, hy, ?_âŸ©
    simp only [mul_smul, hxy, inv_smul_smulâ‚€ hc]

lemma egauge_smul_right (h : c = 0 â†’ s.Nonempty) (x : E) :
    egauge ğ•œ s (c â€¢ x) = â€–câ€–â‚‘ * egauge ğ•œ s x := by
  refine le_antisymm ?_ (le_egauge_smul_right c s x)
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp [egauge_zero_right _ (h rfl)]
  Â· rw [mul_comm, â† ENNReal.div_le_iff_le_mul (.inl <| by simpa) (.inl enorm_ne_top),
      ENNReal.div_eq_inv_mul, â† enorm_inv (by simpa)]
    refine (le_egauge_smul_right _ _ _).trans_eq ?_
    rw [inv_smul_smulâ‚€ hc]

/-- The extended gauge of a point `(a, b)` with respect to the product of balanced sets `U` and `V`
is equal to the maximum of the extended gauges of `a` with respect to `U`
and `b` with respect to `V`.
-/
theorem egauge_prod_mk {F : Type*} [AddCommGroup F] [Module ğ•œ F] {U : Set E} {V : Set F}
    (hU : Balanced ğ•œ U) (hV : Balanced ğ•œ V) (a : E) (b : F) :
    egauge ğ•œ (U Ã—Ë¢ V) (a, b) = max (egauge ğ•œ U a) (egauge ğ•œ V b) := by
  refine le_antisymm (le_of_forall_gt fun r hr â†¦ ?_) (le_egauge_prod _ _ _ _)
  simp only [max_lt_iff, egauge_lt_iff, smul_set_prod, mk_mem_prod] at hr âŠ¢
  rcases hr with âŸ¨âŸ¨x, hx, hxrâŸ©, âŸ¨y, hy, hyrâŸ©âŸ©
  cases le_total â€–xâ€– â€–yâ€– with
  | inl hle => exact âŸ¨y, âŸ¨hU.smul_mono hle hx, hyâŸ©, hyrâŸ©
  | inr hle => exact âŸ¨x, âŸ¨hx, hV.smul_mono hle hyâŸ©, hxrâŸ©

theorem egauge_add_add_le {U V : Set E} (hU : Balanced ğ•œ U) (hV : Balanced ğ•œ V) (a b : E) :
    egauge ğ•œ (U + V) (a + b) â‰¤ max (egauge ğ•œ U a) (egauge ğ•œ V b) := by
  rw [â† egauge_prod_mk hU hV a b, â† add_image_prod]
  exact MapsTo.egauge_le ğ•œ (LinearMap.fst ğ•œ E E + LinearMap.snd ğ•œ E E) (mapsTo_image _ _) (a, b)

end NormedDivisionRing

section Pi

variable {ğ•œ : Type*} {Î¹ : Type*} {E : Î¹ â†’ Type*}
variable [NormedDivisionRing ğ•œ] [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]

/-- The extended gauge of a point `x` in an indexed product
with respect to a product of finitely many balanced sets `U i`, `i âˆˆ I`,
(and the whole spaces for the other indices)
is the supremum of the extended gauges of the components of `x`
with respect to the corresponding balanced set.

This version assumes the following technical condition:
- either `I` is the universal set;
- or one of `x i`, `i âˆˆ I`, is nonzero;
- or `ğ•œ` is nontrivially normed.
-/
theorem egauge_pi' {I : Set Î¹} (hI : I.Finite)
    {U : âˆ€ i, Set (E i)} (hU : âˆ€ i âˆˆ I, Balanced ğ•œ (U i))
    (x : âˆ€ i, E i) (hIâ‚€ : I = univ âˆ¨ (âˆƒ i âˆˆ I, x i â‰  0) âˆ¨ (ğ“[â‰ ] (0 : ğ•œ)).NeBot) :
    egauge ğ•œ (I.pi U) x = â¨† i âˆˆ I, egauge ğ•œ (U i) (x i) := by
  refine le_antisymm ?_ (iSupâ‚‚_le fun i hi â†¦ le_egauge_pi hi _ _)
  refine le_of_forall_gt fun r hr â†¦ ?_
  have : âˆ€ i âˆˆ I, âˆƒ c : ğ•œ, x i âˆˆ c â€¢ U i âˆ§ â€–câ€–â‚‘ < r := fun i hi â†¦
    egauge_lt_iff.mp <| (le_iSupâ‚‚ i hi).trans_lt hr
  choose! c hc hcr using this
  obtain âŸ¨câ‚€, hcâ‚€, hcâ‚€I, hcâ‚€râŸ© :
      âˆƒ câ‚€ : ğ•œ, (câ‚€ â‰  0 âˆ¨ I = univ) âˆ§ (âˆ€ i âˆˆ I, â€–c iâ€– â‰¤ â€–câ‚€â€–) âˆ§ â€–câ‚€â€–â‚‘ < r := by
    have hrâ‚€ : 0 < r := hr.bot_lt
    rcases I.eq_empty_or_nonempty with rfl | hIne
    Â· obtain hÎ¹ | hbot : IsEmpty Î¹ âˆ¨ (ğ“[â‰ ] (0 : ğ•œ)).NeBot := by simpa [@eq_comm _ âˆ…] using hIâ‚€
      Â· use 0
        simp [@eq_comm _ âˆ…, hÎ¹, hrâ‚€]
      Â· rcases exists_enorm_lt ğ•œ hrâ‚€.ne' with âŸ¨câ‚€, hcâ‚€, hcâ‚€râŸ©
        exact âŸ¨câ‚€, .inl hcâ‚€, by simp, hcâ‚€râŸ©
    Â· obtain âŸ¨iâ‚€, hiâ‚€I, hc_maxâŸ© : âˆƒ iâ‚€ âˆˆ I, IsMaxOn (â€–c Â·â€–â‚‘) I iâ‚€ :=
        exists_max_image _ (â€–c Â·â€–â‚‘) hI hIne
      by_cases H : c iâ‚€ â‰  0 âˆ¨ I = univ
      Â· exact âŸ¨c iâ‚€, H, fun i hi â†¦ by simpa [enorm] using hc_max hi, hcr _ hiâ‚€IâŸ©
      Â· push_neg at H
        have hc0 (i : Î¹) (hi : i âˆˆ I) : c i = 0 := by simpa [H] using hc_max hi
        have heg0 (i : Î¹) (hi : i âˆˆ I) : x i = 0 :=
          zero_smul_set_subset (Î± := ğ•œ) (U i) (hc0 i hi â–¸ hc i hi)
        have : (ğ“[â‰ ] (0 : ğ•œ)).NeBot := (hIâ‚€.resolve_left H.2).resolve_left (by simpa)
        rcases exists_enorm_lt ğ•œ hrâ‚€.ne' with âŸ¨câ‚, hcâ‚, hcâ‚râŸ©
        refine âŸ¨câ‚, .inl hcâ‚, fun i hi â†¦ ?_, hcâ‚râŸ©
        simp [hc0 i hi]
  refine egauge_lt_iff.2 âŸ¨câ‚€, ?_, hcâ‚€râŸ©
  rw [smul_set_piâ‚€' hcâ‚€]
  intro i hi
  exact (hU i hi).smul_mono (hcâ‚€I i hi) (hc i hi)

/-- The extended gauge of a point `x` in an indexed product with finite index type
with respect to a product of balanced sets `U i`,
is the supremum of the extended gauges of the components of `x`
with respect to the corresponding balanced set.
-/
theorem egauge_univ_pi [Finite Î¹] {U : âˆ€ i, Set (E i)} (hU : âˆ€ i, Balanced ğ•œ (U i)) (x : âˆ€ i, E i) :
    egauge ğ•œ (univ.pi U) x = â¨† i, egauge ğ•œ (U i) (x i) :=
  egauge_pi' finite_univ (fun i _ â†¦ hU i) x (.inl rfl) |>.trans <| by simp

/-- The extended gauge of a point `x` in an indexed product
with respect to a product of finitely many balanced sets `U i`, `i âˆˆ I`,
(and the whole spaces for the other indices)
is the supremum of the extended gauges of the components of `x`
with respect to the corresponding balanced set.

This version assumes that `ğ•œ` is a nontrivially normed division ring.
See also `egauge_univ_pi` for when `s = univ`,
and `egauge_pi'` for a version with more choices of the technical assumptions.
-/
theorem egauge_pi [(ğ“[â‰ ] (0 : ğ•œ)).NeBot] {I : Set Î¹} {U : âˆ€ i, Set (E i)}
    (hI : I.Finite) (hU : âˆ€ i âˆˆ I, Balanced ğ•œ (U i)) (x : âˆ€ i, E i) :
    egauge ğ•œ (I.pi U) x = â¨† i âˆˆ I, egauge ğ•œ (U i) (x i) :=
  egauge_pi' hI hU x <| .inr <| .inr inferInstance

end Pi

section SeminormedAddCommGroup

variable (ğ•œ : Type*) [NormedField ğ•œ] {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

lemma div_le_egauge_closedBall (r : â„â‰¥0) (x : E) : â€–xâ€–â‚‘ / r â‰¤ egauge ğ•œ (closedBall 0 r) x := by
  rw [le_egauge_iff]
  rintro c âŸ¨y, hy, rflâŸ©
  rw [mem_closedBall_zero_iff, â† coe_nnnorm, NNReal.coe_le_coe] at hy
  rw [enorm_smul]
  apply ENNReal.div_le_of_le_mul
  gcongr
  rwa [enorm_le_coe]

lemma le_egauge_closedBall_one (x : E) : â€–xâ€–â‚‘ â‰¤ egauge ğ•œ (closedBall 0 1) x := by
  simpa using div_le_egauge_closedBall ğ•œ 1 x

lemma div_le_egauge_ball (r : â„â‰¥0) (x : E) : â€–xâ€–â‚‘ / r â‰¤ egauge ğ•œ (ball 0 r) x :=
  (div_le_egauge_closedBall ğ•œ r x).trans <| egauge_anti _ ball_subset_closedBall _

lemma le_egauge_ball_one (x : E) : â€–xâ€–â‚‘ â‰¤ egauge ğ•œ (ball 0 1) x := by
  simpa using div_le_egauge_ball ğ•œ 1 x

variable {ğ•œ}
variable {c : ğ•œ} {x : E} {r : â„â‰¥0}

lemma egauge_ball_le_of_one_lt_norm (hc : 1 < â€–câ€–) (hâ‚€ : r â‰  0 âˆ¨ â€–xâ€– â‰  0) :
    egauge ğ•œ (ball 0 r) x â‰¤ â€–câ€–â‚‘ * â€–xâ€–â‚‘ / r := by
  letI : NontriviallyNormedField ğ•œ := âŸ¨c, hcâŸ©
  rcases (zero_le r).eq_or_lt with rfl | hr
  Â· rw [ENNReal.coe_zero, ENNReal.div_zero (mul_ne_zero _ _)]
    Â· apply le_top
    Â· simpa using one_pos.trans hc
    Â· simpa [enorm, â† NNReal.coe_eq_zero] using hâ‚€
  Â· rcases eq_or_ne â€–xâ€– 0 with hx | hx
    Â· have hx' : â€–xâ€–â‚‘ = 0 := by simpa [enorm, â† coe_nnnorm, NNReal.coe_eq_zero] using hx
      simp only [hx', mul_zero, ENNReal.zero_div, nonpos_iff_eq_zero, egauge_eq_zero_iff]
      refine (frequently_iff_neBot.2 (inferInstance : NeBot (ğ“[â‰ ] (0 : ğ•œ)))).mono fun c hc â†¦ ?_
      simp [mem_smul_set_iff_inv_smul_memâ‚€ hc, norm_smul, hx, hr]
    Â· rcases rescale_to_shell_semi_normed hc hr hx with âŸ¨a, haâ‚€, har, -, hainvâŸ©
      calc
        egauge ğ•œ (ball 0 r) x â‰¤ â†‘(â€–aâ€–â‚Šâ»Â¹) :=
          egauge_le_of_smul_mem_of_ne (mem_ball_zero_iff.2 har) haâ‚€
        _ â‰¤ â†‘(â€–câ€–â‚Š * â€–xâ€–â‚Š / r) := by rwa [ENNReal.coe_le_coe, div_eq_inv_mul, â† mul_assoc]
        _ â‰¤ â€–câ€–â‚‘ * â€–xâ€–â‚‘ / r := ENNReal.coe_div_le.trans <| by simp [ENNReal.coe_mul, enorm]

lemma egauge_ball_one_le_of_one_lt_norm (hc : 1 < â€–câ€–) (x : E) :
    egauge ğ•œ (ball 0 1) x â‰¤ â€–câ€–â‚‘ * â€–xâ€–â‚‘ := by
  simpa using egauge_ball_le_of_one_lt_norm hc (.inl one_ne_zero)

end SeminormedAddCommGroup
