/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Seminorm

/-!
# The Minkowski functional, normed field version

In this file we define `(egauge ğ•œ s Â·)`
to be the Minkowski functional (gauge) of the set `s`
in a topological vector space `E` over a normed field `ğ•œ`,
as a function `E â†’ â„â‰¥0âˆ`.

It is defined as the infimum of the norms of `c : ğ•œ` such that `x âˆˆ c â€¢ s`.
In particular, for `ğ•œ = â„â‰¥0` this definition gives an `â„â‰¥0âˆ`-valued version of `gauge`
defined in `Mathlib/Analysis/Convex/Gauge.lean`.

This definition can be used to generalize the notion of FrÃ©chet derivative
to maps between topological vector spaces without norms.

Currently, we can't reuse results about `egauge` for `gauge`,
because we lack a theory of normed semifields.
-/

open Set Filter Metric
open scoped Topology Pointwise ENNReal NNReal

section SMul

/-- The Minkowski functional for vector spaces over normed fields.
Given a set `s` in a vector space over a normed field `ğ•œ`,
`egauge s` is the functional which sends `x : E`
to the infimum of `â€–câ€–â‚Š` over `c` such that `x` belongs to `s` scaled by `c`.

The definition only requires `ğ•œ` to have a `NNNorm` instance
and `(Â· â€¢ Â·) : ğ•œ â†’ E â†’ E` to be defined.
This way the definition applies, e.g., to `ğ•œ = â„â‰¥0`.
For `ğ•œ = â„â‰¥0`, the function is equal (up to conversion to `â„`)
to the usual Minkowski functional defined in `gauge`. -/
noncomputable def egauge (ğ•œ : Type*) [NNNorm ğ•œ] {E : Type*} [SMul ğ•œ E] (s : Set E) (x : E) : â„â‰¥0âˆ :=
  â¨… (c : ğ•œ) (_ : x âˆˆ c â€¢ s), â€–câ€–â‚Š

variable (ğ•œ : Type*) [NNNorm ğ•œ] {E : Type*} [SMul ğ•œ E] {c : ğ•œ} {s t : Set E} {x : E} {r : â„â‰¥0âˆ}

@[mono, gcongr]
lemma egauge_anti (h : s âŠ† t) (x : E) : egauge ğ•œ t x â‰¤ egauge ğ•œ s x :=
  iInf_mono fun _c â†¦ iInf_mono' fun hc â†¦ âŸ¨smul_set_mono h hc, le_rflâŸ©

@[simp] lemma egauge_empty (x : E) : egauge ğ•œ âˆ… x = âˆ := by simp [egauge]

variable {ğ•œ}

lemma egauge_le_of_mem_smul (h : x âˆˆ c â€¢ s) : egauge ğ•œ s x â‰¤ â€–câ€–â‚Š := iInfâ‚‚_le c h

lemma le_egauge_iff : r â‰¤ egauge ğ•œ s x â†” âˆ€ c : ğ•œ, x âˆˆ c â€¢ s â†’ r â‰¤ â€–câ€–â‚Š := le_iInfâ‚‚_iff

lemma egauge_eq_top : egauge ğ•œ s x = âˆ â†” âˆ€ c : ğ•œ, x âˆ‰ c â€¢ s := by simp [egauge]

lemma egauge_lt_iff : egauge ğ•œ s x < r â†” âˆƒ c : ğ•œ, x âˆˆ c â€¢ s âˆ§ â€–câ€–â‚Š < r := by
  simp [egauge, iInf_lt_iff]

end SMul

section SMulZero

variable (ğ•œ : Type*) [NNNorm ğ•œ] [Nonempty ğ•œ] {E : Type*} [Zero E] [SMulZeroClass ğ•œ E]
  {c : ğ•œ} {s t : Set E} {x : E} {r : â„â‰¥0âˆ}

@[simp] lemma egauge_zero_left_eq_top : egauge ğ•œ 0 x = âˆ â†” x â‰  0 := by
  simp [egauge_eq_top]

@[simp] alias âŸ¨_, egauge_zero_leftâŸ© := egauge_zero_left_eq_top

end SMulZero

section Module

variable {ğ•œ : Type*} [NormedDivisionRing ğ•œ] {Î± E : Type*} [AddCommGroup E] [Module ğ•œ E]
    {c : ğ•œ} {s t : Set E} {x y : E} {r : â„â‰¥0âˆ}

/-- If `c â€¢ x âˆˆ s` and `c â‰  0`, then `egauge ğ•œ s x` is at most `((â€–câ€–â‚Šâ»Â¹ : â„â‰¥0) : â„â‰¥0âˆ).

See also `egauge_le_of_smul_mem`. -/
lemma egauge_le_of_smul_mem_of_ne (h : c â€¢ x âˆˆ s) (hc : c â‰  0) :
    egauge ğ•œ s x â‰¤ â†‘(â€–câ€–â‚Šâ»Â¹ : â„â‰¥0) := by
  rw [â† nnnorm_inv]
  exact egauge_le_of_mem_smul <| (mem_inv_smul_set_iffâ‚€ hc _ _).2 h

/-- If `c â€¢ x âˆˆ s`, then `egauge ğ•œ s x` is at most `(â€–câ€–â‚Š : â„â‰¥0âˆ)â»Â¹.

See also `egauge_le_of_smul_mem_of_ne`. -/
lemma egauge_le_of_smul_mem (h : c â€¢ x âˆˆ s) : egauge ğ•œ s x â‰¤ (â€–câ€–â‚Š : â„â‰¥0âˆ)â»Â¹ := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· exact (egauge_le_of_smul_mem_of_ne h hc).trans ENNReal.coe_inv_le

lemma mem_of_egauge_lt_one (hs : Balanced ğ•œ s) (hx : egauge ğ•œ s x < 1) : x âˆˆ s :=
  let âŸ¨c, hxc, hcâŸ© := egauge_lt_iff.1 hx
  hs c (mod_cast hc.le) hxc

variable (ğ•œ)

@[simp]
lemma egauge_zero_right (hs : s.Nonempty) : egauge ğ•œ s 0 = 0 := by
  have : 0 âˆˆ (0 : ğ•œ) â€¢ s := by simp [zero_smul_set hs]
  simpa using egauge_le_of_mem_smul this

@[simp]
lemma egauge_zero_zero : egauge ğ•œ (0 : Set E) 0 = 0 := egauge_zero_right _ âŸ¨0, rflâŸ©

lemma egauge_le_one (h : x âˆˆ s) : egauge ğ•œ s x â‰¤ 1 := by
  rw [â† one_smul ğ•œ s] at h
  simpa using egauge_le_of_mem_smul h

variable {ğ•œ}

lemma le_egauge_smul_left (c : ğ•œ) (s : Set E) (x : E) :
    egauge ğ•œ s x / â€–câ€–â‚Š â‰¤ egauge ğ•œ (c â€¢ s) x := by
  simp_rw [le_egauge_iff, smul_smul]
  rintro a âŸ¨x, hx, rflâŸ©
  apply ENNReal.div_le_of_le_mul
  rw [â† ENNReal.coe_mul, â† nnnorm_mul]
  exact egauge_le_of_mem_smul <| smul_mem_smul_set hx

lemma egauge_smul_left (hc : c â‰  0) (s : Set E) (x : E) :
    egauge ğ•œ (c â€¢ s) x = egauge ğ•œ s x / â€–câ€–â‚Š := by
  refine le_antisymm ?_ (le_egauge_smul_left _ _ _)
  rw [ENNReal.le_div_iff_mul_le (by simp [*]) (by simp)]
  calc
    egauge ğ•œ (c â€¢ s) x * â€–câ€–â‚Š = egauge ğ•œ (c â€¢ s) x / â€–câ»Â¹â€–â‚Š := by
      rw [nnnorm_inv, ENNReal.coe_inv (by simpa), div_eq_mul_inv, inv_inv]
    _ â‰¤ egauge ğ•œ (câ»Â¹ â€¢ c â€¢ s) x := le_egauge_smul_left _ _ _
    _ = egauge ğ•œ s x := by rw [inv_smul_smulâ‚€ hc]

lemma le_egauge_smul_right (c : ğ•œ) (s : Set E) (x : E) :
    â€–câ€–â‚Š * egauge ğ•œ s x â‰¤ egauge ğ•œ s (c â€¢ x) := by
  rw [le_egauge_iff]
  rintro a âŸ¨y, hy, hxyâŸ©
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· refine ENNReal.mul_le_of_le_div' <| le_trans ?_ ENNReal.coe_div_le
    rw [div_eq_inv_mul, â† nnnorm_inv, â† nnnorm_mul]
    refine egauge_le_of_mem_smul âŸ¨y, hy, ?_âŸ©
    simp only [mul_smul, hxy, inv_smul_smulâ‚€ hc]

lemma egauge_smul_right (h : c = 0 â†’ s.Nonempty) (x : E) :
    egauge ğ•œ s (c â€¢ x) = â€–câ€–â‚Š * egauge ğ•œ s x := by
  refine le_antisymm ?_ (le_egauge_smul_right c s x)
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp [egauge_zero_right _ (h rfl)]
  Â· rw [mul_comm, â† ENNReal.div_le_iff_le_mul (.inl <| by simpa) (.inl ENNReal.coe_ne_top),
      ENNReal.div_eq_inv_mul, â† ENNReal.coe_inv (by simpa), â† nnnorm_inv]
    refine (le_egauge_smul_right _ _ _).trans_eq ?_
    rw [inv_smul_smulâ‚€ hc]

end Module

section SeminormedAddCommGroup

variable (ğ•œ : Type*) [NormedField ğ•œ] {Î± E : Type*}
    [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] {c : ğ•œ} {s t : Set E} {x y : E}

lemma div_le_egauge_closedBall (r : â„â‰¥0) (x : E) : â€–xâ€–â‚Š / r â‰¤ egauge ğ•œ (closedBall 0 r) x := by
  rw [le_egauge_iff]
  rintro c âŸ¨y, hy, rflâŸ©
  rw [mem_closedBall_zero_iff, â† coe_nnnorm, NNReal.coe_le_coe] at hy
  simp only [nnnorm_smul, ENNReal.coe_mul]
  apply ENNReal.div_le_of_le_mul
  gcongr

lemma le_egauge_closedBall_one (x : E) : â€–xâ€–â‚Š â‰¤ egauge ğ•œ (closedBall 0 1) x := by
  simpa using div_le_egauge_closedBall ğ•œ 1 x

lemma div_le_egauge_ball (r : â„â‰¥0) (x : E) : â€–xâ€–â‚Š / r â‰¤ egauge ğ•œ (ball 0 r) x :=
  (div_le_egauge_closedBall ğ•œ r x).trans <| egauge_anti _ ball_subset_closedBall _

lemma le_egauge_ball_one (x : E) : â€–xâ€–â‚Š â‰¤ egauge ğ•œ (ball 0 1) x := by
  simpa using div_le_egauge_ball ğ•œ 1 x

end SeminormedAddCommGroup

section SeminormedAddCommGroup

variable {ğ•œ : Type*} [NormedField ğ•œ] {Î± E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] {c : ğ•œ} {s t : Set E} {x y : E} {r : â„â‰¥0}

lemma egauge_ball_le_of_one_lt_norm (hc : 1 < â€–câ€–) (hâ‚€ : r â‰  0 âˆ¨ x â‰  0) :
    egauge ğ•œ (ball 0 r) x â‰¤ â€–câ€–â‚Š * â€–xâ€–â‚Š / r := by
  rcases (zero_le r).eq_or_lt with rfl | hr
  Â· rw [ENNReal.coe_zero, ENNReal.div_zero (mul_ne_zero _ _)]
    Â· apply le_top
    Â· simpa using one_pos.trans hc
    Â· simpa using hâ‚€
  Â· rcases eq_or_ne x 0 with rfl | hx
    Â· rw [egauge_zero_right] <;> simp [*]
    rcases rescale_to_shell hc hr hx with âŸ¨a, haâ‚€, har, -, hainvâŸ©
    calc
      egauge ğ•œ (ball 0 r) x â‰¤ â†‘(â€–aâ€–â‚Šâ»Â¹) := egauge_le_of_smul_mem_of_ne (mem_ball_zero_iff.2 har) haâ‚€
      _ â‰¤ â†‘(â€–câ€–â‚Š * â€–xâ€–â‚Š / r) := by rwa [ENNReal.coe_le_coe, div_eq_inv_mul, â† mul_assoc]
      _ â‰¤ â€–câ€–â‚Š * â€–xâ€–â‚Š / r := ENNReal.coe_div_le.trans <| by rw [ENNReal.coe_mul]

lemma egauge_ball_one_le_of_one_lt_norm (hc : 1 < â€–câ€–) (x : E) :
    egauge ğ•œ (ball 0 1) x â‰¤ â€–câ€–â‚Š * â€–xâ€–â‚Š := by
  simpa using egauge_ball_le_of_one_lt_norm hc (.inl one_ne_zero)
