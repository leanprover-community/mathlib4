/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Scott Morrison
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.LinearAlgebra.AffineSpace.Independent
import Mathlib.Tactic.FieldSimp

#align_import analysis.convex.caratheodory from "leanprover-community/mathlib"@"e6fab1dc073396d45da082c644642c4f8bff2264"

/-!
# CarathÃ©odory's convexity theorem

Convex hull can be regarded as a refinement of affine span. Both are closure operators but whereas
convex hull takes values in the lattice of convex subsets, affine span takes values in the much
coarser sublattice of affine subspaces.

The cost of this refinement is that one no longer has bases. However CarathÃ©odory's convexity
theorem offers some compensation. Given a set `s` together with a point `x` in its convex hull,
CarathÃ©odory says that one may find an affine-independent family of elements `s` whose convex hull
contains `x`. Thus the difference from the case of affine span is that the affine-independent family
depends on `x`.

In particular, in finite dimensions CarathÃ©odory's theorem implies that the convex hull of a set `s`
in `ğ•œáµˆ` is the union of the convex hulls of the `(d + 1)`-tuples in `s`.

## Main results

* `convexHull_eq_union`: CarathÃ©odory's convexity theorem

## Implementation details

This theorem was formalized as part of the Sphere Eversion project.

## Tags
convex hull, caratheodory

-/


open Set Finset

open BigOperators

universe u

variable {ğ•œ : Type*} {E : Type u} [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]

namespace Caratheodory

/-- If `x` is in the convex hull of some finset `t` whose elements are not affine-independent,
then it is in the convex hull of a strict subset of `t`. -/
theorem mem_convexHull_erase [DecidableEq E] {t : Finset E} (h : Â¬AffineIndependent ğ•œ ((â†‘) : t â†’ E))
    {x : E} (m : x âˆˆ convexHull ğ•œ (â†‘t : Set E)) :
    âˆƒ y : (â†‘t : Set E), x âˆˆ convexHull ğ•œ (â†‘(t.erase y) : Set E) := by
  simp only [Finset.convexHull_eq, mem_setOf_eq] at m âŠ¢
  -- âŠ¢ âˆƒ y w x_1 x_2, centerMass (erase t â†‘y) w id = x
  obtain âŸ¨f, fpos, fsum, rflâŸ© := m
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  obtain âŸ¨g, gcombo, gsum, gposâŸ© := exists_nontrivial_relation_sum_zero_of_not_affine_ind h
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  replace gpos := exists_pos_of_sum_zero_of_exists_nonzero g gsum gpos
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  clear h
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  let s := @Finset.filter _ (fun z => 0 < g z) (fun _ => LinearOrder.decidableLT _ _) t
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  obtain âŸ¨iâ‚€, mem, wâŸ© : âˆƒ iâ‚€ âˆˆ s, âˆ€ i âˆˆ s, f iâ‚€ / g iâ‚€ â‰¤ f i / g i := by
    apply s.exists_min_image fun z => f z / g z
    obtain âŸ¨x, hx, hgxâŸ© : âˆƒ x âˆˆ t, 0 < g x := gpos
    exact âŸ¨x, mem_filter.mpr âŸ¨hx, hgxâŸ©âŸ©
  have hg : 0 < g iâ‚€ := by
    rw [mem_filter] at mem
    exact mem.2
  have hiâ‚€ : iâ‚€ âˆˆ t := filter_subset _ _ mem
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  let k : E â†’ ğ•œ := fun z => f z - f iâ‚€ / g iâ‚€ * g z
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  have hk : k iâ‚€ = 0 := by field_simp [ne_of_gt hg]
  -- âŠ¢ âˆƒ y w x x, centerMass (erase t â†‘y) w id = centerMass t f id
  have ksum : âˆ‘ e in t.erase iâ‚€, k e = 1 := by
    calc
      âˆ‘ e in t.erase iâ‚€, k e = âˆ‘ e in t, k e := by
        conv_rhs => rw [â† insert_erase hiâ‚€, sum_insert (not_mem_erase iâ‚€ t), hk, zero_add]
      _ = âˆ‘ e in t, (f e - f iâ‚€ / g iâ‚€ * g e) := rfl
      _ = 1 := by rw [sum_sub_distrib, fsum, â† mul_sum, gsum, mul_zero, sub_zero]
  refine' âŸ¨âŸ¨iâ‚€, hiâ‚€âŸ©, k, _, by convert ksum, _âŸ©
  -- âŠ¢ âˆ€ (y : E), y âˆˆ erase t â†‘{ val := iâ‚€, property := hiâ‚€ } â†’ 0 â‰¤ k y
  Â· simp only [and_imp, sub_nonneg, mem_erase, Ne.def, Subtype.coe_mk]
    -- âŠ¢ âˆ€ (y : E), Â¬y = iâ‚€ â†’ y âˆˆ t â†’ f iâ‚€ / g iâ‚€ * g y â‰¤ f y
    intro e _ het
    -- âŠ¢ f iâ‚€ / g iâ‚€ * g e â‰¤ f e
    by_cases hes : e âˆˆ s
    -- âŠ¢ f iâ‚€ / g iâ‚€ * g e â‰¤ f e
    Â· have hge : 0 < g e := by
        rw [mem_filter] at hes
        exact hes.2
      rw [â† le_div_iff hge]
      -- âŠ¢ f iâ‚€ / g iâ‚€ â‰¤ f e / g e
      exact w _ hes
      -- ğŸ‰ no goals
    Â· calc
        _ â‰¤ 0 := by
          apply mul_nonpos_of_nonneg_of_nonpos
          Â· apply div_nonneg (fpos iâ‚€ (mem_of_subset (filter_subset _ t) mem)) (le_of_lt hg)
          Â· simpa only [mem_filter, het, true_and_iff, not_lt] using hes
        _ â‰¤ f e := fpos e het
  Â· rw [Subtype.coe_mk, centerMass_eq_of_sum_1 _ id ksum]
    -- âŠ¢ âˆ‘ i in erase t iâ‚€, k i â€¢ id i = centerMass t f id
    calc
      âˆ‘ e in t.erase iâ‚€, k e â€¢ e = âˆ‘ e in t, k e â€¢ e := sum_erase _ (by rw [hk, zero_smul])
      _ = âˆ‘ e in t, (f e - f iâ‚€ / g iâ‚€ * g e) â€¢ e := rfl
      _ = t.centerMass f id := by
        simp only [sub_smul, mul_smul, sum_sub_distrib, â† smul_sum, gcombo, smul_zero, sub_zero,
          centerMass, fsum, inv_one, one_smul, id.def]
#align caratheodory.mem_convex_hull_erase Caratheodory.mem_convexHull_erase

variable {s : Set E} {x : E} (hx : x âˆˆ convexHull ğ•œ s)

/-- Given a point `x` in the convex hull of a set `s`, this is a finite subset of `s` of minimum
cardinality, whose convex hull contains `x`. -/
noncomputable def minCardFinsetOfMemConvexHull : Finset E :=
  Function.argminOn Finset.card Nat.lt_wfRel.2 { t | â†‘t âŠ† s âˆ§ x âˆˆ convexHull ğ•œ (t : Set E) } <| by
    simpa only [convexHull_eq_union_convexHull_finite_subsets s, exists_prop, mem_iUnion] using hx
    -- ğŸ‰ no goals
#align caratheodory.min_card_finset_of_mem_convex_hull Caratheodory.minCardFinsetOfMemConvexHull

theorem minCardFinsetOfMemConvexHull_subseteq : â†‘(minCardFinsetOfMemConvexHull hx) âŠ† s :=
  (Function.argminOn_mem _ _ { t : Finset E | â†‘t âŠ† s âˆ§ x âˆˆ convexHull ğ•œ (t : Set E) } _).1
#align caratheodory.min_card_finset_of_mem_convex_hull_subseteq Caratheodory.minCardFinsetOfMemConvexHull_subseteq

theorem mem_minCardFinsetOfMemConvexHull :
    x âˆˆ convexHull ğ•œ (minCardFinsetOfMemConvexHull hx : Set E) :=
  (Function.argminOn_mem _ _ { t : Finset E | â†‘t âŠ† s âˆ§ x âˆˆ convexHull ğ•œ (t : Set E) } _).2
#align caratheodory.mem_min_card_finset_of_mem_convex_hull Caratheodory.mem_minCardFinsetOfMemConvexHull

theorem minCardFinsetOfMemConvexHull_nonempty : (minCardFinsetOfMemConvexHull hx).Nonempty := by
  rw [â† Finset.coe_nonempty, â† @convexHull_nonempty_iff ğ•œ]
  -- âŠ¢ Set.Nonempty (â†‘(convexHull ğ•œ) â†‘(minCardFinsetOfMemConvexHull hx))
  exact âŸ¨x, mem_minCardFinsetOfMemConvexHull hxâŸ©
  -- ğŸ‰ no goals
#align caratheodory.min_card_finset_of_mem_convex_hull_nonempty Caratheodory.minCardFinsetOfMemConvexHull_nonempty

theorem minCardFinsetOfMemConvexHull_card_le_card {t : Finset E} (htâ‚ : â†‘t âŠ† s)
    (htâ‚‚ : x âˆˆ convexHull ğ•œ (t : Set E)) : (minCardFinsetOfMemConvexHull hx).card â‰¤ t.card :=
  Function.argminOn_le _ _ _ (by exact âŸ¨htâ‚, htâ‚‚âŸ©)
                                 -- ğŸ‰ no goals
#align caratheodory.min_card_finset_of_mem_convex_hull_card_le_card Caratheodory.minCardFinsetOfMemConvexHull_card_le_card

theorem affineIndependent_minCardFinsetOfMemConvexHull :
    AffineIndependent ğ•œ ((â†‘) : minCardFinsetOfMemConvexHull hx â†’ E) := by
  let k := (minCardFinsetOfMemConvexHull hx).card - 1
  -- âŠ¢ AffineIndependent ğ•œ Subtype.val
  have hk : (minCardFinsetOfMemConvexHull hx).card = k + 1 :=
    (Nat.succ_pred_eq_of_pos (Finset.card_pos.mpr (minCardFinsetOfMemConvexHull_nonempty hx))).symm
  classical
  by_contra h
  obtain âŸ¨p, hpâŸ© := mem_convexHull_erase h (mem_minCardFinsetOfMemConvexHull hx)
  have contra := minCardFinsetOfMemConvexHull_card_le_card hx (Set.Subset.trans
    (Finset.erase_subset (p : E) (minCardFinsetOfMemConvexHull hx))
    (minCardFinsetOfMemConvexHull_subseteq hx)) hp
  rw [â† not_lt] at contra
  apply contra
  erw [card_erase_of_mem p.2, hk]
  exact lt_add_one _
#align caratheodory.affine_independent_min_card_finset_of_mem_convex_hull Caratheodory.affineIndependent_minCardFinsetOfMemConvexHull

end Caratheodory

variable {s : Set E}

/-- **CarathÃ©odory's convexity theorem** -/
theorem convexHull_eq_union : convexHull ğ•œ s =
    â‹ƒ (t : Finset E) (hss : â†‘t âŠ† s) (hai : AffineIndependent ğ•œ ((â†‘) : t â†’ E)), convexHull ğ•œ â†‘t := by
  apply Set.Subset.antisymm
  -- âŠ¢ â†‘(convexHull ğ•œ) s âŠ† â‹ƒ (t : Finset E) (_ : â†‘t âŠ† s) (_ : AffineIndependent ğ•œ S â€¦
  Â· intro x hx
    -- âŠ¢ x âˆˆ â‹ƒ (t : Finset E) (_ : â†‘t âŠ† s) (_ : AffineIndependent ğ•œ Subtype.val), â†‘(c â€¦
    simp only [exists_prop, Set.mem_iUnion]
    -- âŠ¢ âˆƒ i, â†‘i âŠ† s âˆ§ AffineIndependent ğ•œ Subtype.val âˆ§ x âˆˆ â†‘(convexHull ğ•œ) â†‘i
    exact âŸ¨Caratheodory.minCardFinsetOfMemConvexHull hx,
      Caratheodory.minCardFinsetOfMemConvexHull_subseteq hx,
      Caratheodory.affineIndependent_minCardFinsetOfMemConvexHull hx,
      Caratheodory.mem_minCardFinsetOfMemConvexHull hxâŸ©
  Â· iterate 3 convert Set.iUnion_subset _; intro
    -- âŠ¢ â†‘(convexHull ğ•œ) â†‘iâœÂ² âŠ† â†‘(convexHull ğ•œ) s
    exact convexHull_mono â€¹_â€º
    -- ğŸ‰ no goals
#align convex_hull_eq_union convexHull_eq_union

/-- A more explicit version of `convexHull_eq_union`. -/
theorem eq_pos_convex_span_of_mem_convexHull {x : E} (hx : x âˆˆ convexHull ğ•œ s) :
    âˆƒ (Î¹ : Sort (u + 1)) (_ : Fintype Î¹),
      âˆƒ (z : Î¹ â†’ E) (w : Î¹ â†’ ğ•œ) (_ : Set.range z âŠ† s) (_ : AffineIndependent ğ•œ z)
        (_ : âˆ€ i, 0 < w i), âˆ‘ i, w i = 1 âˆ§ âˆ‘ i, w i â€¢ z i = x := by
  rw [convexHull_eq_union] at hx
  -- âŠ¢ âˆƒ Î¹ x_1 z w x_2 x_3 x_4, âˆ‘ i : Î¹, w i = 1 âˆ§ âˆ‘ i : Î¹, w i â€¢ z i = x
  simp only [exists_prop, Set.mem_iUnion] at hx
  -- âŠ¢ âˆƒ Î¹ x_1 z w x_2 x_3 x_4, âˆ‘ i : Î¹, w i = 1 âˆ§ âˆ‘ i : Î¹, w i â€¢ z i = x
  obtain âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒâŸ© := hx
  -- âŠ¢ âˆƒ Î¹ x_1 z w x_2 x_3 x_4, âˆ‘ i : Î¹, w i = 1 âˆ§ âˆ‘ i : Î¹, w i â€¢ z i = x
  simp only [t.convexHull_eq, exists_prop, Set.mem_setOf_eq] at htâ‚ƒ
  -- âŠ¢ âˆƒ Î¹ x_1 z w x_2 x_3 x_4, âˆ‘ i : Î¹, w i = 1 âˆ§ âˆ‘ i : Î¹, w i â€¢ z i = x
  obtain âŸ¨w, hwâ‚, hwâ‚‚, hwâ‚ƒâŸ© := htâ‚ƒ
  -- âŠ¢ âˆƒ Î¹ x_1 z w x_2 x_3 x_4, âˆ‘ i : Î¹, w i = 1 âˆ§ âˆ‘ i : Î¹, w i â€¢ z i = x
  let t' := t.filter fun i => w i â‰  0
  -- âŠ¢ âˆƒ Î¹ x_1 z w x_2 x_3 x_4, âˆ‘ i : Î¹, w i = 1 âˆ§ âˆ‘ i : Î¹, w i â€¢ z i = x
  refine' âŸ¨t', t'.fintypeCoeSort, ((â†‘) : t' â†’ E), w âˆ˜ ((â†‘) : t' â†’ E), _, _, _, _, _âŸ©
  Â· rw [Subtype.range_coe_subtype]
    -- âŠ¢ {x | x âˆˆ t'} âŠ† s
    exact Subset.trans (Finset.filter_subset _ t) htâ‚
    -- ğŸ‰ no goals
  Â· exact htâ‚‚.comp_embedding âŸ¨_, inclusion_injective (Finset.filter_subset (fun i => w i â‰  0) t)âŸ©
    -- ğŸ‰ no goals
  Â· exact fun i =>
      (hwâ‚ _ (Finset.mem_filter.mp i.2).1).lt_of_ne (Finset.mem_filter.mp i.property).2.symm
  Â· erw [Finset.sum_attach, Finset.sum_filter_ne_zero, hwâ‚‚]
    -- ğŸ‰ no goals
  Â· change (âˆ‘ i : t' in t'.attach, (fun e => w e â€¢ e) â†‘i) = x
    -- âŠ¢ âˆ‘ i in attach t', (fun e => w e â€¢ e) â†‘i = x
    erw [Finset.sum_attach (f := fun e => w e â€¢ e), Finset.sum_filter_of_ne]
    -- âŠ¢ âˆ‘ x in t, w x â€¢ x = x
    Â· rw [t.centerMass_eq_of_sum_1 id hwâ‚‚] at hwâ‚ƒ
      -- âŠ¢ âˆ‘ x in t, w x â€¢ x = x
      exact hwâ‚ƒ
      -- ğŸ‰ no goals
    Â· intro e _ hwe contra
      -- âŠ¢ False
      apply hwe
      -- âŠ¢ w e â€¢ e = 0
      rw [contra, zero_smul]
      -- ğŸ‰ no goals
#align eq_pos_convex_span_of_mem_convex_hull eq_pos_convex_span_of_mem_convexHull
