/-
Copyright (c) 2022 Paul A. Reichert. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul A. Reichert
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Topology.MetricSpace.HausdorffDistance

/-!
# Convex bodies

This file contains the definition of the type `ConvexBody V`
consisting of
convex, compact, nonempty subsets of a real topological vector space `V`.

`ConvexBody V` is a module over the nonnegative reals (`NNReal`) and a pseudo-metric space.
If `V` is a normed space, `ConvexBody V` is a metric space.

## TODO

- define positive convex bodies, requiring the interior to be nonempty
- introduce support sets
- Characterise the interaction of the distance with algebraic operations, eg
  `dist (a ‚Ä¢ K) (a ‚Ä¢ L) = ‚Äña‚Äñ * dist K L`, `dist (a +·µ• K) (a +·µ• L) = dist K L`

## Tags

convex, convex body
-/


open scoped Pointwise Topology NNReal

variable {V : Type*}

/-- Let `V` be a real topological vector space. A subset of `V` is a convex body if and only if
it is convex, compact, and nonempty.
-/
structure ConvexBody (V : Type*) [TopologicalSpace V] [AddCommMonoid V] [SMul ‚Ñù V] where
  /-- The **carrier set** underlying a convex body: the set of points contained in it -/
  carrier : Set V
  /-- A convex body has convex carrier set -/
  convex' : Convex ‚Ñù carrier
  /-- A convex body has compact carrier set -/
  isCompact' : IsCompact carrier
  /-- A convex body has non-empty carrier set -/
  nonempty' : carrier.Nonempty

namespace ConvexBody

section TVS

variable [TopologicalSpace V] [AddCommGroup V] [Module ‚Ñù V]

instance : SetLike (ConvexBody V) V where
  coe := ConvexBody.carrier
  coe_injective' K L h := by
    cases K
    cases L
    congr

protected theorem convex (K : ConvexBody V) : Convex ‚Ñù (K : Set V) :=
  K.convex'

protected theorem isCompact (K : ConvexBody V) : IsCompact (K : Set V) :=
  K.isCompact'

protected theorem isClosed [T2Space V] (K : ConvexBody V) : IsClosed (K : Set V) :=
  K.isCompact.isClosed

protected theorem nonempty (K : ConvexBody V) : (K : Set V).Nonempty :=
  K.nonempty'

@[ext]
protected theorem ext {K L : ConvexBody V} (h : (K : Set V) = L) : K = L :=
  SetLike.ext' h

@[simp]
theorem coe_mk (s : Set V) (h‚ÇÅ h‚ÇÇ h‚ÇÉ) : (mk s h‚ÇÅ h‚ÇÇ h‚ÇÉ : Set V) = s :=
  rfl

/-- A convex body that is symmetric contains `0`. -/
theorem zero_mem_of_symmetric (K : ConvexBody V) (h_symm : ‚àÄ x ‚àà K, -x ‚àà K) : 0 ‚àà K := by
  obtain ‚ü®x, hx‚ü© := K.nonempty
  rw [show 0 = (1/2 : ‚Ñù) ‚Ä¢ x + (1/2 : ‚Ñù) ‚Ä¢ (- x) by simp]
  apply convex_iff_forall_pos.mp K.convex hx (h_symm x hx)
  all_goals linarith

section ContinuousAdd

instance : Zero (ConvexBody V) where
  zero := ‚ü®0, convex_singleton 0, isCompact_singleton, Set.singleton_nonempty 0‚ü©

@[simp] -- Porting note: add norm_cast; we leave it out for now to reproduce mathlib3 behavior.
theorem coe_zero : (‚Üë(0 : ConvexBody V) : Set V) = 0 :=
  rfl

instance : Inhabited (ConvexBody V) :=
  ‚ü®0‚ü©

variable [ContinuousAdd V]

instance : Add (ConvexBody V) where
  add K L :=
    ‚ü®K + L, K.convex.add L.convex, K.isCompact.add L.isCompact,
      K.nonempty.add L.nonempty‚ü©

instance : SMul ‚Ñï (ConvexBody V) where
  smul := nsmulRec

-- Porting note: add @[simp, norm_cast]; we leave it out for now to reproduce mathlib3 behavior.
theorem coe_nsmul : ‚àÄ (n : ‚Ñï) (K : ConvexBody V), ‚Üë(n ‚Ä¢ K) = n ‚Ä¢ (K : Set V)
  | 0, _ => rfl
  | (n + 1), K => congr_arg‚ÇÇ (Set.image2 (¬∑ + ¬∑)) (coe_nsmul n K) rfl

noncomputable instance : AddMonoid (ConvexBody V) :=
  SetLike.coe_injective.addMonoid _ rfl (fun _ _ ‚Ü¶ rfl) fun _ _ ‚Ü¶ coe_nsmul _ _

@[simp] -- Porting note: add norm_cast; we leave it out for now to reproduce mathlib3 behavior.
theorem coe_add (K L : ConvexBody V) : (‚Üë(K + L) : Set V) = (K : Set V) + L :=
  rfl

noncomputable instance : AddCommMonoid (ConvexBody V) :=
  SetLike.coe_injective.addCommMonoid _ rfl (fun _ _ ‚Ü¶ rfl) fun _ _ ‚Ü¶ coe_nsmul _ _

end ContinuousAdd

variable [ContinuousSMul ‚Ñù V]

instance : SMul ‚Ñù (ConvexBody V) where
  smul c K := ‚ü®c ‚Ä¢ (K : Set V), K.convex.smul _, K.isCompact.smul _, K.nonempty.smul_set‚ü©

@[simp] -- Porting note: add norm_cast; we leave it out for now to reproduce mathlib3 behavior.
theorem coe_smul (c : ‚Ñù) (K : ConvexBody V) : (‚Üë(c ‚Ä¢ K) : Set V) = c ‚Ä¢ (K : Set V) :=
  rfl

variable [ContinuousAdd V]

noncomputable instance : DistribMulAction ‚Ñù (ConvexBody V) :=
  SetLike.coe_injective.distribMulAction ‚ü®‚ü®_, coe_zero‚ü©, coe_add‚ü© coe_smul

@[simp] -- Porting note: add norm_cast; we leave it out for now to reproduce mathlib3 behavior.
theorem coe_smul' (c : ‚Ñù‚â•0) (K : ConvexBody V) : (‚Üë(c ‚Ä¢ K) : Set V) = c ‚Ä¢ (K : Set V) :=
  rfl

/-- The convex bodies in a fixed space $V$ form a module over the nonnegative reals.
-/
noncomputable instance : Module ‚Ñù‚â•0 (ConvexBody V) where
  add_smul c d K := SetLike.ext' <| Convex.add_smul K.convex c.coe_nonneg d.coe_nonneg
  zero_smul K := SetLike.ext' <| Set.zero_smul_set K.nonempty

theorem smul_le_of_le (K : ConvexBody V) (h_zero : 0 ‚àà K) {a b : ‚Ñù‚â•0} (h : a ‚â§ b) :
    a ‚Ä¢ K ‚â§ b ‚Ä¢ K := by
  rw [‚Üê SetLike.coe_subset_coe, coe_smul', coe_smul']
  obtain rfl | ha := eq_zero_or_pos a
  ¬∑ rw [Set.zero_smul_set K.nonempty, Set.zero_subset]
    exact Set.mem_smul_set.mpr ‚ü®0, h_zero, smul_zero _‚ü©
  ¬∑ intro x hx
    obtain ‚ü®y, hy, rfl‚ü© := Set.mem_smul_set.mp hx
    rw [‚Üê Set.mem_inv_smul_set_iff‚ÇÄ ha.ne', smul_smul]
    refine Convex.mem_smul_of_zero_mem K.convex h_zero hy (?_ : 1 ‚â§ a‚Åª¬π * b)
    rwa [le_inv_mul_iff‚ÇÄ ha, mul_one]

end TVS

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup V] [NormedSpace ‚Ñù V] (K L : ConvexBody V)

protected theorem isBounded : Bornology.IsBounded (K : Set V) :=
  K.isCompact.isBounded

theorem hausdorffEdist_ne_top {K L : ConvexBody V} : EMetric.hausdorffEdist (K : Set V) L ‚â† ‚ä§ := by
  apply_rules [Metric.hausdorffEdist_ne_top_of_nonempty_of_bounded, ConvexBody.nonempty,
    ConvexBody.isBounded]

/-- Convex bodies in a fixed seminormed space $V$ form a pseudo-metric space under the Hausdorff
metric. -/
noncomputable instance : PseudoMetricSpace (ConvexBody V) where
  dist K L := Metric.hausdorffDist (K : Set V) L
  dist_self _ := Metric.hausdorffDist_self_zero
  dist_comm _ _ := Metric.hausdorffDist_comm
  dist_triangle _ _ _ := Metric.hausdorffDist_triangle hausdorffEdist_ne_top

@[simp, norm_cast]
theorem hausdorffDist_coe : Metric.hausdorffDist (K : Set V) L = dist K L :=
  rfl

@[simp, norm_cast]
theorem hausdorffEdist_coe : EMetric.hausdorffEdist (K : Set V) L = edist K L := by
  rw [edist_dist]
  exact (ENNReal.ofReal_toReal hausdorffEdist_ne_top).symm

open Filter

/-- Let `K` be a convex body that contains `0` and let `u n` be a sequence of nonnegative real
numbers that tends to `0`. Then the intersection of the dilated bodies `(1 + u n) ‚Ä¢ K` is equal
to `K`. -/
theorem iInter_smul_eq_self [T2Space V] {u : ‚Ñï ‚Üí ‚Ñù‚â•0} (K : ConvexBody V) (h_zero : 0 ‚àà K)
    (hu : Tendsto u atTop (ùìù 0)) :
    ‚ãÇ n : ‚Ñï, (1 + (u n : ‚Ñù)) ‚Ä¢ (K : Set V) = K := by
  ext x
  refine ‚ü®fun h => ?_, fun h => ?_‚ü©
  ¬∑ obtain ‚ü®C, hC_pos, hC_bdd‚ü© := K.isBounded.exists_pos_norm_le
    rw [‚Üê K.isClosed.closure_eq, SeminormedAddCommGroup.mem_closure_iff]
    rw [‚Üê NNReal.tendsto_coe, NormedAddCommGroup.tendsto_atTop] at hu
    intro Œµ hŒµ
    obtain ‚ü®n, hn‚ü© := hu (Œµ / C) (div_pos hŒµ hC_pos)
    obtain ‚ü®y, hyK, rfl‚ü© := Set.mem_smul_set.mp (Set.mem_iInter.mp h n)
    refine ‚ü®y, hyK, ?_‚ü©
    rw [show (1 + u n : ‚Ñù) ‚Ä¢ y - y = (u n : ‚Ñù) ‚Ä¢ y by rw [add_smul, one_smul, add_sub_cancel_left],
      norm_smul, Real.norm_eq_abs]
    specialize hn n le_rfl
    rw [lt_div_iff‚ÇÄ' hC_pos, mul_comm, NNReal.coe_zero, sub_zero, Real.norm_eq_abs] at hn
    refine lt_of_le_of_lt ?_ hn
    exact mul_le_mul_of_nonneg_left (hC_bdd _ hyK) (abs_nonneg _)
  ¬∑ refine Set.mem_iInter.mpr (fun n => Convex.mem_smul_of_zero_mem K.convex h_zero h ?_)
    exact le_add_of_nonneg_right (by positivity)

end SeminormedAddCommGroup

section NormedAddCommGroup

variable [NormedAddCommGroup V] [NormedSpace ‚Ñù V]

/-- Convex bodies in a fixed normed space `V` form a metric space under the Hausdorff metric. -/
noncomputable instance : MetricSpace (ConvexBody V) where
  eq_of_dist_eq_zero {K L} hd := ConvexBody.ext <|
    (K.isClosed.hausdorffDist_zero_iff_eq L.isClosed hausdorffEdist_ne_top).1 hd

end NormedAddCommGroup

end ConvexBody
