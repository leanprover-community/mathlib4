/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.Convex.Hull

/-!
# Extreme sets

This file defines extreme sets and extreme points for sets in a module.

An extreme set of `A` is a subset of `A` that is as far as it can get in any outward direction: If
point `x` is in it and point `y âˆˆ A`, then the line passing through `x` and `y` leaves `A` at `x`.
This is an analytic notion of "being on the side of". It is weaker than being exposed (see
`IsExposed.isExtreme`).

## Main declarations

* `IsExtreme ğ•œ A B`: States that `B` is an extreme set of `A` (in the literature, `A` is often
  implicit).
* `Set.extremePoints ğ•œ A`: Set of extreme points of `A` (corresponding to extreme singletons).
* `Convex.mem_extremePoints_iff_convex_diff`: A useful equivalent condition to being an extreme
  point: `x` is an extreme point iff `A \ {x}` is convex.

## Implementation notes

The exact definition of extremeness has been carefully chosen so as to make as many lemmas
unconditional (in particular, the Krein-Milman theorem doesn't need the set to be convex!).
In practice, `A` is often assumed to be a convex set.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

## TODO

Prove lemmas relating extreme sets and points to the intrinsic frontier.
-/


open Function Set Affine

variable {ğ•œ E F Î¹ : Type*} {M : Î¹ â†’ Type*}

section SMul

variable (ğ•œ) [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E] [SMul ğ•œ E]

/-- A set `B` is an extreme subset of `A` if `B âŠ† A` and all points of `B` only belong to open
segments whose ends are in `B`.

Our definition only requires that the left endpoint of the segment lies in `B`,
but by symmetry of open segments, the right endpoint must also lie in `B`.
See `IsExtreme.right_mem_of_mem_openSegment`. -/
@[mk_iff]
structure IsExtreme (A B : Set E) : Prop where
  subset : B âŠ† A
  left_mem_of_mem_openSegment : âˆ€ â¦ƒxâ¦„, x âˆˆ A â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ A â†’
    âˆ€ â¦ƒzâ¦„, z âˆˆ B â†’ z âˆˆ openSegment ğ•œ x y â†’ x âˆˆ B

/-- A point `x` is an extreme point of a set `A` if `x` belongs to no open segment with ends in
`A`, except for the obvious `openSegment x x`. -/
def Set.extremePoints (A : Set E) : Set E :=
  {x âˆˆ A | âˆ€ â¦ƒxâ‚â¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚â¦„, xâ‚‚ âˆˆ A â†’ x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x}

@[refl]
protected theorem IsExtreme.refl (A : Set E) : IsExtreme ğ•œ A A :=
  âŸ¨Subset.rfl, fun _ hxâ‚A _ _ _ _ _ â†¦ hxâ‚AâŸ©

variable {ğ•œ} {A B C : Set E} {x : E}

protected theorem IsExtreme.rfl : IsExtreme ğ•œ A A :=
  IsExtreme.refl ğ•œ A

theorem IsExtreme.right_mem_of_mem_openSegment (h : IsExtreme ğ•œ A B) {y z : E} (hx : x âˆˆ A)
    (hy : y âˆˆ A) (hz : z âˆˆ B) (hzxy : z âˆˆ openSegment ğ•œ x y) : y âˆˆ B :=
  h.left_mem_of_mem_openSegment hy hx hz <| by rwa [openSegment_symm]

@[trans]
protected theorem IsExtreme.trans (hAB : IsExtreme ğ•œ A B) (hBC : IsExtreme ğ•œ B C) :
    IsExtreme ğ•œ A C := by
  refine âŸ¨hBC.subset.trans hAB.subset, fun xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxC hx â†¦ ?_âŸ©
  exact hBC.left_mem_of_mem_openSegment
    (hAB.left_mem_of_mem_openSegment hxâ‚A hxâ‚‚A (hBC.subset hxC) hx)
    (hAB.right_mem_of_mem_openSegment hxâ‚A hxâ‚‚A (hBC.subset hxC) hx) hxC hx

protected theorem IsExtreme.antisymm : AntiSymmetric (IsExtreme ğ•œ : Set E â†’ Set E â†’ Prop) :=
  fun _ _ hAB hBA â†¦ Subset.antisymm hBA.1 hAB.1

instance : IsPartialOrder (Set E) (IsExtreme ğ•œ) where
  refl := IsExtreme.refl ğ•œ
  trans _ _ _ := IsExtreme.trans
  antisymm := IsExtreme.antisymm

theorem IsExtreme.inter (hAB : IsExtreme ğ•œ A B) (hAC : IsExtreme ğ•œ A C) :
    IsExtreme ğ•œ A (B âˆ© C) := by
  use Subset.trans inter_subset_left hAB.1
  rintro xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x âŸ¨hxB, hxCâŸ© hx
  exact âŸ¨hAB.left_mem_of_mem_openSegment hxâ‚A hxâ‚‚A hxB hx,
    hAC.left_mem_of_mem_openSegment hxâ‚A hxâ‚‚A hxC hxâŸ©

protected theorem IsExtreme.mono (hAC : IsExtreme ğ•œ A C) (hBA : B âŠ† A) (hCB : C âŠ† B) :
    IsExtreme ğ•œ B C :=
  âŸ¨hCB, fun _ hxâ‚B _ hxâ‚‚B _ hxC hx â†¦ hAC.2 (hBA hxâ‚B) (hBA hxâ‚‚B) hxC hxâŸ©

theorem isExtreme_iInter {Î¹ : Sort*} [Nonempty Î¹] {F : Î¹ â†’ Set E}
    (hAF : âˆ€ i : Î¹, IsExtreme ğ•œ A (F i)) : IsExtreme ğ•œ A (â‹‚ i : Î¹, F i) := by
  inhabit Î¹
  refine âŸ¨iInter_subset_of_subset default (hAF default).1, fun xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxF hx â†¦ ?_âŸ©
  rw [mem_iInter] at hxF âŠ¢
  exact fun i â†¦ (hAF i).2 hxâ‚A hxâ‚‚A (hxF i) hx

theorem isExtreme_biInter {F : Set (Set E)} (hF : F.Nonempty) (hA : âˆ€ B âˆˆ F, IsExtreme ğ•œ A B) :
    IsExtreme ğ•œ A (â‹‚ B âˆˆ F, B) := by
  haveI := hF.to_subtype
  simpa only [iInter_subtype] using isExtreme_iInter fun i : F â†¦ hA _ i.2

theorem isExtreme_sInter {F : Set (Set E)} (hF : F.Nonempty) (hAF : âˆ€ B âˆˆ F, IsExtreme ğ•œ A B) :
    IsExtreme ğ•œ A (â‹‚â‚€ F) := by simpa [sInter_eq_biInter] using isExtreme_biInter hF hAF

/-- A point `x` is an extreme point of a set `A`
iff `x âˆˆ A` and for any `xâ‚`, `xâ‚‚` such that `x` belongs to the open segment `(xâ‚, xâ‚‚)`,
we have `xâ‚ = x` and `xâ‚‚ = x`.

We used to use the RHS as the definition of `extremePoints`.
However, the conclusion `xâ‚‚ = x` is redundant,
so we changed the definition to the RHS of `mem_extremePoints_iff_left`. -/
theorem mem_extremePoints : x âˆˆ A.extremePoints ğ•œ â†”
    x âˆˆ A âˆ§ âˆ€áµ‰ (xâ‚ âˆˆ A) (xâ‚‚ âˆˆ A), x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x := by
  refine âŸ¨fun h â†¦ âŸ¨h.1, fun xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx â†¦ âŸ¨h.2 hxâ‚ hxâ‚‚ hx, ?_âŸ©âŸ©,
    fun h â†¦ âŸ¨h.1, fun xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx â†¦ (h.2 xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx).1âŸ©âŸ©
  apply h.2 hxâ‚‚ hxâ‚
  rwa [openSegment_symm]

/-- A point `x` is an extreme point of a set `A`
iff `x âˆˆ A` and for any `xâ‚`, `xâ‚‚` such that `x` belongs to the open segment `(xâ‚, xâ‚‚)`,
we have `xâ‚ = x`. -/
theorem mem_extremePoints_iff_left : x âˆˆ A.extremePoints ğ•œ â†”
    x âˆˆ A âˆ§ âˆ€ xâ‚ âˆˆ A, âˆ€ xâ‚‚ âˆˆ A, x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x :=
  .rfl

/-- x is an extreme point to A iff {x} is an extreme set of A. -/
@[simp] lemma isExtreme_singleton : IsExtreme ğ•œ A {x} â†” x âˆˆ A.extremePoints ğ•œ := by
  simp [isExtreme_iff, extremePoints]

alias âŸ¨IsExtreme.mem_extremePoints, _âŸ© := isExtreme_singleton

theorem extremePoints_subset : A.extremePoints ğ•œ âŠ† A :=
  fun _ hx â†¦ hx.1

@[simp]
theorem extremePoints_empty : (âˆ… : Set E).extremePoints ğ•œ = âˆ… :=
  subset_empty_iff.1 extremePoints_subset

@[simp]
theorem extremePoints_singleton : ({x} : Set E).extremePoints ğ•œ = {x} :=
  extremePoints_subset.antisymm <| singleton_subset_iff.2 âŸ¨mem_singleton x, fun _ hxâ‚ _ _ _ â†¦ hxâ‚âŸ©

theorem inter_extremePoints_subset_extremePoints_of_subset (hBA : B âŠ† A) :
    B âˆ© A.extremePoints ğ•œ âŠ† B.extremePoints ğ•œ :=
  fun _ âŸ¨hxB, hxAâŸ© â†¦ âŸ¨hxB, fun _ hxâ‚ _ hxâ‚‚ hx â†¦ hxA.2 (hBA hxâ‚) (hBA hxâ‚‚) hxâŸ©

theorem IsExtreme.extremePoints_subset_extremePoints (hAB : IsExtreme ğ•œ A B) :
    B.extremePoints ğ•œ âŠ† A.extremePoints ğ•œ :=
  fun _ â†¦ by simpa only [â† isExtreme_singleton] using hAB.trans

theorem IsExtreme.extremePoints_eq (hAB : IsExtreme ğ•œ A B) :
    B.extremePoints ğ•œ = B âˆ© A.extremePoints ğ•œ :=
  Subset.antisymm (fun _ hx â†¦ âŸ¨hx.1, hAB.extremePoints_subset_extremePoints hxâŸ©)
    (inter_extremePoints_subset_extremePoints_of_subset hAB.1)

end SMul

section OrderedSemiring

variable [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommGroup E] [AddCommGroup F] [âˆ€ i, AddCommGroup (M i)]
  [Module ğ•œ E] [Module ğ•œ F] [âˆ€ i, Module ğ•œ (M i)] {A B : Set E}

theorem IsExtreme.convex_diff [IsOrderedRing ğ•œ] (hA : Convex ğ•œ A) (hAB : IsExtreme ğ•œ A B) :
    Convex ğ•œ (A \ B) :=
  convex_iff_openSegment_subset.2 fun _ âŸ¨hxâ‚A, hxâ‚BâŸ© _ âŸ¨hxâ‚‚A, _âŸ© _ hx â†¦
    âŸ¨hA.openSegment_subset hxâ‚A hxâ‚‚A hx, fun hxB â†¦ hxâ‚B (hAB.2 hxâ‚A hxâ‚‚A hxB hx)âŸ©

@[simp]
theorem extremePoints_prod (s : Set E) (t : Set F) :
    (s Ã—Ë¢ t).extremePoints ğ•œ = s.extremePoints ğ•œ Ã—Ë¢ t.extremePoints ğ•œ := by
  ext âŸ¨x, yâŸ©
  refine (and_congr_right fun hx â†¦ âŸ¨fun h â†¦ âŸ¨?_, ?_âŸ©, fun h â†¦ ?_âŸ©).trans and_and_and_comm
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ âŸ¨a, b, ha, hb, hab, hx'âŸ©
    ext
    Â· exact h.1 hxâ‚.1 hxâ‚‚.1 âŸ¨a, b, ha, hb, hab, congrArg Prod.fst hx'âŸ©
    Â· exact h.2 hxâ‚.2 hxâ‚‚.2 âŸ¨a, b, ha, hb, hab, congrArg Prod.snd hx'âŸ©
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx_fst
    refine congrArg Prod.fst (h (mk_mem_prod hxâ‚ hx.2) (mk_mem_prod hxâ‚‚ hx.2) ?_)
    rw [â† Prod.image_mk_openSegment_left]
    exact mem_image_of_mem _ hx_fst
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx_snd
    refine congrArg Prod.snd (h (mk_mem_prod hx.1 hxâ‚) (mk_mem_prod hx.1 hxâ‚‚) ?_)
    rw [â† Prod.image_mk_openSegment_right]
    exact mem_image_of_mem _ hx_snd

@[simp]
theorem extremePoints_pi (s : âˆ€ i, Set (M i)) :
    (univ.pi s).extremePoints ğ•œ = univ.pi fun i â†¦ (s i).extremePoints ğ•œ := by
  classical
  ext x
  simp only [mem_extremePoints_iff_left, mem_univ_pi, @forall_and Î¹]
  refine and_congr_right fun hx â†¦ âŸ¨fun h i â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hi
    rw [â† update_self i xâ‚ x, h (update x i xâ‚) _ (update x i xâ‚‚)]
    Â· rintro j
      obtain rfl | hji := eq_or_ne j i <;> simp [*]
    Â· rw [â† Pi.image_update_openSegment]
      exact âŸ¨_, hi, update_eq_self _ _âŸ©
    Â· rintro j
      obtain rfl | hji := eq_or_ne j i <;> simp [*]
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ âŸ¨a, b, ha, hb, hab, rflâŸ©
    ext i
    exact h _ _ (hxâ‚ _) _ (hxâ‚‚ _) âŸ¨a, b, ha, hb, hab, rflâŸ©

end OrderedSemiring

section OrderedRing
variable {L : Type*} [Ring ğ•œ] [PartialOrder ğ•œ] [IsOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]
  [EquivLike L E F] [LinearEquivClass L ğ•œ E F]

lemma image_extremePoints (f : L) (s : Set E) :
    f '' extremePoints ğ•œ s = extremePoints ğ•œ (f '' s) := by
  ext b
  obtain âŸ¨a, rflâŸ© := EquivLike.surjective f b
  have : âˆ€ x y, f '' openSegment ğ•œ x y = openSegment ğ•œ (f x) (f y) :=
    image_openSegment _ (LinearMapClass.linearMap f).toAffineMap
  simp only [mem_extremePoints, (EquivLike.surjective f).forall,
    (EquivLike.injective f).mem_set_image, (EquivLike.injective f).eq_iff, â† this]

end OrderedRing

section LinearOrderedRing

variable [Ring ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [DenselyOrdered ğ•œ] [NoZeroSMulDivisors ğ•œ E] {A : Set E} {x : E}

/-- A useful restatement using `segment`: `x` is an extreme point iff the only (closed) segments
that contain it are those with `x` as one of their endpoints. -/
theorem mem_extremePoints_iff_forall_segment : x âˆˆ A.extremePoints ğ•œ â†”
    x âˆˆ A âˆ§ âˆ€áµ‰ (xâ‚ âˆˆ A) (xâ‚‚ âˆˆ A), x âˆˆ segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ¨ xâ‚‚ = x := by
  rw [mem_extremePoints]
  refine and_congr_right fun hxA â†¦ forallâ‚„_congr fun xâ‚ hâ‚ xâ‚‚ hâ‚‚ â†¦ ?_
  constructor
  Â· rw [â† insert_endpoints_openSegment]
    rintro H (rfl | rfl | hx)
    exacts [Or.inl rfl, Or.inr rfl, Or.inl <| (H hx).1]
  Â· intro H hx
    rcases H (openSegment_subset_segment _ _ _ hx) with (rfl | rfl)
    exacts [âŸ¨rfl, (left_mem_openSegment_iff.1 hx).symmâŸ©, âŸ¨right_mem_openSegment_iff.1 hx, rflâŸ©]

theorem Convex.mem_extremePoints_iff_convex_diff (hA : Convex ğ•œ A) :
    x âˆˆ A.extremePoints ğ•œ â†” x âˆˆ A âˆ§ Convex ğ•œ (A \ {x}) := by
  use fun hx â†¦ âŸ¨hx.1, (isExtreme_singleton.2 hx).convex_diff hAâŸ©
  rintro âŸ¨hxA, hAxâŸ©
  refine mem_extremePoints_iff_forall_segment.2 âŸ¨hxA, fun xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx â†¦ ?_âŸ©
  rw [convex_iff_segment_subset] at hAx
  by_contra! h
  exact (hAx âŸ¨hxâ‚, fun hxâ‚ â†¦ h.1 (mem_singleton_iff.2 hxâ‚)âŸ©
      âŸ¨hxâ‚‚, fun hxâ‚‚ â†¦ h.2 (mem_singleton_iff.2 hxâ‚‚)âŸ© hx).2 rfl

theorem Convex.mem_extremePoints_iff_mem_diff_convexHull_diff (hA : Convex ğ•œ A) :
    x âˆˆ A.extremePoints ğ•œ â†” x âˆˆ A \ convexHull ğ•œ (A \ {x}) := by
  rw [hA.mem_extremePoints_iff_convex_diff, hA.convex_remove_iff_notMem_convexHull_remove,
    mem_diff]

theorem extremePoints_convexHull_subset : (convexHull ğ•œ A).extremePoints ğ•œ âŠ† A := by
  rintro x hx
  rw [(convex_convexHull ğ•œ _).mem_extremePoints_iff_convex_diff] at hx
  by_contra h
  exact (convexHull_min (subset_diff.2 âŸ¨subset_convexHull ğ•œ _, disjoint_singleton_right.2 hâŸ©) hx.2
    hx.1).2 rfl

end LinearOrderedRing
