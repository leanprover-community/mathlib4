/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.Convex.Hull

#align_import analysis.convex.extreme from "leanprover-community/mathlib"@"c5773405394e073885e2a144c9ca14637e8eb963"

/-!
# Extreme sets

This file defines extreme sets and extreme points for sets in a module.

An extreme set of `A` is a subset of `A` that is as far as it can get in any outward direction: If
point `x` is in it and point `y âˆˆ A`, then the line passing through `x` and `y` leaves `A` at `x`.
This is an analytic notion of "being on the side of". It is weaker than being exposed (see
`IsExposed.isExtreme`).

## Main declarations

* `IsExtreme ğ•œ A B`: States that `B` is an extreme set of `A` (in the literature, `A` is often
  implicit).
* `Set.extremePoints ğ•œ A`: Set of extreme points of `A` (corresponding to extreme singletons).
* `Convex.mem_extremePoints_iff_convex_diff`: A useful equivalent condition to being an extreme
  point: `x` is an extreme point iff `A \ {x}` is convex.

## Implementation notes

The exact definition of extremeness has been carefully chosen so as to make as many lemmas
unconditional (in particular, the Krein-Milman theorem doesn't need the set to be convex!).
In practice, `A` is often assumed to be a convex set.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

## TODO

Prove lemmas relating extreme sets and points to the intrinsic frontier.

More not-yet-PRed stuff is available on the mathlib3 branch `sperner_again`.
-/


open Function Set

open Affine Classical

variable {ğ•œ E F Î¹ : Type*} {Ï€ : Î¹ â†’ Type*}

section SMul

variable (ğ•œ) [OrderedSemiring ğ•œ] [AddCommMonoid E] [SMul ğ•œ E]

/-- A set `B` is an extreme subset of `A` if `B âŠ† A` and all points of `B` only belong to open
segments whose ends are in `B`. -/
def IsExtreme (A B : Set E) : Prop :=
  B âŠ† A âˆ§ âˆ€ â¦ƒxâ‚â¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚â¦„, xâ‚‚ âˆˆ A â†’ âˆ€ â¦ƒxâ¦„, x âˆˆ B â†’ x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ âˆˆ B âˆ§ xâ‚‚ âˆˆ B
#align is_extreme IsExtreme

/-- A point `x` is an extreme point of a set `A` if `x` belongs to no open segment with ends in
`A`, except for the obvious `openSegment x x`. -/
def Set.extremePoints (A : Set E) : Set E :=
  { x âˆˆ A | âˆ€ â¦ƒxâ‚â¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚â¦„, xâ‚‚ âˆˆ A â†’ x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x }
#align set.extreme_points Set.extremePoints

@[refl]
protected theorem IsExtreme.refl (A : Set E) : IsExtreme ğ•œ A A :=
  âŸ¨Subset.rfl, fun _ hxâ‚A _ hxâ‚‚A _ _ _ â†¦ âŸ¨hxâ‚A, hxâ‚‚AâŸ©âŸ©
#align is_extreme.refl IsExtreme.refl

variable {ğ•œ} {A B C : Set E} {x : E}

protected theorem IsExtreme.rfl : IsExtreme ğ•œ A A :=
  IsExtreme.refl ğ•œ A
#align is_extreme.rfl IsExtreme.rfl

@[trans]
protected theorem IsExtreme.trans (hAB : IsExtreme ğ•œ A B) (hBC : IsExtreme ğ•œ B C) :
    IsExtreme ğ•œ A C := by
  refine' âŸ¨Subset.trans hBC.1 hAB.1, fun xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxC hx â†¦ _âŸ©
  -- âŠ¢ xâ‚ âˆˆ C âˆ§ xâ‚‚ âˆˆ C
  obtain âŸ¨hxâ‚B, hxâ‚‚BâŸ© := hAB.2 hxâ‚A hxâ‚‚A (hBC.1 hxC) hx
  -- âŠ¢ xâ‚ âˆˆ C âˆ§ xâ‚‚ âˆˆ C
  exact hBC.2 hxâ‚B hxâ‚‚B hxC hx
  -- ğŸ‰ no goals
#align is_extreme.trans IsExtreme.trans

protected theorem IsExtreme.antisymm : AntiSymmetric (IsExtreme ğ•œ : Set E â†’ Set E â†’ Prop) :=
  fun _ _ hAB hBA â†¦ Subset.antisymm hBA.1 hAB.1
#align is_extreme.antisymm IsExtreme.antisymm

instance : IsPartialOrder (Set E) (IsExtreme ğ•œ) where
  refl := IsExtreme.refl ğ•œ
  trans _ _ _ := IsExtreme.trans
  antisymm := IsExtreme.antisymm

theorem IsExtreme.inter (hAB : IsExtreme ğ•œ A B) (hAC : IsExtreme ğ•œ A C) :
    IsExtreme ğ•œ A (B âˆ© C) := by
  use Subset.trans (inter_subset_left _ _) hAB.1
  -- âŠ¢ âˆ€ â¦ƒxâ‚ : Eâ¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚ : Eâ¦„, xâ‚‚ âˆˆ A â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ B âˆ© C â†’ x âˆˆ openSeg â€¦
  rintro xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x âŸ¨hxB, hxCâŸ© hx
  -- âŠ¢ xâ‚ âˆˆ B âˆ© C âˆ§ xâ‚‚ âˆˆ B âˆ© C
  obtain âŸ¨hxâ‚B, hxâ‚‚BâŸ© := hAB.2 hxâ‚A hxâ‚‚A hxB hx
  -- âŠ¢ xâ‚ âˆˆ B âˆ© C âˆ§ xâ‚‚ âˆˆ B âˆ© C
  obtain âŸ¨hxâ‚C, hxâ‚‚CâŸ© := hAC.2 hxâ‚A hxâ‚‚A hxC hx
  -- âŠ¢ xâ‚ âˆˆ B âˆ© C âˆ§ xâ‚‚ âˆˆ B âˆ© C
  exact âŸ¨âŸ¨hxâ‚B, hxâ‚CâŸ©, hxâ‚‚B, hxâ‚‚CâŸ©
  -- ğŸ‰ no goals
#align is_extreme.inter IsExtreme.inter

protected theorem IsExtreme.mono (hAC : IsExtreme ğ•œ A C) (hBA : B âŠ† A) (hCB : C âŠ† B) :
    IsExtreme ğ•œ B C :=
  âŸ¨hCB, fun _ hxâ‚B _ hxâ‚‚B _ hxC hx â†¦ hAC.2 (hBA hxâ‚B) (hBA hxâ‚‚B) hxC hxâŸ©
#align is_extreme.mono IsExtreme.mono

theorem isExtreme_iInter {Î¹ : Sort*} [Nonempty Î¹] {F : Î¹ â†’ Set E}
    (hAF : âˆ€ i : Î¹, IsExtreme ğ•œ A (F i)) : IsExtreme ğ•œ A (â‹‚ i : Î¹, F i) := by
  obtain i := Classical.arbitrary Î¹
  -- âŠ¢ IsExtreme ğ•œ A (â‹‚ (i : Î¹), F i)
  refine' âŸ¨iInter_subset_of_subset i (hAF i).1, fun xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxF hx â†¦ _âŸ©
  -- âŠ¢ xâ‚ âˆˆ â‹‚ (i : Î¹), F i âˆ§ xâ‚‚ âˆˆ â‹‚ (i : Î¹), F i
  simp_rw [mem_iInter] at hxF âŠ¢
  -- âŠ¢ (âˆ€ (i : Î¹), xâ‚ âˆˆ F i) âˆ§ âˆ€ (i : Î¹), xâ‚‚ âˆˆ F i
  have h := fun i â†¦ (hAF i).2 hxâ‚A hxâ‚‚A (hxF i) hx
  -- âŠ¢ (âˆ€ (i : Î¹), xâ‚ âˆˆ F i) âˆ§ âˆ€ (i : Î¹), xâ‚‚ âˆˆ F i
  exact âŸ¨fun i â†¦ (h i).1, fun i â†¦ (h i).2âŸ©
  -- ğŸ‰ no goals
#align is_extreme_Inter isExtreme_iInter

theorem isExtreme_biInter {F : Set (Set E)} (hF : F.Nonempty) (hA : âˆ€ B âˆˆ F, IsExtreme ğ•œ A B) :
    IsExtreme ğ•œ A (â‹‚ B âˆˆ F, B) := by
  haveI := hF.to_subtype
  -- âŠ¢ IsExtreme ğ•œ A (â‹‚ (B : Set E) (_ : B âˆˆ F), B)
  simpa only [iInter_subtype] using isExtreme_iInter fun i : F â†¦ hA _ i.2
  -- ğŸ‰ no goals
#align is_extreme_bInter isExtreme_biInter

theorem isExtreme_sInter {F : Set (Set E)} (hF : F.Nonempty) (hAF : âˆ€ B âˆˆ F, IsExtreme ğ•œ A B) :
    IsExtreme ğ•œ A (â‹‚â‚€ F) := by
  obtain âŸ¨B, hBâŸ© := hF
  -- âŠ¢ IsExtreme ğ•œ A (â‹‚â‚€ F)
  refine' âŸ¨(sInter_subset_of_mem hB).trans (hAF B hB).1, fun xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxF hx â†¦ _âŸ©
  -- âŠ¢ xâ‚ âˆˆ â‹‚â‚€ F âˆ§ xâ‚‚ âˆˆ â‹‚â‚€ F
  simp_rw [mem_sInter] at hxF âŠ¢
  -- âŠ¢ (âˆ€ (t : Set E), t âˆˆ F â†’ xâ‚ âˆˆ t) âˆ§ âˆ€ (t : Set E), t âˆˆ F â†’ xâ‚‚ âˆˆ t
  have h := fun B hB â†¦ (hAF B hB).2 hxâ‚A hxâ‚‚A (hxF B hB) hx
  -- âŠ¢ (âˆ€ (t : Set E), t âˆˆ F â†’ xâ‚ âˆˆ t) âˆ§ âˆ€ (t : Set E), t âˆˆ F â†’ xâ‚‚ âˆˆ t
  exact âŸ¨fun B hB â†¦ (h B hB).1, fun B hB â†¦ (h B hB).2âŸ©
  -- ğŸ‰ no goals
#align is_extreme_sInter isExtreme_sInter

theorem mem_extremePoints : x âˆˆ A.extremePoints ğ•œ â†”
    x âˆˆ A âˆ§ âˆ€ (xâ‚) (_ : xâ‚ âˆˆ A) (xâ‚‚) (_ : xâ‚‚ âˆˆ A), x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x :=
  Iff.rfl
#align mem_extreme_points mem_extremePoints

/-- x is an extreme point to A iff {x} is an extreme set of A. -/
theorem mem_extremePoints_iff_extreme_singleton : x âˆˆ A.extremePoints ğ•œ â†” IsExtreme ğ•œ A {x} := by
  refine' âŸ¨_, fun hx â†¦ âŸ¨singleton_subset_iff.1 hx.1, fun xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ â†¦ hx.2 hxâ‚ hxâ‚‚ rflâŸ©âŸ©
  -- âŠ¢ x âˆˆ extremePoints ğ•œ A â†’ IsExtreme ğ•œ A {x}
  rintro âŸ¨hxA, hAxâŸ©
  -- âŠ¢ IsExtreme ğ•œ A {x}
  use singleton_subset_iff.2 hxA
  -- âŠ¢ âˆ€ â¦ƒxâ‚ : Eâ¦„, xâ‚ âˆˆ A â†’ âˆ€ â¦ƒxâ‚‚ : Eâ¦„, xâ‚‚ âˆˆ A â†’ âˆ€ â¦ƒx_1 : Eâ¦„, x_1 âˆˆ {x} â†’ x_1 âˆˆ ope â€¦
  rintro xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A y (rfl : y = x)
  -- âŠ¢ y âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ âˆˆ {y} âˆ§ xâ‚‚ âˆˆ {y}
  exact hAx hxâ‚A hxâ‚‚A
  -- ğŸ‰ no goals
#align mem_extreme_points_iff_extreme_singleton mem_extremePoints_iff_extreme_singleton

theorem extremePoints_subset : A.extremePoints ğ•œ âŠ† A :=
  fun _ hx â†¦ hx.1
#align extreme_points_subset extremePoints_subset

@[simp]
theorem extremePoints_empty : (âˆ… : Set E).extremePoints ğ•œ = âˆ… :=
  subset_empty_iff.1 extremePoints_subset
#align extreme_points_empty extremePoints_empty

@[simp]
theorem extremePoints_singleton : ({x} : Set E).extremePoints ğ•œ = {x} :=
  extremePoints_subset.antisymm <|
    singleton_subset_iff.2 âŸ¨mem_singleton x, fun _ hxâ‚ _ hxâ‚‚ _ â†¦ âŸ¨hxâ‚, hxâ‚‚âŸ©âŸ©
#align extreme_points_singleton extremePoints_singleton

theorem inter_extremePoints_subset_extremePoints_of_subset (hBA : B âŠ† A) :
    B âˆ© A.extremePoints ğ•œ âŠ† B.extremePoints ğ•œ :=
  fun _ âŸ¨hxB, hxAâŸ© â†¦ âŸ¨hxB, fun _ hxâ‚ _ hxâ‚‚ hx â†¦ hxA.2 (hBA hxâ‚) (hBA hxâ‚‚) hxâŸ©
#align inter_extreme_points_subset_extreme_points_of_subset inter_extremePoints_subset_extremePoints_of_subset

theorem IsExtreme.extremePoints_subset_extremePoints (hAB : IsExtreme ğ•œ A B) :
    B.extremePoints ğ•œ âŠ† A.extremePoints ğ•œ :=
  fun _ hx â†¦ mem_extremePoints_iff_extreme_singleton.2
    (hAB.trans (mem_extremePoints_iff_extreme_singleton.1 hx))
#align is_extreme.extreme_points_subset_extreme_points IsExtreme.extremePoints_subset_extremePoints

theorem IsExtreme.extremePoints_eq (hAB : IsExtreme ğ•œ A B) :
    B.extremePoints ğ•œ = B âˆ© A.extremePoints ğ•œ :=
  Subset.antisymm (fun _ hx â†¦ âŸ¨hx.1, hAB.extremePoints_subset_extremePoints hxâŸ©)
    (inter_extremePoints_subset_extremePoints_of_subset hAB.1)
#align is_extreme.extreme_points_eq IsExtreme.extremePoints_eq

end SMul

section OrderedSemiring

variable [OrderedSemiring ğ•œ] [AddCommGroup E] [AddCommGroup F] [âˆ€ i, AddCommGroup (Ï€ i)]
  [Module ğ•œ E] [Module ğ•œ F] [âˆ€ i, Module ğ•œ (Ï€ i)] {A B : Set E} {x : E}

theorem IsExtreme.convex_diff (hA : Convex ğ•œ A) (hAB : IsExtreme ğ•œ A B) : Convex ğ•œ (A \ B) :=
  convex_iff_openSegment_subset.2 fun _ âŸ¨hxâ‚A, hxâ‚BâŸ© _ âŸ¨hxâ‚‚A, _âŸ© _ hx â†¦
    âŸ¨hA.openSegment_subset hxâ‚A hxâ‚‚A hx, fun hxB â†¦ hxâ‚B (hAB.2 hxâ‚A hxâ‚‚A hxB hx).1âŸ©
#align is_extreme.convex_diff IsExtreme.convex_diff

@[simp]
theorem extremePoints_prod (s : Set E) (t : Set F) :
    (s Ã—Ë¢ t).extremePoints ğ•œ = s.extremePoints ğ•œ Ã—Ë¢ t.extremePoints ğ•œ := by
  ext
  -- âŠ¢ xâœ âˆˆ extremePoints ğ•œ (s Ã—Ë¢ t) â†” xâœ âˆˆ extremePoints ğ•œ s Ã—Ë¢ extremePoints ğ•œ t
  refine' (and_congr_right fun hx â†¦ âŸ¨fun h â†¦ _, fun h â†¦ _âŸ©).trans and_and_and_comm
  -- âŠ¢ (âˆ€ â¦ƒxâ‚ : Eâ¦„, xâ‚ âˆˆ s â†’ âˆ€ â¦ƒxâ‚‚ : Eâ¦„, xâ‚‚ âˆˆ s â†’ xâœ.fst âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ â€¦
  constructor
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx_fst
    -- âŠ¢ xâ‚ = xâœ.fst âˆ§ xâ‚‚ = xâœ.fst
    refine' (h (mk_mem_prod hxâ‚ hx.2) (mk_mem_prod hxâ‚‚ hx.2) _).imp (congr_arg Prod.fst)
        (congr_arg Prod.fst)
    rw [â† Prod.image_mk_openSegment_left]
    -- âŠ¢ xâœ âˆˆ (fun x => (x, xâœ.snd)) '' openSegment ğ•œ xâ‚ xâ‚‚
    exact âŸ¨_, hx_fst, Prod.mk.etaâŸ©
    -- ğŸ‰ no goals
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx_snd
    -- âŠ¢ xâ‚ = xâœ.snd âˆ§ xâ‚‚ = xâœ.snd
    refine' (h (mk_mem_prod hx.1 hxâ‚) (mk_mem_prod hx.1 hxâ‚‚) _).imp (congr_arg Prod.snd)
        (congr_arg Prod.snd)
    rw [â† Prod.image_mk_openSegment_right]
    -- âŠ¢ xâœ âˆˆ (fun y => (xâœ.fst, y)) '' openSegment ğ•œ xâ‚ xâ‚‚
    exact âŸ¨_, hx_snd, Prod.mk.etaâŸ©
    -- ğŸ‰ no goals
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ âŸ¨a, b, ha, hb, hab, hx'âŸ©
    -- âŠ¢ xâ‚ = xâœ âˆ§ xâ‚‚ = xâœ
    simp_rw [Prod.ext_iff]
    -- âŠ¢ (xâ‚.fst = xâœ.fst âˆ§ xâ‚.snd = xâœ.snd) âˆ§ xâ‚‚.fst = xâœ.fst âˆ§ xâ‚‚.snd = xâœ.snd
    exact and_and_and_comm.1
        âŸ¨h.1 hxâ‚.1 hxâ‚‚.1 âŸ¨a, b, ha, hb, hab, congr_arg Prod.fst hx'âŸ©,
          h.2 hxâ‚.2 hxâ‚‚.2 âŸ¨a, b, ha, hb, hab, congr_arg Prod.snd hx'âŸ©âŸ©
#align extreme_points_prod extremePoints_prod

@[simp]
theorem extremePoints_pi (s : âˆ€ i, Set (Ï€ i)) :
    (univ.pi s).extremePoints ğ•œ = univ.pi fun i â†¦ (s i).extremePoints ğ•œ := by
  ext x
  -- âŠ¢ x âˆˆ extremePoints ğ•œ (pi univ s) â†” x âˆˆ pi univ fun i => extremePoints ğ•œ (s i)
  simp only [mem_extremePoints, mem_pi, mem_univ, true_imp_iff, @forall_and Î¹]
  -- âŠ¢ ((âˆ€ (i : Î¹), x i âˆˆ s i) âˆ§ âˆ€ (xâ‚ : (i : Î¹) â†’ Ï€ i), (âˆ€ (i : Î¹), xâ‚ i âˆˆ s i) â†’  â€¦
  refine' and_congr_right fun hx â†¦ âŸ¨fun h i â†¦ _, fun h â†¦ _âŸ©
  -- âŠ¢ âˆ€ (xâ‚ : Ï€ i), xâ‚ âˆˆ s i â†’ âˆ€ (xâ‚‚ : Ï€ i), xâ‚‚ âˆˆ s i â†’ x i âˆˆ openSegment ğ•œ xâ‚ xâ‚‚  â€¦
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hi
    -- âŠ¢ xâ‚ = x i âˆ§ xâ‚‚ = x i
    refine' (h (update x i xâ‚) _ (update x i xâ‚‚) _ _).imp (fun hâ‚ â†¦ by rw [â† hâ‚, update_same])
        fun hâ‚‚ â†¦ by rw [â† hâ‚‚, update_same]
    iterate 2
      rintro j
      obtain rfl | hji := eq_or_ne j i
      Â· rwa [update_same]
      Â· rw [update_noteq hji]
        exact hx _
    rw [â† Pi.image_update_openSegment]
    -- âŠ¢ x âˆˆ update x i '' openSegment ğ•œ xâ‚ xâ‚‚
    exact âŸ¨_, hi, update_eq_self _ _âŸ©
    -- ğŸ‰ no goals
  Â· rintro xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ âŸ¨a, b, ha, hb, hab, hx'âŸ©
    -- âŠ¢ xâ‚ = x âˆ§ xâ‚‚ = x
    simp_rw [funext_iff, â† forall_and]
    -- âŠ¢ âˆ€ (x_1 : Î¹), xâ‚ x_1 = x x_1 âˆ§ xâ‚‚ x_1 = x x_1
    exact fun i â†¦ h _ _ (hxâ‚ _) _ (hxâ‚‚ _) âŸ¨a, b, ha, hb, hab, congr_fun hx' _âŸ©
    -- ğŸ‰ no goals
#align extreme_points_pi extremePoints_pi

end OrderedSemiring

section LinearOrderedRing

variable [LinearOrderedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E]

variable [DenselyOrdered ğ•œ] [NoZeroSMulDivisors ğ•œ E] {A B : Set E} {x : E}

/-- A useful restatement using `segment`: `x` is an extreme point iff the only (closed) segments
that contain it are those with `x` as one of their endpoints. -/
theorem mem_extremePoints_iff_forall_segment : x âˆˆ A.extremePoints ğ•œ â†”
    x âˆˆ A âˆ§ âˆ€ (xâ‚) (_ : xâ‚ âˆˆ A) (xâ‚‚) (_ : xâ‚‚ âˆˆ A), x âˆˆ segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ¨ xâ‚‚ = x := by
  refine' and_congr_right fun hxA â†¦ forallâ‚„_congr fun xâ‚ hâ‚ xâ‚‚ hâ‚‚ â†¦ _
  -- âŠ¢ x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x â†” x âˆˆ segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ¨ x â€¦
  constructor
  -- âŠ¢ (x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x) â†’ x âˆˆ segment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ¨ â€¦
  Â· rw [â† insert_endpoints_openSegment]
    -- âŠ¢ (x âˆˆ openSegment ğ•œ xâ‚ xâ‚‚ â†’ xâ‚ = x âˆ§ xâ‚‚ = x) â†’ x âˆˆ insert xâ‚ (insert xâ‚‚ (open â€¦
    rintro H (rfl | rfl | hx)
    exacts [Or.inl rfl, Or.inr rfl, Or.inl <| (H hx).1]
    -- ğŸ‰ no goals
  Â· intro H hx
    -- âŠ¢ xâ‚ = x âˆ§ xâ‚‚ = x
    rcases H (openSegment_subset_segment _ _ _ hx) with (rfl | rfl)
    -- âŠ¢ xâ‚ = xâ‚ âˆ§ xâ‚‚ = xâ‚
    exacts [âŸ¨rfl, (left_mem_openSegment_iff.1 hx).symmâŸ©, âŸ¨right_mem_openSegment_iff.1 hx, rflâŸ©]
    -- ğŸ‰ no goals
#align mem_extreme_points_iff_forall_segment mem_extremePoints_iff_forall_segment

theorem Convex.mem_extremePoints_iff_convex_diff (hA : Convex ğ•œ A) :
    x âˆˆ A.extremePoints ğ•œ â†” x âˆˆ A âˆ§ Convex ğ•œ (A \ {x}) := by
  use fun hx â†¦ âŸ¨hx.1, (mem_extremePoints_iff_extreme_singleton.1 hx).convex_diff hAâŸ©
  -- âŠ¢ x âˆˆ A âˆ§ Convex ğ•œ (A \ {x}) â†’ x âˆˆ extremePoints ğ•œ A
  rintro âŸ¨hxA, hAxâŸ©
  -- âŠ¢ x âˆˆ extremePoints ğ•œ A
  refine' mem_extremePoints_iff_forall_segment.2 âŸ¨hxA, fun xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ hx â†¦ _âŸ©
  -- âŠ¢ xâ‚ = x âˆ¨ xâ‚‚ = x
  rw [convex_iff_segment_subset] at hAx
  -- âŠ¢ xâ‚ = x âˆ¨ xâ‚‚ = x
  by_contra' h
  -- âŠ¢ False
  exact (hAx âŸ¨hxâ‚, fun hxâ‚ â†¦ h.1 (mem_singleton_iff.2 hxâ‚)âŸ©
      âŸ¨hxâ‚‚, fun hxâ‚‚ â†¦ h.2 (mem_singleton_iff.2 hxâ‚‚)âŸ© hx).2 rfl
#align convex.mem_extreme_points_iff_convex_diff Convex.mem_extremePoints_iff_convex_diff

theorem Convex.mem_extremePoints_iff_mem_diff_convexHull_diff (hA : Convex ğ•œ A) :
    x âˆˆ A.extremePoints ğ•œ â†” x âˆˆ A \ convexHull ğ•œ (A \ {x}) := by
  rw [hA.mem_extremePoints_iff_convex_diff, hA.convex_remove_iff_not_mem_convexHull_remove,
    mem_diff]
#align convex.mem_extreme_points_iff_mem_diff_convex_hull_diff Convex.mem_extremePoints_iff_mem_diff_convexHull_diff

theorem extremePoints_convexHull_subset : (convexHull ğ•œ A).extremePoints ğ•œ âŠ† A := by
  rintro x hx
  -- âŠ¢ x âˆˆ A
  rw [(convex_convexHull ğ•œ _).mem_extremePoints_iff_convex_diff] at hx
  -- âŠ¢ x âˆˆ A
  by_contra h
  -- âŠ¢ False
  exact (convexHull_min (subset_diff.2 âŸ¨subset_convexHull ğ•œ _, disjoint_singleton_right.2 hâŸ©) hx.2
    hx.1).2 rfl
#align extreme_points_convex_hull_subset extremePoints_convexHull_subset

end LinearOrderedRing
