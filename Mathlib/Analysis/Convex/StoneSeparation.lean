/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Analysis.Convex.Join

/-!
# Stone's separation theorem

This file proves Stone's separation theorem. This tells us that any two disjoint convex sets can be
separated by a convex set whose complement is also convex.

In locally convex real topological vector spaces, the Hahn-Banach separation theorems provide
stronger statements: one may find a separating hyperplane, instead of merely a convex set whose
complement is convex.
-/


open Set

variable {ğ•œ E : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] {s t : Set E}

/-- In a tetrahedron with vertices `x`, `y`, `p`, `q`, any segment `[u, v]` joining the opposite
edges `[x, p]` and `[y, q]` passes through any triangle of vertices `p`, `q`, `z` where
`z âˆˆ [x, y]`. -/
theorem not_disjoint_segment_convexHull_triple {p q u v x y z : E} (hz : z âˆˆ segment ğ•œ x y)
    (hu : u âˆˆ segment ğ•œ x p) (hv : v âˆˆ segment ğ•œ y q) :
    Â¬Disjoint (segment ğ•œ u v) (convexHull ğ•œ {p, q, z}) := by
  rw [not_disjoint_iff]
  obtain âŸ¨az, bz, haz, hbz, habz, rflâŸ© := hz
  obtain rfl | haz' := haz.eq_or_lt
  Â· rw [zero_add] at habz
    rw [zero_smul, zero_add, habz, one_smul]
    refine âŸ¨v, by apply right_mem_segment, segment_subset_convexHull ?_ ?_ hvâŸ© <;> simp
  obtain âŸ¨av, bv, hav, hbv, habv, rflâŸ© := hv
  obtain rfl | hav' := hav.eq_or_lt
  Â· rw [zero_add] at habv
    rw [zero_smul, zero_add, habv, one_smul]
    exact âŸ¨q, right_mem_segment _ _ _, subset_convexHull _ _ <| by simpâŸ©
  obtain âŸ¨au, bu, hau, hbu, habu, rflâŸ© := hu
  have hab : 0 < az * av + bz * au := by positivity
  refine âŸ¨(az * av / (az * av + bz * au)) â€¢ (au â€¢ x + bu â€¢ p) +
    (bz * au / (az * av + bz * au)) â€¢ (av â€¢ y + bv â€¢ q), âŸ¨_, _, ?_, ?_, ?_, rflâŸ©, ?_âŸ©
  Â· positivity
  Â· positivity
  Â· rw [â† add_div, div_self]; positivity
  classical
    let w : Fin 3 â†’ ğ•œ := ![az * av * bu, bz * au * bv, au * av]
    let z : Fin 3 â†’ E := ![p, q, az â€¢ x + bz â€¢ y]
    have hwâ‚€ : âˆ€ i, 0 â‰¤ w i := by
      rintro i
      fin_cases i
      Â· exact mul_nonneg (mul_nonneg haz hav) hbu
      Â· exact mul_nonneg (mul_nonneg hbz hau) hbv
      Â· exact mul_nonneg hau hav
    have hw : âˆ‘ i, w i = az * av + bz * au := by
      trans az * av * bu + (bz * au * bv + au * av)
      Â· simp [w, Fin.sum_univ_succ, Fin.sum_univ_zero]
      linear_combination (au * bv - 1 * au) * habz + (-(1 * az * au) + au) * habv + az * av * habu
    have hz : âˆ€ i, z i âˆˆ ({p, q, az â€¢ x + bz â€¢ y} : Set E) := fun i => by fin_cases i <;> simp [z]
    convert (Finset.centerMass_mem_convexHull (Finset.univ : Finset (Fin 3)) (fun i _ => hwâ‚€ i)
        (by rwa [hw]) fun i _ => hz i : Finset.univ.centerMass w z âˆˆ _)
    rw [Finset.centerMass, hw]
    trans (az * av + bz * au)â»Â¹ â€¢
      ((az * av * bu) â€¢ p + ((bz * au * bv) â€¢ q + (au * av) â€¢ (az â€¢ x + bz â€¢ y)))
    Â· module
    congr 3
    simp [w, z]

/-- **Stone's Separation Theorem** -/
theorem exists_convex_convex_compl_subset (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) (hst : Disjoint s t) :
    âˆƒ C : Set E, Convex ğ•œ C âˆ§ Convex ğ•œ Cá¶œ âˆ§ s âŠ† C âˆ§ t âŠ† Cá¶œ := by
  let S : Set (Set E) := { C | Convex ğ•œ C âˆ§ Disjoint C t }
  obtain âŸ¨C, hsC, hmaxâŸ© :=
    zorn_subset_nonempty S
      (fun c hcS hc âŸ¨_, _âŸ© =>
        âŸ¨â‹ƒâ‚€ c,
          âŸ¨hc.directedOn.convex_sUnion fun s hs => (hcS hs).1,
            disjoint_sUnion_left.2 fun c hc => (hcS hc).2âŸ©,
          fun s => subset_sUnion_of_memâŸ©)
      s âŸ¨hs, hstâŸ©
  obtain hC : _ âˆ§ _ := hmax.prop
  refine
    âŸ¨C, hC.1, convex_iff_segment_subset.2 fun x hx y hy z hz hzC => ?_, hsC, hC.2.subset_compl_leftâŸ©
  suffices h : âˆ€ c âˆˆ Cá¶œ, âˆƒ a âˆˆ C, (segment ğ•œ c a âˆ© t).Nonempty by
    obtain âŸ¨p, hp, u, hu, hutâŸ© := h x hx
    obtain âŸ¨q, hq, v, hv, hvtâŸ© := h y hy
    refine
      not_disjoint_segment_convexHull_triple hz hu hv
        (hC.2.symm.mono (ht.segment_subset hut hvt) <| convexHull_min ?_ hC.1)
    simp [insert_subset_iff, hp, hq, singleton_subset_iff.2 hzC]
  rintro c hc
  by_contra! h
  suffices h : Disjoint (convexHull ğ•œ (insert c C)) t by
    rw [hmax.eq_of_subset âŸ¨convex_convexHull _ _, hâŸ© <|
      (subset_insert ..).trans <| subset_convexHull ..] at hc
    exact hc (subset_convexHull _ _ <| mem_insert _ _)
  rw [convexHull_insert âŸ¨z, hzCâŸ©, convexJoin_singleton_left]
  refine disjoint_iUnionâ‚‚_left.2 fun a ha => disjoint_iff_inter_eq_empty.2 (h a ?_)
  rwa [â† hC.1.convexHull_eq]
