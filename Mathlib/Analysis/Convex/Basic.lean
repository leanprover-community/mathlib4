/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov, YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.BigOperators.Ring.Finset
import Mathlib.Algebra.Order.Module.Synonym
import Mathlib.Algebra.Ring.Action.Pointwise.Set
import Mathlib.Analysis.Convex.Star
import Mathlib.Tactic.FieldSimp
import Mathlib.Tactic.NoncommRing
import Mathlib.LinearAlgebra.AffineSpace.AffineSubspace.Defs

/-!
# Convex sets

In a ğ•œ-vector space, we define the following property:
* `Convex ğ•œ s`: A set `s` is convex if for any two points `x y âˆˆ s` it includes `segment ğ•œ x y`.

We provide various equivalent versions, and prove that some specific sets are convex.

## TODO

Generalize all this file to affine spaces.
-/


variable {ğ•œ E F Î² : Type*}

open LinearMap Set

open scoped Convex Pointwise

/-! ### Convexity of sets -/


section OrderedSemiring

variable [Semiring ğ•œ] [PartialOrder ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F]

section SMul

variable (ğ•œ) [SMul ğ•œ E] [SMul ğ•œ F] (s : Set E) {x : E}

/-- Convexity of sets. -/
def Convex : Prop :=
  âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ StarConvex ğ•œ x s

variable {ğ•œ s}

theorem Convex.starConvex (hs : Convex ğ•œ s) (hx : x âˆˆ s) : StarConvex ğ•œ x s :=
  hs hx

theorem convex_iff_segment_subset : Convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s :=
  forallâ‚‚_congr fun _ _ => starConvex_iff_segment_subset

theorem Convex.segment_subset (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    [x -[ğ•œ] y] âŠ† s :=
  convex_iff_segment_subset.1 h hx hy

theorem Convex.openSegment_subset (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    openSegment ğ•œ x y âŠ† s :=
  (openSegment_subset_segment ğ•œ x y).trans (h.segment_subset hx hy)

theorem convex_iff_add_mem : Convex ğ•œ s â†”
    âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s := by
  simp_rw [convex_iff_segment_subset, segment_subset_iff]

/-- Alternative definition of set convexity, in terms of pointwise set operations. -/
theorem convex_iff_pointwise_add_subset :
    Convex ğ•œ s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ s + b â€¢ s âŠ† s :=
  Iff.intro
    (by
      rintro hA a b ha hb hab w âŸ¨au, âŸ¨u, hu, rflâŸ©, bv, âŸ¨v, hv, rflâŸ©, rflâŸ©
      exact hA hu hv ha hb hab)
    fun h _ hx _ hy _ _ ha hb hab => (h ha hb hab) (Set.add_mem_add âŸ¨_, hx, rflâŸ© âŸ¨_, hy, rflâŸ©)

alias âŸ¨Convex.set_combo_subset, _âŸ© := convex_iff_pointwise_add_subset

theorem convex_empty : Convex ğ•œ (âˆ… : Set E) := fun _ => False.elim

theorem convex_univ : Convex ğ•œ (Set.univ : Set E) := fun _ _ => starConvex_univ _

theorem Convex.inter {t : Set E} (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) : Convex ğ•œ (s âˆ© t) :=
  fun _ hx => (hs hx.1).inter (ht hx.2)

theorem convex_sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, Convex ğ•œ s) : Convex ğ•œ (â‹‚â‚€ S) := fun _ hx =>
  starConvex_sInter fun _ hs => h _ hs <| hx _ hs

theorem convex_iInter {Î¹ : Sort*} {s : Î¹ â†’ Set E} (h : âˆ€ i, Convex ğ•œ (s i)) :
    Convex ğ•œ (â‹‚ i, s i) :=
  sInter_range s â–¸ convex_sInter <| forall_mem_range.2 h

theorem convex_iInterâ‚‚ {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {s : (i : Î¹) â†’ Îº i â†’ Set E}
    (h : âˆ€ i j, Convex ğ•œ (s i j)) : Convex ğ•œ (â‹‚ (i) (j), s i j) :=
  convex_iInter fun i => convex_iInter <| h i

theorem Convex.prod {s : Set E} {t : Set F} (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) :
    Convex ğ•œ (s Ã—Ë¢ t) := fun _ hx => (hs hx.1).prod (ht hx.2)

theorem convex_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommMonoid (E i)] [âˆ€ i, SMul ğ•œ (E i)]
    {s : Set Î¹} {t : âˆ€ i, Set (E i)} (ht : âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ Convex ğ•œ (t i)) : Convex ğ•œ (s.pi t) :=
  fun _ hx => starConvex_pi fun _ hi => ht hi <| hx _ hi

theorem Directed.convex_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set E} (hdir : Directed (Â· âŠ† Â·) s)
    (hc : âˆ€ â¦ƒi : Î¹â¦„, Convex ğ•œ (s i)) : Convex ğ•œ (â‹ƒ i, s i) := by
  rintro x hx y hy a b ha hb hab
  rw [mem_iUnion] at hx hy âŠ¢
  obtain âŸ¨i, hxâŸ© := hx
  obtain âŸ¨j, hyâŸ© := hy
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j
  exact âŸ¨k, hc (hik hx) (hjk hy) ha hb habâŸ©

theorem DirectedOn.convex_sUnion {c : Set (Set E)} (hdir : DirectedOn (Â· âŠ† Â·) c)
    (hc : âˆ€ â¦ƒA : Set Eâ¦„, A âˆˆ c â†’ Convex ğ•œ A) : Convex ğ•œ (â‹ƒâ‚€ c) := by
  rw [sUnion_eq_iUnion]
  exact (directedOn_iff_directed.1 hdir).convex_iUnion fun A => hc A.2

end SMul

section Module

variable [Module ğ•œ E] [Module ğ•œ F] {s : Set E} {x : E}

theorem convex_iff_openSegment_subset [ZeroLEOneClass ğ•œ] :
    Convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ openSegment ğ•œ x y âŠ† s :=
  forallâ‚‚_congr fun _ => starConvex_iff_openSegment_subset

theorem convex_iff_forall_pos :
    Convex ğ•œ s â†”
      âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
  forallâ‚‚_congr fun _ => starConvex_iff_forall_pos

theorem convex_iff_pairwise_pos : Convex ğ•œ s â†”
    s.Pairwise fun x y => âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s := by
  refine convex_iff_forall_pos.trans âŸ¨fun h x hx y hy _ => h hx hy, ?_âŸ©
  intro h x hx y hy a b ha hb hab
  obtain rfl | hxy := eq_or_ne x y
  Â· rwa [Convex.combo_self hab]
  Â· exact h hx hy hxy ha hb hab

theorem Convex.starConvex_iff [ZeroLEOneClass ğ•œ] (hs : Convex ğ•œ s) (h : s.Nonempty) :
    StarConvex ğ•œ x s â†” x âˆˆ s :=
  âŸ¨fun hxs => hxs.mem h, hs.starConvexâŸ©

protected theorem Set.Subsingleton.convex {s : Set E} (h : s.Subsingleton) : Convex ğ•œ s :=
  convex_iff_pairwise_pos.mpr (h.pairwise _)

@[simp] theorem convex_singleton (c : E) : Convex ğ•œ ({c} : Set E) :=
  subsingleton_singleton.convex

theorem convex_zero : Convex ğ•œ (0 : Set E) :=
  convex_singleton _

theorem convex_segment [IsOrderedRing ğ•œ] (x y : E) : Convex ğ•œ [x -[ğ•œ] y] := by
  rintro p âŸ¨ap, bp, hap, hbp, habp, rflâŸ© q âŸ¨aq, bq, haq, hbq, habq, rflâŸ© a b ha hb hab
  refine
    âŸ¨a * ap + b * aq, a * bp + b * bq, add_nonneg (mul_nonneg ha hap) (mul_nonneg hb haq),
      add_nonneg (mul_nonneg ha hbp) (mul_nonneg hb hbq), ?_, ?_âŸ©
  Â· rw [add_add_add_comm, â† mul_add, â† mul_add, habp, habq, mul_one, mul_one, hab]
  Â· match_scalars <;> noncomm_ring

theorem Convex.linear_image (hs : Convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) : Convex ğ•œ (f '' s) := by
  rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© a b ha hb hab
  exact âŸ¨a â€¢ x + b â€¢ y, hs hx hy ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

theorem Convex.is_linear_image (hs : Convex ğ•œ s) {f : E â†’ F} (hf : IsLinearMap ğ•œ f) :
    Convex ğ•œ (f '' s) :=
  hs.linear_image <| hf.mk' f

theorem Convex.linear_preimage {ğ•œâ‚ : Type*} [Semiring ğ•œâ‚] [Module ğ•œâ‚ E] [Module ğ•œâ‚ F] {s : Set F}
    [SMul ğ•œ ğ•œâ‚] [IsScalarTower ğ•œ ğ•œâ‚ E] [IsScalarTower ğ•œ ğ•œâ‚ F] (hs : Convex ğ•œ s) (f : E â†’â‚—[ğ•œâ‚] F) :
    Convex ğ•œ (f â»Â¹' s) := fun x hx y hy a b ha hb hab => by
  rw [mem_preimage, f.map_add, LinearMap.map_smul_of_tower, LinearMap.map_smul_of_tower]
  exact hs hx hy ha hb hab

theorem Convex.is_linear_preimage {ğ•œâ‚ : Type*} [Semiring ğ•œâ‚] [Module ğ•œâ‚ E] [Module ğ•œâ‚ F] {s : Set F}
    [SMul ğ•œ ğ•œâ‚] [IsScalarTower ğ•œ ğ•œâ‚ E] [IsScalarTower ğ•œ ğ•œâ‚ F] (hs : Convex ğ•œ s) {f : E â†’ F}
    (hf : IsLinearMap ğ•œâ‚ f) :
    Convex ğ•œ (f â»Â¹' s) :=
  hs.linear_preimage <| hf.mk' f

theorem Convex.add {t : Set E} (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) : Convex ğ•œ (s + t) := by
  rw [â† add_image_prod]
  exact (hs.prod ht).is_linear_image IsLinearMap.isLinearMap_add

variable (ğ•œ E)

/-- The convex sets form an additive submonoid under pointwise addition. -/
noncomputable def convexAddSubmonoid : AddSubmonoid (Set E) where
  carrier := {s : Set E | Convex ğ•œ s}
  zero_mem' := convex_zero
  add_mem' := Convex.add

@[simp, norm_cast]
theorem coe_convexAddSubmonoid : â†‘(convexAddSubmonoid ğ•œ E) = {s : Set E | Convex ğ•œ s} :=
  rfl

variable {ğ•œ E}

@[simp]
theorem mem_convexAddSubmonoid {s : Set E} : s âˆˆ convexAddSubmonoid ğ•œ E â†” Convex ğ•œ s :=
  Iff.rfl

theorem convex_list_sum {l : List (Set E)} (h : âˆ€ i âˆˆ l, Convex ğ•œ i) : Convex ğ•œ l.sum :=
  (convexAddSubmonoid ğ•œ E).list_sum_mem h

theorem convex_multiset_sum {s : Multiset (Set E)} (h : âˆ€ i âˆˆ s, Convex ğ•œ i) : Convex ğ•œ s.sum :=
  (convexAddSubmonoid ğ•œ E).multiset_sum_mem _ h

theorem convex_sum {Î¹} {s : Finset Î¹} (t : Î¹ â†’ Set E) (h : âˆ€ i âˆˆ s, Convex ğ•œ (t i)) :
    Convex ğ•œ (âˆ‘ i âˆˆ s, t i) :=
  (convexAddSubmonoid ğ•œ E).sum_mem h

theorem Convex.vadd (hs : Convex ğ•œ s) (z : E) : Convex ğ•œ (z +áµ¥ s) := by
  simp_rw [â† image_vadd, vadd_eq_add, â† singleton_add]
  exact (convex_singleton _).add hs

theorem Convex.translate (hs : Convex ğ•œ s) (z : E) : Convex ğ•œ ((fun x => z + x) '' s) :=
  hs.vadd _

/-- The translation of a convex set is also convex. -/
theorem Convex.translate_preimage_right (hs : Convex ğ•œ s) (z : E) :
    Convex ğ•œ ((fun x => z + x) â»Â¹' s) := by
  intro x hx y hy a b ha hb hab
  have h := hs hx hy ha hb hab
  rwa [smul_add, smul_add, add_add_add_comm, â† add_smul, hab, one_smul] at h

/-- The translation of a convex set is also convex. -/
theorem Convex.translate_preimage_left (hs : Convex ğ•œ s) (z : E) :
    Convex ğ•œ ((fun x => x + z) â»Â¹' s) := by
  simpa only [add_comm] using hs.translate_preimage_right z

section OrderedAddCommMonoid

variable [AddCommMonoid Î²] [PartialOrder Î²] [IsOrderedAddMonoid Î²] [Module ğ•œ Î²] [PosSMulMono ğ•œ Î²]

theorem convex_Iic (r : Î²) : Convex ğ•œ (Iic r) := fun x hx y hy a b ha hb hab =>
  calc
    a â€¢ x + b â€¢ y â‰¤ a â€¢ r + b â€¢ r :=
      add_le_add (smul_le_smul_of_nonneg_left hx ha) (smul_le_smul_of_nonneg_left hy hb)
    _ = r := Convex.combo_self hab _

theorem convex_Ici (r : Î²) : Convex ğ•œ (Ici r) :=
  convex_Iic (Î² := Î²áµ’áµˆ) r

theorem convex_Icc (r s : Î²) : Convex ğ•œ (Icc r s) :=
  Ici_inter_Iic.subst ((convex_Ici r).inter <| convex_Iic s)

theorem convex_halfSpace_le {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | f w â‰¤ r } :=
  (convex_Iic r).is_linear_preimage h
theorem convex_halfSpace_ge {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | r â‰¤ f w } :=
  (convex_Ici r).is_linear_preimage h
theorem convex_hyperplane {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | f w = r } := by
  simp_rw [le_antisymm_iff]
  exact (convex_halfSpace_le h r).inter (convex_halfSpace_ge h r)

end OrderedAddCommMonoid

section OrderedCancelAddCommMonoid

variable [AddCommMonoid Î²] [PartialOrder Î²] [IsOrderedCancelAddMonoid Î²]
  [Module ğ•œ Î²] [PosSMulStrictMono ğ•œ Î²]

theorem convex_Iio (r : Î²) : Convex ğ•œ (Iio r) := by
  intro x hx y hy a b ha hb hab
  obtain rfl | ha' := ha.eq_or_lt
  Â· rw [zero_add] at hab
    rwa [zero_smul, zero_add, hab, one_smul]
  rw [mem_Iio] at hx hy
  calc
    a â€¢ x + b â€¢ y < a â€¢ r + b â€¢ r := add_lt_add_of_lt_of_le
        (smul_lt_smul_of_pos_left hx ha') (smul_le_smul_of_nonneg_left hy.le hb)
    _ = r := Convex.combo_self hab _

theorem convex_Ioi (r : Î²) : Convex ğ•œ (Ioi r) :=
  convex_Iio (Î² := Î²áµ’áµˆ) r

theorem convex_Ioo (r s : Î²) : Convex ğ•œ (Ioo r s) :=
  Ioi_inter_Iio.subst ((convex_Ioi r).inter <| convex_Iio s)

theorem convex_Ico (r s : Î²) : Convex ğ•œ (Ico r s) :=
  Ici_inter_Iio.subst ((convex_Ici r).inter <| convex_Iio s)

theorem convex_Ioc (r s : Î²) : Convex ğ•œ (Ioc r s) :=
  Ioi_inter_Iic.subst ((convex_Ioi r).inter <| convex_Iic s)

theorem convex_halfSpace_lt {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | f w < r } :=
  (convex_Iio r).is_linear_preimage h
theorem convex_halfSpace_gt {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | r < f w } :=
  (convex_Ioi r).is_linear_preimage h
end OrderedCancelAddCommMonoid

section LinearOrderedAddCommMonoid

variable [AddCommMonoid Î²] [LinearOrder Î²] [IsOrderedAddMonoid Î²] [Module ğ•œ Î²] [PosSMulMono ğ•œ Î²]

theorem convex_uIcc (r s : Î²) : Convex ğ•œ (uIcc r s) :=
  convex_Icc _ _

end LinearOrderedAddCommMonoid

end Module

section IsScalarTower

variable [ZeroLEOneClass ğ•œ] [Module ğ•œ E]
variable (R : Type*) [Semiring R] [PartialOrder R] [Module R E]
variable [Module R ğ•œ] [IsScalarTower R ğ•œ E]

/-- Lift the convexity of a set up through a scalar tower. -/
theorem Convex.lift [SMulPosMono R ğ•œ] {s : Set E} (hs : Convex ğ•œ s) : Convex R s := by
  intro x hx y hy a b ha hb hab
  suffices (a â€¢ (1 : ğ•œ)) â€¢ x + (b â€¢ (1 : ğ•œ)) â€¢ y âˆˆ s by simpa using this
  refine hs hx hy ?_ ?_ (by simpa [add_smul] using congr($(hab) â€¢ (1 : ğ•œ)))
  all_goals exact zero_smul R (1 : ğ•œ) â–¸ smul_le_smul_of_nonneg_right â€¹_â€º zero_le_one

end IsScalarTower

end AddCommMonoid

section LinearOrderedAddCommMonoid

variable [AddCommMonoid E] [LinearOrder E] [IsOrderedAddMonoid E]
  [PartialOrder Î²] [Module ğ•œ E] [PosSMulMono ğ•œ E]
  {s : Set E} {f : E â†’ Î²}

theorem MonotoneOn.convex_le (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x â‰¤ r }) := fun x hx y hy _ _ ha hb hab =>
  âŸ¨hs hx.1 hy.1 ha hb hab,
    (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1) (Convex.combo_le_max x y ha hb hab)).trans
      (max_rec' { x | f x â‰¤ r } hx.2 hy.2)âŸ©

theorem MonotoneOn.convex_lt (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x < r }) := fun x hx y hy _ _ ha hb hab =>
  âŸ¨hs hx.1 hy.1 ha hb hab,
    (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1)
          (Convex.combo_le_max x y ha hb hab)).trans_lt
      (max_rec' { x | f x < r } hx.2 hy.2)âŸ©

theorem MonotoneOn.convex_ge (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r â‰¤ f x }) :=
  MonotoneOn.convex_le (E := Eáµ’áµˆ) (Î² := Î²áµ’áµˆ) hf.dual hs r

theorem MonotoneOn.convex_gt (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  MonotoneOn.convex_lt (E := Eáµ’áµˆ) (Î² := Î²áµ’áµˆ) hf.dual hs r

theorem AntitoneOn.convex_le (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x â‰¤ r }) :=
  MonotoneOn.convex_ge (Î² := Î²áµ’áµˆ) hf hs r

theorem AntitoneOn.convex_lt (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x < r }) :=
  MonotoneOn.convex_gt (Î² := Î²áµ’áµˆ) hf hs r

theorem AntitoneOn.convex_ge (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r â‰¤ f x }) :=
  MonotoneOn.convex_le (Î² := Î²áµ’áµˆ) hf hs r

theorem AntitoneOn.convex_gt (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  MonotoneOn.convex_lt (Î² := Î²áµ’áµˆ)  hf hs r

theorem Monotone.convex_le (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_le convex_univ r)

theorem Monotone.convex_lt (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_le convex_univ r)

theorem Monotone.convex_ge (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | r â‰¤ f x } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_ge convex_univ r)

theorem Monotone.convex_gt (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_le convex_univ r)

theorem Antitone.convex_le (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_le convex_univ r)

theorem Antitone.convex_lt (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | f x < r } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_lt convex_univ r)

theorem Antitone.convex_ge (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | r â‰¤ f x } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_ge convex_univ r)

theorem Antitone.convex_gt (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | r < f x } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_gt convex_univ r)

end LinearOrderedAddCommMonoid

end OrderedSemiring

section OrderedCommSemiring

variable [CommSemiring ğ•œ] [PartialOrder ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F] {s : Set E}

theorem Convex.smul (hs : Convex ğ•œ s) (c : ğ•œ) : Convex ğ•œ (c â€¢ s) :=
  hs.linear_image (LinearMap.lsmul _ _ c)

theorem Convex.smul_preimage (hs : Convex ğ•œ s) (c : ğ•œ) : Convex ğ•œ ((fun z => c â€¢ z) â»Â¹' s) :=
  hs.linear_preimage (LinearMap.lsmul _ _ c)

theorem Convex.affinity (hs : Convex ğ•œ s) (z : E) (c : ğ•œ) :
    Convex ğ•œ ((fun x => z + c â€¢ x) '' s) := by
  simpa only [â† image_smul, â† image_vadd, image_image] using (hs.smul c).vadd z

end AddCommMonoid

end OrderedCommSemiring

section StrictOrderedCommSemiring

variable [CommSemiring ğ•œ] [PartialOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E]

theorem convex_openSegment (a b : E) : Convex ğ•œ (openSegment ğ•œ a b) := by
  rw [convex_iff_openSegment_subset]
  rintro p âŸ¨ap, bp, hap, hbp, habp, rflâŸ© q âŸ¨aq, bq, haq, hbq, habq, rflâŸ© z âŸ¨a, b, ha, hb, hab, rflâŸ©
  refine âŸ¨a * ap + b * aq, a * bp + b * bq, by positivity, by positivity, ?_, ?_âŸ©
  Â· linear_combination (norm := noncomm_ring) a * habp + b * habq + hab
  Â· module

end StrictOrderedCommSemiring

section OrderedRing

variable [Ring ğ•œ] [PartialOrder ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {s t : Set E}

@[simp]
theorem convex_vadd (a : E) : Convex ğ•œ (a +áµ¥ s) â†” Convex ğ•œ s :=
  âŸ¨fun h â†¦ by simpa using h.vadd (-a), fun h â†¦ h.vadd _âŸ©

/-- Affine subspaces are convex. -/
theorem AffineSubspace.convex (Q : AffineSubspace ğ•œ E) : Convex ğ•œ (Q : Set E) :=
  fun x hx y hy a b _ _ hab â†¦ by simpa [Convex.combo_eq_smul_sub_add hab] using Q.2 _ hy hx hx

/-- The preimage of a convex set under an affine map is convex. -/
theorem Convex.affine_preimage (f : E â†’áµƒ[ğ•œ] F) {s : Set F} (hs : Convex ğ•œ s) : Convex ğ•œ (f â»Â¹' s) :=
  fun _ hx => (hs hx).affine_preimage _

/-- The image of a convex set under an affine map is convex. -/
theorem Convex.affine_image (f : E â†’áµƒ[ğ•œ] F) (hs : Convex ğ•œ s) : Convex ğ•œ (f '' s) := by
  rintro _ âŸ¨x, hx, rflâŸ©
  exact (hs hx).affine_image _

theorem Convex.neg (hs : Convex ğ•œ s) : Convex ğ•œ (-s) :=
  hs.is_linear_preimage IsLinearMap.isLinearMap_neg (ğ•œâ‚ := ğ•œ)

theorem Convex.sub (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) : Convex ğ•œ (s - t) := by
  rw [sub_eq_add_neg]
  exact hs.add ht.neg

variable [AddRightMono ğ•œ]

theorem Convex.add_smul_mem (hs : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : x + y âˆˆ s) {t : ğ•œ}
    (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ s := by
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y) := by match_scalars <;> noncomm_ring
  rw [h]
  exact hs hx hy (sub_nonneg_of_le ht.2) ht.1 (sub_add_cancel _ _)

theorem Convex.smul_mem_of_zero_mem (hs : Convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
    {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : t â€¢ x âˆˆ s := by
  simpa using hs.add_smul_mem zero_mem (by simpa using hx) ht

theorem Convex.mapsTo_lineMap (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    MapsTo (AffineMap.lineMap x y) (Icc (0 : ğ•œ) 1) s := by
  simpa only [mapsTo_iff_image_subset, segment_eq_image_lineMap] using h.segment_subset hx hy

theorem Convex.lineMap_mem (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) {t : ğ•œ}
    (ht : t âˆˆ Icc 0 1) : AffineMap.lineMap x y t âˆˆ s :=
  h.mapsTo_lineMap hx hy ht

theorem Convex.add_smul_sub_mem (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) {t : ğ•œ}
    (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ (y - x) âˆˆ s := by
  rw [add_comm]
  exact h.lineMap_mem hx hy ht

end AddCommGroup

end OrderedRing

section LinearOrderedSemiring

variable [Semiring ğ•œ] [LinearOrder ğ•œ] [IsOrderedRing ğ•œ] [AddCommMonoid E]

theorem Convex_subadditive_le [SMul ğ•œ E] {f : E â†’ ğ•œ} (hf1 : âˆ€ x y, f (x + y) â‰¤ (f x) + (f y))
    (hf2 : âˆ€ â¦ƒcâ¦„ x, 0 â‰¤ c â†’ f (c â€¢ x) â‰¤ c * f x) (B : ğ•œ) :
    Convex ğ•œ { x | f x â‰¤ B } := by
  rw [convex_iff_segment_subset]
  rintro x hx y hy z âŸ¨a, b, ha, hb, hs, rflâŸ©
  calc
    _ â‰¤ a â€¢ (f x) + b â€¢ (f y) := le_trans (hf1 _ _) (add_le_add (hf2 x ha) (hf2 y hb))
    _ â‰¤ a â€¢ B + b â€¢ B := by gcongr <;> assumption
    _ â‰¤ B := by rw [â† add_smul, hs, one_smul]

end LinearOrderedSemiring

theorem Convex.midpoint_mem [Ring ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    [AddCommGroup E] [Module ğ•œ E] [Invertible (2 : ğ•œ)] {s : Set E} {x y : E}
    (h : Convex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s) : midpoint ğ•œ x y âˆˆ s :=
  h.segment_subset hx hy <| midpoint_mem_segment x y

section LinearOrderedField

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {s : Set E}

/-- Alternative definition of set convexity, using division. -/
theorem convex_iff_div :
    Convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’
      âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ s :=
  forallâ‚‚_congr fun _ _ => starConvex_iff_div

theorem Convex.mem_smul_of_zero_mem (h : Convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
    {t : ğ•œ} (ht : 1 â‰¤ t) : x âˆˆ t â€¢ s := by
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans_le ht).ne']
  exact h.smul_mem_of_zero_mem zero_mem hx
    âŸ¨inv_nonneg.2 (zero_le_one.trans ht), inv_le_one_of_one_leâ‚€ htâŸ©

theorem Convex.exists_mem_add_smul_eq (h : Convex ğ•œ s) {x y : E} {p q : ğ•œ} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) : âˆƒ z âˆˆ s, (p + q) â€¢ z = p â€¢ x + q â€¢ y := by
  rcases _root_.em (p = 0 âˆ§ q = 0) with (âŸ¨rfl, rflâŸ© | hpq)
  Â· use x, hx
    simp
  Â· replace hpq : 0 < p + q :=
      (add_nonneg hp hq).lt_of_ne' (mt (add_eq_zero_iff_of_nonneg hp hq).1 hpq)
    refine âŸ¨_, convex_iff_div.1 h hx hy hp hq hpq, ?_âŸ©
    match_scalars <;> field_simp

protected theorem Convex.add_smul (h_conv : Convex ğ•œ s) {p q : ğ•œ} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) :
    (p + q) â€¢ s = p â€¢ s + q â€¢ s := (add_smul_subset _ _ _).antisymm <| by
  rintro _ âŸ¨_, âŸ¨vâ‚, hâ‚, rflâŸ©, _, âŸ¨vâ‚‚, hâ‚‚, rflâŸ©, rflâŸ©
  exact h_conv.exists_mem_add_smul_eq hâ‚ hâ‚‚ hp hq

end AddCommGroup

end LinearOrderedField

/-!
#### Convex sets in an ordered space
Relates `Convex` and `OrdConnected`.
-/


section

theorem Set.OrdConnected.convex_of_chain [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E]
    [PartialOrder E] [IsOrderedAddMonoid E] [Module ğ•œ E] [PosSMulMono ğ•œ E] {s : Set E}
    (hs : s.OrdConnected) (h : IsChain (Â· â‰¤ Â·) s) : Convex ğ•œ s := by
  refine convex_iff_segment_subset.mpr fun x hx y hy => ?_
  obtain hxy | hyx := h.total hx hy
  Â· exact (segment_subset_Icc hxy).trans (hs.out hx hy)
  Â· rw [segment_symm]
    exact (segment_subset_Icc hyx).trans (hs.out hy hx)

theorem Set.OrdConnected.convex [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E] [LinearOrder E]
    [IsOrderedAddMonoid E] [Module ğ•œ E] [PosSMulMono ğ•œ E] {s : Set E} (hs : s.OrdConnected) :
    Convex ğ•œ s :=
  hs.convex_of_chain <| isChain_of_trichotomous s

theorem convex_iff_ordConnected [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] {s : Set ğ•œ} :
    Convex ğ•œ s â†” s.OrdConnected := by
  simp_rw [convex_iff_segment_subset, segment_eq_uIcc, ordConnected_iff_uIcc_subset]

alias âŸ¨Convex.ordConnected, _âŸ© := convex_iff_ordConnected

end

/-! #### Convexity of submodules/subspaces -/


namespace Submodule

variable [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E] [Module ğ•œ E]

protected theorem convex (K : Submodule ğ•œ E) : Convex ğ•œ (â†‘K : Set E) := by
  repeat' intro
  refine add_mem (smul_mem _ _ ?_) (smul_mem _ _ ?_) <;> assumption

protected theorem starConvex (K : Submodule ğ•œ E) : StarConvex ğ•œ (0 : E) K :=
  K.convex K.zero_mem

end Submodule
