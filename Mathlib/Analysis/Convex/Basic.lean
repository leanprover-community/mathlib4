/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudriashov, YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.Module.OrderedSMul
import Mathlib.Analysis.Convex.Star
import Mathlib.LinearAlgebra.AffineSpace.AffineSubspace

#align_import analysis.convex.basic from "leanprover-community/mathlib"@"92bd7b1ffeb306a89f450bee126ddd8a284c259d"

/-!
# Convex sets and functions in vector spaces

In a ğ•œ-vector space, we define the following objects and properties.
* `Convex ğ•œ s`: A set `s` is convex if for any two points `x y âˆˆ s` it includes `segment ğ•œ x y`.
* `stdSimplex ğ•œ Î¹`: The standard simplex in `Î¹ â†’ ğ•œ` (currently requires `Fintype Î¹`). It is the
  intersection of the positive quadrant with the hyperplane `s.sum = 1`.

We also provide various equivalent versions of the definitions above, prove that some specific sets
are convex.

## TODO

Generalize all this file to affine spaces.
-/


variable {ğ•œ E F Î² : Type*}

open LinearMap Set

open scoped Convex Pointwise

/-! ### Convexity of sets -/


section OrderedSemiring

variable [OrderedSemiring ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F]

section SMul

variable (ğ•œ) [SMul ğ•œ E] [SMul ğ•œ F] (s : Set E) {x : E}

/-- Convexity of sets. -/
def Convex : Prop :=
  âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ StarConvex ğ•œ x s
#align convex Convex

variable {ğ•œ s}

theorem Convex.starConvex (hs : Convex ğ•œ s) (hx : x âˆˆ s) : StarConvex ğ•œ x s :=
  hs hx
#align convex.star_convex Convex.starConvex

theorem convex_iff_segment_subset : Convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s :=
  forallâ‚‚_congr fun _ _ => starConvex_iff_segment_subset
#align convex_iff_segment_subset convex_iff_segment_subset

theorem Convex.segment_subset (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    [x -[ğ•œ] y] âŠ† s :=
  convex_iff_segment_subset.1 h hx hy
#align convex.segment_subset Convex.segment_subset

theorem Convex.openSegment_subset (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    openSegment ğ•œ x y âŠ† s :=
  (openSegment_subset_segment ğ•œ x y).trans (h.segment_subset hx hy)
#align convex.open_segment_subset Convex.openSegment_subset

/-- Alternative definition of set convexity, in terms of pointwise set operations. -/
theorem convex_iff_pointwise_add_subset :
    Convex ğ•œ s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ s + b â€¢ s âŠ† s :=
  Iff.intro
    (by
      rintro hA a b ha hb hab w âŸ¨au, âŸ¨u, hu, rflâŸ©, bv, âŸ¨v, hv, rflâŸ©, rflâŸ©
      exact hA hu hv ha hb hab)
    fun h x hx y hy a b ha hb hab => (h ha hb hab) (Set.add_mem_add âŸ¨_, hx, rflâŸ© âŸ¨_, hy, rflâŸ©)
#align convex_iff_pointwise_add_subset convex_iff_pointwise_add_subset

alias âŸ¨Convex.set_combo_subset, _âŸ© := convex_iff_pointwise_add_subset
#align convex.set_combo_subset Convex.set_combo_subset

theorem convex_empty : Convex ğ•œ (âˆ… : Set E) := fun _ => False.elim
#align convex_empty convex_empty

theorem convex_univ : Convex ğ•œ (Set.univ : Set E) := fun _ _ => starConvex_univ _
#align convex_univ convex_univ

theorem Convex.inter {t : Set E} (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) : Convex ğ•œ (s âˆ© t) :=
  fun _ hx => (hs hx.1).inter (ht hx.2)
#align convex.inter Convex.inter

theorem convex_sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, Convex ğ•œ s) : Convex ğ•œ (â‹‚â‚€ S) := fun _ hx =>
  starConvex_sInter fun _ hs => h _ hs <| hx _ hs
#align convex_sInter convex_sInter

theorem convex_iInter {Î¹ : Sort*} {s : Î¹ â†’ Set E} (h : âˆ€ i, Convex ğ•œ (s i)) :
    Convex ğ•œ (â‹‚ i, s i) :=
  sInter_range s â–¸ convex_sInter <| forall_mem_range.2 h
#align convex_Inter convex_iInter

theorem convex_iInterâ‚‚ {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {s : âˆ€ i, Îº i â†’ Set E}
    (h : âˆ€ i j, Convex ğ•œ (s i j)) : Convex ğ•œ (â‹‚ (i) (j), s i j) :=
  convex_iInter fun i => convex_iInter <| h i
#align convex_Interâ‚‚ convex_iInterâ‚‚

theorem Convex.prod {s : Set E} {t : Set F} (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) :
    Convex ğ•œ (s Ã—Ë¢ t) := fun _ hx => (hs hx.1).prod (ht hx.2)
#align convex.prod Convex.prod

theorem convex_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommMonoid (E i)] [âˆ€ i, SMul ğ•œ (E i)]
    {s : Set Î¹} {t : âˆ€ i, Set (E i)} (ht : âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ Convex ğ•œ (t i)) : Convex ğ•œ (s.pi t) :=
  fun _ hx => starConvex_pi fun _ hi => ht hi <| hx _ hi
#align convex_pi convex_pi

theorem Directed.convex_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set E} (hdir : Directed (Â· âŠ† Â·) s)
    (hc : âˆ€ â¦ƒi : Î¹â¦„, Convex ğ•œ (s i)) : Convex ğ•œ (â‹ƒ i, s i) := by
  rintro x hx y hy a b ha hb hab
  rw [mem_iUnion] at hx hy âŠ¢
  obtain âŸ¨i, hxâŸ© := hx
  obtain âŸ¨j, hyâŸ© := hy
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j
  exact âŸ¨k, hc (hik hx) (hjk hy) ha hb habâŸ©
#align directed.convex_Union Directed.convex_iUnion

theorem DirectedOn.convex_sUnion {c : Set (Set E)} (hdir : DirectedOn (Â· âŠ† Â·) c)
    (hc : âˆ€ â¦ƒA : Set Eâ¦„, A âˆˆ c â†’ Convex ğ•œ A) : Convex ğ•œ (â‹ƒâ‚€ c) := by
  rw [sUnion_eq_iUnion]
  exact (directedOn_iff_directed.1 hdir).convex_iUnion fun A => hc A.2
#align directed_on.convex_sUnion DirectedOn.convex_sUnion

end SMul

section Module

variable [Module ğ•œ E] [Module ğ•œ F] {s : Set E} {x : E}

theorem convex_iff_openSegment_subset :
    Convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ openSegment ğ•œ x y âŠ† s :=
  forallâ‚‚_congr fun _ => starConvex_iff_openSegment_subset
#align convex_iff_open_segment_subset convex_iff_openSegment_subset

theorem convex_iff_forall_pos :
    Convex ğ•œ s â†”
      âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
  forallâ‚‚_congr fun _ => starConvex_iff_forall_pos
#align convex_iff_forall_pos convex_iff_forall_pos

theorem convex_iff_pairwise_pos : Convex ğ•œ s â†”
    s.Pairwise fun x y => âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s := by
  refine convex_iff_forall_pos.trans âŸ¨fun h x hx y hy _ => h hx hy, ?_âŸ©
  intro h x hx y hy a b ha hb hab
  obtain rfl | hxy := eq_or_ne x y
  Â· rwa [Convex.combo_self hab]
  Â· exact h hx hy hxy ha hb hab
#align convex_iff_pairwise_pos convex_iff_pairwise_pos

theorem Convex.starConvex_iff (hs : Convex ğ•œ s) (h : s.Nonempty) : StarConvex ğ•œ x s â†” x âˆˆ s :=
  âŸ¨fun hxs => hxs.mem h, hs.starConvexâŸ©
#align convex.star_convex_iff Convex.starConvex_iff

protected theorem Set.Subsingleton.convex {s : Set E} (h : s.Subsingleton) : Convex ğ•œ s :=
  convex_iff_pairwise_pos.mpr (h.pairwise _)
#align set.subsingleton.convex Set.Subsingleton.convex

theorem convex_singleton (c : E) : Convex ğ•œ ({c} : Set E) :=
  subsingleton_singleton.convex
#align convex_singleton convex_singleton

theorem convex_zero : Convex ğ•œ (0 : Set E) :=
  convex_singleton _
#align convex_zero convex_zero

theorem convex_segment (x y : E) : Convex ğ•œ [x -[ğ•œ] y] := by
  rintro p âŸ¨ap, bp, hap, hbp, habp, rflâŸ© q âŸ¨aq, bq, haq, hbq, habq, rflâŸ© a b ha hb hab
  refine
    âŸ¨a * ap + b * aq, a * bp + b * bq, add_nonneg (mul_nonneg ha hap) (mul_nonneg hb haq),
      add_nonneg (mul_nonneg ha hbp) (mul_nonneg hb hbq), ?_, ?_âŸ©
  Â· rw [add_add_add_comm, â† mul_add, â† mul_add, habp, habq, mul_one, mul_one, hab]
  Â· simp_rw [add_smul, mul_smul, smul_add]
    exact add_add_add_comm _ _ _ _
#align convex_segment convex_segment

theorem Convex.linear_image (hs : Convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) : Convex ğ•œ (f '' s) := by
  rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© a b ha hb hab
  exact âŸ¨a â€¢ x + b â€¢ y, hs hx hy ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©
#align convex.linear_image Convex.linear_image

theorem Convex.is_linear_image (hs : Convex ğ•œ s) {f : E â†’ F} (hf : IsLinearMap ğ•œ f) :
    Convex ğ•œ (f '' s) :=
  hs.linear_image <| hf.mk' f
#align convex.is_linear_image Convex.is_linear_image

theorem Convex.linear_preimage {s : Set F} (hs : Convex ğ•œ s) (f : E â†’â‚—[ğ•œ] F) :
    Convex ğ•œ (f â»Â¹' s) := by
  intro x hx y hy a b ha hb hab
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul]
  exact hs hx hy ha hb hab
#align convex.linear_preimage Convex.linear_preimage

theorem Convex.is_linear_preimage {s : Set F} (hs : Convex ğ•œ s) {f : E â†’ F} (hf : IsLinearMap ğ•œ f) :
    Convex ğ•œ (f â»Â¹' s) :=
  hs.linear_preimage <| hf.mk' f
#align convex.is_linear_preimage Convex.is_linear_preimage

theorem Convex.add {t : Set E} (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) : Convex ğ•œ (s + t) := by
  rw [â† add_image_prod]
  exact (hs.prod ht).is_linear_image IsLinearMap.isLinearMap_add
#align convex.add Convex.add

variable (ğ•œ E)

/-- The convex sets form an additive submonoid under pointwise addition. -/
def convexAddSubmonoid : AddSubmonoid (Set E) where
  carrier := {s : Set E | Convex ğ•œ s}
  zero_mem' := convex_zero
  add_mem' := Convex.add
#align convex_add_submonoid convexAddSubmonoid

@[simp, norm_cast]
theorem coe_convexAddSubmonoid : â†‘(convexAddSubmonoid ğ•œ E) = {s : Set E | Convex ğ•œ s} :=
  rfl
#align coe_convex_add_submonoid coe_convexAddSubmonoid

variable {ğ•œ E}

@[simp]
theorem mem_convexAddSubmonoid {s : Set E} : s âˆˆ convexAddSubmonoid ğ•œ E â†” Convex ğ•œ s :=
  Iff.rfl
#align mem_convex_add_submonoid mem_convexAddSubmonoid

theorem convex_list_sum {l : List (Set E)} (h : âˆ€ i âˆˆ l, Convex ğ•œ i) : Convex ğ•œ l.sum :=
  (convexAddSubmonoid ğ•œ E).list_sum_mem h
#align convex_list_sum convex_list_sum

theorem convex_multiset_sum {s : Multiset (Set E)} (h : âˆ€ i âˆˆ s, Convex ğ•œ i) : Convex ğ•œ s.sum :=
  (convexAddSubmonoid ğ•œ E).multiset_sum_mem _ h
#align convex_multiset_sum convex_multiset_sum

theorem convex_sum {Î¹} {s : Finset Î¹} (t : Î¹ â†’ Set E) (h : âˆ€ i âˆˆ s, Convex ğ•œ (t i)) :
    Convex ğ•œ (âˆ‘ i âˆˆ s, t i) :=
  (convexAddSubmonoid ğ•œ E).sum_mem h
#align convex_sum convex_sum

theorem Convex.vadd (hs : Convex ğ•œ s) (z : E) : Convex ğ•œ (z +áµ¥ s) := by
  simp_rw [â† image_vadd, vadd_eq_add, â† singleton_add]
  exact (convex_singleton _).add hs
#align convex.vadd Convex.vadd

theorem Convex.translate (hs : Convex ğ•œ s) (z : E) : Convex ğ•œ ((fun x => z + x) '' s) :=
  hs.vadd _
#align convex.translate Convex.translate

/-- The translation of a convex set is also convex. -/
theorem Convex.translate_preimage_right (hs : Convex ğ•œ s) (z : E) :
    Convex ğ•œ ((fun x => z + x) â»Â¹' s) := by
  intro x hx y hy a b ha hb hab
  have h := hs hx hy ha hb hab
  rwa [smul_add, smul_add, add_add_add_comm, â† add_smul, hab, one_smul] at h
#align convex.translate_preimage_right Convex.translate_preimage_right

/-- The translation of a convex set is also convex. -/
theorem Convex.translate_preimage_left (hs : Convex ğ•œ s) (z : E) :
    Convex ğ•œ ((fun x => x + z) â»Â¹' s) := by
  simpa only [add_comm] using hs.translate_preimage_right z
#align convex.translate_preimage_left Convex.translate_preimage_left

section OrderedAddCommMonoid

variable [OrderedAddCommMonoid Î²] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]

theorem convex_Iic (r : Î²) : Convex ğ•œ (Iic r) := fun x hx y hy a b ha hb hab =>
  calc
    a â€¢ x + b â€¢ y â‰¤ a â€¢ r + b â€¢ r :=
      add_le_add (smul_le_smul_of_nonneg_left hx ha) (smul_le_smul_of_nonneg_left hy hb)
    _ = r := Convex.combo_self hab _
#align convex_Iic convex_Iic

theorem convex_Ici (r : Î²) : Convex ğ•œ (Ici r) :=
  @convex_Iic ğ•œ Î²áµ’áµˆ _ _ _ _ r
#align convex_Ici convex_Ici

theorem convex_Icc (r s : Î²) : Convex ğ•œ (Icc r s) :=
  Ici_inter_Iic.subst ((convex_Ici r).inter <| convex_Iic s)
#align convex_Icc convex_Icc

theorem convex_halfspace_le {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | f w â‰¤ r } :=
  (convex_Iic r).is_linear_preimage h
#align convex_halfspace_le convex_halfspace_le

theorem convex_halfspace_ge {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | r â‰¤ f w } :=
  (convex_Ici r).is_linear_preimage h
#align convex_halfspace_ge convex_halfspace_ge

theorem convex_hyperplane {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | f w = r } := by
  simp_rw [le_antisymm_iff]
  exact (convex_halfspace_le h r).inter (convex_halfspace_ge h r)
#align convex_hyperplane convex_hyperplane

end OrderedAddCommMonoid

section OrderedCancelAddCommMonoid

variable [OrderedCancelAddCommMonoid Î²] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]

theorem convex_Iio (r : Î²) : Convex ğ•œ (Iio r) := by
  intro x hx y hy a b ha hb hab
  obtain rfl | ha' := ha.eq_or_lt
  Â· rw [zero_add] at hab
    rwa [zero_smul, zero_add, hab, one_smul]
  rw [mem_Iio] at hx hy
  calc
    a â€¢ x + b â€¢ y < a â€¢ r + b â€¢ r := add_lt_add_of_lt_of_le
        (smul_lt_smul_of_pos_left hx ha') (smul_le_smul_of_nonneg_left hy.le hb)
    _ = r := Convex.combo_self hab _
#align convex_Iio convex_Iio

theorem convex_Ioi (r : Î²) : Convex ğ•œ (Ioi r) :=
  @convex_Iio ğ•œ Î²áµ’áµˆ _ _ _ _ r
#align convex_Ioi convex_Ioi

theorem convex_Ioo (r s : Î²) : Convex ğ•œ (Ioo r s) :=
  Ioi_inter_Iio.subst ((convex_Ioi r).inter <| convex_Iio s)
#align convex_Ioo convex_Ioo

theorem convex_Ico (r s : Î²) : Convex ğ•œ (Ico r s) :=
  Ici_inter_Iio.subst ((convex_Ici r).inter <| convex_Iio s)
#align convex_Ico convex_Ico

theorem convex_Ioc (r s : Î²) : Convex ğ•œ (Ioc r s) :=
  Ioi_inter_Iic.subst ((convex_Ioi r).inter <| convex_Iic s)
#align convex_Ioc convex_Ioc

theorem convex_halfspace_lt {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | f w < r } :=
  (convex_Iio r).is_linear_preimage h
#align convex_halfspace_lt convex_halfspace_lt

theorem convex_halfspace_gt {f : E â†’ Î²} (h : IsLinearMap ğ•œ f) (r : Î²) : Convex ğ•œ { w | r < f w } :=
  (convex_Ioi r).is_linear_preimage h
#align convex_halfspace_gt convex_halfspace_gt

end OrderedCancelAddCommMonoid

section LinearOrderedAddCommMonoid

variable [LinearOrderedAddCommMonoid Î²] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]

theorem convex_uIcc (r s : Î²) : Convex ğ•œ (uIcc r s) :=
  convex_Icc _ _
#align convex_uIcc convex_uIcc

end LinearOrderedAddCommMonoid

end Module

end AddCommMonoid

section LinearOrderedAddCommMonoid

variable [LinearOrderedAddCommMonoid E] [OrderedAddCommMonoid Î²] [Module ğ•œ E] [OrderedSMul ğ•œ E]
  {s : Set E} {f : E â†’ Î²}

theorem MonotoneOn.convex_le (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x â‰¤ r }) := fun x hx y hy _ _ ha hb hab =>
  âŸ¨hs hx.1 hy.1 ha hb hab,
    (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1) (Convex.combo_le_max x y ha hb hab)).trans
      (max_rec' { x | f x â‰¤ r } hx.2 hy.2)âŸ©
#align monotone_on.convex_le MonotoneOn.convex_le

theorem MonotoneOn.convex_lt (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x < r }) := fun x hx y hy _ _ ha hb hab =>
  âŸ¨hs hx.1 hy.1 ha hb hab,
    (hf (hs hx.1 hy.1 ha hb hab) (max_rec' s hx.1 hy.1)
          (Convex.combo_le_max x y ha hb hab)).trans_lt
      (max_rec' { x | f x < r } hx.2 hy.2)âŸ©
#align monotone_on.convex_lt MonotoneOn.convex_lt

theorem MonotoneOn.convex_ge (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r â‰¤ f x }) :=
  @MonotoneOn.convex_le ğ•œ Eáµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual hs r
#align monotone_on.convex_ge MonotoneOn.convex_ge

theorem MonotoneOn.convex_gt (hf : MonotoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  @MonotoneOn.convex_lt ğ•œ Eáµ’áµˆ Î²áµ’áµˆ _ _ _ _ _ _ _ hf.dual hs r
#align monotone_on.convex_gt MonotoneOn.convex_gt

theorem AntitoneOn.convex_le (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x â‰¤ r }) :=
  @MonotoneOn.convex_ge ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r
#align antitone_on.convex_le AntitoneOn.convex_le

theorem AntitoneOn.convex_lt (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x < r }) :=
  @MonotoneOn.convex_gt ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r
#align antitone_on.convex_lt AntitoneOn.convex_lt

theorem AntitoneOn.convex_ge (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r â‰¤ f x }) :=
  @MonotoneOn.convex_le ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r
#align antitone_on.convex_ge AntitoneOn.convex_ge

theorem AntitoneOn.convex_gt (hf : AntitoneOn f s) (hs : Convex ğ•œ s) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  @MonotoneOn.convex_lt ğ•œ E Î²áµ’áµˆ _ _ _ _ _ _ _ hf hs r
#align antitone_on.convex_gt AntitoneOn.convex_gt

theorem Monotone.convex_le (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_le convex_univ r)
#align monotone.convex_le Monotone.convex_le

theorem Monotone.convex_lt (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_le convex_univ r)
#align monotone.convex_lt Monotone.convex_lt

theorem Monotone.convex_ge (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | r â‰¤ f x } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_ge convex_univ r)
#align monotone.convex_ge Monotone.convex_ge

theorem Monotone.convex_gt (hf : Monotone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.monotoneOn univ).convex_le convex_univ r)
#align monotone.convex_gt Monotone.convex_gt

theorem Antitone.convex_le (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | f x â‰¤ r } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_le convex_univ r)
#align antitone.convex_le Antitone.convex_le

theorem Antitone.convex_lt (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | f x < r } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_lt convex_univ r)
#align antitone.convex_lt Antitone.convex_lt

theorem Antitone.convex_ge (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | r â‰¤ f x } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_ge convex_univ r)
#align antitone.convex_ge Antitone.convex_ge

theorem Antitone.convex_gt (hf : Antitone f) (r : Î²) : Convex ğ•œ { x | r < f x } :=
  Set.sep_univ.subst ((hf.antitoneOn univ).convex_gt convex_univ r)
#align antitone.convex_gt Antitone.convex_gt

end LinearOrderedAddCommMonoid

end OrderedSemiring

section OrderedCommSemiring

variable [OrderedCommSemiring ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F] {s : Set E}

theorem Convex.smul (hs : Convex ğ•œ s) (c : ğ•œ) : Convex ğ•œ (c â€¢ s) :=
  hs.linear_image (LinearMap.lsmul _ _ c)
#align convex.smul Convex.smul

theorem Convex.smul_preimage (hs : Convex ğ•œ s) (c : ğ•œ) : Convex ğ•œ ((fun z => c â€¢ z) â»Â¹' s) :=
  hs.linear_preimage (LinearMap.lsmul _ _ c)
#align convex.smul_preimage Convex.smul_preimage

theorem Convex.affinity (hs : Convex ğ•œ s) (z : E) (c : ğ•œ) :
    Convex ğ•œ ((fun x => z + c â€¢ x) '' s) := by
  simpa only [â† image_smul, â† image_vadd, image_image] using (hs.smul c).vadd z
#align convex.affinity Convex.affinity

end AddCommMonoid

end OrderedCommSemiring

section StrictOrderedCommSemiring

variable [StrictOrderedCommSemiring ğ•œ] [AddCommGroup E] [Module ğ•œ E]

theorem convex_openSegment (a b : E) : Convex ğ•œ (openSegment ğ•œ a b) := by
  rw [convex_iff_openSegment_subset]
  rintro p âŸ¨ap, bp, hap, hbp, habp, rflâŸ© q âŸ¨aq, bq, haq, hbq, habq, rflâŸ© z âŸ¨a, b, ha, hb, hab, rflâŸ©
  refine âŸ¨a * ap + b * aq, a * bp + b * bq, by positivity, by positivity, ?_, ?_âŸ©
  Â· rw [add_add_add_comm, â† mul_add, â† mul_add, habp, habq, mul_one, mul_one, hab]
  Â· simp_rw [add_smul, mul_smul, smul_add, add_add_add_comm]
#align convex_open_segment convex_openSegment

end StrictOrderedCommSemiring

section OrderedRing

variable [OrderedRing ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {s t : Set E}

@[simp]
theorem convex_vadd (a : E) : Convex ğ•œ (a +áµ¥ s) â†” Convex ğ•œ s :=
  âŸ¨fun h â†¦ by simpa using h.vadd (-a), fun h â†¦ h.vadd _âŸ©

theorem Convex.add_smul_mem (hs : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : x + y âˆˆ s) {t : ğ•œ}
    (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ s := by
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y) := by
    rw [smul_add, â† add_assoc, â† add_smul, sub_add_cancel, one_smul]
  rw [h]
  exact hs hx hy (sub_nonneg_of_le ht.2) ht.1 (sub_add_cancel _ _)
#align convex.add_smul_mem Convex.add_smul_mem

theorem Convex.smul_mem_of_zero_mem (hs : Convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
    {t : ğ•œ} (ht : t âˆˆ Icc (0 : ğ•œ) 1) : t â€¢ x âˆˆ s := by
  simpa using hs.add_smul_mem zero_mem (by simpa using hx) ht
#align convex.smul_mem_of_zero_mem Convex.smul_mem_of_zero_mem

theorem Convex.mapsTo_lineMap (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) :
    MapsTo (AffineMap.lineMap x y) (Icc (0 : ğ•œ) 1) s := by
  simpa only [mapsTo', segment_eq_image_lineMap] using h.segment_subset hx hy

theorem Convex.lineMap_mem (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) {t : ğ•œ}
    (ht : t âˆˆ Icc 0 1) : AffineMap.lineMap x y t âˆˆ s :=
  h.mapsTo_lineMap hx hy ht

theorem Convex.add_smul_sub_mem (h : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) {t : ğ•œ}
    (ht : t âˆˆ Icc (0 : ğ•œ) 1) : x + t â€¢ (y - x) âˆˆ s := by
  rw [add_comm]
  exact h.lineMap_mem hx hy ht
#align convex.add_smul_sub_mem Convex.add_smul_sub_mem

/-- Affine subspaces are convex. -/
theorem AffineSubspace.convex (Q : AffineSubspace ğ•œ E) : Convex ğ•œ (Q : Set E) :=
  fun x hx y hy a b _ _ hab â†¦ by simpa [Convex.combo_eq_smul_sub_add hab] using Q.2 _ hy hx hx
#align affine_subspace.convex AffineSubspace.convex

/-- The preimage of a convex set under an affine map is convex. -/
theorem Convex.affine_preimage (f : E â†’áµƒ[ğ•œ] F) {s : Set F} (hs : Convex ğ•œ s) : Convex ğ•œ (f â»Â¹' s) :=
  fun _ hx => (hs hx).affine_preimage _
#align convex.affine_preimage Convex.affine_preimage

/-- The image of a convex set under an affine map is convex. -/
theorem Convex.affine_image (f : E â†’áµƒ[ğ•œ] F) (hs : Convex ğ•œ s) : Convex ğ•œ (f '' s) := by
  rintro _ âŸ¨x, hx, rflâŸ©
  exact (hs hx).affine_image _
#align convex.affine_image Convex.affine_image

theorem Convex.neg (hs : Convex ğ•œ s) : Convex ğ•œ (-s) :=
  hs.is_linear_preimage IsLinearMap.isLinearMap_neg
#align convex.neg Convex.neg

theorem Convex.sub (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) : Convex ğ•œ (s - t) := by
  rw [sub_eq_add_neg]
  exact hs.add ht.neg
#align convex.sub Convex.sub

end AddCommGroup

end OrderedRing

section LinearOrderedRing

variable [LinearOrderedRing ğ•œ] [AddCommMonoid E]

theorem Convex_subadditive_le [SMul ğ•œ E] {f : E â†’ ğ•œ} (hf1 : âˆ€ x y, f (x + y) â‰¤ (f x) + (f y))
    (hf2 : âˆ€ â¦ƒcâ¦„ x, 0 â‰¤ c â†’ f (c â€¢ x) â‰¤ c * f x) (B : ğ•œ) :
    Convex ğ•œ { x | f x â‰¤ B } := by
  rw [convex_iff_segment_subset]
  rintro x hx y hy z âŸ¨a, b, ha, hb, hs, rflâŸ©
  calc
    _ â‰¤ a â€¢ (f x) + b â€¢ (f y) := le_trans (hf1 _ _) (add_le_add (hf2 x ha) (hf2 y hb))
    _ â‰¤ a â€¢ B + b â€¢ B :=
        add_le_add (smul_le_smul_of_nonneg_left hx ha) (smul_le_smul_of_nonneg_left hy hb)
    _ â‰¤ B := by rw [â† add_smul, hs, one_smul]

end LinearOrderedRing

section LinearOrderedField

variable [LinearOrderedField ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {s : Set E}

/-- Alternative definition of set convexity, using division. -/
theorem convex_iff_div :
    Convex ğ•œ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’
      âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ s :=
  forallâ‚‚_congr fun _ _ => starConvex_iff_div
#align convex_iff_div convex_iff_div

theorem Convex.mem_smul_of_zero_mem (h : Convex ğ•œ s) {x : E} (zero_mem : (0 : E) âˆˆ s) (hx : x âˆˆ s)
    {t : ğ•œ} (ht : 1 â‰¤ t) : x âˆˆ t â€¢ s := by
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans_le ht).ne']
  exact h.smul_mem_of_zero_mem zero_mem hx âŸ¨inv_nonneg.2 (zero_le_one.trans ht), inv_le_one htâŸ©
#align convex.mem_smul_of_zero_mem Convex.mem_smul_of_zero_mem

theorem Convex.exists_mem_add_smul_eq (h : Convex ğ•œ s) {x y : E} {p q : ğ•œ} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) : âˆƒ z âˆˆ s, (p + q) â€¢ z = p â€¢ x + q â€¢ y := by
  rcases _root_.em (p = 0 âˆ§ q = 0) with (âŸ¨rfl, rflâŸ© | hpq)
  Â· use x, hx
    simp
  Â· replace hpq : 0 < p + q := (add_nonneg hp hq).lt_of_ne' (mt (add_eq_zero_iff' hp hq).1 hpq)
    refine âŸ¨_, convex_iff_div.1 h hx hy hp hq hpq, ?_âŸ©
    simp only [smul_add, smul_smul, mul_div_cancelâ‚€ _ hpq.ne']

theorem Convex.add_smul (h_conv : Convex ğ•œ s) {p q : ğ•œ} (hp : 0 â‰¤ p) (hq : 0 â‰¤ q) :
    (p + q) â€¢ s = p â€¢ s + q â€¢ s := (add_smul_subset _ _ _).antisymm <| by
  rintro _ âŸ¨_, âŸ¨vâ‚, hâ‚, rflâŸ©, _, âŸ¨vâ‚‚, hâ‚‚, rflâŸ©, rflâŸ©
  exact h_conv.exists_mem_add_smul_eq hâ‚ hâ‚‚ hp hq
#align convex.add_smul Convex.add_smul

end AddCommGroup

end LinearOrderedField

/-!
#### Convex sets in an ordered space
Relates `Convex` and `OrdConnected`.
-/


section

theorem Set.OrdConnected.convex_of_chain [OrderedSemiring ğ•œ] [OrderedAddCommMonoid E] [Module ğ•œ E]
    [OrderedSMul ğ•œ E] {s : Set E} (hs : s.OrdConnected) (h : IsChain (Â· â‰¤ Â·) s) : Convex ğ•œ s := by
  refine convex_iff_segment_subset.mpr fun x hx y hy => ?_
  obtain hxy | hyx := h.total hx hy
  Â· exact (segment_subset_Icc hxy).trans (hs.out hx hy)
  Â· rw [segment_symm]
    exact (segment_subset_Icc hyx).trans (hs.out hy hx)
#align set.ord_connected.convex_of_chain Set.OrdConnected.convex_of_chain

theorem Set.OrdConnected.convex [OrderedSemiring ğ•œ] [LinearOrderedAddCommMonoid E] [Module ğ•œ E]
    [OrderedSMul ğ•œ E] {s : Set E} (hs : s.OrdConnected) : Convex ğ•œ s :=
  hs.convex_of_chain <| isChain_of_trichotomous s
#align set.ord_connected.convex Set.OrdConnected.convex

theorem convex_iff_ordConnected [LinearOrderedField ğ•œ] {s : Set ğ•œ} :
    Convex ğ•œ s â†” s.OrdConnected := by
  simp_rw [convex_iff_segment_subset, segment_eq_uIcc, ordConnected_iff_uIcc_subset]
#align convex_iff_ord_connected convex_iff_ordConnected

alias âŸ¨Convex.ordConnected, _âŸ© := convex_iff_ordConnected
#align convex.ord_connected Convex.ordConnected

end

/-! #### Convexity of submodules/subspaces -/


namespace Submodule

variable [OrderedSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E]

protected theorem convex (K : Submodule ğ•œ E) : Convex ğ•œ (â†‘K : Set E) := by
  repeat' intro
  refine add_mem (smul_mem _ _ ?_) (smul_mem _ _ ?_) <;> assumption
#align submodule.convex Submodule.convex

protected theorem starConvex (K : Submodule ğ•œ E) : StarConvex ğ•œ (0 : E) K :=
  K.convex K.zero_mem
#align submodule.star_convex Submodule.starConvex

end Submodule

/-! ### Simplex -/


section Simplex

section OrderedSemiring

variable (ğ•œ) (Î¹ : Type*) [OrderedSemiring ğ•œ] [Fintype Î¹]

/-- The standard simplex in the space of functions `Î¹ â†’ ğ•œ` is the set of vectors with non-negative
coordinates with total sum `1`. This is the free object in the category of convex spaces. -/
def stdSimplex : Set (Î¹ â†’ ğ•œ) :=
  { f | (âˆ€ x, 0 â‰¤ f x) âˆ§ âˆ‘ x, f x = 1 }
#align std_simplex stdSimplex

theorem stdSimplex_eq_inter : stdSimplex ğ•œ Î¹ = (â‹‚ x, { f | 0 â‰¤ f x }) âˆ© { f | âˆ‘ x, f x = 1 } := by
  ext f
  simp only [stdSimplex, Set.mem_inter_iff, Set.mem_iInter, Set.mem_setOf_eq]
#align std_simplex_eq_inter stdSimplex_eq_inter

theorem convex_stdSimplex : Convex ğ•œ (stdSimplex ğ•œ Î¹) := by
  refine fun f hf g hg a b ha hb hab => âŸ¨fun x => ?_, ?_âŸ©
  Â· apply_rules [add_nonneg, mul_nonneg, hf.1, hg.1]
  Â· erw [Finset.sum_add_distrib]
    simp only [Pi.smul_apply] -- Porting note: `erw` failed to rewrite with `â† Finset.smul_sum`
    rw [â† Finset.smul_sum, â† Finset.smul_sum, hf.2, hg.2, smul_eq_mul,
      smul_eq_mul, mul_one, mul_one]
    exact hab
#align convex_std_simplex convex_stdSimplex

@[nontriviality] lemma stdSimplex_of_subsingleton [Subsingleton ğ•œ] : stdSimplex ğ•œ Î¹ = univ :=
  eq_univ_of_forall fun _ â†¦ âŸ¨fun _ â†¦ (Subsingleton.elim _ _).le, Subsingleton.elim _ _âŸ©

/-- The standard simplex in the zero-dimensional space is empty. -/
lemma stdSimplex_of_isEmpty_index [IsEmpty Î¹] [Nontrivial ğ•œ] : stdSimplex ğ•œ Î¹ = âˆ… :=
  eq_empty_of_forall_not_mem <| by rintro f âŸ¨-, hfâŸ©; simp at hf

lemma stdSimplex_unique [Unique Î¹] : stdSimplex ğ•œ Î¹ = {fun _ â†¦ 1} := by
  refine eq_singleton_iff_unique_mem.2 âŸ¨âŸ¨fun _ â†¦ zero_le_one, Fintype.sum_unique _âŸ©, ?_âŸ©
  rintro f âŸ¨-, hfâŸ©
  rw [Fintype.sum_unique] at hf
  exact funext (Unique.forall_iff.2 hf)

variable {Î¹} [DecidableEq Î¹]

theorem single_mem_stdSimplex (i : Î¹) : Pi.single i 1 âˆˆ stdSimplex ğ•œ Î¹ :=
  âŸ¨le_update_iff.2 âŸ¨zero_le_one, fun _ _ â†¦ le_rflâŸ©, by simpâŸ©

theorem ite_eq_mem_stdSimplex (i : Î¹) : (if i = Â· then (1 : ğ•œ) else 0) âˆˆ stdSimplex ğ•œ Î¹ := by
  simpa only [@eq_comm _ i, â† Pi.single_apply] using single_mem_stdSimplex ğ•œ i
#align ite_eq_mem_std_simplex ite_eq_mem_stdSimplex

#adaptation_note /-- as of `nightly-2024-03-11`, we need a type annotation on the segment in the
following two lemmas. -/

/-- The edges are contained in the simplex. -/
lemma segment_single_subset_stdSimplex (i j : Î¹) :
    ([Pi.single i 1 -[ğ•œ] Pi.single j 1] : Set (Î¹ â†’ ğ•œ)) âŠ† stdSimplex ğ•œ Î¹ :=
  (convex_stdSimplex ğ•œ Î¹).segment_subset (single_mem_stdSimplex _ _) (single_mem_stdSimplex _ _)

lemma stdSimplex_fin_two :
    stdSimplex ğ•œ (Fin 2) = ([Pi.single 0 1 -[ğ•œ] Pi.single 1 1] : Set (Fin 2 â†’ ğ•œ)) := by
  refine Subset.antisymm ?_ (segment_single_subset_stdSimplex ğ•œ (0 : Fin 2) 1)
  rintro f âŸ¨hfâ‚€, hfâ‚âŸ©
  rw [Fin.sum_univ_two] at hfâ‚
  refine âŸ¨f 0, f 1, hfâ‚€ 0, hfâ‚€ 1, hfâ‚, funext <| Fin.forall_fin_two.2 ?_âŸ©
  simp

end OrderedSemiring

section OrderedRing

variable (ğ•œ) [OrderedRing ğ•œ]

/-- The standard one-dimensional simplex in `Fin 2 â†’ ğ•œ` is equivalent to the unit interval. -/
@[simps (config := .asFn)]
def stdSimplexEquivIcc : stdSimplex ğ•œ (Fin 2) â‰ƒ Icc (0 : ğ•œ) 1 where
  toFun f := âŸ¨f.1 0, f.2.1 _, f.2.2 â–¸
    Finset.single_le_sum (fun i _ â†¦ f.2.1 i) (Finset.mem_univ _)âŸ©
  invFun x := âŸ¨![x, 1 - x], Fin.forall_fin_two.2 âŸ¨x.2.1, sub_nonneg.2 x.2.2âŸ©,
    calc
      âˆ‘ i : Fin 2, ![(x : ğ•œ), 1 - x] i = x + (1 - x) := Fin.sum_univ_two _
      _ = 1 := add_sub_cancel _ _âŸ©
  left_inv f := Subtype.eq <| funext <| Fin.forall_fin_two.2 <| .intro rfl <|
      calc
        (1 : ğ•œ) - f.1 0 = f.1 0 + f.1 1 - f.1 0 := by rw [â† Fin.sum_univ_two f.1, f.2.2]
        _ = f.1 1 := add_sub_cancel_left _ _
  right_inv x := Subtype.eq rfl

end OrderedRing

end Simplex
