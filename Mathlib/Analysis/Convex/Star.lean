/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Segment
import Mathlib.Tactic.GCongr

#align_import analysis.convex.star from "leanprover-community/mathlib"@"9003f28797c0664a49e4179487267c494477d853"

/-!
# Star-convex sets

This files defines star-convex sets (aka star domains, star-shaped set, radially convex set).

A set is star-convex at `x` if every segment from `x` to a point in the set is contained in the set.

This is the prototypical example of a contractible set in homotopy theory (by scaling every point
towards `x`), but has wider uses.

Note that this has nothing to do with star rings, `Star` and co.

## Main declarations

* `StarConvex ğ•œ x s`: `s` is star-convex at `x` with scalars `ğ•œ`.

## Implementation notes

Instead of saying that a set is star-convex, we say a set is star-convex *at a point*. This has the
advantage of allowing us to talk about convexity as being "everywhere star-convexity" and of making
the union of star-convex sets be star-convex.

Incidentally, this choice means we don't need to assume a set is nonempty for it to be star-convex.
Concretely, the empty set is star-convex at every point.

## TODO

Balanced sets are star-convex.

The closure of a star-convex set is star-convex.

Star-convex sets are contractible.

A nonempty open star-convex set in `â„^n` is diffeomorphic to the entire space.
-/


open Set

open Convex Pointwise

variable {ğ•œ E F : Type*}

section OrderedSemiring

variable [OrderedSemiring ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F]

section SMul

variable (ğ•œ) [SMul ğ•œ E] [SMul ğ•œ F] (x : E) (s : Set E)

/-- Star-convexity of sets. `s` is star-convex at `x` if every segment from `x` to a point in `s` is
contained in `s`. -/
def StarConvex : Prop :=
  âˆ€ â¦ƒy : Eâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s
#align star_convex StarConvex

variable {ğ•œ x s} {t : Set E}

theorem starConvex_iff_segment_subset : StarConvex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ [x -[ğ•œ] y] âŠ† s := by
  constructor
  -- âŠ¢ StarConvex ğ•œ x s â†’ âˆ€ â¦ƒy : Eâ¦„, y âˆˆ s â†’ [x-[ğ•œ]y] âŠ† s
  Â· rintro h y hy z âŸ¨a, b, ha, hb, hab, rflâŸ©
    -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
    exact h hy ha hb hab
    -- ğŸ‰ no goals
  Â· rintro h y hy a b ha hb hab
    -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
    exact h hy âŸ¨a, b, ha, hb, hab, rflâŸ©
    -- ğŸ‰ no goals
#align star_convex_iff_segment_subset starConvex_iff_segment_subset

theorem StarConvex.segment_subset (h : StarConvex ğ•œ x s) {y : E} (hy : y âˆˆ s) : [x -[ğ•œ] y] âŠ† s :=
  starConvex_iff_segment_subset.1 h hy
#align star_convex.segment_subset StarConvex.segment_subset

theorem StarConvex.openSegment_subset (h : StarConvex ğ•œ x s) {y : E} (hy : y âˆˆ s) :
    openSegment ğ•œ x y âŠ† s :=
  (openSegment_subset_segment ğ•œ x y).trans (h.segment_subset hy)
#align star_convex.open_segment_subset StarConvex.openSegment_subset

/-- Alternative definition of star-convexity, in terms of pointwise set operations. -/
theorem starConvex_iff_pointwise_add_subset :
    StarConvex ğ•œ x s â†” âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ {x} + b â€¢ s âŠ† s := by
  refine'
    âŸ¨_, fun h y hy a b ha hb hab =>
      h ha hb hab (add_mem_add (smul_mem_smul_set <| mem_singleton _) âŸ¨_, hy, rflâŸ©)âŸ©
  rintro hA a b ha hb hab w âŸ¨au, bv, âŸ¨u, rfl : u = x, rflâŸ©, âŸ¨v, hv, rflâŸ©, rflâŸ©
  -- âŠ¢ (fun x x_1 => x + x_1) ((fun x => a â€¢ x) u) ((fun x => b â€¢ x) v) âˆˆ s
  exact hA hv ha hb hab
  -- ğŸ‰ no goals
#align star_convex_iff_pointwise_add_subset starConvex_iff_pointwise_add_subset

theorem starConvex_empty (x : E) : StarConvex ğ•œ x âˆ… := fun _ hy => hy.elim
#align star_convex_empty starConvex_empty

theorem starConvex_univ (x : E) : StarConvex ğ•œ x univ := fun _ _ _ _ _ _ _ => trivial
#align star_convex_univ starConvex_univ

theorem StarConvex.inter (hs : StarConvex ğ•œ x s) (ht : StarConvex ğ•œ x t) : StarConvex ğ•œ x (s âˆ© t) :=
  fun _ hy _ _ ha hb hab => âŸ¨hs hy.left ha hb hab, ht hy.right ha hb habâŸ©
#align star_convex.inter StarConvex.inter

theorem starConvex_sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, StarConvex ğ•œ x s) :
    StarConvex ğ•œ x (â‹‚â‚€ S) := fun _ hy _ _ ha hb hab s hs => h s hs (hy s hs) ha hb hab
#align star_convex_sInter starConvex_sInter

theorem starConvex_iInter {Î¹ : Sort*} {s : Î¹ â†’ Set E} (h : âˆ€ i, StarConvex ğ•œ x (s i)) :
    StarConvex ğ•œ x (â‹‚ i, s i) :=
  sInter_range s â–¸ starConvex_sInter <| forall_range_iff.2 h
#align star_convex_Inter starConvex_iInter

theorem StarConvex.union (hs : StarConvex ğ•œ x s) (ht : StarConvex ğ•œ x t) :
    StarConvex ğ•œ x (s âˆª t) := by
  rintro y (hy | hy) a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s âˆª t
  Â· exact Or.inl (hs hy ha hb hab)
    -- ğŸ‰ no goals
  Â· exact Or.inr (ht hy ha hb hab)
    -- ğŸ‰ no goals
#align star_convex.union StarConvex.union

theorem starConvex_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set E} (hs : âˆ€ i, StarConvex ğ•œ x (s i)) :
    StarConvex ğ•œ x (â‹ƒ i, s i) := by
  rintro y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ â‹ƒ (i : Î¹), s i
  rw [mem_iUnion] at hy âŠ¢
  -- âŠ¢ âˆƒ i, a â€¢ x + b â€¢ y âˆˆ s i
  obtain âŸ¨i, hyâŸ© := hy
  -- âŠ¢ âˆƒ i, a â€¢ x + b â€¢ y âˆˆ s i
  exact âŸ¨i, hs i hy ha hb habâŸ©
  -- ğŸ‰ no goals
#align star_convex_Union starConvex_iUnion

theorem starConvex_sUnion {S : Set (Set E)} (hS : âˆ€ s âˆˆ S, StarConvex ğ•œ x s) :
    StarConvex ğ•œ x (â‹ƒâ‚€ S) := by
  rw [sUnion_eq_iUnion]
  -- âŠ¢ StarConvex ğ•œ x (â‹ƒ (i : â†‘S), â†‘i)
  exact starConvex_iUnion fun s => hS _ s.2
  -- ğŸ‰ no goals
#align star_convex_sUnion starConvex_sUnion

theorem StarConvex.prod {y : F} {s : Set E} {t : Set F} (hs : StarConvex ğ•œ x s)
    (ht : StarConvex ğ•œ y t) : StarConvex ğ•œ (x, y) (s Ã—Ë¢ t) := fun _ hy _ _ ha hb hab =>
  âŸ¨hs hy.1 ha hb hab, ht hy.2 ha hb habâŸ©
#align star_convex.prod StarConvex.prod

theorem starConvex_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommMonoid (E i)] [âˆ€ i, SMul ğ•œ (E i)]
    {x : âˆ€ i, E i} {s : Set Î¹} {t : âˆ€ i, Set (E i)} (ht : âˆ€ â¦ƒiâ¦„, i âˆˆ s â†’ StarConvex ğ•œ (x i) (t i)) :
    StarConvex ğ•œ x (s.pi t) := fun _ hy _ _ ha hb hab i hi => ht hi (hy i hi) ha hb hab
#align star_convex_pi starConvex_pi

end SMul

section Module

variable [Module ğ•œ E] [Module ğ•œ F] {x y z : E} {s : Set E}

theorem StarConvex.mem (hs : StarConvex ğ•œ x s) (h : s.Nonempty) : x âˆˆ s := by
  obtain âŸ¨y, hyâŸ© := h
  -- âŠ¢ x âˆˆ s
  convert hs hy zero_le_one le_rfl (add_zero 1)
  -- âŠ¢ x = 1 â€¢ x + 0 â€¢ y
  rw [one_smul, zero_smul, add_zero]
  -- ğŸ‰ no goals
#align star_convex.mem StarConvex.mem

theorem starConvex_iff_forall_pos (hx : x âˆˆ s) : StarConvex ğ•œ x s â†”
    âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s := by
  refine' âŸ¨fun h y hy a b ha hb hab => h hy ha.le hb.le hab, _âŸ©
  -- âŠ¢ (âˆ€ â¦ƒy : Eâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ â€¦
  intro h y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
  obtain rfl | ha := ha.eq_or_lt
  -- âŠ¢ 0 â€¢ x + b â€¢ y âˆˆ s
  Â· rw [zero_add] at hab
    -- âŠ¢ 0 â€¢ x + b â€¢ y âˆˆ s
    rwa [hab, one_smul, zero_smul, zero_add]
    -- ğŸ‰ no goals
  obtain rfl | hb := hb.eq_or_lt
  -- âŠ¢ a â€¢ x + 0 â€¢ y âˆˆ s
  Â· rw [add_zero] at hab
    -- âŠ¢ a â€¢ x + 0 â€¢ y âˆˆ s
    rwa [hab, one_smul, zero_smul, add_zero]
    -- ğŸ‰ no goals
  exact h hy ha hb hab
  -- ğŸ‰ no goals
#align star_convex_iff_forall_pos starConvex_iff_forall_pos

theorem starConvex_iff_forall_ne_pos (hx : x âˆˆ s) :
    StarConvex ğ•œ x s â†”
      âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s := by
  refine' âŸ¨fun h y hy _ a b ha hb hab => h hy ha.le hb.le hab, _âŸ©
  -- âŠ¢ (âˆ€ â¦ƒy : Eâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + â€¦
  intro h y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
  obtain rfl | ha' := ha.eq_or_lt
  -- âŠ¢ 0 â€¢ x + b â€¢ y âˆˆ s
  Â· rw [zero_add] at hab
    -- âŠ¢ 0 â€¢ x + b â€¢ y âˆˆ s
    rwa [hab, zero_smul, one_smul, zero_add]
    -- ğŸ‰ no goals
  obtain rfl | hb' := hb.eq_or_lt
  -- âŠ¢ a â€¢ x + 0 â€¢ y âˆˆ s
  Â· rw [add_zero] at hab
    -- âŠ¢ a â€¢ x + 0 â€¢ y âˆˆ s
    rwa [hab, zero_smul, one_smul, add_zero]
    -- ğŸ‰ no goals
  obtain rfl | hxy := eq_or_ne x y
  -- âŠ¢ a â€¢ x + b â€¢ x âˆˆ s
  Â· rwa [Convex.combo_self hab]
    -- ğŸ‰ no goals
  exact h hy hxy ha' hb' hab
  -- ğŸ‰ no goals
#align star_convex_iff_forall_ne_pos starConvex_iff_forall_ne_pos

theorem starConvex_iff_openSegment_subset (hx : x âˆˆ s) :
    StarConvex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ openSegment ğ•œ x y âŠ† s :=
  starConvex_iff_segment_subset.trans <|
    forallâ‚‚_congr fun _ hy => (openSegment_subset_iff_segment_subset hx hy).symm
#align star_convex_iff_open_segment_subset starConvex_iff_openSegment_subset

theorem starConvex_singleton (x : E) : StarConvex ğ•œ x {x} := by
  rintro y (rfl : y = x) a b _ _ hab
  -- âŠ¢ a â€¢ y + b â€¢ y âˆˆ {y}
  exact Convex.combo_self hab _
  -- ğŸ‰ no goals
#align star_convex_singleton starConvex_singleton

theorem StarConvex.linear_image (hs : StarConvex ğ•œ x s) (f : E â†’â‚—[ğ•œ] F) :
    StarConvex ğ•œ (f x) (s.image f) := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ â†‘f x + b â€¢ y âˆˆ â†‘f '' s
  obtain âŸ¨y', hy', rflâŸ© := hy
  -- âŠ¢ a â€¢ â†‘f x + b â€¢ â†‘f y' âˆˆ â†‘f '' s
  exact âŸ¨a â€¢ x + b â€¢ y', hs hy' ha hb hab, by rw [f.map_add, f.map_smul, f.map_smul]âŸ©
  -- ğŸ‰ no goals
#align star_convex.linear_image StarConvex.linear_image

theorem StarConvex.is_linear_image (hs : StarConvex ğ•œ x s) {f : E â†’ F} (hf : IsLinearMap ğ•œ f) :
    StarConvex ğ•œ (f x) (f '' s) :=
  hs.linear_image <| hf.mk' f
#align star_convex.is_linear_image StarConvex.is_linear_image

theorem StarConvex.linear_preimage {s : Set F} (f : E â†’â‚—[ğ•œ] F) (hs : StarConvex ğ•œ (f x) s) :
    StarConvex ğ•œ x (s.preimage f) := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ â†‘f â»Â¹' s
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul]
  -- âŠ¢ a â€¢ â†‘f x + b â€¢ â†‘f y âˆˆ s
  exact hs hy ha hb hab
  -- ğŸ‰ no goals
#align star_convex.linear_preimage StarConvex.linear_preimage

theorem StarConvex.is_linear_preimage {s : Set F} {f : E â†’ F} (hs : StarConvex ğ•œ (f x) s)
    (hf : IsLinearMap ğ•œ f) : StarConvex ğ•œ x (preimage f s) :=
  hs.linear_preimage <| hf.mk' f
#align star_convex.is_linear_preimage StarConvex.is_linear_preimage

theorem StarConvex.add {t : Set E} (hs : StarConvex ğ•œ x s) (ht : StarConvex ğ•œ y t) :
    StarConvex ğ•œ (x + y) (s + t) := by
  rw [â† add_image_prod]
  -- âŠ¢ StarConvex ğ•œ (x + y) ((fun x => x.fst + x.snd) '' s Ã—Ë¢ t)
  exact (hs.prod ht).is_linear_image IsLinearMap.isLinearMap_add
  -- ğŸ‰ no goals
#align star_convex.add StarConvex.add

theorem StarConvex.add_left (hs : StarConvex ğ•œ x s) (z : E) :
    StarConvex ğ•œ (z + x) ((fun x => z + x) '' s) := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ (z + x) + b â€¢ y âˆˆ (fun x => z + x) '' s
  obtain âŸ¨y', hy', rflâŸ© := hy
  -- âŠ¢ a â€¢ (z + x) + b â€¢ (fun x => z + x) y' âˆˆ (fun x => z + x) '' s
  refine' âŸ¨a â€¢ x + b â€¢ y', hs hy' ha hb hab, _âŸ©
  -- âŠ¢ (fun x => z + x) (a â€¢ x + b â€¢ y') = a â€¢ (z + x) + b â€¢ (fun x => z + x) y'
  rw [smul_add, smul_add, add_add_add_comm, â† add_smul, hab, one_smul]
  -- ğŸ‰ no goals
#align star_convex.add_left StarConvex.add_left

theorem StarConvex.add_right (hs : StarConvex ğ•œ x s) (z : E) :
    StarConvex ğ•œ (x + z) ((fun x => x + z) '' s) := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ (x + z) + b â€¢ y âˆˆ (fun x => x + z) '' s
  obtain âŸ¨y', hy', rflâŸ© := hy
  -- âŠ¢ a â€¢ (x + z) + b â€¢ (fun x => x + z) y' âˆˆ (fun x => x + z) '' s
  refine' âŸ¨a â€¢ x + b â€¢ y', hs hy' ha hb hab, _âŸ©
  -- âŠ¢ (fun x => x + z) (a â€¢ x + b â€¢ y') = a â€¢ (x + z) + b â€¢ (fun x => x + z) y'
  rw [smul_add, smul_add, add_add_add_comm, â† add_smul, hab, one_smul]
  -- ğŸ‰ no goals
#align star_convex.add_right StarConvex.add_right

/-- The translation of a star-convex set is also star-convex. -/
theorem StarConvex.preimage_add_right (hs : StarConvex ğ•œ (z + x) s) :
    StarConvex ğ•œ x ((fun x => z + x) â»Â¹' s) := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ (fun x => z + x) â»Â¹' s
  have h := hs hy ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ (fun x => z + x) â»Â¹' s
  rwa [smul_add, smul_add, add_add_add_comm, â† add_smul, hab, one_smul] at h
  -- ğŸ‰ no goals
#align star_convex.preimage_add_right StarConvex.preimage_add_right

/-- The translation of a star-convex set is also star-convex. -/
theorem StarConvex.preimage_add_left (hs : StarConvex ğ•œ (x + z) s) :
    StarConvex ğ•œ x ((fun x => x + z) â»Â¹' s) := by
  rw [add_comm] at hs
  -- âŠ¢ StarConvex ğ•œ x ((fun x => x + z) â»Â¹' s)
  simpa only [add_comm] using hs.preimage_add_right
  -- ğŸ‰ no goals
#align star_convex.preimage_add_left StarConvex.preimage_add_left

end Module

end AddCommMonoid

section AddCommGroup

variable [AddCommGroup E] [Module ğ•œ E] {x y : E}

theorem StarConvex.sub' {s : Set (E Ã— E)} (hs : StarConvex ğ•œ (x, y) s) :
    StarConvex ğ•œ (x - y) ((fun x : E Ã— E => x.1 - x.2) '' s) :=
  hs.is_linear_image IsLinearMap.isLinearMap_sub
#align star_convex.sub' StarConvex.sub'

end AddCommGroup

end OrderedSemiring

section OrderedCommSemiring

variable [OrderedCommSemiring ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F] {x : E} {s : Set E}

theorem StarConvex.smul (hs : StarConvex ğ•œ x s) (c : ğ•œ) : StarConvex ğ•œ (c â€¢ x) (c â€¢ s) :=
  hs.linear_image <| LinearMap.lsmul _ _ c
#align star_convex.smul StarConvex.smul

theorem StarConvex.preimage_smul {c : ğ•œ} (hs : StarConvex ğ•œ (c â€¢ x) s) :
    StarConvex ğ•œ x ((fun z => c â€¢ z) â»Â¹' s) :=
  hs.linear_preimage (LinearMap.lsmul _ _ c)
#align star_convex.preimage_smul StarConvex.preimage_smul

theorem StarConvex.affinity (hs : StarConvex ğ•œ x s) (z : E) (c : ğ•œ) :
    StarConvex ğ•œ (z + c â€¢ x) ((fun x => z + c â€¢ x) '' s) := by
  have h := (hs.smul c).add_left z
  -- âŠ¢ StarConvex ğ•œ (z + c â€¢ x) ((fun x => z + c â€¢ x) '' s)
  rwa [â† image_smul, image_image] at h
  -- ğŸ‰ no goals
#align star_convex.affinity StarConvex.affinity

end AddCommMonoid

end OrderedCommSemiring

section OrderedRing

variable [OrderedRing ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [SMulWithZero ğ•œ E] {s : Set E}

theorem starConvex_zero_iff :
    StarConvex ğ•œ 0 s â†” âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒa : ğ•œâ¦„, 0 â‰¤ a â†’ a â‰¤ 1 â†’ a â€¢ x âˆˆ s := by
  refine'
    forall_congr' fun x => forall_congr' fun _ => âŸ¨fun h a haâ‚€ haâ‚ => _, fun h a b ha hb hab => _âŸ©
  Â· simpa only [sub_add_cancel, eq_self_iff_true, forall_true_left, zero_add, smul_zero] using
      h (sub_nonneg_of_le haâ‚) haâ‚€
  Â· rw [smul_zero, zero_add]
    -- âŠ¢ b â€¢ x âˆˆ s
    exact h hb (by rw [â† hab]; exact le_add_of_nonneg_left ha)
    -- ğŸ‰ no goals
#align star_convex_zero_iff starConvex_zero_iff

end AddCommMonoid

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {x y : E} {s t : Set E}

theorem StarConvex.add_smul_mem (hs : StarConvex ğ•œ x s) (hy : x + y âˆˆ s) {t : ğ•œ} (htâ‚€ : 0 â‰¤ t)
    (htâ‚ : t â‰¤ 1) : x + t â€¢ y âˆˆ s := by
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y) := by
    rw [smul_add, â† add_assoc, â† add_smul, sub_add_cancel, one_smul]
  rw [h]
  -- âŠ¢ (1 - t) â€¢ x + t â€¢ (x + y) âˆˆ s
  exact hs hy (sub_nonneg_of_le htâ‚) htâ‚€ (sub_add_cancel _ _)
  -- ğŸ‰ no goals
#align star_convex.add_smul_mem StarConvex.add_smul_mem

theorem StarConvex.smul_mem (hs : StarConvex ğ•œ 0 s) (hx : x âˆˆ s) {t : ğ•œ} (htâ‚€ : 0 â‰¤ t)
    (htâ‚ : t â‰¤ 1) : t â€¢ x âˆˆ s := by simpa using hs.add_smul_mem (by simpa using hx) htâ‚€ htâ‚
                                    -- ğŸ‰ no goals
#align star_convex.smul_mem StarConvex.smul_mem

theorem StarConvex.add_smul_sub_mem (hs : StarConvex ğ•œ x s) (hy : y âˆˆ s) {t : ğ•œ} (htâ‚€ : 0 â‰¤ t)
    (htâ‚ : t â‰¤ 1) : x + t â€¢ (y - x) âˆˆ s := by
  apply hs.segment_subset hy
  -- âŠ¢ x + t â€¢ (y - x) âˆˆ [x-[ğ•œ]y]
  rw [segment_eq_image']
  -- âŠ¢ x + t â€¢ (y - x) âˆˆ (fun Î¸ => x + Î¸ â€¢ (y - x)) '' Icc 0 1
  exact mem_image_of_mem _ âŸ¨htâ‚€, htâ‚âŸ©
  -- ğŸ‰ no goals
#align star_convex.add_smul_sub_mem StarConvex.add_smul_sub_mem

/-- The preimage of a star-convex set under an affine map is star-convex. -/
theorem StarConvex.affine_preimage (f : E â†’áµƒ[ğ•œ] F) {s : Set F} (hs : StarConvex ğ•œ (f x) s) :
    StarConvex ğ•œ x (f â»Â¹' s) := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ â†‘f â»Â¹' s
  rw [mem_preimage, Convex.combo_affine_apply hab]
  -- âŠ¢ a â€¢ â†‘f x + b â€¢ â†‘f y âˆˆ s
  exact hs hy ha hb hab
  -- ğŸ‰ no goals
#align star_convex.affine_preimage StarConvex.affine_preimage

/-- The image of a star-convex set under an affine map is star-convex. -/
theorem StarConvex.affine_image (f : E â†’áµƒ[ğ•œ] F) {s : Set E} (hs : StarConvex ğ•œ x s) :
    StarConvex ğ•œ (f x) (f '' s) := by
  rintro y âŸ¨y', âŸ¨hy', hy'fâŸ©âŸ© a b ha hb hab
  -- âŠ¢ a â€¢ â†‘f x + b â€¢ y âˆˆ â†‘f '' s
  refine' âŸ¨a â€¢ x + b â€¢ y', âŸ¨hs hy' ha hb hab, _âŸ©âŸ©
  -- âŠ¢ â†‘f (a â€¢ x + b â€¢ y') = a â€¢ â†‘f x + b â€¢ y
  rw [Convex.combo_affine_apply hab, hy'f]
  -- ğŸ‰ no goals
#align star_convex.affine_image StarConvex.affine_image

theorem StarConvex.neg (hs : StarConvex ğ•œ x s) : StarConvex ğ•œ (-x) (-s) := by
  rw [â† image_neg]
  -- âŠ¢ StarConvex ğ•œ (-x) (Neg.neg '' s)
  exact hs.is_linear_image IsLinearMap.isLinearMap_neg
  -- ğŸ‰ no goals
#align star_convex.neg StarConvex.neg

theorem StarConvex.sub (hs : StarConvex ğ•œ x s) (ht : StarConvex ğ•œ y t) :
    StarConvex ğ•œ (x - y) (s - t) := by
  simp_rw [sub_eq_add_neg]
  -- âŠ¢ StarConvex ğ•œ (x + -y) (s + -t)
  exact hs.add ht.neg
  -- ğŸ‰ no goals
#align star_convex.sub StarConvex.sub

end AddCommGroup

end OrderedRing

section LinearOrderedField

variable [LinearOrderedField ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [Module ğ•œ E] {x : E} {s : Set E}

/-- Alternative definition of star-convexity, using division. -/
theorem starConvex_iff_div : StarConvex ğ•œ x s â†” âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’
    âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ s :=
  âŸ¨fun h y hy a b ha hb hab => by
    apply h hy
    Â· positivity
      -- ğŸ‰ no goals
    Â· positivity
      -- ğŸ‰ no goals
    Â· rw [â† add_div]
      -- âŠ¢ (a + b) / (a + b) = 1
      exact div_self hab.ne',
      -- ğŸ‰ no goals
  fun h y hy a b ha hb hab => by
    have h' := h hy ha hb
    -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
    rw [hab, div_one, div_one] at h'
    -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
    exact h' zero_lt_oneâŸ©
    -- ğŸ‰ no goals
#align star_convex_iff_div starConvex_iff_div

theorem StarConvex.mem_smul (hs : StarConvex ğ•œ 0 s) (hx : x âˆˆ s) {t : ğ•œ} (ht : 1 â‰¤ t) :
    x âˆˆ t â€¢ s := by
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (zero_lt_one.trans_le ht).ne']
  -- âŠ¢ tâ»Â¹ â€¢ x âˆˆ s
  exact hs.smul_mem hx (by positivity) (inv_le_one ht)
  -- ğŸ‰ no goals
#align star_convex.mem_smul StarConvex.mem_smul

end AddCommGroup

end LinearOrderedField

/-!
#### Star-convex sets in an ordered space

Relates `starConvex` and `Set.ordConnected`.
-/


section OrdConnected

theorem Set.OrdConnected.starConvex [OrderedSemiring ğ•œ] [OrderedAddCommMonoid E] [Module ğ•œ E]
    [OrderedSMul ğ•œ E] {x : E} {s : Set E} (hs : s.OrdConnected) (hx : x âˆˆ s)
    (h : âˆ€ y âˆˆ s, x â‰¤ y âˆ¨ y â‰¤ x) : StarConvex ğ•œ x s := by
  intro y hy a b ha hb hab
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
  obtain hxy | hyx := h _ hy
  -- âŠ¢ a â€¢ x + b â€¢ y âˆˆ s
  Â· refine' hs.out hx hy (mem_Icc.2 âŸ¨_, _âŸ©)
    -- âŠ¢ x â‰¤ a â€¢ x + b â€¢ y
    calc
      x = a â€¢ x + b â€¢ x := (Convex.combo_self hab _).symm
      _ â‰¤ a â€¢ x + b â€¢ y := by gcongr
    calc
      a â€¢ x + b â€¢ y â‰¤ a â€¢ y + b â€¢ y := by gcongr
      _ = y := Convex.combo_self hab _
  Â· refine' hs.out hy hx (mem_Icc.2 âŸ¨_, _âŸ©)
    -- âŠ¢ y â‰¤ a â€¢ x + b â€¢ y
    calc
      y = a â€¢ y + b â€¢ y := (Convex.combo_self hab _).symm
      _ â‰¤ a â€¢ x + b â€¢ y := by gcongr
    calc
      a â€¢ x + b â€¢ y â‰¤ a â€¢ x + b â€¢ x := by gcongr
      _ = x := Convex.combo_self hab _
#align set.ord_connected.star_convex Set.OrdConnected.starConvex

theorem starConvex_iff_ordConnected [LinearOrderedField ğ•œ] {x : ğ•œ} {s : Set ğ•œ} (hx : x âˆˆ s) :
    StarConvex ğ•œ x s â†” s.OrdConnected := by
  simp_rw [ordConnected_iff_uIcc_subset_left hx, starConvex_iff_segment_subset, segment_eq_uIcc]
  -- ğŸ‰ no goals
#align star_convex_iff_ord_connected starConvex_iff_ordConnected

alias âŸ¨StarConvex.ordConnected, _âŸ© := starConvex_iff_ordConnected
#align star_convex.ord_connected StarConvex.ordConnected

end OrdConnected
