/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.Convex.Extreme
import Mathlib.Analysis.Convex.Function
import Mathlib.Topology.Algebra.Module.LinearMap
import Mathlib.Topology.Order.OrderClosed

/-!
# Exposed sets

This file defines exposed sets and exposed points for sets in a real vector space.

An exposed subset of `A` is a subset of `A` that is the set of all maximal points of a functional
(a continuous linear map `E â†’ ğ•œ`) over `A`. By convention, `âˆ…` is an exposed subset of all sets.
This allows for better functoriality of the definition (the intersection of two exposed subsets is
exposed, faces of a polytope form a bounded lattice).
This is an analytic notion of "being on the side of". It is stronger than being extreme (see
`IsExposed.isExtreme`), but weaker (for exposed points) than being a vertex.

An exposed set of `A` is sometimes called a "face of `A`", but we decided to reserve this
terminology to the more specific notion of a face of a polytope (sometimes hopefully soon out
on mathlib!).

## Main declarations

* `IsExposed ğ•œ A B`: States that `B` is an exposed set of `A` (in the literature, `A` is often
  implicit).
* `IsExposed.isExtreme`: An exposed set is also extreme.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

## TODO

Prove lemmas relating exposed sets and points to the intrinsic frontier.
-/

open Affine Set

section PreorderSemiring

variable (ğ•œ : Type*) {E : Type*} [TopologicalSpace ğ•œ] [Semiring ğ•œ] [Preorder ğ•œ] [AddCommMonoid E]
  [TopologicalSpace E] [Module ğ•œ E] {A B : Set E}

/-- A set `B` is exposed with respect to `A` iff it maximizes some functional over `A` (and contains
all points maximizing it). Written `IsExposed ğ•œ A B`. -/
def IsExposed (A B : Set E) : Prop :=
  B.Nonempty â†’ âˆƒ l : E â†’L[ğ•œ] ğ•œ, B = { x âˆˆ A | âˆ€ y âˆˆ A, l y â‰¤ l x }

end PreorderSemiring

section OrderedRing

variable {ğ•œ : Type*} {E : Type*} [TopologicalSpace ğ•œ] [Ring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E]
  [TopologicalSpace E] [Module ğ•œ E] {l : E â†’L[ğ•œ] ğ•œ} {A B C : Set E} {x : E}

/-- A useful way to build exposed sets from intersecting `A` with half-spaces (modelled by an
inequality with a functional). -/
def ContinuousLinearMap.toExposed (l : E â†’L[ğ•œ] ğ•œ) (A : Set E) : Set E :=
  { x âˆˆ A | âˆ€ y âˆˆ A, l y â‰¤ l x }

theorem ContinuousLinearMap.toExposed.isExposed : IsExposed ğ•œ A (l.toExposed A) := fun _ â†¦ âŸ¨l, rflâŸ©

theorem isExposed_empty : IsExposed ğ•œ A âˆ… := fun âŸ¨_, hxâŸ© â†¦ by
  exfalso
  exact hx

namespace IsExposed

protected theorem subset (hAB : IsExposed ğ•œ A B) : B âŠ† A := by
  rintro x hx
  obtain âŸ¨_, rflâŸ© := hAB âŸ¨x, hxâŸ©
  exact hx.1

@[refl]
protected theorem refl (A : Set E) : IsExposed ğ•œ A A := fun âŸ¨_, _âŸ© â†¦
  âŸ¨0, Subset.antisymm (fun _ hx â†¦ âŸ¨hx, fun _ _ â†¦ le_refl 0âŸ©) fun _ hx â†¦ hx.1âŸ©

protected theorem antisymm (hB : IsExposed ğ•œ A B) (hA : IsExposed ğ•œ B A) : A = B :=
  hA.subset.antisymm hB.subset

/-! `IsExposed` is *not* transitive: Consider a (topologically) open cube with vertices
`Aâ‚€â‚€â‚€, ..., Aâ‚â‚â‚` and add to it the triangle `Aâ‚€â‚€â‚€Aâ‚€â‚€â‚Aâ‚€â‚â‚€`. Then `Aâ‚€â‚€â‚Aâ‚€â‚â‚€` is an exposed subset
of `Aâ‚€â‚€â‚€Aâ‚€â‚€â‚Aâ‚€â‚â‚€` which is an exposed subset of the cube, but `Aâ‚€â‚€â‚Aâ‚€â‚â‚€` is not itself an exposed
subset of the cube. -/
protected theorem mono (hC : IsExposed ğ•œ A C) (hBA : B âŠ† A) (hCB : C âŠ† B) : IsExposed ğ•œ B C := by
  rintro âŸ¨w, hwâŸ©
  obtain âŸ¨l, rflâŸ© := hC âŸ¨w, hwâŸ©
  exact âŸ¨l, Subset.antisymm (fun x hx â†¦ âŸ¨hCB hx, fun y hy â†¦ hx.2 y (hBA hy)âŸ©) fun x hx â†¦
    âŸ¨hBA hx.1, fun y hy â†¦ (hw.2 y hy).trans (hx.2 w (hCB hw))âŸ©âŸ©

/-- If `B` is a nonempty exposed subset of `A`, then `B` is the intersection of `A` with some closed
half-space. The converse is *not* true. It would require that the corresponding open half-space
doesn't intersect `A`. -/
theorem eq_inter_halfSpace' {A B : Set E} (hAB : IsExposed ğ•œ A B) (hB : B.Nonempty) :
    âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆƒ a, B = { x âˆˆ A | a â‰¤ l x } := by
  obtain âŸ¨l, rflâŸ© := hAB hB
  obtain âŸ¨w, hwâŸ© := hB
  exact âŸ¨l, l w, Subset.antisymm (fun x hx â†¦ âŸ¨hx.1, hx.2 w hw.1âŸ©) fun x hx â†¦
    âŸ¨hx.1, fun y hy â†¦ (hw.2 y hy).trans hx.2âŸ©âŸ©
/-- For nontrivial `ğ•œ`, if `B` is an exposed subset of `A`, then `B` is the intersection of `A` with
some closed half-space. The converse is *not* true. It would require that the corresponding open
half-space doesn't intersect `A`. -/
theorem eq_inter_halfSpace [IsOrderedRing ğ•œ] [Nontrivial ğ•œ] {A B : Set E} (hAB : IsExposed ğ•œ A B) :
    âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆƒ a, B = { x âˆˆ A | a â‰¤ l x } := by
  obtain rfl | hB := B.eq_empty_or_nonempty
  Â· refine âŸ¨0, 1, ?_âŸ©
    rw [eq_comm, eq_empty_iff_forall_notMem]
    rintro x âŸ¨-, hâŸ©
    rw [ContinuousLinearMap.zero_apply] at h
    have : Â¬(1 : ğ•œ) â‰¤ 0 := not_le_of_gt zero_lt_one
    contradiction
  exact hAB.eq_inter_halfSpace' hB
protected theorem inter [IsOrderedRing ğ•œ] [ContinuousAdd ğ•œ] {A B C : Set E} (hB : IsExposed ğ•œ A B)
    (hC : IsExposed ğ•œ A C) : IsExposed ğ•œ A (B âˆ© C) := by
  rintro âŸ¨w, hwB, hwCâŸ©
  obtain âŸ¨lâ‚, rflâŸ© := hB âŸ¨w, hwBâŸ©
  obtain âŸ¨lâ‚‚, rflâŸ© := hC âŸ¨w, hwCâŸ©
  refine âŸ¨lâ‚ + lâ‚‚, Subset.antisymm ?_ ?_âŸ©
  Â· rintro x âŸ¨âŸ¨hxA, hxBâŸ©, âŸ¨-, hxCâŸ©âŸ©
    exact âŸ¨hxA, fun z hz â†¦ add_le_add (hxB z hz) (hxC z hz)âŸ©
  rintro x âŸ¨hxA, hxâŸ©
  refine âŸ¨âŸ¨hxA, fun y hy â†¦ ?_âŸ©, hxA, fun y hy â†¦ ?_âŸ©
  Â· exact
      (add_le_add_iff_right (lâ‚‚ x)).1 ((add_le_add (hwB.2 y hy) (hwC.2 x hxA)).trans (hx w hwB.1))
  Â· exact
      (add_le_add_iff_left (lâ‚ x)).1 (le_trans (add_le_add (hwB.2 x hxA) (hwC.2 y hy)) (hx w hwB.1))

theorem sInter [IsOrderedRing ğ•œ] [ContinuousAdd ğ•œ] {F : Finset (Set E)} (hF : F.Nonempty)
    (hAF : âˆ€ B âˆˆ F, IsExposed ğ•œ A B) : IsExposed ğ•œ A (â‹‚â‚€ F) := by
  classical
  induction F using Finset.induction with
  | empty => exfalso; exact Finset.not_nonempty_empty hF
  | insert C F _ hF' =>
    rw [Finset.coe_insert, sInter_insert]
    obtain rfl | hFnemp := F.eq_empty_or_nonempty
    Â· rw [Finset.coe_empty, sInter_empty, inter_univ]
      exact hAF C (Finset.mem_singleton_self C)
    Â· exact (hAF C (Finset.mem_insert_self C F)).inter
        (hF' hFnemp fun B hB â†¦ hAF B (Finset.mem_insert_of_mem hB))

theorem inter_left (hC : IsExposed ğ•œ A C) (hCB : C âŠ† B) : IsExposed ğ•œ (A âˆ© B) C := by
  rintro âŸ¨w, hwâŸ©
  obtain âŸ¨l, rflâŸ© := hC âŸ¨w, hwâŸ©
  exact âŸ¨l, Subset.antisymm (fun x hx â†¦ âŸ¨âŸ¨hx.1, hCB hxâŸ©, fun y hy â†¦ hx.2 y hy.1âŸ©)
    fun x âŸ¨âŸ¨hxC, _âŸ©, hxâŸ© â†¦ âŸ¨hxC, fun y hy â†¦ (hw.2 y hy).trans (hx w âŸ¨hC.subset hw, hCB hwâŸ©)âŸ©âŸ©

theorem inter_right (hC : IsExposed ğ•œ B C) (hCA : C âŠ† A) : IsExposed ğ•œ (A âˆ© B) C := by
  rw [inter_comm]
  exact hC.inter_left hCA

protected theorem isClosed [OrderClosedTopology ğ•œ] {A B : Set E} (hAB : IsExposed ğ•œ A B)
    (hA : IsClosed A) : IsClosed B := by
  obtain rfl | hB := B.eq_empty_or_nonempty
  Â· simp
  obtain âŸ¨l, a, rflâŸ© := hAB.eq_inter_halfSpace' hB
  exact hA.isClosed_le continuousOn_const l.continuous.continuousOn

protected theorem isCompact [OrderClosedTopology ğ•œ] [T2Space E] {A B : Set E}
    (hAB : IsExposed ğ•œ A B) (hA : IsCompact A) : IsCompact B :=
  hA.of_isClosed_subset (hAB.isClosed hA.isClosed) hAB.subset

end IsExposed

variable (ğ•œ) in
/-- A point is exposed with respect to `A` iff there exists a hyperplane whose intersection with
`A` is exactly that point. -/
def Set.exposedPoints (A : Set E) : Set E :=
  { x âˆˆ A | âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆ€ y âˆˆ A, l y â‰¤ l x âˆ§ (l x â‰¤ l y â†’ y = x) }

theorem exposed_point_def :
    x âˆˆ A.exposedPoints ğ•œ â†” x âˆˆ A âˆ§ âˆƒ l : E â†’L[ğ•œ] ğ•œ, âˆ€ y âˆˆ A, l y â‰¤ l x âˆ§ (l x â‰¤ l y â†’ y = x) :=
  Iff.rfl

theorem exposedPoints_subset : A.exposedPoints ğ•œ âŠ† A := fun _ hx â†¦ hx.1

@[simp]
theorem exposedPoints_empty : (âˆ… : Set E).exposedPoints ğ•œ = âˆ… :=
  subset_empty_iff.1 exposedPoints_subset

/-- Exposed points exactly correspond to exposed singletons. -/
theorem mem_exposedPoints_iff_exposed_singleton : x âˆˆ A.exposedPoints ğ•œ â†” IsExposed ğ•œ A {x} := by
  use fun âŸ¨hxA, l, hlâŸ© _ â†¦
    âŸ¨l,
      Eq.symm <|
        eq_singleton_iff_unique_mem.2
          âŸ¨âŸ¨hxA, fun y hy â†¦ (hl y hy).1âŸ©, fun z hz â†¦ (hl z hz.1).2 (hz.2 x hxA)âŸ©âŸ©
  rintro h
  obtain âŸ¨l, hlâŸ© := h âŸ¨x, mem_singleton _âŸ©
  rw [eq_comm, eq_singleton_iff_unique_mem] at hl
  exact
    âŸ¨hl.1.1, l, fun y hy â†¦
      âŸ¨hl.1.2 y hy, fun hxy â†¦ hl.2 y âŸ¨hy, fun z hz â†¦ (hl.1.2 z hz).trans hxyâŸ©âŸ©âŸ©

end OrderedRing

section LinearOrderedRing

variable {ğ•œ : Type*} {E : Type*} [TopologicalSpace ğ•œ]
  [Ring ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommMonoid E]
  [TopologicalSpace E] [Module ğ•œ E] {A B : Set E}

namespace IsExposed

protected theorem convex (hAB : IsExposed ğ•œ A B) (hA : Convex ğ•œ A) : Convex ğ•œ B := by
  obtain rfl | hB := B.eq_empty_or_nonempty
  Â· exact convex_empty
  obtain âŸ¨l, rflâŸ© := hAB hB
  exact fun xâ‚ hxâ‚ xâ‚‚ hxâ‚‚ a b ha hb hab â†¦
    âŸ¨hA hxâ‚.1 hxâ‚‚.1 ha hb hab, fun y hy â†¦
      ((l.toLinearMap.concaveOn convex_univ).convex_ge _ âŸ¨mem_univ _, hxâ‚.2 y hyâŸ©
          âŸ¨mem_univ _, hxâ‚‚.2 y hyâŸ© ha hb hab).2âŸ©

protected theorem isExtreme (hAB : IsExposed ğ•œ A B) : IsExtreme ğ•œ A B := by
  refine âŸ¨hAB.subset, fun xâ‚ hxâ‚A xâ‚‚ hxâ‚‚A x hxB hx â†¦ ?_âŸ©
  obtain âŸ¨l, rflâŸ© := hAB âŸ¨x, hxBâŸ©
  have hl : ConvexOn ğ•œ univ l := l.toLinearMap.convexOn convex_univ
  have hlxâ‚ := hxB.2 xâ‚ hxâ‚A
  have hlxâ‚‚ := hxB.2 xâ‚‚ hxâ‚‚A
  refine âŸ¨âŸ¨hxâ‚A, fun y hy â†¦ ?_âŸ©, âŸ¨hxâ‚‚A, fun y hy â†¦ ?_âŸ©âŸ©
  Â· rw [hlxâ‚.antisymm (hl.le_left_of_right_le (mem_univ _) (mem_univ _) hx hlxâ‚‚)]
    exact hxB.2 y hy
  Â· rw [hlxâ‚‚.antisymm (hl.le_right_of_left_le (mem_univ _) (mem_univ _) hx hlxâ‚)]
    exact hxB.2 y hy

end IsExposed

theorem exposedPoints_subset_extremePoints : A.exposedPoints ğ•œ âŠ† A.extremePoints ğ•œ := fun _ hx â†¦
  (mem_exposedPoints_iff_exposed_singleton.1 hx).isExtreme.mem_extremePoints

end LinearOrderedRing
