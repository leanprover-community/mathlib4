/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Convex.Gauge
import Mathlib.Analysis.Convex.Normed
/-!
# "Gauge rescale" homeomorphism between convex sets

Given two convex von Neumann bounded neighbourhoods of the origin
in a real topological vector space,
we construct a homeomorphism `gaugeRescaleHomeomorph`
that sends the interior, the closure, and the frontier of one set
to the interior, the closure, and the frontier of the other set.
-/

open Metric Bornology Filter Set
open scoped NNReal Topology Pointwise

noncomputable section

section Module

variable {E : Type*} [AddCommGroup E] [Module ‚Ñù E]

/-- The gauge rescale map `gaugeRescale s t` sends each point `x` to the point `y` on the same ray
that has the same gauge w.r.t. `t` as `x` has w.r.t. `s`.

The characteristic property is satisfied if `gauge t x ‚â† 0`, see `gauge_gaugeRescale'`.
In particular, it is satisfied for all `x`,
provided that `t` is absorbent and von Neumann bounded. -/
def gaugeRescale (s t : Set E) (x : E) : E := (gauge s x / gauge t x) ‚Ä¢ x

theorem gaugeRescale_def (s t : Set E) (x : E) :
    gaugeRescale s t x = (gauge s x / gauge t x) ‚Ä¢ x :=
  rfl

@[simp] theorem gaugeRescale_zero (s t : Set E) : gaugeRescale s t 0 = 0 := smul_zero _

theorem gaugeRescale_smul (s t : Set E) {c : ‚Ñù} (hc : 0 ‚â§ c) (x : E) :
    gaugeRescale s t (c ‚Ä¢ x) = c ‚Ä¢ gaugeRescale s t x := by
  simp only [gaugeRescale, gauge_smul_of_nonneg hc, smul_smul, smul_eq_mul]
  rw [mul_div_mul_comm, mul_right_comm, div_self_mul_self]

variable [TopologicalSpace E] [T1Space E]

theorem gaugeRescale_self_apply {s : Set E} (hsa : Absorbent ‚Ñù s) (hsb : IsVonNBounded ‚Ñù s)
    (x : E) : gaugeRescale s s x = x := by
  rcases eq_or_ne x 0 with rfl | hx; ¬∑ simp
  rw [gaugeRescale, div_self, one_smul]
  exact ((gauge_pos hsa hsb).2 hx).ne'

theorem gaugeRescale_self {s : Set E} (hsa : Absorbent ‚Ñù s) (hsb : IsVonNBounded ‚Ñù s) :
    gaugeRescale s s = id :=
  funext <| gaugeRescale_self_apply hsa hsb

theorem gauge_gaugeRescale' (s : Set E) {t : Set E} {x : E} (hx : gauge t x ‚â† 0) :
    gauge t (gaugeRescale s t x) = gauge s x := by
  rw [gaugeRescale, gauge_smul_of_nonneg (div_nonneg (gauge_nonneg _) (gauge_nonneg _)),
    smul_eq_mul, div_mul_cancel‚ÇÄ _ hx]

theorem gauge_gaugeRescale (s : Set E) {t : Set E} (hta : Absorbent ‚Ñù t) (htb : IsVonNBounded ‚Ñù t)
    (x : E) : gauge t (gaugeRescale s t x) = gauge s x := by
  rcases eq_or_ne x 0 with rfl | hx
  ¬∑ simp
  ¬∑ exact gauge_gaugeRescale' s ((gauge_pos hta htb).2 hx).ne'

theorem gauge_gaugeRescale_le (s t : Set E) (x : E) :
    gauge t (gaugeRescale s t x) ‚â§ gauge s x := by
  by_cases hx : gauge t x = 0
  ¬∑ simp [gaugeRescale, hx, gauge_nonneg]
  ¬∑ exact (gauge_gaugeRescale' s hx).le

theorem gaugeRescale_gaugeRescale {s t u : Set E} (hta : Absorbent ‚Ñù t) (htb : IsVonNBounded ‚Ñù t)
    (x : E) : gaugeRescale t u (gaugeRescale s t x) = gaugeRescale s u x := by
  rcases eq_or_ne x 0 with rfl | hx; ¬∑ simp
  rw [gaugeRescale_def s t x, gaugeRescale_smul, gaugeRescale, gaugeRescale, smul_smul,
    div_mul_div_cancel]
  exacts [((gauge_pos hta htb).2 hx).ne', div_nonneg (gauge_nonneg _) (gauge_nonneg _)]

/-- `gaugeRescale` bundled as an `Equiv`. -/
def gaugeRescaleEquiv (s t : Set E) (hsa : Absorbent ‚Ñù s) (hsb : IsVonNBounded ‚Ñù s)
    (hta : Absorbent ‚Ñù t) (htb : IsVonNBounded ‚Ñù t) : E ‚âÉ E where
  toFun := gaugeRescale s t
  invFun := gaugeRescale t s
  left_inv x := by rw [gaugeRescale_gaugeRescale, gaugeRescale_self_apply] <;> assumption
  right_inv x := by rw [gaugeRescale_gaugeRescale, gaugeRescale_self_apply] <;> assumption

variable [TopologicalAddGroup E] [ContinuousSMul ‚Ñù E] {s t : Set E}

theorem mapsTo_gaugeRescale_interior (h‚ÇÄ : t ‚àà ùìù 0) (hc : Convex ‚Ñù t) :
    MapsTo (gaugeRescale s t) (interior s) (interior t) := fun x hx ‚Ü¶ by
  rw [‚Üê gauge_lt_one_iff_mem_interior] <;> try assumption
  exact (gauge_gaugeRescale_le _ _ _).trans_lt (interior_subset_gauge_lt_one _ hx)

theorem mapsTo_gaugeRescale_closure {s t : Set E} (hsc : Convex ‚Ñù s) (hs‚ÇÄ : s ‚àà ùìù 0)
    (htc : Convex ‚Ñù t) (ht‚ÇÄ : 0 ‚àà t) (hta : Absorbent ‚Ñù t) :
    MapsTo (gaugeRescale s t) (closure s) (closure t) := fun _x hx ‚Ü¶
  mem_closure_of_gauge_le_one htc ht‚ÇÄ hta <| (gauge_gaugeRescale_le _ _ _).trans <|
    (gauge_le_one_iff_mem_closure hsc hs‚ÇÄ).2 hx

theorem continuous_gaugeRescale {s t : Set E} (hs : Convex ‚Ñù s) (hs‚ÇÄ : s ‚àà ùìù 0)
    (ht : Convex ‚Ñù t) (ht‚ÇÄ : t ‚àà ùìù 0) (htb : IsVonNBounded ‚Ñù t) :
    Continuous (gaugeRescale s t) := by
  have hta : Absorbent ‚Ñù t := absorbent_nhds_zero ht‚ÇÄ
  refine continuous_iff_continuousAt.2 fun x ‚Ü¶ ?_
  rcases eq_or_ne x 0 with rfl | hx
  ¬∑ rw [ContinuousAt, gaugeRescale_zero]
    nth_rewrite 2 [‚Üê comap_gauge_nhds_zero htb ht‚ÇÄ]
    simp only [tendsto_comap_iff, (¬∑ ‚àò ¬∑), gauge_gaugeRescale _ hta htb]
    exact tendsto_gauge_nhds_zero hs‚ÇÄ
  ¬∑ exact ((continuousAt_gauge hs hs‚ÇÄ).div (continuousAt_gauge ht ht‚ÇÄ)
      ((gauge_pos hta htb).2 hx).ne').smul continuousAt_id

/-- `gaugeRescale` bundled as a `Homeomorph`. -/
def gaugeRescaleHomeomorph (s t : Set E)
    (hsc : Convex ‚Ñù s) (hs‚ÇÄ : s ‚àà ùìù 0) (hsb : IsVonNBounded ‚Ñù s)
    (htc : Convex ‚Ñù t) (ht‚ÇÄ : t ‚àà ùìù 0) (htb : IsVonNBounded ‚Ñù t) : E ‚âÉ‚Çú E where
  toEquiv := gaugeRescaleEquiv s t (absorbent_nhds_zero hs‚ÇÄ) hsb (absorbent_nhds_zero ht‚ÇÄ) htb
  continuous_toFun := by apply continuous_gaugeRescale <;> assumption
  continuous_invFun := by apply continuous_gaugeRescale <;> assumption

theorem image_gaugeRescaleHomeomorph_interior {s t : Set E}
    (hsc : Convex ‚Ñù s) (hs‚ÇÄ : s ‚àà ùìù 0) (hsb : IsVonNBounded ‚Ñù s)
    (htc : Convex ‚Ñù t) (ht‚ÇÄ : t ‚àà ùìù 0) (htb : IsVonNBounded ‚Ñù t) :
    gaugeRescaleHomeomorph s t hsc hs‚ÇÄ hsb htc ht‚ÇÄ htb '' interior s = interior t :=
  Subset.antisymm (mapsTo_gaugeRescale_interior ht‚ÇÄ htc).image_subset <| by
    rw [‚Üê Homeomorph.preimage_symm, ‚Üê image_subset_iff]
    exact (mapsTo_gaugeRescale_interior hs‚ÇÄ hsc).image_subset

theorem image_gaugeRescaleHomeomorph_closure {s t : Set E}
    (hsc : Convex ‚Ñù s) (hs‚ÇÄ : s ‚àà ùìù 0) (hsb : IsVonNBounded ‚Ñù s)
    (htc : Convex ‚Ñù t) (ht‚ÇÄ : t ‚àà ùìù 0) (htb : IsVonNBounded ‚Ñù t) :
    gaugeRescaleHomeomorph s t hsc hs‚ÇÄ hsb htc ht‚ÇÄ htb '' closure s = closure t := by
  refine Subset.antisymm (mapsTo_gaugeRescale_closure hsc hs‚ÇÄ htc
    (mem_of_mem_nhds ht‚ÇÄ) (absorbent_nhds_zero ht‚ÇÄ)).image_subset ?_
  rw [‚Üê Homeomorph.preimage_symm, ‚Üê image_subset_iff]
  exact (mapsTo_gaugeRescale_closure htc ht‚ÇÄ hsc
    (mem_of_mem_nhds hs‚ÇÄ) (absorbent_nhds_zero hs‚ÇÄ)).image_subset

/-- Given two convex bounded sets in a topological vector space with nonempty interiors,
there exists a homeomorphism of the ambient space
that sends the interior, the closure, and the frontier of one set
to the interior, the closure, and the frontier of the other set.

In particular, if both `s` and `t` are open set or both `s` and `t` are closed sets,
then `e` maps `s` to `t`. -/
theorem exists_homeomorph_image_eq {s t : Set E}
    (hsc : Convex ‚Ñù s) (hsne : (interior s).Nonempty) (hsb : IsVonNBounded ‚Ñù s)
    (hst : Convex ‚Ñù t) (htne : (interior t).Nonempty) (htb : IsVonNBounded ‚Ñù t) :
    ‚àÉ e : E ‚âÉ‚Çú E, e '' interior s = interior t ‚àß e '' closure s = closure t ‚àß
      e '' frontier s = frontier t := by
  rsuffices ‚ü®e, h‚ÇÅ, h‚ÇÇ‚ü© : ‚àÉ e : E ‚âÉ‚Çú E, e '' interior s = interior t ‚àß e '' closure s = closure t
  ¬∑ refine ‚ü®e, h‚ÇÅ, h‚ÇÇ, ?_‚ü©
    simp_rw [‚Üê closure_diff_interior, image_diff e.injective, h‚ÇÅ, h‚ÇÇ]
  rcases hsne with ‚ü®x, hx‚ü©
  rcases htne with ‚ü®y, hy‚ü©
  set h : E ‚âÉ‚Çú E := by
    apply gaugeRescaleHomeomorph (-x +·µ• s) (-y +·µ• t) <;>
      simp [‚Üê mem_interior_iff_mem_nhds, interior_vadd, mem_vadd_set_iff_neg_vadd_mem, *]
  refine ‚ü®.trans (.addLeft (-x)) <| h.trans <| .addLeft y, ?_, ?_‚ü©
  ¬∑ calc
      (fun a ‚Ü¶ y + h (-x + a)) '' interior s = y +·µ• h '' interior (-x +·µ• s) := by
        simp_rw [interior_vadd, ‚Üê image_vadd, image_image, vadd_eq_add]
      _ = _ := by rw [image_gaugeRescaleHomeomorph_interior, interior_vadd, vadd_neg_vadd]
  ¬∑ calc
      (fun a ‚Ü¶ y + h (-x + a)) '' closure s = y +·µ• h '' closure (-x +·µ• s) := by
        simp_rw [closure_vadd, ‚Üê image_vadd, image_image, vadd_eq_add]
      _ = _ := by rw [image_gaugeRescaleHomeomorph_closure, closure_vadd, vadd_neg_vadd]

end Module

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-- If `s` is a convex bounded set with a nonempty interior in a real normed space,
then there is a homeomorphism of the ambient space to itself
that sends the interior of `s` to the unit open ball
and the closure of `s` to the unit closed ball. -/
theorem exists_homeomorph_image_interior_closure_frontier_eq_unitBall {s : Set E}
    (hc : Convex ‚Ñù s) (hne : (interior s).Nonempty) (hb : IsBounded s) :
    ‚àÉ h : E ‚âÉ‚Çú E, h '' interior s = ball 0 1 ‚àß h '' closure s = closedBall 0 1 ‚àß
      h '' frontier s = sphere 0 1 := by
  simpa [isOpen_ball.interior_eq, closure_ball, frontier_ball]
    using exists_homeomorph_image_eq hc hne (NormedSpace.isVonNBounded_of_isBounded _ hb)
    (convex_ball 0 1) (by simp [isOpen_ball.interior_eq]) (NormedSpace.isVonNBounded_ball _ _ _)
