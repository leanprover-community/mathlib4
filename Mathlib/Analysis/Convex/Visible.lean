/-
Copyright (c) 2024 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.BigOperators.Field
import Mathlib.Algebra.Group.Pointwise.Set.Card
import Mathlib.Analysis.Convex.Between
import Mathlib.Analysis.Convex.Combination
import Mathlib.Topology.Algebra.Affine
import Mathlib.Topology.MetricSpace.Pseudo.Lemmas
import Mathlib.Topology.Order.Monotone

/-!
# Points in sight

This file defines the relation of visibility with respect to a set, and lower bounds how many
elements of a set a point sees in terms of the dimension of that set.

## TODO

The art gallery problem can be stated using the visibility predicate: A set `A` (the art gallery) is
guarded by a finite set `G` (the guards) iff `âˆ€ a âˆˆ A, âˆƒ g âˆˆ G, IsVisible â„ sá¶œ a g`.
-/

open AffineMap Filter Finset Set
open scoped Cardinal Pointwise Topology

variable {ğ•œ V P : Type*}

section AddTorsor
variable [Field ğ•œ] [LinearOrder ğ•œ] [IsOrderedRing ğ•œ]
  [AddCommGroup V] [Module ğ•œ V] [AddTorsor V P]
  {s t : Set P} {x y z : P}

omit [IsOrderedRing ğ•œ] in
variable (ğ•œ) in
/-- Two points are visible to each other through a set if no point of that set lies strictly
between them.

By convention, a point `x` sees itself through any set `s`, even when `x âˆˆ s`. -/
def IsVisible (s : Set P) (x y : P) : Prop := âˆ€ â¦ƒzâ¦„, z âˆˆ s â†’ Â¬ Sbtw ğ•œ x z y

@[simp, refl]
lemma IsVisible.rfl : IsVisible ğ•œ s x x := by simp [IsVisible]

lemma isVisible_comm : IsVisible ğ•œ s x y â†” IsVisible ğ•œ s y x := by
  simp [IsVisible, sbtw_comm]

@[symm] alias âŸ¨IsVisible.symm, _âŸ© := isVisible_comm

omit [IsOrderedRing ğ•œ] in
lemma IsVisible.mono (hst : s âŠ† t) (ht : IsVisible ğ•œ t x y) : IsVisible ğ•œ s x y :=
  fun _z hz â†¦ ht <| hst hz

lemma isVisible_iff_lineMap (hxy : x â‰  y) :
    IsVisible ğ•œ s x y â†” âˆ€ Î´ âˆˆ Set.Ioo (0 : ğ•œ) 1, lineMap x y Î´ âˆ‰ s := by
  simp [IsVisible, sbtw_iff_mem_image_Ioo_and_ne, hxy]
  aesop

end AddTorsor

section Module
variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup V] [Module ğ•œ V] {s : Set V} {x y z : V}

/-- If a point `x` sees a convex combination of points of a set `s` through `convexHull â„ s âˆŒ x`,
then it sees all terms of that combination.

Note that the converse does not hold. -/
lemma IsVisible.of_convexHull_of_pos {Î¹ : Type*} {t : Finset Î¹} {a : Î¹ â†’ V} {w : Î¹ â†’ ğ•œ}
    (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : âˆ‘ i âˆˆ t, w i = 1) (ha : âˆ€ i âˆˆ t, a i âˆˆ s)
    (hx : x âˆ‰ convexHull ğ•œ s) (hw : IsVisible ğ•œ (convexHull ğ•œ s) x (âˆ‘ i âˆˆ t, w i â€¢ a i)) {i : Î¹}
    (hi : i âˆˆ t) (hwi : 0 < w i) : IsVisible ğ•œ (convexHull ğ•œ s) x (a i) := by
  classical
  obtain hwi | hwi : w i = 1 âˆ¨ w i < 1 := eq_or_lt_of_le <| (single_le_sum hwâ‚€ hi).trans_eq hwâ‚
  Â· convert hw
    rw [â† one_smul ğ•œ (a i), â† hwi, eq_comm]
    rw [â† hwi, â† sub_eq_zero, â† sum_erase_eq_sub hi,
      sum_eq_zero_iff_of_nonneg fun j hj â†¦ hwâ‚€ _ <| erase_subset _ _ hj] at hwâ‚
    refine sum_eq_single _ (fun j hj hji â†¦ ?_) (by simp [hi])
    rw [hwâ‚ _ <| mem_erase.2 âŸ¨hji, hjâŸ©, zero_smul]
  rintro _ hÎµ âŸ¨âŸ¨Îµ, âŸ¨hÎµâ‚€, hÎµâ‚âŸ©, rflâŸ©, hâŸ©
  replace hÎµâ‚€ : 0 < Îµ := hÎµâ‚€.lt_of_ne <| by rintro rfl; simp at h
  replace hÎµâ‚ : Îµ < 1 := hÎµâ‚.lt_of_ne <| by rintro rfl; simp at h
  have : 0 < 1 - Îµ := by linarith
  have hwi : 0 < 1 - w i := by linarith
  refine hw (z := lineMap x (âˆ‘ j âˆˆ t, w j â€¢ a j) ((w i)â»Â¹ / ((1 - Îµ) / Îµ + (w i)â»Â¹)))
    ?_ <| sbtw_lineMap_iff.2 âŸ¨(ne_of_mem_of_not_mem ((convex_convexHull ..).sum_mem hwâ‚€ hwâ‚
    fun i hi â†¦ subset_convexHull _ _ <| ha _ hi) hx).symm, by positivity,
    (div_lt_one <| by positivity).2 ?_âŸ©
  Â· have : Wbtw ğ•œ
      (lineMap x (a i) Îµ)
      (lineMap x (âˆ‘ j âˆˆ t, w j â€¢ a j) ((w i)â»Â¹ / ((1 - Îµ) / Îµ + (w i)â»Â¹)))
      (âˆ‘ j âˆˆ t.erase i, (w j / (1 - w i)) â€¢ a j) := by
      refine âŸ¨((1 - w i) / w i) / ((1 - Îµ) / Îµ + (1 - w i) / w i + 1), âŸ¨by positivity, ?_âŸ©, ?_âŸ©
      Â· refine (div_le_one <| by positivity).2 ?_
        calc
          (1 - w i) / w i = 0 + (1 - w i) / w i + 0 := by simp
          _ â‰¤ (1 - Îµ) / Îµ + (1 - w i) / w i + 1 := by gcongr <;> positivity
      have :
        w i â€¢ a i + (1 - w i) â€¢ âˆ‘ j âˆˆ t.erase i, (w j / (1 - w i)) â€¢ a j = âˆ‘ j âˆˆ t, w j â€¢ a j := by
        rw [smul_sum]
        simp_rw [smul_smul, mul_div_cancelâ‚€ _ hwi.ne']
        exact add_sum_erase _ (fun i â†¦ w i â€¢ a i) hi
      simp_rw [lineMap_apply_module, â† this, smul_add, smul_smul]
      match_scalars <;> field_simp <;> ring
    refine (convex_convexHull _ _).mem_of_wbtw this hÎµ <| (convex_convexHull _ _).sum_mem ?_ ?_ ?_
    Â· intro j hj
      positivity [hwâ‚€ j <| erase_subset _ _ hj]
    Â· rw [â† sum_div, sum_erase_eq_sub hi, hwâ‚, div_self hwi.ne']
    Â· exact fun j hj â†¦ subset_convexHull _ _ <| ha _ <| erase_subset _ _ hj
  Â· exact lt_add_of_pos_left _ <| by positivity

variable [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] [TopologicalSpace V] [IsTopologicalAddGroup V]
  [ContinuousSMul ğ•œ V]

/-- One cannot see any point in the interior of a set. -/
lemma IsVisible.eq_of_mem_interior (hsxy : IsVisible ğ•œ s x y) (hy : y âˆˆ interior s) :
    x = y := by
  by_contra! hxy
  suffices h : âˆ€á¶  (_Î´ : ğ•œ) in ğ“[>] 0, False by obtain âŸ¨_, âŸ¨âŸ©âŸ© := h.exists
  have hmem : âˆ€á¶  (Î´ : ğ•œ) in ğ“[>] 0, lineMap y x Î´ âˆˆ s :=
    lineMap_continuous.continuousWithinAt.eventually_mem
      (by simpa using mem_interior_iff_mem_nhds.1 hy)
  filter_upwards [hmem, Ioo_mem_nhdsGT zero_lt_one] with Î´ hmem hsbt using hsxy.symm hmem (by aesop)

/-- One cannot see any point of an open set. -/
lemma IsOpen.eq_of_isVisible_of_left_mem (hs : IsOpen s) (hsxy : IsVisible ğ•œ s x y) (hy : y âˆˆ s) :
    x = y :=
  hsxy.eq_of_mem_interior (by simpa [hs.interior_eq])

end Module

section Real
variable [AddCommGroup V] [Module â„ V] {s : Set V} {x y z : V}

/-- All points of the convex hull of a set `s` visible from a point `x âˆ‰ convexHull â„ s` lie in the
convex hull of such points that actually lie in `s`.

Note that the converse does not hold. -/
lemma IsVisible.mem_convexHull_isVisible (hx : x âˆ‰ convexHull â„ s) (hy : y âˆˆ convexHull â„ s)
    (hxy : IsVisible â„ (convexHull â„ s) x y) :
    y âˆˆ convexHull â„ {z âˆˆ s | IsVisible â„ (convexHull â„ s) x z} := by
  classical
  obtain âŸ¨Î¹, _, w, a, hwâ‚€, hwâ‚, ha, rflâŸ© := mem_convexHull_iff_exists_fintype.1 hy
  rw [â† Fintype.sum_subset (s := {i | w i â‰  0})
    fun i hi â†¦ mem_filter.2 âŸ¨mem_univ _, left_ne_zero_of_smul hiâŸ©]
  exact (convex_convexHull ..).sum_mem (fun i _ â†¦ hwâ‚€ _) (by rwa [sum_filter_ne_zero])
    fun i hi â†¦ subset_convexHull _ _ âŸ¨ha _, IsVisible.of_convexHull_of_pos (fun _ _ â†¦ hwâ‚€ _) hwâ‚
      (by simpa) hx hxy (mem_univ _) <| (hwâ‚€ _).lt_of_ne' (mem_filter.1 hi).2âŸ©

variable [TopologicalSpace V] [IsTopologicalAddGroup V] [ContinuousSMul â„ V]

/-- If `s` is a closed set, then any point `x` sees some point of `s` in any direction where there
is something to see. -/
lemma IsClosed.exists_wbtw_isVisible (hs : IsClosed s) (hy : y âˆˆ s) (x : V) :
    âˆƒ z âˆˆ s, Wbtw â„ x z y âˆ§ IsVisible â„ s x z := by
  let t : Set â„ := Ici 0 âˆ© lineMap x y â»Â¹' s
  have htâ‚ : 1 âˆˆ t := by simpa [t]
  have ht : BddBelow t := bddBelow_Ici.inter_of_left
  let Î´ : â„ := sInf t
  have hÎ´â‚ : Î´ â‰¤ 1 := csInf_le ht htâ‚
  obtain âŸ¨hÎ´â‚€, hÎ´âŸ© : 0 â‰¤ Î´ âˆ§ lineMap x y Î´ âˆˆ s :=
    (isClosed_Ici.inter <| hs.preimage lineMap_continuous).csInf_mem âŸ¨1, htâ‚âŸ© ht
  refine âŸ¨lineMap x y Î´, hÎ´, wbtw_lineMap_iff.2 <| .inr âŸ¨hÎ´â‚€, hÎ´â‚âŸ©, ?_âŸ©
  rintro _ hÎµ âŸ¨âŸ¨Îµ, âŸ¨hÎµâ‚€, hÎµâ‚âŸ©, rflâŸ©, -, hâŸ©
  replace hÎ´â‚€ : 0 < Î´ := hÎ´â‚€.lt_of_ne' <| by rintro hÎ´â‚€; simp [hÎ´â‚€] at h
  replace hÎµâ‚ : Îµ < 1 := hÎµâ‚.lt_of_ne <| by rintro rfl; simp at h
  rw [lineMap_lineMap_right] at hÎµ
  exact (csInf_le ht âŸ¨mul_nonneg hÎµâ‚€ hÎ´â‚€.le, hÎµâŸ©).not_gt <| mul_lt_of_lt_one_left hÎ´â‚€ hÎµâ‚

-- TODO: Once we have cone hulls, the RHS can be strengthened to
-- `coneHull â„ x {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y}`
/-- A set whose convex hull is closed lies in the cone based at a point `x` generated by its points
visible from `x` through its convex hull. -/
lemma IsClosed.convexHull_subset_affineSpan_isVisible (hs : IsClosed (convexHull â„ s))
    (hx : x âˆ‰ convexHull â„ s) :
    convexHull â„ s âŠ† affineSpan â„ ({x} âˆª {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y}) := by
  rintro y hy
  obtain âŸ¨z, hz, hxzy, hxzâŸ© := hs.exists_wbtw_isVisible hy x
  -- TODO: `calc` doesn't work with `âˆˆ` :(
  exact AffineSubspace.right_mem_of_wbtw hxzy (subset_affineSpan _ _ <| subset_union_left rfl)
    (affineSpan_mono _ subset_union_right <| convexHull_subset_affineSpan _ <|
      hxz.mem_convexHull_isVisible hx hz) (ne_of_mem_of_not_mem hz hx).symm

open Submodule in
/-- If `s` is a closed set of dimension `d` and `x` is a point outside of its convex hull,
then `x` sees at least `d` points of the convex hull of `s` that actually lie in `s`. -/
lemma rank_le_card_isVisible (hs : IsClosed (convexHull â„ s)) (hx : x âˆ‰ convexHull â„ s) :
    Module.rank â„ (span â„ (-x +áµ¥ s)) â‰¤ #{y âˆˆ s | IsVisible â„ (convexHull â„ s) x y} := by
  calc
    Module.rank â„ (span â„ (-x +áµ¥ s)) â‰¤
      Module.rank â„ (span â„
        (-x +áµ¥ affineSpan â„ ({x} âˆª {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y}) : Set V)) := by
      push_cast
      refine Submodule.rank_mono ?_
      gcongr
      exact (subset_convexHull ..).trans <| hs.convexHull_subset_affineSpan_isVisible hx
    _ = Module.rank â„ (span â„ (-x +áµ¥ {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y})) := by
      suffices h :
        -x +áµ¥ (affineSpan â„ ({x} âˆª {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y}) : Set V) =
          span â„ (-x +áµ¥ {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y}) by
        rw [AffineSubspace.coe_pointwise_vadd, h, span_span]
      simp [â† AffineSubspace.coe_pointwise_vadd, AffineSubspace.pointwise_vadd_span,
        vadd_set_insert, -coe_affineSpan, affineSpan_insert_zero]
    _ â‰¤ #(-x +áµ¥ {y âˆˆ s | IsVisible â„ (convexHull â„ s) x y}) := rank_span_le _
    _ = #{y âˆˆ s | IsVisible â„ (convexHull â„ s) x y} := by simp

end Real
