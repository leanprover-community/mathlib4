/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Function

/-!
# Quasiconvex and quasiconcave functions

This file defines quasiconvexity, quasiconcavity and quasilinearity of functions, which are
generalizations of unimodality and monotonicity. Convexity implies quasiconvexity, concavity implies
quasiconcavity, and monotonicity implies quasilinearity.

## Main declarations

* `QuasiconvexOn ğ•œ s f`: Quasiconvexity of the function `f` on the set `s` with scalars `ğ•œ`. This
  means that, for all `r`, `{x âˆˆ s | f x â‰¤ r}` is `ğ•œ`-convex.
* `QuasiconcaveOn ğ•œ s f`: Quasiconcavity of the function `f` on the set `s` with scalars `ğ•œ`. This
  means that, for all `r`, `{x âˆˆ s | r â‰¤ f x}` is `ğ•œ`-convex.
* `QuasilinearOn ğ•œ s f`: Quasilinearity of the function `f` on the set `s` with scalars `ğ•œ`. This
  means that `f` is both quasiconvex and quasiconcave.

## References

* https://en.wikipedia.org/wiki/Quasiconvex_function
-/


open Function OrderDual Set

variable {ğ•œ E Î² : Type*}

section OrderedSemiring

variable [Semiring ğ•œ] [PartialOrder ğ•œ]

section AddCommMonoid_E

variable [AddCommMonoid E]

section LE_Î²

variable (ğ•œ) [LE Î²] [SMul ğ•œ E] (s : Set E) (f : E â†’ Î²)

/-- A function is quasiconvex if all its sublevels are convex.
This means that, for all `r`, `{x âˆˆ s | f x â‰¤ r}` is `ğ•œ`-convex. -/
def QuasiconvexOn : Prop :=
  âˆ€ r, Convex ğ•œ ({ x âˆˆ s | f x â‰¤ r })

/-- A function is quasiconcave if all its superlevels are convex.
This means that, for all `r`, `{x âˆˆ s | r â‰¤ f x}` is `ğ•œ`-convex. -/
def QuasiconcaveOn : Prop :=
  âˆ€ r, Convex ğ•œ ({ x âˆˆ s | r â‰¤ f x })

/-- A function is quasilinear if it is both quasiconvex and quasiconcave.
This means that, for all `r`,
the sets `{x âˆˆ s | f x â‰¤ r}` and `{x âˆˆ s | r â‰¤ f x}` are `ğ•œ`-convex. -/
def QuasilinearOn : Prop :=
  QuasiconvexOn ğ•œ s f âˆ§ QuasiconcaveOn ğ•œ s f

variable {ğ•œ s f}

theorem QuasiconvexOn.dual : QuasiconvexOn ğ•œ s f â†’ QuasiconcaveOn ğ•œ s (toDual âˆ˜ f) :=
  id

theorem QuasiconcaveOn.dual : QuasiconcaveOn ğ•œ s f â†’ QuasiconvexOn ğ•œ s (toDual âˆ˜ f) :=
  id

theorem QuasilinearOn.dual : QuasilinearOn ğ•œ s f â†’ QuasilinearOn ğ•œ s (toDual âˆ˜ f) :=
  And.symm

theorem Convex.quasiconvexOn_of_convex_le (hs : Convex ğ•œ s) (h : âˆ€ r, Convex ğ•œ { x | f x â‰¤ r }) :
    QuasiconvexOn ğ•œ s f := fun r â†¦ hs.inter (h r)

theorem Convex.quasiconcaveOn_of_convex_ge (hs : Convex ğ•œ s) (h : âˆ€ r, Convex ğ•œ { x | r â‰¤ f x }) :
    QuasiconcaveOn ğ•œ s f :=
  Convex.quasiconvexOn_of_convex_le (Î² := Î²áµ’áµˆ) hs h

theorem QuasiconvexOn.convex [IsDirected Î² (Â· â‰¤ Â·)] (hf : QuasiconvexOn ğ•œ s f) : Convex ğ•œ s :=
  fun x hx y hy _ _ ha hb hab â†¦
  let âŸ¨_, hxz, hyzâŸ© := exists_ge_ge (f x) (f y)
  (hf _ âŸ¨hx, hxzâŸ© âŸ¨hy, hyzâŸ© ha hb hab).1

theorem QuasiconcaveOn.convex [IsDirected Î² (Â· â‰¥ Â·)] (hf : QuasiconcaveOn ğ•œ s f) : Convex ğ•œ s :=
  hf.dual.convex

end LE_Î²

section Semilattice_Î²

variable [SMul ğ•œ E] {s : Set E} {f g : E â†’ Î²}

theorem QuasiconvexOn.sup [SemilatticeSup Î²] (hf : QuasiconvexOn ğ•œ s f)
    (hg : QuasiconvexOn ğ•œ s g) : QuasiconvexOn ğ•œ s (f âŠ” g) := by
  intro r
  simp_rw [Pi.sup_def, sup_le_iff, Set.sep_and]
  exact (hf r).inter (hg r)

theorem QuasiconcaveOn.inf [SemilatticeInf Î²] (hf : QuasiconcaveOn ğ•œ s f)
    (hg : QuasiconcaveOn ğ•œ s g) : QuasiconcaveOn ğ•œ s (f âŠ“ g) :=
  hf.dual.sup hg

end Semilattice_Î²

section LinearOrder_Î²

variable [LinearOrder Î²] [SMul ğ•œ E] {s : Set E} {f : E â†’ Î²}

theorem quasiconvexOn_iff_le_max : QuasiconvexOn ğ•œ s f â†” Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„,
    y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ f (a â€¢ x + b â€¢ y) â‰¤ max (f x) (f y) :=
  âŸ¨fun hf â†¦
    âŸ¨hf.convex, fun _ hx _ hy _ _ ha hb hab â†¦
      (hf _ âŸ¨hx, le_max_left _ _âŸ© âŸ¨hy, le_max_right _ _âŸ© ha hb hab).2âŸ©,
    fun hf _ _ hx _ hy _ _ ha hb hab â†¦
    âŸ¨hf.1 hx.1 hy.1 ha hb hab, (hf.2 hx.1 hy.1 ha hb hab).trans <| max_le hx.2 hy.2âŸ©âŸ©

theorem quasiconcaveOn_iff_min_le : QuasiconcaveOn ğ•œ s f â†” Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„,
    y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ min (f x) (f y) â‰¤ f (a â€¢ x + b â€¢ y) :=
  quasiconvexOn_iff_le_max (Î² := Î²áµ’áµˆ)

theorem quasilinearOn_iff_mem_uIcc : QuasilinearOn ğ•œ s f â†” Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„,
    y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ f (a â€¢ x + b â€¢ y) âˆˆ uIcc (f x) (f y) := by
  rw [QuasilinearOn, quasiconvexOn_iff_le_max, quasiconcaveOn_iff_min_le, and_and_and_comm,
    and_self_iff]
  apply and_congr_right'
  simp_rw [â† forall_and, â† Icc_min_max, mem_Icc, and_comm]

theorem QuasiconvexOn.convex_lt (hf : QuasiconvexOn ğ•œ s f) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x < r }) := by
  refine fun x hx y hy a b ha hb hab â†¦ ?_
  have h := hf _ âŸ¨hx.1, le_max_left _ _âŸ© âŸ¨hy.1, le_max_right _ _âŸ© ha hb hab
  exact âŸ¨h.1, h.2.trans_lt <| max_lt hx.2 hy.2âŸ©

theorem QuasiconcaveOn.convex_gt (hf : QuasiconcaveOn ğ•œ s f) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  hf.dual.convex_lt r

end LinearOrder_Î²

section OrderedSMul_Î²

variable [AddCommMonoid Î²] [PartialOrder Î²] [IsOrderedAddMonoid Î²]
  [Module ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]
  {s : Set E} {f : E â†’ Î²}

theorem ConvexOn.quasiconvexOn (hf : ConvexOn ğ•œ s f) : QuasiconvexOn ğ•œ s f :=
  hf.convex_le

theorem ConcaveOn.quasiconcaveOn (hf : ConcaveOn ğ•œ s f) : QuasiconcaveOn ğ•œ s f :=
  hf.convex_ge

end OrderedSMul_Î²

end AddCommMonoid_E

section LinearOrderedAddCommMonoid_E

variable [AddCommMonoid E] [LinearOrder E] [IsOrderedAddMonoid E]
  [PartialOrder Î²] [Module ğ•œ E] [OrderedSMul ğ•œ E]
  {s : Set E} {f : E â†’ Î²}

theorem MonotoneOn.quasiconvexOn (hf : MonotoneOn f s) (hs : Convex ğ•œ s) : QuasiconvexOn ğ•œ s f :=
  hf.convex_le hs

theorem MonotoneOn.quasiconcaveOn (hf : MonotoneOn f s) (hs : Convex ğ•œ s) : QuasiconcaveOn ğ•œ s f :=
  hf.convex_ge hs

theorem MonotoneOn.quasilinearOn (hf : MonotoneOn f s) (hs : Convex ğ•œ s) : QuasilinearOn ğ•œ s f :=
  âŸ¨hf.quasiconvexOn hs, hf.quasiconcaveOn hsâŸ©

theorem AntitoneOn.quasiconvexOn (hf : AntitoneOn f s) (hs : Convex ğ•œ s) : QuasiconvexOn ğ•œ s f :=
  hf.convex_le hs

theorem AntitoneOn.quasiconcaveOn (hf : AntitoneOn f s) (hs : Convex ğ•œ s) : QuasiconcaveOn ğ•œ s f :=
  hf.convex_ge hs

theorem AntitoneOn.quasilinearOn (hf : AntitoneOn f s) (hs : Convex ğ•œ s) : QuasilinearOn ğ•œ s f :=
  âŸ¨hf.quasiconvexOn hs, hf.quasiconcaveOn hsâŸ©

theorem Monotone.quasiconvexOn (hf : Monotone f) : QuasiconvexOn ğ•œ univ f :=
  (hf.monotoneOn _).quasiconvexOn convex_univ

theorem Monotone.quasiconcaveOn (hf : Monotone f) : QuasiconcaveOn ğ•œ univ f :=
  (hf.monotoneOn _).quasiconcaveOn convex_univ

theorem Monotone.quasilinearOn (hf : Monotone f) : QuasilinearOn ğ•œ univ f :=
  âŸ¨hf.quasiconvexOn, hf.quasiconcaveOnâŸ©

theorem Antitone.quasiconvexOn (hf : Antitone f) : QuasiconvexOn ğ•œ univ f :=
  (hf.antitoneOn _).quasiconvexOn convex_univ

theorem Antitone.quasiconcaveOn (hf : Antitone f) : QuasiconcaveOn ğ•œ univ f :=
  (hf.antitoneOn _).quasiconcaveOn convex_univ

theorem Antitone.quasilinearOn (hf : Antitone f) : QuasilinearOn ğ•œ univ f :=
  âŸ¨hf.quasiconvexOn, hf.quasiconcaveOnâŸ©

end LinearOrderedAddCommMonoid_E

end OrderedSemiring

section LinearOrderedField

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] {s : Set ğ•œ} {f : ğ•œ â†’ Î²}

theorem QuasilinearOn.monotoneOn_or_antitoneOn [LinearOrder Î²] (hf : QuasilinearOn ğ•œ s f) :
    MonotoneOn f s âˆ¨ AntitoneOn f s := by
  simp_rw [monotoneOn_or_antitoneOn_iff_uIcc, â† segment_eq_uIcc]
  rintro a ha b hb c _ h
  refine âŸ¨((hf.2 _).segment_subset ?_ ?_ h).2, ((hf.1 _).segment_subset ?_ ?_ h).2âŸ© <;> simp [*]

theorem quasilinearOn_iff_monotoneOn_or_antitoneOn [LinearOrder Î²]
    (hs : Convex ğ•œ s) : QuasilinearOn ğ•œ s f â†” MonotoneOn f s âˆ¨ AntitoneOn f s :=
  âŸ¨fun h â†¦ h.monotoneOn_or_antitoneOn, fun h â†¦
    h.elim (fun h â†¦ h.quasilinearOn hs) fun h â†¦ h.quasilinearOn hsâŸ©

end LinearOrderedField
