/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.Order.BigOperators.Ring.Finset
import Mathlib.Analysis.Convex.Hull
import Mathlib.LinearAlgebra.AffineSpace.Basis

/-!
# Convex combinations

This file defines convex combinations of points in a vector space.

## Main declarations

* `Finset.centerMass`: Center of mass of a finite family of points.

## Implementation notes

We divide by the sum of the weights in the definition of `Finset.centerMass` because of the way
mathematical arguments go: one doesn't change weights, but merely adds some. This also makes a few
lemmas unconditional on the sum of the weights being `1`.
-/

assert_not_exists Cardinal

open Set Function Pointwise

universe u u'

section
variable {R R' E F Î¹ Î¹' Î± : Type*} [Field R] [Field R'] [AddCommGroup E] [AddCommGroup F]
  [AddCommGroup Î±] [LinearOrder Î±] [Module R E] [Module R F] [Module R Î±] {s : Set E}

/-- Center of mass of a finite collection of points with prescribed weights.
Note that we require neither `0 â‰¤ w i` nor `âˆ‘ w = 1`. -/
def Finset.centerMass (t : Finset Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E) : E :=
  (âˆ‘ i âˆˆ t, w i)â»Â¹ â€¢ âˆ‘ i âˆˆ t, w i â€¢ z i

variable (i j : Î¹) (c : R) (t : Finset Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E)

open Finset

theorem Finset.centerMass_empty : (âˆ… : Finset Î¹).centerMass w z = 0 := by
  simp only [centerMass, sum_empty, smul_zero]

theorem Finset.centerMass_pair [DecidableEq Î¹] (hne : i â‰  j) :
    ({i, j} : Finset Î¹).centerMass w z = (w i / (w i + w j)) â€¢ z i + (w j / (w i + w j)) â€¢ z j := by
  simp only [centerMass, sum_pair hne]
  module

variable {w}

theorem Finset.centerMass_insert [DecidableEq Î¹] (ha : i âˆ‰ t) (hw : âˆ‘ j âˆˆ t, w j â‰  0) :
    (insert i t).centerMass w z =
      (w i / (w i + âˆ‘ j âˆˆ t, w j)) â€¢ z i +
        ((âˆ‘ j âˆˆ t, w j) / (w i + âˆ‘ j âˆˆ t, w j)) â€¢ t.centerMass w z := by
  simp only [centerMass, sum_insert ha, smul_add, (mul_smul _ _ _).symm, â† div_eq_inv_mul]
  congr 2
  rw [div_mul_eq_mul_div, mul_inv_cancelâ‚€ hw, one_div]

theorem Finset.centerMass_singleton (hw : w i â‰  0) : ({i} : Finset Î¹).centerMass w z = z i := by
  rw [centerMass, sum_singleton, sum_singleton]
  match_scalars
  field

@[simp] lemma Finset.centerMass_neg_left : t.centerMass (-w) z = t.centerMass w z := by
  simp [centerMass, inv_neg]

lemma Finset.centerMass_smul_left {c : R'} [Module R' R] [Module R' E] [SMulCommClass R' R R]
    [IsScalarTower R' R R] [SMulCommClass R R' E] [IsScalarTower R' R E] (hc : c â‰  0) :
    t.centerMass (c â€¢ w) z = t.centerMass w z := by
  simp [centerMass, -smul_assoc, smul_assoc c, â† smul_sum, smul_invâ‚€, smul_smul_smul_comm, hc]

theorem Finset.centerMass_eq_of_sum_1 (hw : âˆ‘ i âˆˆ t, w i = 1) :
    t.centerMass w z = âˆ‘ i âˆˆ t, w i â€¢ z i := by
  simp only [Finset.centerMass, hw, inv_one, one_smul]

theorem Finset.centerMass_smul : (t.centerMass w fun i => c â€¢ z i) = c â€¢ t.centerMass w z := by
  simp only [Finset.centerMass, Finset.smul_sum, (mul_smul _ _ _).symm, mul_comm c, mul_assoc]

/-- A convex combination of two centers of mass is a center of mass as well. This version
deals with two different index types. -/
theorem Finset.centerMass_segment' (s : Finset Î¹) (t : Finset Î¹') (ws : Î¹ â†’ R) (zs : Î¹ â†’ E)
    (wt : Î¹' â†’ R) (zt : Î¹' â†’ E) (hws : âˆ‘ i âˆˆ s, ws i = 1) (hwt : âˆ‘ i âˆˆ t, wt i = 1) (a b : R)
    (hab : a + b = 1) : a â€¢ s.centerMass ws zs + b â€¢ t.centerMass wt zt = (s.disjSum t).centerMass
    (Sum.elim (fun i => a * ws i) fun j => b * wt j) (Sum.elim zs zt) := by
  rw [s.centerMass_eq_of_sum_1 _ hws, t.centerMass_eq_of_sum_1 _ hwt, smul_sum, smul_sum, â†
    Finset.sum_sumElim, Finset.centerMass_eq_of_sum_1]
  Â· congr with âŸ¨âŸ© <;> simp only [Sum.elim_inl, Sum.elim_inr, mul_smul]
  Â· rw [sum_sumElim, â† mul_sum, â† mul_sum, hws, hwt, mul_one, mul_one, hab]

/-- A convex combination of two centers of mass is a center of mass as well. This version
works if two centers of mass share the set of original points. -/
theorem Finset.centerMass_segment (s : Finset Î¹) (wâ‚ wâ‚‚ : Î¹ â†’ R) (z : Î¹ â†’ E)
    (hwâ‚ : âˆ‘ i âˆˆ s, wâ‚ i = 1) (hwâ‚‚ : âˆ‘ i âˆˆ s, wâ‚‚ i = 1) (a b : R) (hab : a + b = 1) :
    a â€¢ s.centerMass wâ‚ z + b â€¢ s.centerMass wâ‚‚ z =
    s.centerMass (fun i => a * wâ‚ i + b * wâ‚‚ i) z := by
  have hw : (âˆ‘ i âˆˆ s, (a * wâ‚ i + b * wâ‚‚ i)) = 1 := by
    simp only [â† mul_sum, sum_add_distrib, mul_one, *]
  simp only [Finset.centerMass_eq_of_sum_1, smul_sum, sum_add_distrib, add_smul, mul_smul, *]

theorem Finset.centerMass_ite_eq [DecidableEq Î¹] (hi : i âˆˆ t) :
    t.centerMass (fun j => if i = j then (1 : R) else 0) z = z i := by
  rw [Finset.centerMass_eq_of_sum_1]
  Â· trans âˆ‘ j âˆˆ t, if i = j then z i else 0
    Â· congr with i
      split_ifs with h
      exacts [h â–¸ one_smul _ _, zero_smul _ _]
    Â· rw [sum_ite_eq, if_pos hi]
  Â· rw [sum_ite_eq, if_pos hi]

variable {t}

theorem Finset.centerMass_subset {t' : Finset Î¹} (ht : t âŠ† t') (h : âˆ€ i âˆˆ t', i âˆ‰ t â†’ w i = 0) :
    t.centerMass w z = t'.centerMass w z := by
  rw [centerMass, sum_subset ht h, smul_sum, centerMass, smul_sum]
  apply sum_subset ht
  intro i hit' hit
  rw [h i hit' hit, zero_smul, smul_zero]

theorem Finset.centerMass_filter_ne_zero [âˆ€ i, Decidable (w i â‰  0)] :
    {i âˆˆ t | w i â‰  0}.centerMass w z = t.centerMass w z :=
  Finset.centerMass_subset z (filter_subset _ _) fun i hit hit' => by
    simpa only [hit, mem_filter, true_and, Ne, Classical.not_not] using hit'

namespace Finset

variable [LinearOrder R] [IsOrderedAddMonoid Î±] [PosSMulMono R Î±]

theorem centerMass_le_sup {s : Finset Î¹} {f : Î¹ â†’ Î±} {w : Î¹ â†’ R} (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i)
    (hwâ‚ : 0 < âˆ‘ i âˆˆ s, w i) :
    s.centerMass w f â‰¤ s.sup' (nonempty_of_ne_empty <| by rintro rfl; simp at hwâ‚) f := by
  rw [centerMass, inv_smul_le_iff_of_pos hwâ‚, sum_smul]
  exact sum_le_sum fun i hi => smul_le_smul_of_nonneg_left (le_sup' _ hi) <| hwâ‚€ i hi

theorem inf_le_centerMass {s : Finset Î¹} {f : Î¹ â†’ Î±} {w : Î¹ â†’ R} (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i)
    (hwâ‚ : 0 < âˆ‘ i âˆˆ s, w i) :
    s.inf' (nonempty_of_ne_empty <| by rintro rfl; simp at hwâ‚) f â‰¤ s.centerMass w f :=
  centerMass_le_sup (Î± := Î±áµ’áµˆ) hwâ‚€ hwâ‚

end Finset

variable {z}

lemma Finset.centerMass_of_sum_add_sum_eq_zero {s t : Finset Î¹}
    (hw : âˆ‘ i âˆˆ s, w i + âˆ‘ i âˆˆ t, w i = 0) (hz : âˆ‘ i âˆˆ s, w i â€¢ z i + âˆ‘ i âˆˆ t, w i â€¢ z i = 0) :
    s.centerMass w z = t.centerMass w z := by
  simp [centerMass, eq_neg_of_add_eq_zero_right hw, eq_neg_of_add_eq_zero_left hz]

variable [LinearOrder R] [IsStrictOrderedRing R] [IsOrderedAddMonoid Î±] [PosSMulMono R Î±]

/-- The center of mass of a finite subset of a convex set belongs to the set
provided that all weights are non-negative, and the total weight is positive. -/
theorem Convex.centerMass_mem (hs : Convex R s) :
    (âˆ€ i âˆˆ t, 0 â‰¤ w i) â†’ (0 < âˆ‘ i âˆˆ t, w i) â†’ (âˆ€ i âˆˆ t, z i âˆˆ s) â†’ t.centerMass w z âˆˆ s := by
  classical
  induction t using Finset.induction with
  | empty => simp
  | insert i t hi ht =>
    intro hâ‚€ hpos hmem
    have zi : z i âˆˆ s := hmem _ (mem_insert_self _ _)
    have hsâ‚€ : âˆ€ j âˆˆ t, 0 â‰¤ w j := fun j hj => hâ‚€ j <| mem_insert_of_mem hj
    rw [sum_insert hi] at hpos
    by_cases hsum_t : âˆ‘ j âˆˆ t, w j = 0
    Â· have ws : âˆ€ j âˆˆ t, w j = 0 := (sum_eq_zero_iff_of_nonneg hsâ‚€).1 hsum_t
      have wz : âˆ‘ j âˆˆ t, w j â€¢ z j = 0 := sum_eq_zero fun i hi => by simp [ws i hi]
      simp only [centerMass, sum_insert hi, wz, hsum_t, add_zero]
      simp only [hsum_t, add_zero] at hpos
      rw [â† mul_smul, inv_mul_cancelâ‚€ (ne_of_gt hpos), one_smul]
      exact zi
    Â· rw [Finset.centerMass_insert _ _ _ hi hsum_t]
      refine convex_iff_div.1 hs zi (ht hsâ‚€ ?_ ?_) ?_ (sum_nonneg hsâ‚€) hpos
      Â· exact lt_of_le_of_ne (sum_nonneg hsâ‚€) (Ne.symm hsum_t)
      Â· intro j hj
        exact hmem j (mem_insert_of_mem hj)
      Â· exact hâ‚€ _ (mem_insert_self _ _)

theorem Convex.sum_mem (hs : Convex R s) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i âˆˆ t, w i = 1)
    (hz : âˆ€ i âˆˆ t, z i âˆˆ s) : (âˆ‘ i âˆˆ t, w i â€¢ z i) âˆˆ s := by
  simpa only [hâ‚, centerMass, inv_one, one_smul] using
    hs.centerMass_mem hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hz

/-- A version of `Convex.sum_mem` for `finsum`s. If `s` is a convex set, `w : Î¹ â†’ R` is a family of
nonnegative weights with sum one and `z : Î¹ â†’ E` is a family of elements of a module over `R` such
that `z i âˆˆ s` whenever `w i â‰  0`, then the sum `âˆ‘á¶  i, w i â€¢ z i` belongs to `s`. See also
`PartitionOfUnity.finsum_smul_mem_convex`. -/
theorem Convex.finsum_mem {Î¹ : Sort*} {w : Î¹ â†’ R} {z : Î¹ â†’ E} {s : Set E} (hs : Convex R s)
    (hâ‚€ : âˆ€ i, 0 â‰¤ w i) (hâ‚ : âˆ‘á¶  i, w i = 1) (hz : âˆ€ i, w i â‰  0 â†’ z i âˆˆ s) :
    (âˆ‘á¶  i, w i â€¢ z i) âˆˆ s := by
  have hfin_w : (support (w âˆ˜ PLift.down)).Finite := by
    by_contra H
    rw [finsum, dif_neg H] at hâ‚
    exact zero_ne_one hâ‚
  have hsub : support ((fun i => w i â€¢ z i) âˆ˜ PLift.down) âŠ† hfin_w.toFinset :=
    (support_smul_subset_left _ _).trans hfin_w.coe_toFinset.ge
  rw [finsum_eq_sum_plift_of_support_subset hsub]
  refine hs.sum_mem (fun _ _ => hâ‚€ _) ?_ fun i hi => hz _ ?_
  Â· rwa [finsum, dif_pos hfin_w] at hâ‚
  Â· rwa [hfin_w.mem_toFinset] at hi

theorem convex_iff_sum_mem : Convex R s â†” âˆ€ (t : Finset E) (w : E â†’ R),
    (âˆ€ i âˆˆ t, 0 â‰¤ w i) â†’ âˆ‘ i âˆˆ t, w i = 1 â†’ (âˆ€ x âˆˆ t, x âˆˆ s) â†’ (âˆ‘ x âˆˆ t, w x â€¢ x) âˆˆ s := by
  classical
  refine âŸ¨fun hs t w hwâ‚€ hwâ‚ hts => hs.sum_mem hwâ‚€ hwâ‚ hts, ?_âŸ©
  intro h x hx y hy a b ha hb hab
  by_cases h_cases : x = y
  Â· rw [h_cases, â† add_smul, hab, one_smul]
    exact hy
  Â· convert h {x, y} (fun z => if z = y then b else a) _ _ _
    Â· simp only [sum_pair h_cases, if_neg h_cases, if_pos trivial]
    Â· grind
    Â· simp only [sum_pair h_cases, if_neg h_cases, if_pos trivial, hab]
    Â· intro i hi
      simp only [Finset.mem_singleton, Finset.mem_insert] at hi
      cases hi <;> subst i <;> assumption

theorem Finset.centerMass_mem_convexHull (t : Finset Î¹) {w : Î¹ â†’ R} (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hws : 0 < âˆ‘ i âˆˆ t, w i) {z : Î¹ â†’ E} (hz : âˆ€ i âˆˆ t, z i âˆˆ s) :
    t.centerMass w z âˆˆ convexHull R s :=
  (convex_convexHull R s).centerMass_mem hwâ‚€ hws fun i hi => subset_convexHull R s <| hz i hi

/-- A version of `Finset.centerMass_mem_convexHull` for when the weights are nonpositive. -/
lemma Finset.centerMass_mem_convexHull_of_nonpos (t : Finset Î¹) (hwâ‚€ : âˆ€ i âˆˆ t, w i â‰¤ 0)
    (hws : âˆ‘ i âˆˆ t, w i < 0) (hz : âˆ€ i âˆˆ t, z i âˆˆ s) : t.centerMass w z âˆˆ convexHull R s := by
  rw [â† centerMass_neg_left]
  exact Finset.centerMass_mem_convexHull _ (fun _i hi â†¦ neg_nonneg.2 <| hwâ‚€ _ hi) (by simpa) hz

/-- A refinement of `Finset.centerMass_mem_convexHull` when the indexed family is a `Finset` of
the space. -/
theorem Finset.centerMass_id_mem_convexHull (t : Finset E) {w : E â†’ R} (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hws : 0 < âˆ‘ i âˆˆ t, w i) : t.centerMass w id âˆˆ convexHull R (t : Set E) :=
  t.centerMass_mem_convexHull hwâ‚€ hws fun _ => mem_coe.2

/-- A version of `Finset.centerMass_mem_convexHull` for when the weights are nonpositive. -/
lemma Finset.centerMass_id_mem_convexHull_of_nonpos (t : Finset E) {w : E â†’ R}
    (hwâ‚€ : âˆ€ i âˆˆ t, w i â‰¤ 0) (hws : âˆ‘ i âˆˆ t, w i < 0) :
    t.centerMass w id âˆˆ convexHull R (t : Set E) :=
  t.centerMass_mem_convexHull_of_nonpos hwâ‚€ hws fun _ â†¦ mem_coe.2

omit [LinearOrder R] [IsStrictOrderedRing R] in
theorem affineCombination_eq_centerMass {Î¹ : Type*} {t : Finset Î¹} {p : Î¹ â†’ E} {w : Î¹ â†’ R}
    (hwâ‚‚ : âˆ‘ i âˆˆ t, w i = 1) : t.affineCombination R p w = centerMass t w p := by
  rw [affineCombination_eq_weightedVSubOfPoint_vadd_of_sum_eq_one _ w _ hwâ‚‚ (0 : E),
    Finset.weightedVSubOfPoint_apply, vadd_eq_add, add_zero, t.centerMass_eq_of_sum_1 _ hwâ‚‚]
  simp_rw [vsub_eq_sub, sub_zero]

theorem affineCombination_mem_convexHull {s : Finset Î¹} {v : Î¹ â†’ E} {w : Î¹ â†’ R}
    (hwâ‚€ : âˆ€ i âˆˆ s, 0 â‰¤ w i) (hwâ‚ : s.sum w = 1) :
    s.affineCombination R v w âˆˆ convexHull R (range v) := by
  rw [affineCombination_eq_centerMass hwâ‚]
  apply s.centerMass_mem_convexHull hwâ‚€
  Â· simp [hwâ‚]
  Â· simp

/-- The centroid can be regarded as a center of mass. -/
@[simp]
theorem Finset.centroid_eq_centerMass (s : Finset Î¹) (hs : s.Nonempty) (p : Î¹ â†’ E) :
    s.centroid R p = s.centerMass (s.centroidWeights R) p :=
  affineCombination_eq_centerMass (s.sum_centroidWeights_eq_one_of_nonempty R hs)

theorem Finset.centroid_mem_convexHull (s : Finset E) (hs : s.Nonempty) :
    s.centroid R id âˆˆ convexHull R (s : Set E) := by
  rw [s.centroid_eq_centerMass hs]
  apply s.centerMass_id_mem_convexHull
  Â· simp only [inv_nonneg, imp_true_iff, Nat.cast_nonneg, Finset.centroidWeights_apply]
  Â· have hs_card : (#s : R) â‰  0 := by simp [Finset.nonempty_iff_ne_empty.mp hs]
    simp only [hs_card, Finset.sum_const, nsmul_eq_mul, mul_inv_cancelâ‚€, Ne, not_false_iff,
      Finset.centroidWeights_apply, zero_lt_one]

theorem convexHull_range_eq_exists_affineCombination (v : Î¹ â†’ E) : convexHull R (range v) =
    { x | âˆƒ (s : Finset Î¹) (w : Î¹ â†’ R), (âˆ€ i âˆˆ s, 0 â‰¤ w i) âˆ§ s.sum w = 1 âˆ§
      s.affineCombination R v w = x } := by
  classical
  refine Subset.antisymm (convexHull_min ?_ ?_) ?_
  Â· intro x hx
    obtain âŸ¨i, hiâŸ© := Set.mem_range.mp hx
    exact âŸ¨{i}, Function.const Î¹ (1 : R), by simp, by simp, by simp [hi]âŸ©
  Â· rintro x âŸ¨s, w, hwâ‚€, hwâ‚, rflâŸ© y âŸ¨s', w', hwâ‚€', hwâ‚', rflâŸ© a b ha hb hab
    let W : Î¹ â†’ R := fun i => (if i âˆˆ s then a * w i else 0) + if i âˆˆ s' then b * w' i else 0
    have hWâ‚ : (s âˆª s').sum W = 1 := by
      rw [sum_add_distrib, â† sum_subset subset_union_left,
        â† sum_subset subset_union_right, sum_ite_of_true,
        sum_ite_of_true, â† mul_sum, â† mul_sum, hwâ‚, hwâ‚', â† add_mul, hab,
        mul_one] <;> intros <;> simp_all
    refine âŸ¨s âˆª s', W, ?_, hWâ‚, ?_âŸ©
    Â· rintro i -
      by_cases hi : i âˆˆ s <;> by_cases hi' : i âˆˆ s' <;>
        simp [W, hi, hi', add_nonneg, mul_nonneg ha (hwâ‚€ i _), mul_nonneg hb (hwâ‚€' i _)]
    Â· simp_rw [W, affineCombination_eq_linear_combination (s âˆª s') v _ hWâ‚,
        affineCombination_eq_linear_combination s v w hwâ‚,
        affineCombination_eq_linear_combination s' v w' hwâ‚', add_smul, sum_add_distrib]
      rw [â† sum_subset subset_union_left, â† sum_subset subset_union_right]
      Â· simp only [ite_smul, sum_ite_of_true fun _ hi => hi, mul_smul, â† smul_sum]
      Â· intro i _ hi'
        simp [hi']
      Â· intro i _ hi'
        simp [hi']
  Â· rintro x âŸ¨s, w, hwâ‚€, hwâ‚, rflâŸ©
    exact affineCombination_mem_convexHull hwâ‚€ hwâ‚

/--
Convex hull of `s` is equal to the set of all centers of masses of `Finset`s `t`, `z '' t âŠ† s`.
For universe reasons, you shouldn't use this lemma to prove that a given center of mass belongs
to the convex hull. Use convexity of the convex hull instead.
-/
theorem convexHull_eq (s : Set E) : convexHull R s =
    { x : E | âˆƒ (Î¹ : Type) (t : Finset Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E), (âˆ€ i âˆˆ t, 0 â‰¤ w i) âˆ§
      âˆ‘ i âˆˆ t, w i = 1 âˆ§ (âˆ€ i âˆˆ t, z i âˆˆ s) âˆ§ t.centerMass w z = x } := by
  refine Subset.antisymm (convexHull_min ?_ ?_) ?_
  Â· intro x hx
    use PUnit, {PUnit.unit}, fun _ => 1, fun _ => x, fun _ _ => zero_le_one, sum_singleton _ _,
      fun _ _ => hx
    simp only [Finset.centerMass, Finset.sum_singleton, inv_one, one_smul]
  Â· rintro x âŸ¨Î¹, sx, wx, zx, hwxâ‚€, hwxâ‚, hzx, rflâŸ© y âŸ¨Î¹', sy, wy, zy, hwyâ‚€, hwyâ‚, hzy, rflâŸ© a b ha
      hb hab
    rw [Finset.centerMass_segment' _ _ _ _ _ _ hwxâ‚ hwyâ‚ _ _ hab]
    refine âŸ¨_, _, _, _, ?_, ?_, ?_, rflâŸ©
    Â· rintro i hi
      rw [Finset.mem_disjSum] at hi
      rcases hi with (âŸ¨j, hj, rflâŸ© | âŸ¨j, hj, rflâŸ©) <;> simp only [Sum.elim_inl, Sum.elim_inr] <;>
        apply_rules [mul_nonneg, hwxâ‚€, hwyâ‚€]
    Â· simp [â† mul_sum, *]
    Â· intro i hi
      rw [Finset.mem_disjSum] at hi
      rcases hi with (âŸ¨j, hj, rflâŸ© | âŸ¨j, hj, rflâŸ©) <;> apply_rules [hzx, hzy]
  Â· rintro _ âŸ¨Î¹, t, w, z, hwâ‚€, hwâ‚, hz, rflâŸ©
    exact t.centerMass_mem_convexHull hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one) hz

/-- Universe polymorphic version of the reverse implication of `mem_convexHull_iff_exists_fintype`.
-/
lemma mem_convexHull_of_exists_fintype {s : Set E} {x : E} [Fintype Î¹] (w : Î¹ â†’ R) (z : Î¹ â†’ E)
    (hwâ‚€ : âˆ€ i, 0 â‰¤ w i) (hwâ‚ : âˆ‘ i, w i = 1) (hz : âˆ€ i, z i âˆˆ s) (hx : âˆ‘ i, w i â€¢ z i = x) :
    x âˆˆ convexHull R s := by
  rw [â† hx, â† centerMass_eq_of_sum_1 _ _ hwâ‚]
  exact centerMass_mem_convexHull _ (by simpa using hwâ‚€) (by simp [hwâ‚]) (by simpa using hz)

/-- The convex hull of `s` is equal to the set of centers of masses of finite families of points in
`s`.

For universe reasons, you shouldn't use this lemma to prove that a given center of mass belongs
to the convex hull. Use `mem_convexHull_of_exists_fintype` of the convex hull instead. -/
lemma mem_convexHull_iff_exists_fintype {s : Set E} {x : E} :
    x âˆˆ convexHull R s â†” âˆƒ (Î¹ : Type) (_ : Fintype Î¹) (w : Î¹ â†’ R) (z : Î¹ â†’ E), (âˆ€ i, 0 â‰¤ w i) âˆ§
      âˆ‘ i, w i = 1 âˆ§ (âˆ€ i, z i âˆˆ s) âˆ§ âˆ‘ i, w i â€¢ z i = x := by
  constructor
  Â· simp only [convexHull_eq, mem_setOf_eq]
    rintro âŸ¨Î¹, t, w, z, hâŸ©
    refine âŸ¨t, inferInstance, w âˆ˜ (â†‘), z âˆ˜ (â†‘), ?_âŸ©
    simpa [â† sum_attach t, centerMass_eq_of_sum_1 _ _ h.2.1] using h
  Â· rintro âŸ¨Î¹, _, w, z, hwâ‚€, hwâ‚, hz, hxâŸ©
    exact mem_convexHull_of_exists_fintype w z hwâ‚€ hwâ‚ hz hx

theorem Finset.convexHull_eq (s : Finset E) : convexHull R â†‘s =
    { x : E | âˆƒ w : E â†’ R, (âˆ€ y âˆˆ s, 0 â‰¤ w y) âˆ§ âˆ‘ y âˆˆ s, w y = 1 âˆ§ s.centerMass w id = x } := by
  classical
  refine Set.Subset.antisymm (convexHull_min ?_ ?_) ?_
  Â· intro x hx
    rw [Finset.mem_coe] at hx
    refine âŸ¨_, ?_, ?_, Finset.centerMass_ite_eq _ _ _ hxâŸ©
    Â· intros
      split_ifs
      exacts [zero_le_one, le_refl 0]
    Â· rw [Finset.sum_ite_eq, if_pos hx]
  Â· rintro x âŸ¨wx, hwxâ‚€, hwxâ‚, rflâŸ© y âŸ¨wy, hwyâ‚€, hwyâ‚, rflâŸ© a b ha hb hab
    rw [Finset.centerMass_segment _ _ _ _ hwxâ‚ hwyâ‚ _ _ hab]
    refine âŸ¨_, ?_, ?_, rflâŸ©
    Â· rintro i hi
      apply_rules [add_nonneg, mul_nonneg, hwxâ‚€, hwyâ‚€]
    Â· simp only [Finset.sum_add_distrib, â† mul_sum, mul_one, *]
  Â· rintro _ âŸ¨w, hwâ‚€, hwâ‚, rflâŸ©
    exact
      s.centerMass_mem_convexHull (fun x hx => hwâ‚€ _ hx) (hwâ‚.symm â–¸ zero_lt_one) fun x hx => hx

theorem Finset.mem_convexHull {s : Finset E} {x : E} : x âˆˆ convexHull R (s : Set E) â†”
    âˆƒ w : E â†’ R, (âˆ€ y âˆˆ s, 0 â‰¤ w y) âˆ§ âˆ‘ y âˆˆ s, w y = 1 âˆ§ s.centerMass w id = x := by
  rw [Finset.convexHull_eq, Set.mem_setOf_eq]

/-- This is a version of `Finset.mem_convexHull` stated without `Finset.centerMass`. -/
lemma Finset.mem_convexHull' {s : Finset E} {x : E} :
    x âˆˆ convexHull R (s : Set E) â†”
      âˆƒ w : E â†’ R, (âˆ€ y âˆˆ s, 0 â‰¤ w y) âˆ§ âˆ‘ y âˆˆ s, w y = 1 âˆ§ âˆ‘ y âˆˆ s, w y â€¢ y = x := by
  rw [mem_convexHull]
  refine exists_congr fun w â†¦ and_congr_right' <| and_congr_right fun hw â†¦ ?_
  simp_rw [centerMass_eq_of_sum_1 _ _ hw, id_eq]

theorem Set.Finite.convexHull_eq {s : Set E} (hs : s.Finite) : convexHull R s =
    { x : E | âˆƒ w : E â†’ R, (âˆ€ y âˆˆ s, 0 â‰¤ w y) âˆ§ âˆ‘ y âˆˆ hs.toFinset, w y = 1 âˆ§
      hs.toFinset.centerMass w id = x } := by
  simpa only [Set.Finite.coe_toFinset, Set.Finite.mem_toFinset, exists_prop] using
    hs.toFinset.convexHull_eq

/-- A weak version of CarathÃ©odory's theorem. -/
theorem convexHull_eq_union_convexHull_finite_subsets (s : Set E) :
    convexHull R s = â‹ƒ (t : Finset E) (_ : â†‘t âŠ† s), convexHull R â†‘t := by
  classical
  refine Subset.antisymm ?_ ?_
  Â· rw [_root_.convexHull_eq]
    rintro x âŸ¨Î¹, t, w, z, hwâ‚€, hwâ‚, hz, rflâŸ©
    simp only [mem_iUnion]
    refine âŸ¨t.image z, ?_, ?_âŸ©
    Â· rw [coe_image, Set.image_subset_iff]
      exact hz
    Â· apply t.centerMass_mem_convexHull hwâ‚€
      Â· simp only [hwâ‚, zero_lt_one]
      Â· exact fun i hi => Finset.mem_coe.2 (Finset.mem_image_of_mem _ hi)
  Â· exact iUnion_subset fun i => iUnion_subset convexHull_mono

theorem mk_mem_convexHull_prod {t : Set F} {x : E} {y : F} (hx : x âˆˆ convexHull R s)
    (hy : y âˆˆ convexHull R t) : (x, y) âˆˆ convexHull R (s Ã—Ë¢ t) := by
  rw [mem_convexHull_iff_exists_fintype] at hx hy âŠ¢
  obtain âŸ¨Î¹, _, w, f, hwâ‚€, hwâ‚, hfs, hfâŸ© := hx
  obtain âŸ¨Îº, _, v, g, hvâ‚€, hvâ‚, hgt, hgâŸ© := hy
  have h_sum : âˆ‘ i : Î¹ Ã— Îº, w i.1 * v i.2 = 1 := by
    rw [Fintype.sum_prod_type, â† sum_mul_sum, hwâ‚, hvâ‚, mul_one]
  refine âŸ¨Î¹ Ã— Îº, inferInstance, fun p => w p.1 * v p.2, fun p â†¦ (f p.1, g p.2),
    fun p â†¦ mul_nonneg (hwâ‚€ _) (hvâ‚€ _), h_sum, fun p â†¦ âŸ¨hfs _, hgt _âŸ©, ?_âŸ©
  ext
  Â· simp_rw [Prod.fst_sum, Prod.smul_mk, Fintype.sum_prod_type, mul_comm (w _), mul_smul,
      sum_comm (Î³ := Î¹), â† Fintype.sum_smul_sum, hvâ‚, one_smul, hf]
  Â· simp_rw [Prod.snd_sum, Prod.smul_mk, Fintype.sum_prod_type, mul_smul, â† Fintype.sum_smul_sum,
      hwâ‚, one_smul, hg]

@[simp]
theorem convexHull_prod (s : Set E) (t : Set F) :
    convexHull R (s Ã—Ë¢ t) = convexHull R s Ã—Ë¢ convexHull R t :=
  Subset.antisymm
      (convexHull_min (prod_mono (subset_convexHull _ _) <| subset_convexHull _ _) <|
        (convex_convexHull _ _).prod <| convex_convexHull _ _) <|
    prod_subset_iff.2 fun _ hx _ => mk_mem_convexHull_prod hx

theorem convexHull_add (s t : Set E) : convexHull R (s + t) = convexHull R s + convexHull R t := by
  simp_rw [â† add_image_prod, â† IsLinearMap.isLinearMap_add.image_convexHull, convexHull_prod]

variable (R E)

/-- `convexHull` is an additive monoid morphism under pointwise addition. -/
@[simps]
noncomputable def convexHullAddMonoidHom : Set E â†’+ Set E where
  toFun := convexHull R
  map_add' := convexHull_add
  map_zero' := convexHull_zero

variable {R E}

theorem convexHull_sub (s t : Set E) : convexHull R (s - t) = convexHull R s - convexHull R t := by
  simp_rw [sub_eq_add_neg, convexHull_add, â† convexHull_neg]

theorem convexHull_list_sum (l : List (Set E)) : convexHull R l.sum = (l.map <| convexHull R).sum :=
  map_list_sum (convexHullAddMonoidHom R E) l

theorem convexHull_multiset_sum (s : Multiset (Set E)) :
    convexHull R s.sum = (s.map <| convexHull R).sum :=
  map_multiset_sum (convexHullAddMonoidHom R E) s

theorem convexHull_sum {Î¹} (s : Finset Î¹) (t : Î¹ â†’ Set E) :
    convexHull R (âˆ‘ i âˆˆ s, t i) = âˆ‘ i âˆˆ s, convexHull R (t i) :=
  map_sum (convexHullAddMonoidHom R E) _ _

/-- The convex hull of an affine basis is the intersection of the half-spaces defined by the
corresponding barycentric coordinates. -/
theorem AffineBasis.convexHull_eq_nonneg_coord {Î¹ : Type*} (b : AffineBasis Î¹ R E) :
    convexHull R (range b) = { x | âˆ€ i, 0 â‰¤ b.coord i x } := by
  rw [convexHull_range_eq_exists_affineCombination]
  ext x
  refine âŸ¨?_, fun hx => ?_âŸ©
  Â· rintro âŸ¨s, w, hwâ‚€, hwâ‚, rflâŸ© i
    by_cases hi : i âˆˆ s
    Â· rw [b.coord_apply_combination_of_mem hi hwâ‚]
      exact hwâ‚€ i hi
    Â· rw [b.coord_apply_combination_of_notMem hi hwâ‚]
  Â· have hx' : x âˆˆ affineSpan R (range b) := by
      rw [b.tot]
      exact AffineSubspace.mem_top R E x
    obtain âŸ¨s, w, hwâ‚, rflâŸ© := (mem_affineSpan_iff_eq_affineCombination R E).mp hx'
    refine âŸ¨s, w, ?_, hwâ‚, rflâŸ©
    intro i hi
    specialize hx i
    rw [b.coord_apply_combination_of_mem hi hwâ‚] at hx
    exact hx

variable {s t tâ‚ tâ‚‚ : Finset E}

/-- Two simplices glue nicely if the union of their vertices is affine independent. -/
lemma AffineIndependent.convexHull_inter (hs : AffineIndependent R ((â†‘) : s â†’ E))
    (htâ‚ : tâ‚ âŠ† s) (htâ‚‚ : tâ‚‚ âŠ† s) :
    convexHull R (tâ‚ âˆ© tâ‚‚ : Set E) = convexHull R tâ‚ âˆ© convexHull R tâ‚‚ := by
  classical
  refine (Set.subset_inter (convexHull_mono inf_le_left) <|
    convexHull_mono inf_le_right).antisymm ?_
  simp_rw [Set.subset_def, mem_inter_iff, Set.inf_eq_inter, â† coe_inter, mem_convexHull']
  rintro x âŸ¨âŸ¨wâ‚, hâ‚wâ‚, hâ‚‚wâ‚, hâ‚ƒwâ‚âŸ©, wâ‚‚, -, hâ‚‚wâ‚‚, hâ‚ƒwâ‚‚âŸ©
  let w (x : E) : R := (if x âˆˆ tâ‚ then wâ‚ x else 0) - if x âˆˆ tâ‚‚ then wâ‚‚ x else 0
  have hâ‚w : âˆ‘ i âˆˆ s, w i = 0 := by simp [w, Finset.inter_eq_right.2, *]
  replace hs := hs.eq_zero_of_sum_eq_zero_subtype hâ‚w <| by
    simp only [w, sub_smul, zero_smul, ite_smul, Finset.sum_sub_distrib, â† Finset.sum_filter, hâ‚ƒwâ‚,
      Finset.filter_mem_eq_inter, Finset.inter_eq_right.2 htâ‚, Finset.inter_eq_right.2 htâ‚‚, hâ‚ƒwâ‚‚,
      sub_self]
  have ht (x) (hxâ‚ : x âˆˆ tâ‚) (hxâ‚‚ : x âˆ‰ tâ‚‚) : wâ‚ x = 0 := by
    simpa [w, hxâ‚, hxâ‚‚] using hs _ (htâ‚ hxâ‚)
  refine âŸ¨wâ‚, ?_, ?_, ?_âŸ©
  Â· simp only [and_imp, Finset.mem_inter]
    exact fun y hyâ‚ _ â†¦ hâ‚wâ‚ y hyâ‚
  all_goals
  Â· rwa [sum_subset inter_subset_left]
    rintro x
    simp_intro hxâ‚ hxâ‚‚
    simp [ht x hxâ‚ hxâ‚‚]

/-- Two simplices glue nicely if the union of their vertices is affine independent.

Note that `AffineIndependent.convexHull_inter` should be more versatile in most use cases. -/
lemma AffineIndependent.convexHull_inter' [DecidableEq E]
    (hs : AffineIndependent R ((â†‘) : â†‘(tâ‚ âˆª tâ‚‚) â†’ E)) :
    convexHull R (tâ‚ âˆ© tâ‚‚ : Set E) = convexHull R tâ‚ âˆ© convexHull R tâ‚‚ :=
  hs.convexHull_inter subset_union_left subset_union_right

end

section pi
variable {ğ•œ Î¹ : Type*} {E : Î¹ â†’ Type*} [Finite Î¹] [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [Î  i, AddCommGroup (E i)] [Î  i, Module ğ•œ (E i)] {s : Set Î¹} {t : Î  i, Set (E i)} {x : Î  i, E i}

open Finset Fintype

lemma mem_convexHull_pi (h : âˆ€ i âˆˆ s, x i âˆˆ convexHull ğ•œ (t i)) : x âˆˆ convexHull ğ•œ (s.pi t) := by
  classical
  cases nonempty_fintype Î¹
  wlog hs : s = Set.univ generalizing s t
  Â· rw [â† pi_univ_ite]
    refine this (fun i _ â†¦ ?_) rfl
    split_ifs with hi
    Â· exact h i hi
    Â· simp
  subst hs
  simp only [Set.mem_univ, mem_convexHull_iff_exists_fintype, true_implies] at h
  choose Îº _ w f hwâ‚€ hwâ‚ hft hf using h
  refine mem_convexHull_of_exists_fintype (fun k : Î  i, Îº i â†¦ âˆ i, w i (k i)) (fun g i â†¦ f _ (g i))
    (fun g â†¦ prod_nonneg fun _ _ â†¦ hwâ‚€ _ _) ?_ (fun _ _ _ â†¦ hft _ _) ?_
  Â· rw [â† Fintype.prod_sum]
    exact prod_eq_one fun _ _ â†¦ hwâ‚ _
  ext i
  calc
    _ = âˆ‘ g : âˆ€ i, Îº i, (âˆ i, w i (g i)) â€¢ f i (g i) := by
      simp only [Finset.sum_apply, Pi.smul_apply]
    _ = âˆ‘ j : Îº i, âˆ‘ g : {g : âˆ€ k, Îº k // g i = j},
          (âˆ k, w k (g.1 k)) â€¢ f i ((g : âˆ€ i, Îº i) i) := by
      rw [â† Fintype.sum_fiberwise fun g : âˆ€ k, Îº k â†¦ g i]
    _ = âˆ‘ j : Îº i, (âˆ‘ g : {g : âˆ€ k, Îº k // g i = j}, âˆ k, w k (g.1 k)) â€¢ f i j := by
      simp_rw [sum_smul]
      congr! with j _ g _
      exact g.2
    _ = âˆ‘ j : Îº i, w i j â€¢ f i j := ?_
    _ = x i := hf _
  congr! with j _
  calc
    âˆ‘ g : {g : âˆ€ k, Îº k // g i = j}, âˆ k, w k (g.1 k)
      = âˆ‘ g âˆˆ piFinset fun k â†¦ if hk : k = i then hk â–¸ {j} else univ, âˆ k, w k (g k) :=
      Finset.sum_bij' (fun g _ â†¦ g) (fun g hg â†¦ âŸ¨g, by simpa using mem_piFinset.1 hg iâŸ©)
        (by aesop) (by simp) (by simp) (by simp) (by simp)
    _ = w i j := by
      rw [â† prod_univ_sum, â† prod_mul_prod_compl, Finset.prod_singleton, Finset.sum_eq_single,
        Finset.prod_eq_one, mul_one] <;> simp +contextual [hwâ‚]

@[simp] lemma convexHull_pi (s : Set Î¹) (t : Î  i, Set (E i)) :
    convexHull ğ•œ (s.pi t) = s.pi (fun i â†¦ convexHull ğ•œ (t i)) :=
  Set.Subset.antisymm (convexHull_min (Set.pi_mono fun _ _ â†¦ subset_convexHull _ _) <| convex_pi <|
    fun _ _ â†¦ convex_convexHull _ _) fun _ â†¦ mem_convexHull_pi

end pi
