/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Hull

#align_import analysis.convex.join from "leanprover-community/mathlib"@"951bf1d9e98a2042979ced62c0620bcfb3587cf8"

/-!
# Convex join

This file defines the convex join of two sets. The convex join of `s` and `t` is the union of the
segments with one end in `s` and the other in `t`. This is notably a useful gadget to deal with
convex hulls of finite sets.
-/


open Set

variable {Î¹ : Sort*} {ğ•œ E : Type*}

section OrderedSemiring

variable (ğ•œ) [OrderedSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] {s t sâ‚ sâ‚‚ tâ‚ tâ‚‚ u : Set E}
  {x y : E}

/-- The join of two sets is the union of the segments joining them. This can be interpreted as the
topological join, but within the original space. -/
def convexJoin (s t : Set E) : Set E :=
  â‹ƒ (x âˆˆ s) (y âˆˆ t), segment ğ•œ x y
#align convex_join convexJoin

variable {ğ•œ}

theorem mem_convexJoin : x âˆˆ convexJoin ğ•œ s t â†” âˆƒ a âˆˆ s, âˆƒ b âˆˆ t, x âˆˆ segment ğ•œ a b := by
  simp [convexJoin]
#align mem_convex_join mem_convexJoin

theorem convexJoin_comm (s t : Set E) : convexJoin ğ•œ s t = convexJoin ğ•œ t s :=
  (iUnionâ‚‚_comm _).trans <| by simp_rw [convexJoin, segment_symm]
#align convex_join_comm convexJoin_comm

theorem convexJoin_mono (hs : sâ‚ âŠ† sâ‚‚) (ht : tâ‚ âŠ† tâ‚‚) : convexJoin ğ•œ sâ‚ tâ‚ âŠ† convexJoin ğ•œ sâ‚‚ tâ‚‚ :=
  biUnion_mono hs fun _ _ => biUnion_subset_biUnion_left ht
#align convex_join_mono convexJoin_mono

theorem convexJoin_mono_left (hs : sâ‚ âŠ† sâ‚‚) : convexJoin ğ•œ sâ‚ t âŠ† convexJoin ğ•œ sâ‚‚ t :=
  convexJoin_mono hs Subset.rfl
#align convex_join_mono_left convexJoin_mono_left

theorem convexJoin_mono_right (ht : tâ‚ âŠ† tâ‚‚) : convexJoin ğ•œ s tâ‚ âŠ† convexJoin ğ•œ s tâ‚‚ :=
  convexJoin_mono Subset.rfl ht
#align convex_join_mono_right convexJoin_mono_right

@[simp]
theorem convexJoin_empty_left (t : Set E) : convexJoin ğ•œ âˆ… t = âˆ… := by simp [convexJoin]
#align convex_join_empty_left convexJoin_empty_left

@[simp]
theorem convexJoin_empty_right (s : Set E) : convexJoin ğ•œ s âˆ… = âˆ… := by simp [convexJoin]
#align convex_join_empty_right convexJoin_empty_right

@[simp]
theorem convexJoin_singleton_left (t : Set E) (x : E) :
    convexJoin ğ•œ {x} t = â‹ƒ y âˆˆ t, segment ğ•œ x y := by simp [convexJoin]
#align convex_join_singleton_left convexJoin_singleton_left

@[simp]
theorem convexJoin_singleton_right (s : Set E) (y : E) :
    convexJoin ğ•œ s {y} = â‹ƒ x âˆˆ s, segment ğ•œ x y := by simp [convexJoin]
#align convex_join_singleton_right convexJoin_singleton_right

-- Porting note (#10618): simp can prove it
theorem convexJoin_singletons (x : E) : convexJoin ğ•œ {x} {y} = segment ğ•œ x y := by simp
#align convex_join_singletons convexJoin_singletons

@[simp]
theorem convexJoin_union_left (sâ‚ sâ‚‚ t : Set E) :
    convexJoin ğ•œ (sâ‚ âˆª sâ‚‚) t = convexJoin ğ•œ sâ‚ t âˆª convexJoin ğ•œ sâ‚‚ t := by
  simp_rw [convexJoin, mem_union, iUnion_or, iUnion_union_distrib]
#align convex_join_union_left convexJoin_union_left

@[simp]
theorem convexJoin_union_right (s tâ‚ tâ‚‚ : Set E) :
    convexJoin ğ•œ s (tâ‚ âˆª tâ‚‚) = convexJoin ğ•œ s tâ‚ âˆª convexJoin ğ•œ s tâ‚‚ := by
  simp_rw [convexJoin_comm s, convexJoin_union_left]
#align convex_join_union_right convexJoin_union_right

@[simp]
theorem convexJoin_iUnion_left (s : Î¹ â†’ Set E) (t : Set E) :
    convexJoin ğ•œ (â‹ƒ i, s i) t = â‹ƒ i, convexJoin ğ•œ (s i) t := by
  simp_rw [convexJoin, mem_iUnion, iUnion_exists]
  exact iUnion_comm _
#align convex_join_Union_left convexJoin_iUnion_left

@[simp]
theorem convexJoin_iUnion_right (s : Set E) (t : Î¹ â†’ Set E) :
    convexJoin ğ•œ s (â‹ƒ i, t i) = â‹ƒ i, convexJoin ğ•œ s (t i) := by
  simp_rw [convexJoin_comm s, convexJoin_iUnion_left]
#align convex_join_Union_right convexJoin_iUnion_right

theorem segment_subset_convexJoin (hx : x âˆˆ s) (hy : y âˆˆ t) : segment ğ•œ x y âŠ† convexJoin ğ•œ s t :=
  subset_iUnionâ‚‚_of_subset x hx <| subset_iUnionâ‚‚ (s := fun y _ â†¦ segment ğ•œ x y) y hy
#align segment_subset_convex_join segment_subset_convexJoin

theorem subset_convexJoin_left (h : t.Nonempty) : s âŠ† convexJoin ğ•œ s t := fun _x hx =>
  let âŸ¨_y, hyâŸ© := h
  segment_subset_convexJoin hx hy <| left_mem_segment _ _ _
#align subset_convex_join_left subset_convexJoin_left

theorem subset_convexJoin_right (h : s.Nonempty) : t âŠ† convexJoin ğ•œ s t :=
  convexJoin_comm (ğ•œ := ğ•œ) t s â–¸ subset_convexJoin_left h
#align subset_convex_join_right subset_convexJoin_right

theorem convexJoin_subset (hs : s âŠ† u) (ht : t âŠ† u) (hu : Convex ğ•œ u) : convexJoin ğ•œ s t âŠ† u :=
  iUnionâ‚‚_subset fun _x hx => iUnionâ‚‚_subset fun _y hy => hu.segment_subset (hs hx) (ht hy)
#align convex_join_subset convexJoin_subset

theorem convexJoin_subset_convexHull (s t : Set E) : convexJoin ğ•œ s t âŠ† convexHull ğ•œ (s âˆª t) :=
  convexJoin_subset (subset_union_left.trans <| subset_convexHull _ _)
      (subset_union_right.trans <| subset_convexHull _ _) <|
    convex_convexHull _ _
#align convex_join_subset_convex_hull convexJoin_subset_convexHull

end OrderedSemiring

section LinearOrderedField

variable [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s t u : Set E} {x y : E}

theorem convexJoin_assoc_aux (s t u : Set E) :
    convexJoin ğ•œ (convexJoin ğ•œ s t) u âŠ† convexJoin ğ•œ s (convexJoin ğ•œ t u) := by
  simp_rw [subset_def, mem_convexJoin]
  rintro _ âŸ¨z, âŸ¨x, hx, y, hy, aâ‚, bâ‚, haâ‚, hbâ‚, habâ‚, rflâŸ©, z, hz, aâ‚‚, bâ‚‚, haâ‚‚, hbâ‚‚, habâ‚‚, rflâŸ©
  obtain rfl | hbâ‚‚ := hbâ‚‚.eq_or_lt
  Â· refine âŸ¨x, hx, y, âŸ¨y, hy, z, hz, left_mem_segment ğ•œ _ _âŸ©, aâ‚, bâ‚, haâ‚, hbâ‚, habâ‚, ?_âŸ©
    rw [add_zero] at habâ‚‚
    rw [habâ‚‚, one_smul, zero_smul, add_zero]
  have haâ‚‚bâ‚ : 0 â‰¤ aâ‚‚ * bâ‚ := mul_nonneg haâ‚‚ hbâ‚
  have hab : 0 < aâ‚‚ * bâ‚ + bâ‚‚ := add_pos_of_nonneg_of_pos haâ‚‚bâ‚ hbâ‚‚
  refine
    âŸ¨x, hx, (aâ‚‚ * bâ‚ / (aâ‚‚ * bâ‚ + bâ‚‚)) â€¢ y + (bâ‚‚ / (aâ‚‚ * bâ‚ + bâ‚‚)) â€¢ z,
      âŸ¨y, hy, z, hz, _, _, ?_, ?_, ?_, rflâŸ©,
      aâ‚‚ * aâ‚, aâ‚‚ * bâ‚ + bâ‚‚, mul_nonneg haâ‚‚ haâ‚, hab.le, ?_, ?_âŸ©
  Â· exact div_nonneg haâ‚‚bâ‚ hab.le
  Â· exact div_nonneg hbâ‚‚.le hab.le
  Â· rw [â† add_div, div_self hab.ne']
  Â· rw [â† add_assoc, â† mul_add, habâ‚, mul_one, habâ‚‚]
  Â· simp_rw [smul_add, â† mul_smul, mul_div_cancelâ‚€ _ hab.ne', add_assoc]
#align convex_join_assoc_aux convexJoin_assoc_aux

theorem convexJoin_assoc (s t u : Set E) :
    convexJoin ğ•œ (convexJoin ğ•œ s t) u = convexJoin ğ•œ s (convexJoin ğ•œ t u) := by
  refine (convexJoin_assoc_aux _ _ _).antisymm ?_
  simp_rw [convexJoin_comm s, convexJoin_comm _ u]
  exact convexJoin_assoc_aux _ _ _
#align convex_join_assoc convexJoin_assoc

theorem convexJoin_left_comm (s t u : Set E) :
    convexJoin ğ•œ s (convexJoin ğ•œ t u) = convexJoin ğ•œ t (convexJoin ğ•œ s u) := by
  simp_rw [â† convexJoin_assoc, convexJoin_comm]
#align convex_join_left_comm convexJoin_left_comm

theorem convexJoin_right_comm (s t u : Set E) :
    convexJoin ğ•œ (convexJoin ğ•œ s t) u = convexJoin ğ•œ (convexJoin ğ•œ s u) t := by
  simp_rw [convexJoin_assoc, convexJoin_comm]
#align convex_join_right_comm convexJoin_right_comm

theorem convexJoin_convexJoin_convexJoin_comm (s t u v : Set E) :
    convexJoin ğ•œ (convexJoin ğ•œ s t) (convexJoin ğ•œ u v) =
      convexJoin ğ•œ (convexJoin ğ•œ s u) (convexJoin ğ•œ t v) := by
  simp_rw [â† convexJoin_assoc, convexJoin_right_comm]
#align convex_join_convex_join_convex_join_comm convexJoin_convexJoin_convexJoin_comm

-- Porting note: moved 3 lemmas from below to golf
protected theorem Convex.convexJoin (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) :
    Convex ğ•œ (convexJoin ğ•œ s t) := by
  simp only [Convex, StarConvex, convexJoin, mem_iUnion]
  rintro _ âŸ¨xâ‚, hxâ‚, yâ‚, hyâ‚, aâ‚, bâ‚, haâ‚, hbâ‚, habâ‚, rflâŸ©
    _ âŸ¨xâ‚‚, hxâ‚‚, yâ‚‚, hyâ‚‚, aâ‚‚, bâ‚‚, haâ‚‚, hbâ‚‚, habâ‚‚, rflâŸ© p q hp hq hpq
  rcases hs.exists_mem_add_smul_eq hxâ‚ hxâ‚‚ (mul_nonneg hp haâ‚) (mul_nonneg hq haâ‚‚) with âŸ¨x, hxs, hxâŸ©
  rcases ht.exists_mem_add_smul_eq hyâ‚ hyâ‚‚ (mul_nonneg hp hbâ‚) (mul_nonneg hq hbâ‚‚) with âŸ¨y, hyt, hyâŸ©
  refine âŸ¨_, hxs, _, hyt, p * aâ‚ + q * aâ‚‚, p * bâ‚ + q * bâ‚‚, ?_, ?_, ?_, ?_âŸ© <;> try positivity
  Â· rwa [add_add_add_comm, â† mul_add, â† mul_add, habâ‚, habâ‚‚, mul_one, mul_one]
  Â· rw [hx, hy, add_add_add_comm]
    simp only [smul_add, smul_smul]
#align convex.convex_join Convex.convexJoin

protected theorem Convex.convexHull_union (hs : Convex ğ•œ s) (ht : Convex ğ•œ t) (hsâ‚€ : s.Nonempty)
    (htâ‚€ : t.Nonempty) : convexHull ğ•œ (s âˆª t) = convexJoin ğ•œ s t :=
  (convexHull_min (union_subset (subset_convexJoin_left htâ‚€) <| subset_convexJoin_right hsâ‚€) <|
        hs.convexJoin ht).antisymm <|
    convexJoin_subset_convexHull _ _
#align convex.convex_hull_union Convex.convexHull_union

theorem convexHull_union (hs : s.Nonempty) (ht : t.Nonempty) :
    convexHull ğ•œ (s âˆª t) = convexJoin ğ•œ (convexHull ğ•œ s) (convexHull ğ•œ t) := by
  rw [â† convexHull_convexHull_union_left, â† convexHull_convexHull_union_right]
  exact (convex_convexHull ğ•œ s).convexHull_union (convex_convexHull ğ•œ t) hs.convexHull ht.convexHull
#align convex_hull_union convexHull_union

theorem convexHull_insert (hs : s.Nonempty) :
    convexHull ğ•œ (insert x s) = convexJoin ğ•œ {x} (convexHull ğ•œ s) := by
  rw [insert_eq, convexHull_union (singleton_nonempty _) hs, convexHull_singleton]
#align convex_hull_insert convexHull_insert

theorem convexJoin_segments (a b c d : E) :
    convexJoin ğ•œ (segment ğ•œ a b) (segment ğ•œ c d) = convexHull ğ•œ {a, b, c, d} := by
  simp_rw [â† convexHull_pair, convexHull_insert (insert_nonempty _ _),
    convexHull_insert (singleton_nonempty _), convexJoin_assoc,
    convexHull_singleton]
#align convex_join_segments convexJoin_segments

theorem convexJoin_segment_singleton (a b c : E) :
    convexJoin ğ•œ (segment ğ•œ a b) {c} = convexHull ğ•œ {a, b, c} := by
  rw [â† pair_eq_singleton, â† convexJoin_segments, segment_same, pair_eq_singleton]
#align convex_join_segment_singleton convexJoin_segment_singleton

theorem convexJoin_singleton_segment (a b c : E) :
    convexJoin ğ•œ {a} (segment ğ•œ b c) = convexHull ğ•œ {a, b, c} := by
  rw [â† segment_same ğ•œ, convexJoin_segments, insert_idem]
#align convex_join_singleton_segment convexJoin_singleton_segment

-- Porting note: moved 3 lemmas up to golf

end LinearOrderedField
