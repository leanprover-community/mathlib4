/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, S√©bastien Gou√´zel, Heather Macbeth
-/
import Mathlib.Analysis.Convex.Slope
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Tactic.LinearCombination

#align_import analysis.convex.specific_functions.basic from "leanprover-community/mathlib"@"8f9fea08977f7e450770933ee6abb20733b47c92"

/-!
# Collection of convex functions

In this file we prove that the following functions are convex or strictly convex:

* `strictConvexOn_exp` : The exponential function is strictly convex.
* `Even.convexOn_pow`: For an even `n : ‚Ñï`, `fun x ‚Ü¶ x ^ n` is convex.
* `convexOn_pow`: For `n : ‚Ñï`, `fun x ‚Ü¶ x ^ n` is convex on $[0, +‚àû)$.
* `convexOn_zpow`: For `m : ‚Ñ§`, `fun x ‚Ü¶ x ^ m` is convex on $[0, +‚àû)$.
* `strictConcaveOn_log_Ioi`, `strictConcaveOn_log_Iio`: `Real.log` is strictly concave on
  $(0, +‚àû)$ and $(-‚àû, 0)$ respectively.
* `convexOn_rpow`, `strictConvexOn_rpow` : For `p : ‚Ñù`, `fun x ‚Ü¶ x ^ p` is convex on $[0, +‚àû)$ when
  `1 ‚â§ p` and strictly convex when `1 < p`.

The proofs in this file are deliberately elementary, *not* by appealing to the sign of the second
derivative.  This is in order to keep this file early in the import hierarchy, since it is on the
path to H√∂lder's and Minkowski's inequalities and after that to Lp spaces and most of measure
theory.

## TODO

For `p : ‚Ñù`, prove that `fun x ‚Ü¶ x ^ p` is concave when `0 ‚â§ p ‚â§ 1` and strictly concave when
`0 < p < 1`.
-/


local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

open Real Set BigOperators NNReal

/-- `Real.exp` is strictly convex on the whole real line.

We give an elementary proof rather than using the second derivative test, since this lemma is
needed early in the analysis library. -/
theorem strictConvexOn_exp : StrictConvexOn ‚Ñù univ exp := by
  apply strictConvexOn_of_slope_strict_mono_adjacent convex_univ
  -- ‚ä¢ ‚àÄ {x y z : ‚Ñù}, x ‚àà univ ‚Üí z ‚àà univ ‚Üí x < y ‚Üí y < z ‚Üí (exp y - exp x) / (y -  ‚Ä¶
  rintro x y z - - hxy hyz
  -- ‚ä¢ (exp y - exp x) / (y - x) < (exp z - exp y) / (z - y)
  trans exp y
  -- ‚ä¢ (exp y - exp x) / (y - x) < exp y
  ¬∑ have h1 : 0 < y - x := by linarith
    -- ‚ä¢ (exp y - exp x) / (y - x) < exp y
    have h2 : x - y < 0 := by linarith
    -- ‚ä¢ (exp y - exp x) / (y - x) < exp y
    rw [div_lt_iff h1]
    -- ‚ä¢ exp y - exp x < exp y * (y - x)
    calc
      exp y - exp x = exp y - exp y * exp (x - y) := by rw [‚Üê exp_add]; ring_nf
      _ = exp y * (1 - exp (x - y)) := by ring
      _ < exp y * -(x - y) := by gcongr; linarith [add_one_lt_exp_of_nonzero h2.ne]
      _ = exp y * (y - x) := by ring
  ¬∑ have h1 : 0 < z - y := by linarith
    -- ‚ä¢ exp y < (exp z - exp y) / (z - y)
    rw [lt_div_iff h1]
    -- ‚ä¢ exp y * (z - y) < exp z - exp y
    calc
      exp y * (z - y) < exp y * (exp (z - y) - 1) := by
        gcongr _ * ?_
        linarith [add_one_lt_exp_of_nonzero h1.ne']
      _ = exp (z - y) * exp y - exp y := by ring
      _ ‚â§ exp z - exp y := by rw [‚Üê exp_add]; ring_nf; rfl
#align strict_convex_on_exp strictConvexOn_exp

/-- `Real.exp` is convex on the whole real line. -/
theorem convexOn_exp : ConvexOn ‚Ñù univ exp :=
  strictConvexOn_exp.convexOn
#align convex_on_exp convexOn_exp

/-- `x^n`, `n : ‚Ñï` is convex on `[0, +‚àû)` for all `n`.

We give an elementary proof rather than using the second derivative test, since this lemma is
needed early in the analysis library. -/
theorem convexOn_pow (n : ‚Ñï) : ConvexOn ‚Ñù (Ici 0) fun x : ‚Ñù => x ^ n := by
  induction' n with k IH
  -- ‚ä¢ ConvexOn ‚Ñù (Ici 0) fun x => x ^ Nat.zero
  ¬∑ exact convexOn_const (1 : ‚Ñù) (convex_Ici _)
    -- üéâ no goals
  refine' ‚ü®convex_Ici _, _‚ü©
  -- ‚ä¢ ‚àÄ ‚¶Éx : ‚Ñù‚¶Ñ, x ‚àà Ici 0 ‚Üí ‚àÄ ‚¶Éy : ‚Ñù‚¶Ñ, y ‚àà Ici 0 ‚Üí ‚àÄ ‚¶Éa b : ‚Ñù‚¶Ñ, 0 ‚â§ a ‚Üí 0 ‚â§ b ‚Üí a ‚Ä¶
  rintro a (ha : 0 ‚â§ a) b (hb : 0 ‚â§ b) Œº ŒΩ hŒº hŒΩ h
  -- ‚ä¢ (fun x => x ^ Nat.succ k) (Œº ‚Ä¢ a + ŒΩ ‚Ä¢ b) ‚â§ Œº ‚Ä¢ (fun x => x ^ Nat.succ k) a  ‚Ä¶
  have H := IH.2 ha hb hŒº hŒΩ h
  -- ‚ä¢ (fun x => x ^ Nat.succ k) (Œº ‚Ä¢ a + ŒΩ ‚Ä¢ b) ‚â§ Œº ‚Ä¢ (fun x => x ^ Nat.succ k) a  ‚Ä¶
  have : 0 ‚â§ (b ^ k - a ^ k) * (b - a) * Œº * ŒΩ := by
    cases' le_or_lt a b with hab hab
    ¬∑ have : a ^ k ‚â§ b ^ k := by gcongr
      have : 0 ‚â§ (b ^ k - a ^ k) * (b - a) := by nlinarith
      positivity
    ¬∑ have : b ^ k ‚â§ a ^ k := by gcongr
      have : 0 ‚â§ (b ^ k - a ^ k) * (b - a) := by nlinarith
      positivity
  calc
    (Œº * a + ŒΩ * b) ^ k.succ = (Œº * a + ŒΩ * b) * (Œº * a + ŒΩ * b) ^ k := pow_succ _ _
    _ ‚â§ (Œº * a + ŒΩ * b) * (Œº * a ^ k + ŒΩ * b ^ k) := by gcongr; exact H
    _ ‚â§ (Œº * a + ŒΩ * b) * (Œº * a ^ k + ŒΩ * b ^ k) + (b ^ k - a ^ k) * (b - a) * Œº * ŒΩ := by linarith
    _ = (Œº + ŒΩ) * (Œº * a ^ k.succ + ŒΩ * b ^ k.succ) := by rw [Nat.succ_eq_add_one]; ring
    _ = Œº * a ^ k.succ + ŒΩ * b ^ k.succ := by rw [h]; ring
#align convex_on_pow convexOn_pow

/-- `x^n`, `n : ‚Ñï` is convex on the whole real line whenever `n` is even.

We give an elementary proof rather than using the second derivative test, since this lemma is
needed early in the analysis library. -/
nonrec theorem Even.convexOn_pow {n : ‚Ñï} (hn : Even n) :
    ConvexOn ‚Ñù Set.univ fun x : ‚Ñù => x ^ n := by
  refine' ‚ü®convex_univ, _‚ü©
  -- ‚ä¢ ‚àÄ ‚¶Éx : ‚Ñù‚¶Ñ, x ‚àà univ ‚Üí ‚àÄ ‚¶Éy : ‚Ñù‚¶Ñ, y ‚àà univ ‚Üí ‚àÄ ‚¶Éa b : ‚Ñù‚¶Ñ, 0 ‚â§ a ‚Üí 0 ‚â§ b ‚Üí a + ‚Ä¶
  rintro a - b - Œº ŒΩ hŒº hŒΩ h
  -- ‚ä¢ (fun x => x ^ n) (Œº ‚Ä¢ a + ŒΩ ‚Ä¢ b) ‚â§ Œº ‚Ä¢ (fun x => x ^ n) a + ŒΩ ‚Ä¢ (fun x => x  ‚Ä¶
  obtain ‚ü®k, rfl‚ü© := hn.exists_two_nsmul _
  -- ‚ä¢ (fun x => x ^ (2 ‚Ä¢ k)) (Œº ‚Ä¢ a + ŒΩ ‚Ä¢ b) ‚â§ Œº ‚Ä¢ (fun x => x ^ (2 ‚Ä¢ k)) a + ŒΩ ‚Ä¢  ‚Ä¶
  -- Porting note: added type ascription to LHS
  have : (0 : ‚Ñù) ‚â§ (a - b) ^ 2 * Œº * ŒΩ := by positivity
  -- ‚ä¢ (fun x => x ^ (2 ‚Ä¢ k)) (Œº ‚Ä¢ a + ŒΩ ‚Ä¢ b) ‚â§ Œº ‚Ä¢ (fun x => x ^ (2 ‚Ä¢ k)) a + ŒΩ ‚Ä¢  ‚Ä¶
  calc
    (Œº * a + ŒΩ * b) ^ (2 * k) = ((Œº * a + ŒΩ * b) ^ 2) ^ k := by rw [pow_mul]
    _ ‚â§ ((Œº + ŒΩ) * (Œº * a ^ 2 + ŒΩ * b ^ 2)) ^ k := by gcongr; linarith
    _ = (Œº * a ^ 2 + ŒΩ * b ^ 2) ^ k := by rw [h]; ring
    _ ‚â§ Œº * (a ^ 2) ^ k + ŒΩ * (b ^ 2) ^ k := ?_
    _ ‚â§ Œº * a ^ (2 * k) + ŒΩ * b ^ (2 * k) := by ring_nf; rfl
  -- Porting note: `rw [mem_Ici]` was `dsimp`
  refine' (convexOn_pow k).2 _ _ hŒº hŒΩ h <;> rw [mem_Ici] <;> positivity
  -- ‚ä¢ a ^ 2 ‚àà Ici 0
                                             -- ‚ä¢ 0 ‚â§ a ^ 2
                                             -- ‚ä¢ 0 ‚â§ b ^ 2
                                                              -- üéâ no goals
                                                              -- üéâ no goals
#align even.convex_on_pow Even.convexOn_pow

open Int in
/-- `x^m`, `m : ‚Ñ§` is convex on `(0, +‚àû)` for all `m`.

We give an elementary proof rather than using the second derivative test, since this lemma is
needed early in the analysis library. -/
theorem convexOn_zpow : ‚àÄ m : ‚Ñ§, ConvexOn ‚Ñù (Ioi 0) fun x : ‚Ñù => x ^ m
  | (n : ‚Ñï) => by
    simp_rw [zpow_ofNat]
    -- ‚ä¢ ConvexOn ‚Ñù (Ioi 0) fun x => x ^ n
    exact (convexOn_pow n).subset Ioi_subset_Ici_self (convex_Ioi _)
    -- üéâ no goals
  | -[n+1] => by
    simp_rw [zpow_negSucc]
    -- ‚ä¢ ConvexOn ‚Ñù (Ioi 0) fun x => (x ^ (n + 1))‚Åª¬π
    refine' ‚ü®convex_Ioi _, _‚ü©
    -- ‚ä¢ ‚àÄ ‚¶Éx : ‚Ñù‚¶Ñ, x ‚àà Ioi 0 ‚Üí ‚àÄ ‚¶Éy : ‚Ñù‚¶Ñ, y ‚àà Ioi 0 ‚Üí ‚àÄ ‚¶Éa b : ‚Ñù‚¶Ñ, 0 ‚â§ a ‚Üí 0 ‚â§ b ‚Üí a ‚Ä¶
    rintro a (ha : 0 < a) b (hb : 0 < b) Œº ŒΩ hŒº hŒΩ h
    -- ‚ä¢ (fun x => (x ^ (n + 1))‚Åª¬π) (Œº ‚Ä¢ a + ŒΩ ‚Ä¢ b) ‚â§ Œº ‚Ä¢ (fun x => (x ^ (n + 1))‚Åª¬π)  ‚Ä¶
    field_simp
    -- ‚ä¢ 1 / (Œº * a + ŒΩ * b) ^ (n + 1) ‚â§ (Œº * b ^ (n + 1) + ŒΩ * a ^ (n + 1)) / (a ^ ( ‚Ä¶
    rw [div_le_div_iff]
    ¬∑ -- Porting note: added type ascription to LHS
      calc
        (1 : ‚Ñù) * (a ^ (n + 1) * b ^ (n + 1)) = ((Œº + ŒΩ) ^ 2 * (a * b)) ^ (n + 1) := by rw [h]; ring
        _ ‚â§ ((Œº * b + ŒΩ * a) * (Œº * a + ŒΩ * b)) ^ (n + 1) := ?_
        _ = (Œº * b + ŒΩ * a) ^ (n + 1) * (Œº * a + ŒΩ * b) ^ (n + 1) := by rw [mul_pow]
        _ ‚â§ (Œº * b ^ (n + 1) + ŒΩ * a ^ (n + 1)) * (Œº * a + ŒΩ * b) ^ (n + 1) := ?_
      ¬∑ -- Porting note: added type ascription to LHS
        gcongr (?_ : ‚Ñù) ^ _
        -- ‚ä¢ (Œº + ŒΩ) ^ 2 * (a * b) ‚â§ (Œº * b + ŒΩ * a) * (Œº * a + ŒΩ * b)
        have : (0 : ‚Ñù) ‚â§ Œº * ŒΩ * (a - b) ^ 2 := by positivity
        -- ‚ä¢ (Œº + ŒΩ) ^ 2 * (a * b) ‚â§ (Œº * b + ŒΩ * a) * (Œº * a + ŒΩ * b)
        linarith
        -- üéâ no goals
      ¬∑ gcongr
        -- ‚ä¢ (Œº * b + ŒΩ * a) ^ (n + 1) ‚â§ Œº * b ^ (n + 1) + ŒΩ * a ^ (n + 1)
        apply (convexOn_pow (n + 1)).2 hb.le ha.le hŒº hŒΩ h
        -- üéâ no goals
    ¬∑ have : 0 < Œº * a + ŒΩ * b := by cases le_or_lt a b <;> nlinarith
      -- ‚ä¢ 0 < (Œº * a + ŒΩ * b) ^ (n + 1)
      positivity
      -- üéâ no goals
    ¬∑ positivity
      -- üéâ no goals
#align convex_on_zpow convexOn_zpow

/- `Real.log` is strictly concave on $(0, +‚àû)$.

We give an elementary proof rather than using the second derivative test, since this lemma is
needed early in the analysis library. -/
theorem strictConcaveOn_log_Ioi : StrictConcaveOn ‚Ñù (Ioi 0) log := by
  apply strictConcaveOn_of_slope_strict_anti_adjacent (convex_Ioi (0 : ‚Ñù))
  -- ‚ä¢ ‚àÄ {x y z : ‚Ñù}, x ‚àà Ioi 0 ‚Üí z ‚àà Ioi 0 ‚Üí x < y ‚Üí y < z ‚Üí (log z - log y) / (z  ‚Ä¶
  rintro x y z (hx : 0 < x) (hz : 0 < z) hxy hyz
  -- ‚ä¢ (log z - log y) / (z - y) < (log y - log x) / (y - x)
  have hy : 0 < y := hx.trans hxy
  -- ‚ä¢ (log z - log y) / (z - y) < (log y - log x) / (y - x)
  trans y‚Åª¬π
  -- ‚ä¢ (log z - log y) / (z - y) < y‚Åª¬π
  ¬∑ have h : 0 < z - y := by linarith
    -- ‚ä¢ (log z - log y) / (z - y) < y‚Åª¬π
    rw [div_lt_iff h]
    -- ‚ä¢ log z - log y < y‚Åª¬π * (z - y)
    have hyz' : 0 < z / y := by positivity
    -- ‚ä¢ log z - log y < y‚Åª¬π * (z - y)
    have hyz'' : z / y ‚â† 1 := by
      contrapose! h
      rw [div_eq_one_iff_eq hy.ne'] at h
      simp [h]
    calc
      log z - log y = log (z / y) := by rw [‚Üê log_div hz.ne' hy.ne']
      _ < z / y - 1 := (log_lt_sub_one_of_pos hyz' hyz'')
      _ = y‚Åª¬π * (z - y) := by field_simp
  ¬∑ have h : 0 < y - x := by linarith
    -- ‚ä¢ y‚Åª¬π < (log y - log x) / (y - x)
    rw [lt_div_iff h]
    -- ‚ä¢ y‚Åª¬π * (y - x) < log y - log x
    have hxy' : 0 < x / y := by positivity
    -- ‚ä¢ y‚Åª¬π * (y - x) < log y - log x
    have hxy'' : x / y ‚â† 1 := by
      contrapose! h
      rw [div_eq_one_iff_eq hy.ne'] at h
      simp [h]
    calc
      y‚Åª¬π * (y - x) = 1 - x / y := by field_simp
      _ < -log (x / y) := by linarith [log_lt_sub_one_of_pos hxy' hxy'']
      _ = -(log x - log y) := by rw [log_div hx.ne' hy.ne']
      _ = log y - log x := by ring
#align strict_concave_on_log_Ioi strictConcaveOn_log_Ioi

/-- **Bernoulli's inequality** for real exponents, strict version: for `1 < p` and `-1 ‚â§ s`, with
`s ‚â† 0`, we have `1 + p * s < (1 + s) ^ p`. -/
theorem one_add_mul_self_lt_rpow_one_add {s : ‚Ñù} (hs : -1 ‚â§ s) (hs' : s ‚â† 0) {p : ‚Ñù} (hp : 1 < p) :
    1 + p * s < (1 + s) ^ p := by
  rcases eq_or_lt_of_le hs with (rfl | hs)
  -- ‚ä¢ 1 + p * -1 < (1 + -1) ^ p
  ¬∑ have : p ‚â† 0 := by positivity
    -- ‚ä¢ 1 + p * -1 < (1 + -1) ^ p
    simpa [zero_rpow this]
    -- üéâ no goals
  have hs1 : 0 < 1 + s := by linarith
  -- ‚ä¢ 1 + p * s < (1 + s) ^ p
  cases' le_or_lt (1 + p * s) 0 with hs2 hs2
  -- ‚ä¢ 1 + p * s < (1 + s) ^ p
  ¬∑ exact hs2.trans_lt (rpow_pos_of_pos hs1 _)
    -- üéâ no goals
  rw [rpow_def_of_pos hs1, ‚Üê exp_log hs2]
  -- ‚ä¢ exp (log (1 + p * s)) < exp (log (1 + s) * p)
  apply exp_strictMono
  -- ‚ä¢ log (1 + p * s) < log (1 + s) * p
  have hp : 0 < p := by positivity
  -- ‚ä¢ log (1 + p * s) < log (1 + s) * p
  have hs3 : 1 + s ‚â† 1 := by contrapose! hs'; linarith
  -- ‚ä¢ log (1 + p * s) < log (1 + s) * p
  have hs4 : 1 + p * s ‚â† 1 := by contrapose! hs'; nlinarith
  -- ‚ä¢ log (1 + p * s) < log (1 + s) * p
  cases' lt_or_gt_of_ne hs' with hs' hs'
  -- ‚ä¢ log (1 + p * s) < log (1 + s) * p
  ¬∑ rw [‚Üê div_lt_iff hp, ‚Üê div_lt_div_right_of_neg hs']
    -- ‚ä¢ log (1 + s) / s < log (1 + p * s) / p / s
    -- Porting note: previously we could write `zero_lt_one` inline,
    -- but now Lean doesn't guess we are talking about `1` fast enough.
    haveI : (1 : ‚Ñù) ‚àà Ioi 0 := zero_lt_one
    -- ‚ä¢ log (1 + s) / s < log (1 + p * s) / p / s
    convert strictConcaveOn_log_Ioi.secant_strict_mono this hs2 hs1 hs4 hs3 _ using 1
    ¬∑ field_simp
      -- üéâ no goals
    ¬∑ field_simp
      -- üéâ no goals
    ¬∑ nlinarith
      -- üéâ no goals
  ¬∑ rw [‚Üê div_lt_iff hp, ‚Üê div_lt_div_right hs']
    -- ‚ä¢ log (1 + p * s) / p / s < log (1 + s) / s
    -- Porting note: previously we could write `zero_lt_one` inline,
    -- but now Lean doesn't guess we are talking about `1` fast enough.
    haveI : (1 : ‚Ñù) ‚àà Ioi 0 := zero_lt_one
    -- ‚ä¢ log (1 + p * s) / p / s < log (1 + s) / s
    convert strictConcaveOn_log_Ioi.secant_strict_mono this hs1 hs2 hs3 hs4 _ using 1
    ¬∑ field_simp
      -- üéâ no goals
    ¬∑ field_simp
      -- üéâ no goals
    ¬∑ nlinarith
      -- üéâ no goals
#align one_add_mul_self_lt_rpow_one_add one_add_mul_self_lt_rpow_one_add

/-- **Bernoulli's inequality** for real exponents, non-strict version: for `1 ‚â§ p` and `-1 ‚â§ s`
we have `1 + p * s ‚â§ (1 + s) ^ p`. -/
theorem one_add_mul_self_le_rpow_one_add {s : ‚Ñù} (hs : -1 ‚â§ s) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    1 + p * s ‚â§ (1 + s) ^ p := by
  rcases eq_or_lt_of_le hp with (rfl | hp)
  -- ‚ä¢ 1 + 1 * s ‚â§ (1 + s) ^ 1
  ¬∑ simp
    -- üéâ no goals
  by_cases hs' : s = 0
  -- ‚ä¢ 1 + p * s ‚â§ (1 + s) ^ p
  ¬∑ simp [hs']
    -- üéâ no goals
  exact (one_add_mul_self_lt_rpow_one_add hs hs' hp).le
  -- üéâ no goals
#align one_add_mul_self_le_rpow_one_add one_add_mul_self_le_rpow_one_add

/- For `p : ‚Ñù` with `1 < p`, `fun x ‚Ü¶ x ^ p` is strictly convex on $[0, +‚àû)$.

We give an elementary proof rather than using the second derivative test, since this lemma is
needed early in the analysis library. -/
theorem strictConvexOn_rpow {p : ‚Ñù} (hp : 1 < p) : StrictConvexOn ‚Ñù (Ici 0) fun x : ‚Ñù => x ^ p := by
  apply strictConvexOn_of_slope_strict_mono_adjacent (convex_Ici (0 : ‚Ñù))
  -- ‚ä¢ ‚àÄ {x y z : ‚Ñù}, x ‚àà Ici 0 ‚Üí z ‚àà Ici 0 ‚Üí x < y ‚Üí y < z ‚Üí (y ^ p - x ^ p) / (y  ‚Ä¶
  rintro x y z (hx : 0 ‚â§ x) (hz : 0 ‚â§ z) hxy hyz
  -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < (z ^ p - y ^ p) / (z - y)
  have hy : 0 < y := by linarith
  -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < (z ^ p - y ^ p) / (z - y)
  have hy' : 0 < y ^ p := rpow_pos_of_pos hy _
  -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < (z ^ p - y ^ p) / (z - y)
  have H1 : y ^ (p - 1 + 1) = y ^ (p - 1) * y := rpow_add_one hy.ne' _
  -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < (z ^ p - y ^ p) / (z - y)
  ring_nf at H1
  -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < (z ^ p - y ^ p) / (z - y)
  trans p * y ^ (p - 1)
  -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < p * y ^ (p - 1)
  ¬∑ have h3 : 0 < y - x := by linarith only [hxy]
    -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < p * y ^ (p - 1)
    have hyx'' : x / y < 1 := by rwa [div_lt_one hy]
    -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < p * y ^ (p - 1)
    have hyx''' : x / y - 1 < 0 := by linarith only [hyx'']
    -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < p * y ^ (p - 1)
    have hyx'''' : 0 ‚â§ x / y := by positivity
    -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < p * y ^ (p - 1)
    have hyx''''' : -1 ‚â§ x / y - 1 := by linarith only [hyx'''']
    -- ‚ä¢ (y ^ p - x ^ p) / (y - x) < p * y ^ (p - 1)
    have : 1 - (1 + (x / y - 1)) ^ p < -p * (x / y - 1) := by
      linarith [one_add_mul_self_lt_rpow_one_add hyx''''' hyx'''.ne hp]
    rw [div_lt_iff h3, ‚Üê div_lt_div_right hy']
    -- ‚ä¢ (y ^ p - x ^ p) / y ^ p < p * y ^ (p - 1) * (y - x) / y ^ p
    convert this using 1
    -- ‚ä¢ (y ^ p - x ^ p) / y ^ p = 1 - (1 + (x / y - 1)) ^ p
    ¬∑ have H : (x / y) ^ p = x ^ p / y ^ p := div_rpow hx hy.le _
      -- ‚ä¢ (y ^ p - x ^ p) / y ^ p = 1 - (1 + (x / y - 1)) ^ p
      ring_nf at H ‚ä¢
      -- ‚ä¢ -(x ^ p * (y ^ p)‚Åª¬π) + y ^ p * (y ^ p)‚Åª¬π = 1 - (x * y‚Åª¬π) ^ p
      field_simp at H ‚ä¢
      -- ‚ä¢ -x ^ p + y ^ p = (1 - (x / y) ^ p) * y ^ p
      linear_combination H
      -- üéâ no goals
    ¬∑ ring_nf at H1 ‚ä¢
      -- ‚ä¢ p * y ^ (-1 + p) * y * (y ^ p)‚Åª¬π - p * y ^ (-1 + p) * x * (y ^ p)‚Åª¬π = p - p  ‚Ä¶
      field_simp
      -- ‚ä¢ (p * y ^ (-1 + p) * y - p * y ^ (-1 + p) * x) * y = (p * y - p * x) * y ^ p
      linear_combination p * (-y + x) * H1
      -- üéâ no goals
  ¬∑ have hyz' : 0 < z - y := by linarith only [hyz]
    -- ‚ä¢ p * y ^ (p - 1) < (z ^ p - y ^ p) / (z - y)
    have hyz'' : 1 < z / y := by rwa [one_lt_div hy]
    -- ‚ä¢ p * y ^ (p - 1) < (z ^ p - y ^ p) / (z - y)
    have hyz''' : 0 < z / y - 1 := by linarith only [hyz'']
    -- ‚ä¢ p * y ^ (p - 1) < (z ^ p - y ^ p) / (z - y)
    have hyz'''' : -1 ‚â§ z / y - 1 := by linarith only [hyz'']
    -- ‚ä¢ p * y ^ (p - 1) < (z ^ p - y ^ p) / (z - y)
    have : p * (z / y - 1) < (1 + (z / y - 1)) ^ p - 1 := by
      linarith [one_add_mul_self_lt_rpow_one_add hyz'''' hyz'''.ne' hp]
    rw [lt_div_iff hyz', ‚Üê div_lt_div_right hy']
    -- ‚ä¢ p * y ^ (p - 1) * (z - y) / y ^ p < (z ^ p - y ^ p) / y ^ p
    convert this using 1
    -- ‚ä¢ p * y ^ (p - 1) * (z - y) / y ^ p = p * (z / y - 1)
    ¬∑ ring_nf at H1 ‚ä¢
      -- ‚ä¢ -(p * y ^ (-1 + p) * y * (y ^ p)‚Åª¬π) + p * y ^ (-1 + p) * z * (y ^ p)‚Åª¬π = -p  ‚Ä¶
      field_simp at H1 ‚ä¢
      -- ‚ä¢ (-(p * y ^ (-1 + p) * y * y ^ p) + p * y ^ (-1 + p) * z * y ^ p) * y = (-(p  ‚Ä¶
      linear_combination p * (y - z) * y ^ p * H1
      -- üéâ no goals
    ¬∑ have H : (z / y) ^ p = z ^ p / y ^ p := div_rpow hz hy.le _
      -- ‚ä¢ (z ^ p - y ^ p) / y ^ p = (1 + (z / y - 1)) ^ p - 1
      ring_nf at H ‚ä¢
      -- ‚ä¢ z ^ p * (y ^ p)‚Åª¬π - y ^ p * (y ^ p)‚Åª¬π = -1 + (z * y‚Åª¬π) ^ p
      field_simp at H ‚ä¢
      -- ‚ä¢ z ^ p - y ^ p = (-1 + (z / y) ^ p) * y ^ p
      linear_combination -H
      -- üéâ no goals
#align strict_convex_on_rpow strictConvexOn_rpow

theorem convexOn_rpow {p : ‚Ñù} (hp : 1 ‚â§ p) : ConvexOn ‚Ñù (Ici 0) fun x : ‚Ñù => x ^ p := by
  rcases eq_or_lt_of_le hp with (rfl | hp)
  -- ‚ä¢ ConvexOn ‚Ñù (Ici 0) fun x => x ^ 1
  ¬∑ simpa using convexOn_id (convex_Ici _)
    -- üéâ no goals
  exact (strictConvexOn_rpow hp).convexOn
  -- üéâ no goals
#align convex_on_rpow convexOn_rpow

theorem strictConcaveOn_log_Iio : StrictConcaveOn ‚Ñù (Iio 0) log := by
  refine' ‚ü®convex_Iio _, _‚ü©
  -- ‚ä¢ ‚àÄ ‚¶Éx : ‚Ñù‚¶Ñ, x ‚àà Iio 0 ‚Üí ‚àÄ ‚¶Éy : ‚Ñù‚¶Ñ, y ‚àà Iio 0 ‚Üí x ‚â† y ‚Üí ‚àÄ ‚¶Éa b : ‚Ñù‚¶Ñ, 0 < a ‚Üí 0 ‚Ä¶
  rintro x (hx : x < 0) y (hy : y < 0) hxy a b ha hb hab
  -- ‚ä¢ a ‚Ä¢ log x + b ‚Ä¢ log y < log (a ‚Ä¢ x + b ‚Ä¢ y)
  have hx' : 0 < -x := by linarith
  -- ‚ä¢ a ‚Ä¢ log x + b ‚Ä¢ log y < log (a ‚Ä¢ x + b ‚Ä¢ y)
  have hy' : 0 < -y := by linarith
  -- ‚ä¢ a ‚Ä¢ log x + b ‚Ä¢ log y < log (a ‚Ä¢ x + b ‚Ä¢ y)
  have hxy' : -x ‚â† -y := by contrapose! hxy; linarith
  -- ‚ä¢ a ‚Ä¢ log x + b ‚Ä¢ log y < log (a ‚Ä¢ x + b ‚Ä¢ y)
  calc
    a ‚Ä¢ log x + b ‚Ä¢ log y = a ‚Ä¢ log (-x) + b ‚Ä¢ log (-y) := by simp_rw [log_neg_eq_log]
    _ < log (a ‚Ä¢ -x + b ‚Ä¢ -y) := (strictConcaveOn_log_Ioi.2 hx' hy' hxy' ha hb hab)
    _ = log (-(a ‚Ä¢ x + b ‚Ä¢ y)) := by congr 1; simp only [Algebra.id.smul_eq_mul]; ring
    _ = _ := by rw [log_neg_eq_log]

#align strict_concave_on_log_Iio strictConcaveOn_log_Iio
