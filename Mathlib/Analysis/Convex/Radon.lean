/-
Copyright (c) 2023 Vasily Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasily Nesterov
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Tactic.Linarith
import Mathlib.Data.Set.Card
import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional

/-!
# Radon's theorem on convex sets

Radon's theorem states that any affine dependent set can be partitioned into two sets whose convex
hulls intersect.

## Tags

convex hull, Radon, Helly, affine independence
-/

open Finset Set
open BigOperators

variable {Î¹ ğ•œ E : Type*} [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- **Radon's theorem on convex sets**: Any family `f` of affine dependent vectors contains a set
`I` with the property that convex hulls of `I` and `Iá¶œ` intersect. -/
theorem Convex.radon_partition {f : Î¹ â†’ E} (h : Â¬ AffineIndependent ğ•œ f) :
    âˆƒ I, (convexHull ğ•œ (f '' I) âˆ© convexHull ğ•œ (f '' Iá¶œ)).Nonempty := by
  rw [affineIndependent_iff] at h
  push_neg at h
  obtain âŸ¨s, w, h_wsum, h_vsum, nonzero_w_index, h1, h2âŸ© := h
  let I : Finset Î¹ := s.filter fun i â†¦ 0 â‰¤ w i
  let J : Finset Î¹ := s.filter fun i â†¦ w i < 0
  let p : E := centerMass I w f -- point of intersection
  have hJI : âˆ‘ j in J, w j + âˆ‘ i in I, w i = 0 := by
    simpa only [h_wsum, not_lt] using sum_filter_add_sum_filter_not s (fun i â†¦ w i < 0) w
  have hI : 0 < âˆ‘ i in I, w i := by
    rcases exists_pos_of_sum_zero_of_exists_nonzero _ h_wsum âŸ¨nonzero_w_index, h1, h2âŸ©
      with âŸ¨pos_w_index, h1', h2'âŸ©
    exact sum_pos' (Î» _i hi â†¦ (mem_filter.1 hi).2)
      âŸ¨pos_w_index, by simp only [mem_filter, h1', h2'.le, and_self, h2']âŸ©
  have hp : centerMass J w f = p := Finset.centerMass_of_sum_add_sum_eq_zero hJI $ by
    simpa only [â†h_vsum, not_lt] using sum_filter_add_sum_filter_not s (fun i â†¦ w i < 0) _
  refine âŸ¨I, p, ?_, ?_âŸ©
  Â· exact centerMass_mem_convexHull _ (fun _i hi â†¦ (mem_filter.mp hi).2) hI
      (fun _i hi â†¦ Set.mem_image_of_mem _ hi)
  rw [â†hp]
  refine centerMass_mem_convexHull_of_nonpos _ (fun _ hi â†¦ (mem_filter.mp hi).2.le) ?_
    (fun _i hi â†¦ Set.mem_image_of_mem _ fun hi' â†¦ ?_)
  Â· linarith only [hI, hJI]
  Â· exact (mem_filter.mp hi').2.not_lt (mem_filter.mp hi).2


open FiniteDimensional

variable [FiniteDimensional ğ•œ E]

/-- **Helly's theorem on convex sets**: If `F` is a finite family of convex sets in a vector space
of finite dimension `d`, and any `d + 1` sets of `F` intersect, then all sets of `F` intersect. -/
theorem helly_theorem (F : Set (Set E)) {hF_fin : Set.Finite F}
    (h_card : (finrank ğ•œ E) + 1 â‰¤ ncard F) (h_convex : âˆ€ X âˆˆ F, Convex ğ•œ X)
    (h_inter : âˆ€ G : Set (Set E), (G âŠ† F) â†’ (ncard G = (finrank ğ•œ E) + 1) â†’
    (â‹‚â‚€ G).Nonempty) : (â‹‚â‚€ F).Nonempty := by
  obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, ncard F = n := âŸ¨ncard F, rflâŸ© -- for induction on ncard F
  rw [hn] at h_card
  induction' n, h_card using Nat.le_induction with k h_card hk generalizing F
  exact h_inter F (Eq.subset rfl) hn
  have h1 : âˆ€ X âˆˆ F, (â‹‚â‚€ (F \ {X})).Nonempty := by
    intro X hX
    apply @hk _ (Finite.diff hF_fin {X})
    Â· exact fun Y hY â†¦ h_convex Y (mem_of_mem_diff hY)
    Â· exact fun G hG_1 hG_2 â†¦ h_inter G (Subset.trans hG_1 (diff_subset F {X})) hG_2
    Â· rw [ncard_diff_singleton_of_mem hX hF_fin, Nat.sub_eq_of_eq_add hn]
  let a : F â†’ E := fun X â†¦ (h1 X (Subtype.mem X)).some
  have h2 : Â¬AffineIndependent ğ•œ a := by
    have : Fintype â†‘F := Finite.fintype hF_fin -- for instance inferring
    rw [â†finrank_vectorSpan_le_iff_not_affineIndependent ğ•œ a (n := (k - 1))]
    Â· exact (Submodule.finrank_le (vectorSpan ğ•œ (Set.range a))).trans (Nat.le_pred_of_lt h_card)
    Â· rw [â†Finite.card_toFinset hF_fin, â†ncard_eq_toFinset_card F hF_fin, hn,
      â†Nat.sub_add_comm (Nat.one_le_of_lt h_card)]
      rfl
  obtain âŸ¨I, p, h4_I, h4_IcâŸ© := Convex.radon_partition h2
  use p
  rw [mem_sInter]
  intro X hX
  have h3 (X Y : Set E) (hX : X âˆˆ F) (hY : Y âˆˆ F) (hneq : X â‰  Y) : a âŸ¨Y, hYâŸ© âˆˆ X := by
    apply @mem_of_subset_of_mem _ (â‹‚â‚€ (F \ {Y})) _
    apply sInter_subset_of_mem
    Â· simp only [mem_diff, hX, mem_singleton_iff, hneq, not_false_eq_true, and_self]
    Â· exact (h1 Y hY).some_mem
  have h4 (I : Set F) (h : âŸ¨X, hXâŸ© âˆˆ I) : (convexHull ğ•œ) (a '' Iá¶œ) âŠ† X := by
    rw [Convex.convexHull_subset_iff (h_convex X hX)]
    intro z hz; simp only [Set.mem_image] at hz; rcases hz with âŸ¨Y, hY1, hY2âŸ©
    rw [â†hY2]
    apply h3
    Â· exact hX
    Â· by_contra heq
      simp only [heq, Subtype.coe_eta] at h
      contradiction
  by_cases (âŸ¨X, hXâŸ© âˆˆ I)
  Â· exact h4 I h h4_Ic
  Â· apply h4 Iá¶œ h; rwa [compl_compl]
