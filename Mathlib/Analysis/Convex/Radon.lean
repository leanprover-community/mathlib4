/-
Copyright (c) 2023 Vasily Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasily Nesterov
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Data.Set.Card
import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional
import Mathlib.Topology.Separation.Hausdorff

/-!
# Radon's theorem on convex sets

Radon's theorem states that any affine dependent set can be partitioned into two sets whose convex
hulls intersect nontrivially.

As a corollary, we prove Helly's theorem, which is a basic result in discrete geometry on the
intersection of convex sets. Let `Xâ‚, â‹¯, Xâ‚™` be a finite family of convex sets in `â„áµˆ` with
`n â‰¥ d + 1`. The theorem states that if any `d + 1` sets from this family intersect nontrivially,
then the whole family intersect nontrivially. For the infinite family of sets it is not true, as
example of `Set.Ioo 0 (1 / n)` for `n : â„•` shows. But the statement is true, if we assume
compactness of sets (see `helly_theorem_compact`)

## Tags

convex hull, affine independence, Radon, Helly
-/

open Fintype Finset Set

namespace Convex

variable {Î¹ ğ•œ E : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E]

/-- **Radon's theorem on convex sets**.

Any family `f` of affine dependent vectors contains a set `I` with the property that convex hulls of
`I` and `Iá¶œ` intersect nontrivially.
In particular, any `d + 2` points in a `d`-dimensional space can be partitioned this way, since they
are affinely dependent (see `finrank_vectorSpan_le_iff_not_affineIndependent`). -/
theorem radon_partition {f : Î¹ â†’ E} (h : Â¬ AffineIndependent ğ•œ f) :
    âˆƒ I, (convexHull ğ•œ (f '' I) âˆ© convexHull ğ•œ (f '' Iá¶œ)).Nonempty := by
  rw [affineIndependent_iff] at h
  push_neg at h
  obtain âŸ¨s, w, h_wsum, h_vsum, nonzero_w_index, h1, h2âŸ© := h
  let I : Finset Î¹ := {i âˆˆ s | 0 â‰¤ w i}
  let J : Finset Î¹ := {i âˆˆ s | w i < 0}
  let p : E := centerMass I w f -- point of intersection
  have hJI : âˆ‘ j âˆˆ J, w j + âˆ‘ i âˆˆ I, w i = 0 := by
    simpa only [h_wsum, not_lt] using sum_filter_add_sum_filter_not s (fun i â†¦ w i < 0) w
  have hI : 0 < âˆ‘ i âˆˆ I, w i := by
    rcases exists_pos_of_sum_zero_of_exists_nonzero _ h_wsum âŸ¨nonzero_w_index, h1, h2âŸ©
      with âŸ¨pos_w_index, h1', h2'âŸ©
    exact sum_pos' (fun _i hi â†¦ (mem_filter.1 hi).2)
      âŸ¨pos_w_index, by simp only [I, mem_filter, h1', h2'.le, and_self, h2']âŸ©
  have hp : centerMass J w f = p := centerMass_of_sum_add_sum_eq_zero hJI <| by
    simpa only [â† h_vsum, not_lt] using sum_filter_add_sum_filter_not s (fun i â†¦ w i < 0) _
  refine âŸ¨I, p, ?_, ?_âŸ©
  Â· exact centerMass_mem_convexHull _ (fun _i hi â†¦ (mem_filter.mp hi).2) hI
      (fun _i hi â†¦ mem_image_of_mem _ hi)
  rw [â† hp]
  refine centerMass_mem_convexHull_of_nonpos _ (fun _ hi â†¦ (mem_filter.mp hi).2.le) ?_
    (fun _i hi â†¦ mem_image_of_mem _ fun hi' â†¦ ?_)
  Â· linarith only [hI, hJI]
  Â· exact (mem_filter.mp hi').2.not_gt (mem_filter.mp hi).2

open Module

omit [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] in
/-- Corner case for `helly_theorem'`. -/
private lemma helly_theorem_corner {F : Î¹ â†’ Set E} {s : Finset Î¹}
    (h_card_small : #s â‰¤ finrank ğ•œ E + 1)
    (h_inter : âˆ€ I âŠ† s, #I â‰¤ finrank ğ•œ E + 1 â†’ (â‹‚ i âˆˆ I, F i).Nonempty) :
    (â‹‚ i âˆˆ s, F i).Nonempty := h_inter s (by simp) h_card_small

variable [FiniteDimensional ğ•œ E]

/-- **Helly's theorem** for finite families of convex sets.

If `F` is a finite family of convex sets in a vector space of finite dimension `d`, and any
`k â‰¤ d + 1` sets of `F` intersect nontrivially, then all sets of `F` intersect nontrivially. -/
theorem helly_theorem' {F : Î¹ â†’ Set E} {s : Finset Î¹}
    (h_convex : âˆ€ i âˆˆ s, Convex ğ•œ (F i))
    (h_inter : âˆ€ I âŠ† s, #I â‰¤ finrank ğ•œ E + 1 â†’ (â‹‚ i âˆˆ I, F i).Nonempty) :
    (â‹‚ i âˆˆ s, F i).Nonempty := by
  classical
  obtain h_card | h_card := lt_or_ge #s (finrank ğ•œ E + 1)
  Â· exact helly_theorem_corner (le_of_lt h_card) h_inter
  generalize hn : #s = n
  rw [hn] at h_card
  induction' n, h_card using Nat.le_induction with k h_card hk generalizing Î¹
  Â· exact helly_theorem_corner (le_of_eq hn) h_inter
  /- Construct a family of vectors indexed by `Î¹` such that the vector corresponding to `i : Î¹`
  is an arbitrary element of the intersection of all `F j` except `F i`. -/
  let a (i : s) : E := Set.Nonempty.some (s := â‹‚ j âˆˆ s.erase i, F j) <| by
    apply hk (s := s.erase i)
    Â· exact fun i hi â†¦ h_convex i (mem_of_mem_erase hi)
    Â· intro J hJ_ss hJ_card
      exact h_inter J (subset_trans hJ_ss (erase_subset i.val s)) hJ_card
    Â· simp only [coe_mem, card_erase_of_mem]; omega
  /- This family of vectors is not affine independent because the number of them exceeds the
  dimension of the space. -/
  have h_ind : Â¬AffineIndependent ğ•œ a := by
    rw [â† finrank_vectorSpan_le_iff_not_affineIndependent ğ•œ a (n := (k - 1))]
    Â· exact (Submodule.finrank_le (vectorSpan ğ•œ (range a))).trans (Nat.le_pred_of_lt h_card)
    Â· simp only [card_coe]; omega
  /- Use `radon_partition` to conclude there is a subset `I` of `s` and a point `p : E` which
  lies in the convex hull of either `a '' I` or `a '' Iá¶œ`. We claim that `p âˆˆ â‹‚ i âˆˆ s, F i`. -/
  obtain âŸ¨I, p, hp_I, hp_IcâŸ© := radon_partition h_ind
  use p
  apply mem_biInter
  intro i hi
  let i : s := âŸ¨i, hiâŸ©
  /- It suffices to show that for any subcollection `J` of `s` containing `i`, the convex
  hull of `a '' (s \ J)` is contained in `F i`. -/
  suffices âˆ€ J : Set s, (i âˆˆ J) â†’ (convexHull ğ•œ) (a '' Já¶œ) âŠ† F i by
    by_cases h : i âˆˆ I
    Â· exact this I h hp_Ic
    Â· apply this Iá¶œ h; rwa [compl_compl]
  /- Given any subcollection `J` of `Î¹` containing `i`, because `F i` is convex, we need only
  show that `a j âˆˆ F i` for each `j âˆˆ s \ J`. -/
  intro J hi
  rw [convexHull_subset_iff (h_convex i.1 i.2)]
  rintro v âŸ¨j, hj, hj_vâŸ©
  rw [â† hj_v]
  /- Since `j âˆˆ Já¶œ` and `i âˆˆ J`, we conclude that `i â‰  j`, and hence by the definition of `a`:
  `a j âˆˆ â‹‚ F '' (Set.univ \ {j}) âŠ† F i`. -/
  apply mem_of_subset_of_mem (sâ‚ := â‹‚ k âˆˆ (s.erase j), F k)
  Â· apply biInter_subset_of_mem
    simp only [erase_val]
    suffices h : i.val âˆˆ s.erase j by assumption
    simp only [mem_erase]
    constructor
    Â· exact fun h' â†¦ hj ((show i = j from SetCoe.ext h') â–¸ hi)
    Â· assumption
  Â· apply Nonempty.some_mem

/-- **Helly's theorem** for finite families of convex sets in its classical form.

If `F` is a family of `n` convex sets in a vector space of finite dimension `d`, with `n â‰¥ d + 1`,
and any `d + 1` sets of `F` intersect nontrivially, then all sets of `F` intersect nontrivially. -/
theorem helly_theorem {F : Î¹ â†’ Set E} {s : Finset Î¹}
    (h_card : finrank ğ•œ E + 1 â‰¤ #s)
    (h_convex : âˆ€ i âˆˆ s, Convex ğ•œ (F i))
    (h_inter : âˆ€ I âŠ† s, #I = finrank ğ•œ E + 1 â†’ (â‹‚ i âˆˆ I, F i).Nonempty) :
    (â‹‚ i âˆˆ s, F i).Nonempty := by
  apply helly_theorem' h_convex
  intro I hI_ss hI_card
  obtain âŸ¨J, hI_ss_J, hJ_ss, hJ_cardâŸ© := exists_subsuperset_card_eq hI_ss hI_card h_card
  apply Set.Nonempty.mono <| biInter_mono hI_ss_J (fun _ _ â†¦ Set.Subset.rfl)
  exact h_inter J hJ_ss hJ_card

/-- **Helly's theorem** for finite sets of convex sets.

If `F` is a finite set of convex sets in a vector space of finite dimension `d`, and any `k â‰¤ d + 1`
sets from `F` intersect nontrivially, then all sets from `F` intersect nontrivially. -/
theorem helly_theorem_set' {F : Finset (Set E)}
    (h_convex : âˆ€ X âˆˆ F, Convex ğ•œ X)
    (h_inter : âˆ€ G : Finset (Set E), G âŠ† F â†’ #G â‰¤ finrank ğ•œ E + 1 â†’ (â‹‚â‚€ G : Set E).Nonempty) :
    (â‹‚â‚€ (F : Set (Set E))).Nonempty := by
  classical -- for DecidableEq, required for the family version
  rw [show â‹‚â‚€ F = â‹‚ X âˆˆ F, (X : Set E) by ext; simp]
  apply helly_theorem' h_convex
  intro G hG_ss hG_card
  rw [show â‹‚ X âˆˆ G, X = â‹‚â‚€ G by ext; simp]
  exact h_inter G hG_ss hG_card

/-- **Helly's theorem** for finite sets of convex sets in its classical form.

If `F` is a finite set of convex sets in a vector space of finite dimension `d`, with `n â‰¥ d + 1`,
and any `d + 1` sets from `F` intersect nontrivially,
then all sets from `F` intersect nontrivially. -/
theorem helly_theorem_set {F : Finset (Set E)}
    (h_card : finrank ğ•œ E + 1 â‰¤ #F)
    (h_convex : âˆ€ X âˆˆ F, Convex ğ•œ X)
    (h_inter : âˆ€ G : Finset (Set E), G âŠ† F â†’ #G = finrank ğ•œ E + 1 â†’ (â‹‚â‚€ G : Set E).Nonempty) :
    (â‹‚â‚€ (F : Set (Set E))).Nonempty := by
  apply helly_theorem_set' h_convex
  intro I hI_ss hI_card
  obtain âŸ¨J, _, hJ_ss, hJ_cardâŸ© := exists_subsuperset_card_eq hI_ss hI_card h_card
  have : â‹‚â‚€ (J : Set (Set E)) âŠ† â‹‚â‚€ I := sInter_mono (by simpa [hI_ss])
  apply Set.Nonempty.mono this
  exact h_inter J hJ_ss (by omega)

/-- **Helly's theorem** for families of compact convex sets.

If `F` is a family of compact convex sets in a vector space of finite dimension `d`, and any
`k â‰¤ d + 1` sets of `F` intersect nontrivially, then all sets of `F` intersect nontrivially. -/
theorem helly_theorem_compact' [TopologicalSpace E] [T2Space E] {F : Î¹ â†’ Set E}
    (h_convex : âˆ€ i, Convex ğ•œ (F i)) (h_compact : âˆ€ i, IsCompact (F i))
    (h_inter : âˆ€ I : Finset Î¹, #I â‰¤ finrank ğ•œ E + 1 â†’ (â‹‚ i âˆˆ I, F i).Nonempty) :
    (â‹‚ i, F i).Nonempty := by
  classical
  /- If `Î¹` is empty the statement is trivial. -/
  rcases isEmpty_or_nonempty Î¹ with _ | h_nonempty
  Â· simp only [iInter_of_empty, Set.univ_nonempty]
  /- By the finite version of theorem, every finite subfamily has an intersection. -/
  have h_fin (I : Finset Î¹) : (â‹‚ i âˆˆ I, F i).Nonempty := by
    apply helly_theorem' (s := I) (ğ•œ := ğ•œ) (by simp [h_convex])
    exact fun J _ hJ_card â†¦ h_inter J hJ_card
  /- The following is a clumsy proof that family of compact sets with the finite intersection
  property has a nonempty intersection. -/
  have i0 : Î¹ := Nonempty.some h_nonempty
  rw [show â‹‚ i, F i = (F i0) âˆ© â‹‚ i, F i by simp [iInter_subset]]
  apply IsCompact.inter_iInter_nonempty
  Â· exact h_compact i0
  Â· intro i
    exact (h_compact i).isClosed
  Â· intro I
    simpa using h_fin ({i0} âˆª I)

/-- **Helly's theorem** for families of compact convex sets in its classical form.

If `F` is a (possibly infinite) family of more than `d + 1` compact convex sets in a vector space of
finite dimension `d`, and any `d + 1` sets of `F` intersect nontrivially,
then all sets of `F` intersect nontrivially. -/
theorem helly_theorem_compact [TopologicalSpace E] [T2Space E] {F : Î¹ â†’ Set E}
    (h_card : finrank ğ•œ E + 1 â‰¤ ENat.card Î¹)
    (h_convex : âˆ€ i, Convex ğ•œ (F i)) (h_compact : âˆ€ i, IsCompact (F i))
    (h_inter : âˆ€ I : Finset Î¹, #I = finrank ğ•œ E + 1 â†’ (â‹‚ i âˆˆ I, F i).Nonempty) :
    (â‹‚ i, F i).Nonempty := by
  apply helly_theorem_compact' h_convex h_compact
  intro I hI_card
  have hJ : âˆƒ J : Finset Î¹, I âŠ† J âˆ§ #J = finrank ğ•œ E + 1 := by
    by_cases h : Infinite Î¹
    Â· exact Infinite.exists_superset_card_eq _ _ hI_card
    Â· have : Finite Î¹ := Finite.of_not_infinite h
      have : Fintype Î¹ := Fintype.ofFinite Î¹
      apply exists_superset_card_eq hI_card
      simp only [ENat.card_eq_coe_fintype_card] at h_card
      rwa [â† Nat.cast_one, â† Nat.cast_add, Nat.cast_le] at h_card
  obtain âŸ¨J, hJ_ss, hJ_cardâŸ© := hJ
  apply Set.Nonempty.mono <| biInter_mono hJ_ss (by intro _ _; rfl)
  exact h_inter J hJ_card

/-- **Helly's theorem** for sets of compact convex sets.

If `F` is a set of compact convex sets in a vector space of finite dimension `d`, and any
`k â‰¤ d + 1` sets from `F` intersect nontrivially, then all sets from `F` intersect nontrivially. -/
theorem helly_theorem_set_compact' [TopologicalSpace E] [T2Space E] {F : Set (Set E)}
    (h_convex : âˆ€ X âˆˆ F, Convex ğ•œ X) (h_compact : âˆ€ X âˆˆ F, IsCompact X)
    (h_inter : âˆ€ G : Finset (Set E), (G : Set (Set E)) âŠ† F â†’ #G â‰¤ finrank ğ•œ E + 1 â†’
    (â‹‚â‚€ G : Set E).Nonempty) :
    (â‹‚â‚€ (F : Set (Set E))).Nonempty := by
  classical -- for DecidableEq, required for the family version
  rw [show â‹‚â‚€ F = â‹‚ X : F, (X : Set E) by ext; simp]
  refine helly_theorem_compact' (F := fun x : F â†¦ x.val)
    (fun X â†¦ h_convex X (by simp)) (fun X â†¦ h_compact X (by simp)) ?_
  intro G _
  let G' : Finset (Set E) := image Subtype.val G
  rw [show â‹‚ i âˆˆ G, â†‘i = â‹‚â‚€ (G' : Set (Set E)) by simp [G']]
  apply h_inter G'
  Â· simp [G']
  Â· apply le_trans card_image_le
    assumption

/-- **Helly's theorem** for sets of compact convex sets in its classical version.

If `F` is a (possibly infinite) set of more than `d + 1` compact convex sets in a vector space of
finite dimension `d`, and any `d + 1` sets from `F` intersect nontrivially,
then all sets from `F` intersect nontrivially. -/
theorem helly_theorem_set_compact [TopologicalSpace E] [T2Space E] {F : Set (Set E)}
    (h_card : finrank ğ•œ E + 1 â‰¤ F.encard)
    (h_convex : âˆ€ X âˆˆ F, Convex ğ•œ X) (h_compact : âˆ€ X âˆˆ F, IsCompact X)
    (h_inter : âˆ€ G : Finset (Set E), (G : Set (Set E)) âŠ† F â†’ #G = finrank ğ•œ E + 1 â†’
    (â‹‚â‚€ G : Set E).Nonempty) :
    (â‹‚â‚€ (F : Set (Set E))).Nonempty := by
  apply helly_theorem_set_compact' h_convex h_compact
  intro I hI_ss hI_card
  obtain âŸ¨J, _, hJ_ss, hJ_cardâŸ© := exists_superset_subset_encard_eq hI_ss (hkt := h_card)
    (by simpa only [encard_coe_eq_coe_finsetCard, â† ENat.coe_one, â† ENat.coe_add, Nat.cast_le])
  apply Set.Nonempty.mono <| sInter_mono (by simpa [hI_ss])
  have hJ_fin : Fintype J := Finite.fintype <| finite_of_encard_eq_coe hJ_card
  let J' := J.toFinset
  rw [â† coe_toFinset J]
  apply h_inter J'
  Â· simpa [J']
  Â· rwa [encard_eq_coe_toFinset_card J, â† ENat.coe_one, â† ENat.coe_add, Nat.cast_inj] at hJ_card

end Convex
