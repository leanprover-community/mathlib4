/-
Copyright (c) 2023 Vasily Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasily Nesterov
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Data.Set.Card
import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional
import Mathlib.Tactic.Linarith
import Mathlib.Topology.Compactness.Compact
import Mathlib.Topology.Separation

/-!
# Radon's theorem on convex sets

Radon's theorem states that any affine dependent set can be partitioned into two sets whose convex
hulls intersect.

As a corollary, we prove Helly's theorem, which is a basic result in discrete geometry on the
intersection of convex sets. Let `Xâ‚, â‹¯, Xâ‚™` be a finite family of convex sets in `â„áµˆ` with
`n â‰¥ d + 1`. The theorem states that if any `d + 1` sets from this family intersect nontrivially,
then the whole family intersect nontrivially. For the infinite family of sets it is not true, as
example of `Set.Ioo 0 (1 / n)` for `n : â„•` shows. But the statement is true, if we assume
compactness of sets (see `Convex.helly_theorem_infinite`)

## Tags

convex hull, radon, affine independence
-/

open Finset Set
open BigOperators

variable {Î¹ ğ•œ E : Type*} [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- **Radon's theorem on convex sets**: Any family `f` of affine dependent vectors contains a set
`I` with the property that convex hulls of `I` and `Iá¶œ` intersect. -/
theorem Convex.radon_partition {f : Î¹ â†’ E} (h : Â¬ AffineIndependent ğ•œ f) :
    âˆƒ I, (convexHull ğ•œ (f '' I) âˆ© convexHull ğ•œ (f '' Iá¶œ)).Nonempty := by
  rw [affineIndependent_iff] at h
  push_neg at h
  obtain âŸ¨s, w, h_wsum, h_vsum, nonzero_w_index, h1, h2âŸ© := h
  let I : Finset Î¹ := s.filter fun i â†¦ 0 â‰¤ w i
  let J : Finset Î¹ := s.filter fun i â†¦ w i < 0
  let p : E := centerMass I w f -- point of intersection
  have hJI : âˆ‘ j in J, w j + âˆ‘ i in I, w i = 0 := by
    simpa only [h_wsum, not_lt] using sum_filter_add_sum_filter_not s (fun i â†¦ w i < 0) w
  have hI : 0 < âˆ‘ i in I, w i := by
    rcases exists_pos_of_sum_zero_of_exists_nonzero _ h_wsum âŸ¨nonzero_w_index, h1, h2âŸ©
      with âŸ¨pos_w_index, h1', h2'âŸ©
    exact sum_pos' (Î» _i hi â†¦ (mem_filter.1 hi).2)
      âŸ¨pos_w_index, by simp only [I, mem_filter, h1', h2'.le, and_self, h2']âŸ©
  have hp : centerMass J w f = p := Finset.centerMass_of_sum_add_sum_eq_zero hJI <| by
    simpa only [â† h_vsum, not_lt] using sum_filter_add_sum_filter_not s (fun i â†¦ w i < 0) _
  refine âŸ¨I, p, ?_, ?_âŸ©
  Â· exact centerMass_mem_convexHull _ (fun _i hi â†¦ (mem_filter.mp hi).2) hI
      (fun _i hi â†¦ Set.mem_image_of_mem _ hi)
  rw [â† hp]
  refine centerMass_mem_convexHull_of_nonpos _ (fun _ hi â†¦ (mem_filter.mp hi).2.le) ?_
    (fun _i hi â†¦ Set.mem_image_of_mem _ fun hi' â†¦ ?_)
  Â· linarith only [hI, hJI]
  Â· exact (mem_filter.mp hi').2.not_lt (mem_filter.mp hi).2

open FiniteDimensional

variable [FiniteDimensional ğ•œ E]

/-- **Helly's theorem on convex sets**: If `F` is a finite family of convex sets in a vector space
of finite dimension `d`, and any `d + 1` sets of `F` intersect, then all sets of `F` intersect. -/
theorem Convex.helly_theorem (F : Î¹ â†’ Set E) {hF_fin : Finite Î¹}
    (h_convex : âˆ€ i : Î¹, Convex ğ•œ (F i))
    (h_inter : âˆ€ I : Set Î¹, (ncard I â‰¤ (finrank ğ•œ E) + 1) â†’
    (â‹‚ i âˆˆ I, F i).Nonempty) : (â‹‚ i : Î¹, F i).Nonempty := by
  by_cases h_card : Nat.card Î¹ < (finrank ğ•œ E) + 1
  Â· rw [show â‹‚ i, F i = â‹‚ i âˆˆ Set.univ, F i by simp]
    apply h_inter Set.univ
    rw [Set.ncard_univ]
    exact le_of_lt h_card
  Â· obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, Nat.card Î¹ = n := âŸ¨Nat.card Î¹, rflâŸ© -- for induction on ncard F
    simp only [not_lt] at h_card
    rw [hn] at h_card
    induction' n, h_card using Nat.le_induction with k h_card hk generalizing Î¹
    Â· rw [show â‹‚ i, F i = â‹‚ i âˆˆ Set.univ, F i by simp]
      apply h_inter Set.univ
      rw [Set.ncard_univ]
      exact hn.le
      /- Construct a family of vectors indexed by `Î¹` such that the vector corresponding to `i : Î¹`
      is an arbitrary element of the intersection `â‹‚ i : Î¹, F i` which *does not lie in `F i`*. -/
    Â· let a (i : Î¹) : E := Set.Nonempty.some (s := â‹‚â‚€ (F '' (Set.univ \ {i}))) <| by
        let Î¹' := Set.univ \ {i}
        let F' : Î¹' â†’ Set E := fun j â†¦ F j
        rw [show â‹‚â‚€ (F '' (Set.univ \ {i})) = â‹‚ i, F' i by simp [Î¹']]
        apply hk (F := F')
        Â· exact fun i â†¦ h_convex â†‘i
        Â· intro J hJ_card
          rw [show (â‹‚ i âˆˆ J, F' i) = (â‹‚ i âˆˆ Subtype.val '' J, F i) by simp]
          apply h_inter
          exact le_trans Set.ncard_image_le hJ_card
        Â· rw [Nat.card_coe_set_eq Î¹', Set.ncard_diff_singleton_of_mem]
          Â· rw [Set.ncard_univ]
            omega
          Â· trivial
        Â· exact Subtype.finite
      /- This family of vectors is not affine independent because the number of them exceeds the
      dimension of the space. -/
      have h2 : Â¬AffineIndependent ğ•œ a := by
        have : Fintype Î¹ := by exact Fintype.ofFinite Î¹ -- for instance inferring
        rw [â† finrank_vectorSpan_le_iff_not_affineIndependent ğ•œ a (n := (k - 1))]
        Â· exact (Submodule.finrank_le (vectorSpan ğ•œ (Set.range a))).trans (Nat.le_pred_of_lt h_card)
        Â· rw [Fintype.card_eq_nat_card]; omega
      /- Use `Convex.radon_partition` to conclude there is a subset `I` of `Î¹` and a point `p : E`
      which lies in the convex hull of either `a '' I` or `a '' Iá¶œ`. We claim that `p âˆˆ â‹‚â‚€ F`. -/
      obtain âŸ¨I, p, h4_I, h4_IcâŸ© := Convex.radon_partition h2
      use p
      apply Set.mem_iInter_of_mem
      intro i
      /- It suffices to show that for any set `F i` in a subcollection `I` of `Î¹`, that the convex
      hull of `a '' Iá¶œ` is contained in `F i`. -/
      suffices âˆ€ I : Set Î¹, i âˆˆ I â†’ (convexHull ğ•œ) (a '' Iá¶œ) âŠ† F i by
        by_cases h : i âˆˆ I
        Â· exact this I h h4_Ic
        Â· apply this Iá¶œ h; rwa [compl_compl]
      /- Given any subcollection `I` of `Î¹` containing `i`, because `F i` is convex, we need only
      show that `a j âˆˆ F i` for each `j âˆˆ Iá¶œ`. -/
      intro I hi
      rw [Convex.convexHull_subset_iff (h_convex i)]
      rintro - âŸ¨j, hj, rflâŸ©
      /- Since `j âˆˆ Iá¶œ` and `i âˆˆ I`, we conclude that `i â‰  j`, and hence by the definition of `a`:
      `a j âˆˆ â‹‚ F '' (Set.univ \ {j}) âŠ† F i`. -/
      apply Set.mem_of_subset_of_mem (sâ‚ := â‹‚â‚€ (F '' (Set.univ \ {j})))
      Â· apply sInter_subset_of_mem
        use i
        simp only [mem_diff, Set.mem_univ, mem_singleton_iff, true_and, and_true]
        exact fun h' â†¦ hj (h' â–¸ hi)
      Â· apply Set.Nonempty.some_mem

/-- The set version of `Convex.helly_theorem`. -/
theorem Convex.helly_theorem_set (F : Set (Set E)) {hF_fin : Set.Finite F}
    (h_convex : âˆ€ X âˆˆ F, Convex ğ•œ X)
    (h_inter : âˆ€ G : Set (Set E), (G âŠ† F) â†’ (ncard G â‰¤ (finrank ğ•œ E) + 1) â†’
    (â‹‚â‚€ G).Nonempty) : (â‹‚â‚€ F).Nonempty := by
  rw [show â‹‚â‚€ F = â‹‚ X : F, â†‘X by ext; simp]
  apply Convex.helly_theorem (F := fun x : F => x.val) (ğ•œ := ğ•œ)
  Â· exact fun X â†¦ h_convex X (by simp)
  Â· intro G _
    let G' : Set (Set E) := Subtype.val '' G
    rw [show â‹‚ i âˆˆ G, â†‘i = â‹‚â‚€ G' by simp [G']]
    apply h_inter G' (by simp [G'])
    rwa [ncard_image_of_injective G Subtype.val_injective]
  Â· exact hF_fin

/-- The version of `Convex.helly_theorem` for infinite families with additional compactness
assumption. -/
theorem Convex.helly_theorem_infinite [TopologicalSpace E] [T2Space E] (F : Î¹ â†’ Set E)
    (h_convex : âˆ€ i : Î¹, Convex ğ•œ (F i)) (h_compact : âˆ€ i : Î¹, IsCompact (F i)) (h_inf : Infinite Î¹)
    (h_inter : âˆ€ I : Set Î¹, I.Finite â†’ (ncard I â‰¤ (finrank ğ•œ E) + 1) â†’
    (â‹‚ i âˆˆ I, F i).Nonempty) : (â‹‚ i : Î¹, F i).Nonempty := by
    /- By the finite version of theorem, every finite subfamily has an intersection. -/
    have h1 (I : Set Î¹) (hI_fin : I.Finite) : (â‹‚ i âˆˆ I, F i).Nonempty := by
      rw [show â‹‚ i âˆˆ I, F i = â‹‚ i : I, F â†‘i by simp only [iInter_coe_set]]
      apply Convex.helly_theorem (Î¹ := I) (fun i : I â†¦ F i) (ğ•œ := ğ•œ)
      Â· simp only [Subtype.forall]; exact fun a _ â†¦ h_convex a
      Â· intro J hJ_card
        rw [show â‹‚ i âˆˆ J, F â†‘i = â‹‚ i âˆˆ Subtype.val '' J, F i by
          simp only [Set.mem_image, Subtype.exists, exists_and_right, exists_eq_right,
          iInter_exists, iInter_coe_set]]
        have hJ_fin : J.Finite := by
          have : Finite â†‘I := hI_fin
          exact toFinite J
        apply h_inter J
        Â· exact Finite.image Subtype.val hJ_fin
        Â· exact le_trans (Set.ncard_image_le (hs := hJ_fin)) hJ_card
      Â· exact hI_fin
    /- The following is a clumsy proof that family of compact sets with the finite intersection
    property has a nonempty intersection -/
    have i0 : Î¹ := Nonempty.some h_inf.nonempty
    rw [show â‹‚ i, F i = (F i0) âˆ© (â‹‚ i, F i) by aesop]
    apply IsCompact.inter_iInter_nonempty
    Â· exact h_compact i0
    Â· intro i
      exact (h_compact i).isClosed
    Â· intro I
      simpa using (h1 ({i0} âˆª I))
