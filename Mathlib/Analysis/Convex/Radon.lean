/-
Copyright (c) 2023 Vasily Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasily Nesterov
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.LinearAlgebra.AffineSpace.Independent

/-!
# Radon's convexity theorem

Radon's theorem on convex sets states that any affine dependent set can be partitioned into two sets
whose convex hulls intersect.

## Main results

* `Radon_partition`: Radon convexity theorem

## Tags

convex hull, radon

-/


open Set Finset
open BigOperators

universe u

variable {ğ•œ : Type*} {E : Type u} [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- Any family `f` of affine dependent vectors contains a set `I` with the property that
convex hulls of `I` and `Iá¶œ` intersect. -/
theorem radon_partition {Î¹ : Type*} {f : Î¹ â†’ E} (h : Â¬AffineIndependent ğ•œ f) :
    âˆƒ (I : Set Î¹), (convexHull ğ•œ (f '' I) âˆ© convexHull ğ•œ (f '' Iá¶œ)).Nonempty := by
  rw [affineIndependent_iff] at h; push_neg at h
  rcases h with âŸ¨s1, w, h_wsum, h_vsum, nonzero_w_index, h1, h2âŸ©
  let I : Finset Î¹ := s1.filter (fun i â†¦ 0 â‰¤ w i)
  let J : Finset Î¹ := s1.filter (fun i â†¦ w i < 0)
  use I

  let w' : Î¹ â†’ ğ•œ := fun i â†¦ |w i|
  let p : E := centerMass I w' f -- point of intersection
  let p' : E := centerMass J w' f

  have h6 : âˆ€ v, 0 â‰¤ w' v := by
    intro; positivity

  let weights_sum_I := âˆ‘ i in I, w' i
  let weights_sum_J := âˆ‘ i in J, w' i

  have h3 : weights_sum_J = weights_sum_I := by
    simp; rw [@sum_congr _ _ J _ _ (fun i â†¦ -w i) _ rfl, @sum_congr _ _ I _ _ w _ rfl,
      sum_neg_distrib, neg_eq_iff_add_eq_zero]
    Â· simpa only [â† add_eq_zero_iff_eq_neg, not_le, â† h_wsum, add_comm]
      using sum_filter_add_sum_filter_not s1 (fun i â†¦ 0 â‰¤ w i) w
    all_goals intro i hi
    Â· rw [abs_eq_self.mpr]; exact (mem_filter.mp hi).2
    Â· rw [abs_eq_neg_self.mpr]; exact le_of_lt (mem_filter.mp hi).2

  have h_I_pos : 0 < weights_sum_I := by
    rcases exists_pos_of_sum_zero_of_exists_nonzero _ h_wsum âŸ¨nonzero_w_index, h1, h2âŸ©
      with âŸ¨pos_w_index, h1', h2'âŸ©
    exact sum_pos' (fun i _ => h6 i)
      âŸ¨pos_w_index, by simp only [mem_filter, h1', h2'.le], abs_pos_of_pos h2'âŸ©

  have h4 : p = p' := by
    apply eq_of_sub_eq_zero
    simp only [centerMass]; simp_rw [h3, â† smul_sub]
    apply smul_eq_zero.mpr; right
    rw [@sum_congr _ _ J _ _ (fun i â†¦ -(w i â€¢ f i)) _ rfl,
      @sum_congr _ _ I _ _ (fun i â†¦ w i â€¢ f i) _ rfl, sum_neg_distrib, sub_neg_eq_add]
    Â· simp_rw [â† h_vsum, â† sum_filter_add_sum_filter_not s1 (fun i â†¦ 0 â‰¤ w i) (fun i â†¦ w i â€¢ f i)]
      simp only [not_le]
    all_goals intro i hi
    Â· rw [abs_eq_self.mpr]; exact (mem_filter.mp hi).2
    Â· rw [abs_eq_neg_self.mpr, neg_smul]; exact le_of_lt (mem_filter.mp hi).2

  use p
  apply Set.mem_inter
  Â· apply Convex.centerMass_mem (convex_convexHull _ _) (fun i _ => h6 i) h_I_pos
    exact fun i hi â†¦ subset_convexHull _ _ (Set.mem_image_of_mem _ hi)
  Â· rw [h4]
    apply Convex.centerMass_mem (convex_convexHull _ _) (fun i _ => h6 i)
    simp_rw [h3]; exact h_I_pos
    intro i hi1
    apply subset_convexHull _ _ (Set.mem_image_of_mem _ _)
    intro hi2
    exact not_lt_of_le (mem_filter.mp hi2).2 (mem_filter.mp hi1).2

/-- If `s` is a set of affine dependent vectors, it has subset `I` with the property that
convex hulls of `I` and `s \ I` intersect. -/
theorem radon_set_partition (s : Set E)
    (h : Â¬AffineIndependent ğ•œ ((â†‘) : s â†’ E)) : âˆƒ (I : Set E), (I âŠ† s) âˆ§
    (Set.Nonempty ((convexHull ğ•œ I) âˆ© (convexHull ğ•œ (s \ I)))) := by
  obtain âŸ¨I, hIâŸ© := radon_partition h
  refine âŸ¨(â†‘) '' I, Subtype.coe_image_subset _ _, ?_âŸ©
  rwa [compl_eq_univ_diff, image_diff Subtype.val_injective, image_univ, Subtype.range_coe] at hI
