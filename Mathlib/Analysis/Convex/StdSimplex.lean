/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Topology.MetricSpace.ProperSpace.Real
import Mathlib.Topology.UnitInterval

/-!
# The standard simplex

In this file, given an ordered semiring `ğ•œ` and a finite type `Î¹`,
we define `stdSimplex : Set (Î¹ â†’ ğ•œ)` as the set of vectors with non-negative
coordinates with total sum `1`.

-/

open Set Convex Bornology

section OrderedSemiring

variable (ğ•œ) (Î¹ : Type*) [Semiring ğ•œ] [PartialOrder ğ•œ] [Fintype Î¹]

/-- The standard simplex in the space of functions `Î¹ â†’ ğ•œ` is the set of vectors with non-negative
coordinates with total sum `1`. This is the free object in the category of convex spaces. -/
def stdSimplex : Set (Î¹ â†’ ğ•œ) :=
  { f | (âˆ€ x, 0 â‰¤ f x) âˆ§ âˆ‘ x, f x = 1 }

theorem stdSimplex_eq_inter : stdSimplex ğ•œ Î¹ = (â‹‚ x, { f | 0 â‰¤ f x }) âˆ© { f | âˆ‘ x, f x = 1 } := by
  ext f
  simp only [stdSimplex, Set.mem_inter_iff, Set.mem_iInter, Set.mem_setOf_eq]

theorem convex_stdSimplex [IsOrderedRing ğ•œ] : Convex ğ•œ (stdSimplex ğ•œ Î¹) := by
  refine fun f hf g hg a b ha hb hab => âŸ¨fun x => ?_, ?_âŸ©
  Â· apply_rules [add_nonneg, mul_nonneg, hf.1, hg.1]
  Â· simp_rw [Pi.add_apply, Pi.smul_apply]
    rwa [Finset.sum_add_distrib, â† Finset.smul_sum, â† Finset.smul_sum, hf.2, hg.2, smul_eq_mul,
      smul_eq_mul, mul_one, mul_one]

@[nontriviality] lemma stdSimplex_of_subsingleton [Subsingleton ğ•œ] : stdSimplex ğ•œ Î¹ = univ :=
  eq_univ_of_forall fun _ â†¦ âŸ¨fun _ â†¦ (Subsingleton.elim _ _).le, Subsingleton.elim _ _âŸ©

/-- The standard simplex in the zero-dimensional space is empty. -/
lemma stdSimplex_of_isEmpty_index [IsEmpty Î¹] [Nontrivial ğ•œ] : stdSimplex ğ•œ Î¹ = âˆ… :=
  eq_empty_of_forall_notMem <| by rintro f âŸ¨-, hfâŸ©; simp at hf

lemma stdSimplex_unique [ZeroLEOneClass ğ•œ] [Nonempty Î¹] [Subsingleton Î¹] :
    stdSimplex ğ•œ Î¹ = {fun _ â†¦ 1} := by
  cases nonempty_unique Î¹
  refine eq_singleton_iff_unique_mem.2 âŸ¨âŸ¨fun _ â†¦ zero_le_one, Fintype.sum_unique _âŸ©, ?_âŸ©
  rintro f âŸ¨-, hfâŸ©
  rw [Fintype.sum_unique] at hf
  exact funext (Unique.forall_iff.2 hf)

variable {Î¹}

variable {ğ•œ} in
/-- All values of a function `f âˆˆ stdSimplex ğ•œ Î¹` belong to `[0, 1]`. -/
theorem mem_Icc_of_mem_stdSimplex [IsOrderedAddMonoid ğ•œ]
    {f : Î¹ â†’ ğ•œ} (hf : f âˆˆ stdSimplex ğ•œ Î¹) (x) :
    f x âˆˆ Icc (0 : ğ•œ) 1 :=
  âŸ¨hf.1 x, hf.2 â–¸ Finset.single_le_sum (fun y _ => hf.1 y) (Finset.mem_univ x)âŸ©

variable [DecidableEq Î¹] [ZeroLEOneClass ğ•œ]

theorem single_mem_stdSimplex (i : Î¹) : Pi.single i 1 âˆˆ stdSimplex ğ•œ Î¹ :=
  âŸ¨le_update_iff.2 âŸ¨zero_le_one, fun _ _ â†¦ le_rflâŸ©, by simpâŸ©

theorem ite_eq_mem_stdSimplex (i : Î¹) : (if i = Â· then (1 : ğ•œ) else 0) âˆˆ stdSimplex ğ•œ Î¹ := by
  simpa only [@eq_comm _ i, â† Pi.single_apply] using single_mem_stdSimplex ğ•œ i

variable [IsOrderedRing ğ•œ]

#adaptation_note /-- nightly-2024-03-11
we need a type annotation on the segment in the following two lemmas. -/

/-- The edges are contained in the simplex. -/
lemma segment_single_subset_stdSimplex (i j : Î¹) :
    ([Pi.single i 1 -[ğ•œ] Pi.single j 1] : Set (Î¹ â†’ ğ•œ)) âŠ† stdSimplex ğ•œ Î¹ :=
  (convex_stdSimplex ğ•œ Î¹).segment_subset (single_mem_stdSimplex _ _) (single_mem_stdSimplex _ _)

lemma stdSimplex_fin_two :
    stdSimplex ğ•œ (Fin 2) = ([Pi.single 0 1 -[ğ•œ] Pi.single 1 1] : Set (Fin 2 â†’ ğ•œ)) := by
  refine Subset.antisymm ?_ (segment_single_subset_stdSimplex ğ•œ (0 : Fin 2) 1)
  rintro f âŸ¨hfâ‚€, hfâ‚âŸ©
  rw [Fin.sum_univ_two] at hfâ‚
  refine âŸ¨f 0, f 1, hfâ‚€ 0, hfâ‚€ 1, hfâ‚, funext <| Fin.forall_fin_two.2 ?_âŸ©
  simp

end OrderedSemiring

section OrderedRing

variable (ğ•œ) [Ring ğ•œ] [PartialOrder ğ•œ] [IsOrderedRing ğ•œ]

/-- The standard one-dimensional simplex in `Fin 2 â†’ ğ•œ` is equivalent to the unit interval.
This bijection sends the zeroth vertex `Pi.single 0 1` to `0` and
the first vertex `Pi.single 1 1` to `1`. -/
@[simps -fullyApplied]
def stdSimplexEquivIcc : stdSimplex ğ•œ (Fin 2) â‰ƒ Icc (0 : ğ•œ) 1 where
  toFun f := âŸ¨f.1 1, f.2.1 _, f.2.2 â–¸
    Finset.single_le_sum (fun i _ â†¦ f.2.1 i) (Finset.mem_univ _)âŸ©
  invFun x := âŸ¨![1 - x, x], Fin.forall_fin_two.2 âŸ¨sub_nonneg.2 x.2.2, x.2.1âŸ©, by simpâŸ©
  left_inv f := Subtype.eq <| funext <| Fin.forall_fin_two.2 <| by
    simp [â† (show f.1 0 + f.1 1 = 1 by simpa using f.2.2)]

@[simp]
lemma stdSimplexEquivIcc_zero :
    stdSimplexEquivIcc ğ•œ âŸ¨_, single_mem_stdSimplex ğ•œ 0âŸ© = 0 := rfl

@[simp]
lemma stdSimplexEquivIcc_one :
    stdSimplexEquivIcc ğ•œ âŸ¨_, single_mem_stdSimplex ğ•œ 1âŸ© = 1 := rfl

end OrderedRing

section Field

variable (R : Type*) (Î¹ : Type*) [Field R] [LinearOrder R] [IsStrictOrderedRing R] [Fintype Î¹]

/-- `stdSimplex ğ•œ Î¹` is the convex hull of the canonical basis in `Î¹ â†’ ğ•œ`. -/
theorem convexHull_basis_eq_stdSimplex [DecidableEq Î¹] :
    convexHull R (range fun i j : Î¹ => if i = j then (1 : R) else 0) = stdSimplex R Î¹ := by
  refine Subset.antisymm (convexHull_min ?_ (convex_stdSimplex R Î¹)) ?_
  Â· rintro _ âŸ¨i, rflâŸ©
    exact ite_eq_mem_stdSimplex R i
  Â· rintro w âŸ¨hwâ‚€, hwâ‚âŸ©
    rw [pi_eq_sum_univ w]
    rw [â† Finset.univ.centerMass_eq_of_sum_1 _ hwâ‚]
    exact Finset.univ.centerMass_mem_convexHull (fun i _ => hwâ‚€ i) (hwâ‚.symm â–¸ zero_lt_one)
      fun i _ => mem_range_self i

/-- `stdSimplex ğ•œ Î¹` is the convex hull of the points `Pi.single i 1` for `i : `i`. -/
theorem convexHull_rangle_single_eq_stdSimplex [DecidableEq Î¹] :
    convexHull R (range fun i : Î¹ â†¦ Pi.single i 1) = stdSimplex R Î¹ := by
  convert convexHull_basis_eq_stdSimplex R Î¹
  aesop

variable {Î¹ R}

/-- The convex hull of a finite set is the image of the standard simplex in `s â†’ â„`
under the linear map sending each function `w` to `âˆ‘ x âˆˆ s, w x â€¢ x`.

Since we have no sums over finite sets, we use sum over `@Finset.univ _ hs.fintype`.
The map is defined in terms of operations on `(s â†’ â„) â†’â‚—[â„] â„` so that later we will not need
to prove that this map is linear. -/
theorem Set.Finite.convexHull_eq_image {E : Type*} [AddCommGroup E] [Module R E]
    {s : Set E} (hs : s.Finite) : convexHull R s =
    haveI := hs.fintype
    (â‡‘(âˆ‘ x : s, (LinearMap.proj (R := R) x).smulRight x.1)) '' stdSimplex R s := by
  classical
  letI := hs.fintype
  rw [â† convexHull_basis_eq_stdSimplex, LinearMap.image_convexHull, â† Set.range_comp]
  apply congr_arg
  aesop

end Field

section Topology

variable {Î¹ : Type*} [Fintype Î¹]

/-- Every vector in `stdSimplex ğ•œ Î¹` has `max`-norm at most `1`. -/
theorem stdSimplex_subset_closedBall : stdSimplex â„ Î¹ âŠ† Metric.closedBall 0 1 := fun f hf â†¦ by
  rw [Metric.mem_closedBall, dist_pi_le_iff zero_le_one]
  intro x
  rw [Pi.zero_apply, Real.dist_0_eq_abs, abs_of_nonneg <| hf.1 x]
  exact (mem_Icc_of_mem_stdSimplex hf x).2

variable (Î¹)

/-- `stdSimplex â„ Î¹` is bounded. -/
theorem bounded_stdSimplex : IsBounded (stdSimplex â„ Î¹) :=
  (Metric.isBounded_iff_subset_closedBall 0).2 âŸ¨1, stdSimplex_subset_closedBallâŸ©

/-- `stdSimplex â„ Î¹` is closed. -/
theorem isClosed_stdSimplex : IsClosed (stdSimplex â„ Î¹) :=
  (stdSimplex_eq_inter â„ Î¹).symm â–¸
    IsClosed.inter (isClosed_iInter fun i => isClosed_le continuous_const (continuous_apply i))
      (isClosed_eq (continuous_finset_sum _ fun x _ => continuous_apply x) continuous_const)

/-- `stdSimplex â„ Î¹` is compact. -/
theorem isCompact_stdSimplex : IsCompact (stdSimplex â„ Î¹) :=
  Metric.isCompact_iff_isClosed_bounded.2 âŸ¨isClosed_stdSimplex Î¹, bounded_stdSimplex Î¹âŸ©

instance stdSimplex.instCompactSpace_coe : CompactSpace â†¥(stdSimplex â„ Î¹) :=
  isCompact_iff_compactSpace.mp <| isCompact_stdSimplex _

/-- The standard one-dimensional simplex in `â„Â² = Fin 2 â†’ â„`
is homeomorphic to the unit interval. -/
@[simps! -fullyApplied]
def stdSimplexHomeomorphUnitInterval : stdSimplex â„ (Fin 2) â‰ƒâ‚œ unitInterval where
  toEquiv := stdSimplexEquivIcc â„
  continuous_toFun := .subtype_mk ((continuous_apply 1).comp continuous_subtype_val) _
  continuous_invFun := by
    apply Continuous.subtype_mk
    exact (continuous_pi <| Fin.forall_fin_two.2
      âŸ¨continuous_const.sub continuous_subtype_val, continuous_subtype_valâŸ©)

@[simp]
lemma stdSimplexHomeomorphUnitInterval_zero :
    stdSimplexHomeomorphUnitInterval âŸ¨_, single_mem_stdSimplex _ 0âŸ© = 0 := rfl

@[simp]
lemma stdSimplexHomeomorphUnitInterval_one :
    stdSimplexHomeomorphUnitInterval âŸ¨_, single_mem_stdSimplex _ 1âŸ© = 1 := rfl

end Topology
