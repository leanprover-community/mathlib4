/-
Copyright (c) 2023 Paul Reichert. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Paul Reichert, YaÃ«l Dillies
-/
import Mathlib.Analysis.NormedSpace.AddTorsorBases

#align_import analysis.convex.intrinsic from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Intrinsic frontier and interior

This file defines the intrinsic frontier, interior and closure of a set in a normed additive torsor.
These are also known as relative frontier, interior, closure.

The intrinsic frontier/interior/closure of a set `s` is the frontier/interior/closure of `s`
considered as a set in its affine span.

The intrinsic interior is in general greater than the topological interior, the intrinsic frontier
in general less than the topological frontier, and the intrinsic closure in cases of interest the
same as the topological closure.

## Definitions

* `intrinsicInterior`: Intrinsic interior
* `intrinsicFrontier`: Intrinsic frontier
* `intrinsicClosure`: Intrinsic closure

## Results

The main results are:
* `AffineIsometry.image_intrinsicInterior`/`AffineIsometry.image_intrinsicFrontier`/
  `AffineIsometry.image_intrinsicClosure`: Intrinsic interiors/frontiers/closures commute with
  taking the image under an affine isometry.
* `Set.Nonempty.intrinsicInterior`: The intrinsic interior of a nonempty convex set is nonempty.

## References

* Chapter 8 of [Barry Simon, *Convexity*][simon2011]
* Chapter 1 of [Rolf Schneider, *Convex Bodies: The Brunn-Minkowski theory*][schneider2013].

## TODO

* `IsClosed s â†’ IsExtreme ğ•œ s (intrinsicFrontier ğ•œ s)`
* `x âˆˆ s â†’ y âˆˆ intrinsicInterior ğ•œ s â†’ openSegment ğ•œ x y âŠ† intrinsicInterior ğ•œ s`
-/


open AffineSubspace Set

open scoped Pointwise

variable {ğ•œ V W Q P : Type*}

section AddTorsor

variable (ğ•œ) [Ring ğ•œ] [AddCommGroup V] [Module ğ•œ V] [TopologicalSpace P] [AddTorsor V P]
  {s t : Set P} {x : P}

/-- The intrinsic interior of a set is its interior considered as a set in its affine span. -/
def intrinsicInterior (s : Set P) : Set P :=
  (â†‘) '' interior ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s)
#align intrinsic_interior intrinsicInterior

/-- The intrinsic frontier of a set is its frontier considered as a set in its affine span. -/
def intrinsicFrontier (s : Set P) : Set P :=
  (â†‘) '' frontier ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s)
#align intrinsic_frontier intrinsicFrontier

/-- The intrinsic closure of a set is its closure considered as a set in its affine span. -/
def intrinsicClosure (s : Set P) : Set P :=
  (â†‘) '' closure ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s)
#align intrinsic_closure intrinsicClosure

variable {ğ•œ}

@[simp]
theorem mem_intrinsicInterior :
    x âˆˆ intrinsicInterior ğ•œ s â†” âˆƒ y, y âˆˆ interior ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s) âˆ§ â†‘y = x :=
  mem_image _ _ _
#align mem_intrinsic_interior mem_intrinsicInterior

@[simp]
theorem mem_intrinsicFrontier :
    x âˆˆ intrinsicFrontier ğ•œ s â†” âˆƒ y, y âˆˆ frontier ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s) âˆ§ â†‘y = x :=
  mem_image _ _ _
#align mem_intrinsic_frontier mem_intrinsicFrontier

@[simp]
theorem mem_intrinsicClosure :
    x âˆˆ intrinsicClosure ğ•œ s â†” âˆƒ y, y âˆˆ closure ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s) âˆ§ â†‘y = x :=
  mem_image _ _ _
#align mem_intrinsic_closure mem_intrinsicClosure

theorem intrinsicInterior_subset : intrinsicInterior ğ•œ s âŠ† s :=
  image_subset_iff.2 interior_subset
#align intrinsic_interior_subset intrinsicInterior_subset

theorem intrinsicFrontier_subset (hs : IsClosed s) : intrinsicFrontier ğ•œ s âŠ† s :=
  image_subset_iff.2 (hs.preimage continuous_induced_dom).frontier_subset
#align intrinsic_frontier_subset intrinsicFrontier_subset

theorem intrinsicFrontier_subset_intrinsicClosure : intrinsicFrontier ğ•œ s âŠ† intrinsicClosure ğ•œ s :=
  image_subset _ frontier_subset_closure
#align intrinsic_frontier_subset_intrinsic_closure intrinsicFrontier_subset_intrinsicClosure

theorem subset_intrinsicClosure : s âŠ† intrinsicClosure ğ•œ s :=
  fun x hx => âŸ¨âŸ¨x, subset_affineSpan _ _ hxâŸ©, subset_closure hx, rflâŸ©
#align subset_intrinsic_closure subset_intrinsicClosure

@[simp]
theorem intrinsicInterior_empty : intrinsicInterior ğ•œ (âˆ… : Set P) = âˆ… := by simp [intrinsicInterior]
#align intrinsic_interior_empty intrinsicInterior_empty

@[simp]
theorem intrinsicFrontier_empty : intrinsicFrontier ğ•œ (âˆ… : Set P) = âˆ… := by simp [intrinsicFrontier]
#align intrinsic_frontier_empty intrinsicFrontier_empty

@[simp]
theorem intrinsicClosure_empty : intrinsicClosure ğ•œ (âˆ… : Set P) = âˆ… := by simp [intrinsicClosure]
#align intrinsic_closure_empty intrinsicClosure_empty

@[simp]
theorem intrinsicClosure_nonempty : (intrinsicClosure ğ•œ s).Nonempty â†” s.Nonempty :=
  âŸ¨by simp_rw [nonempty_iff_ne_empty]; rintro h rfl; exact h intrinsicClosure_empty,
    Nonempty.mono subset_intrinsicClosureâŸ©
#align intrinsic_closure_nonempty intrinsicClosure_nonempty

alias âŸ¨Set.Nonempty.ofIntrinsicClosure, Set.Nonempty.intrinsicClosureâŸ© := intrinsicClosure_nonempty
#align set.nonempty.of_intrinsic_closure Set.Nonempty.ofIntrinsicClosure
#align set.nonempty.intrinsic_closure Set.Nonempty.intrinsicClosure

--attribute [protected] Set.Nonempty.intrinsicClosure -- Porting note: removed

@[simp]
theorem intrinsicInterior_singleton (x : P) : intrinsicInterior ğ•œ ({x} : Set P) = {x} := by
  simpa only [intrinsicInterior, preimage_coe_affineSpan_singleton, interior_univ, image_univ,
    Subtype.range_coe] using coe_affineSpan_singleton _ _ _
#align intrinsic_interior_singleton intrinsicInterior_singleton

@[simp]
theorem intrinsicFrontier_singleton (x : P) : intrinsicFrontier ğ•œ ({x} : Set P) = âˆ… := by
  rw [intrinsicFrontier, preimage_coe_affineSpan_singleton, frontier_univ, image_empty]
#align intrinsic_frontier_singleton intrinsicFrontier_singleton

@[simp]
theorem intrinsicClosure_singleton (x : P) : intrinsicClosure ğ•œ ({x} : Set P) = {x} := by
  simpa only [intrinsicClosure, preimage_coe_affineSpan_singleton, closure_univ, image_univ,
    Subtype.range_coe] using coe_affineSpan_singleton _ _ _
#align intrinsic_closure_singleton intrinsicClosure_singleton

/-!
Note that neither `intrinsicInterior` nor `intrinsicFrontier` is monotone.
-/


theorem intrinsicClosure_mono (h : s âŠ† t) : intrinsicClosure ğ•œ s âŠ† intrinsicClosure ğ•œ t := by
  refine image_subset_iff.2 fun x hx => ?_
  refine âŸ¨Set.inclusion (affineSpan_mono _ h) x, ?_, rflâŸ©
  refine (continuous_inclusion (affineSpan_mono _ h)).closure_preimage_subset _ (closure_mono ?_ hx)
  exact fun y hy => h hy
#align intrinsic_closure_mono intrinsicClosure_mono

theorem interior_subset_intrinsicInterior : interior s âŠ† intrinsicInterior ğ•œ s :=
  fun x hx => âŸ¨âŸ¨x, subset_affineSpan _ _ <| interior_subset hxâŸ©,
    preimage_interior_subset_interior_preimage continuous_subtype_val hx, rflâŸ©
#align interior_subset_intrinsic_interior interior_subset_intrinsicInterior

theorem intrinsicClosure_subset_closure : intrinsicClosure ğ•œ s âŠ† closure s :=
  image_subset_iff.2 <| continuous_subtype_val.closure_preimage_subset _
#align intrinsic_closure_subset_closure intrinsicClosure_subset_closure

theorem intrinsicFrontier_subset_frontier : intrinsicFrontier ğ•œ s âŠ† frontier s :=
  image_subset_iff.2 <| continuous_subtype_val.frontier_preimage_subset _
#align intrinsic_frontier_subset_frontier intrinsicFrontier_subset_frontier

theorem intrinsicClosure_subset_affineSpan : intrinsicClosure ğ•œ s âŠ† affineSpan ğ•œ s :=
  (image_subset_range _ _).trans Subtype.range_coe.subset
#align intrinsic_closure_subset_affine_span intrinsicClosure_subset_affineSpan

@[simp]
theorem intrinsicClosure_diff_intrinsicFrontier (s : Set P) :
    intrinsicClosure ğ•œ s \ intrinsicFrontier ğ•œ s = intrinsicInterior ğ•œ s :=
  (image_diff Subtype.coe_injective _ _).symm.trans <| by
    rw [closure_diff_frontier, intrinsicInterior]
#align intrinsic_closure_diff_intrinsic_frontier intrinsicClosure_diff_intrinsicFrontier

@[simp]
theorem intrinsicClosure_diff_intrinsicInterior (s : Set P) :
    intrinsicClosure ğ•œ s \ intrinsicInterior ğ•œ s = intrinsicFrontier ğ•œ s :=
  (image_diff Subtype.coe_injective _ _).symm
#align intrinsic_closure_diff_intrinsic_interior intrinsicClosure_diff_intrinsicInterior

@[simp]
theorem intrinsicInterior_union_intrinsicFrontier (s : Set P) :
    intrinsicInterior ğ•œ s âˆª intrinsicFrontier ğ•œ s = intrinsicClosure ğ•œ s := by
  simp [intrinsicClosure, intrinsicInterior, intrinsicFrontier, closure_eq_interior_union_frontier,
    image_union]
#align intrinsic_interior_union_intrinsic_frontier intrinsicInterior_union_intrinsicFrontier

@[simp]
theorem intrinsicFrontier_union_intrinsicInterior (s : Set P) :
    intrinsicFrontier ğ•œ s âˆª intrinsicInterior ğ•œ s = intrinsicClosure ğ•œ s := by
  rw [union_comm, intrinsicInterior_union_intrinsicFrontier]
#align intrinsic_frontier_union_intrinsic_interior intrinsicFrontier_union_intrinsicInterior

theorem isClosed_intrinsicClosure (hs : IsClosed (affineSpan ğ•œ s : Set P)) :
    IsClosed (intrinsicClosure ğ•œ s) :=
  (closedEmbedding_subtype_val hs).isClosedMap _ isClosed_closure
#align is_closed_intrinsic_closure isClosed_intrinsicClosure

theorem isClosed_intrinsicFrontier (hs : IsClosed (affineSpan ğ•œ s : Set P)) :
    IsClosed (intrinsicFrontier ğ•œ s) :=
  (closedEmbedding_subtype_val hs).isClosedMap _ isClosed_frontier
#align is_closed_intrinsic_frontier isClosed_intrinsicFrontier

@[simp]
theorem affineSpan_intrinsicClosure (s : Set P) :
    affineSpan ğ•œ (intrinsicClosure ğ•œ s) = affineSpan ğ•œ s :=
  (affineSpan_le.2 intrinsicClosure_subset_affineSpan).antisymm <|
    affineSpan_mono _ subset_intrinsicClosure
#align affine_span_intrinsic_closure affineSpan_intrinsicClosure

protected theorem IsClosed.intrinsicClosure (hs : IsClosed ((â†‘) â»Â¹' s : Set <| affineSpan ğ•œ s)) :
    intrinsicClosure ğ•œ s = s := by
  rw [intrinsicClosure, hs.closure_eq, image_preimage_eq_of_subset]
  exact (subset_affineSpan _ _).trans Subtype.range_coe.superset
#align is_closed.intrinsic_closure IsClosed.intrinsicClosure

@[simp]
theorem intrinsicClosure_idem (s : Set P) :
    intrinsicClosure ğ•œ (intrinsicClosure ğ•œ s) = intrinsicClosure ğ•œ s := by
  refine IsClosed.intrinsicClosure ?_
  set t := affineSpan ğ•œ (intrinsicClosure ğ•œ s) with ht
  clear_value t
  obtain rfl := ht.trans (affineSpan_intrinsicClosure _)
  rw [intrinsicClosure, preimage_image_eq _ Subtype.coe_injective]
  exact isClosed_closure
#align intrinsic_closure_idem intrinsicClosure_idem

end AddTorsor

namespace AffineIsometry

variable [NormedField ğ•œ] [SeminormedAddCommGroup V] [SeminormedAddCommGroup W] [NormedSpace ğ•œ V]
  [NormedSpace ğ•œ W] [MetricSpace P] [PseudoMetricSpace Q] [NormedAddTorsor V P]
  [NormedAddTorsor W Q]

-- Porting note: Removed attribute `local nolint fails_quickly`
attribute [local instance] AffineSubspace.toNormedAddTorsor AffineSubspace.nonempty_map

@[simp]
theorem image_intrinsicInterior (Ï† : P â†’áµƒâ±[ğ•œ] Q) (s : Set P) :
    intrinsicInterior ğ•œ (Ï† '' s) = Ï† '' intrinsicInterior ğ•œ s := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· simp only [intrinsicInterior_empty, image_empty]
  haveI : Nonempty s := hs.to_subtype
  let f := ((affineSpan ğ•œ s).isometryEquivMap Ï†).toHomeomorph
  have : Ï†.toAffineMap âˆ˜ (â†‘) âˆ˜ f.symm = (â†‘) := funext isometryEquivMap.apply_symm_apply
  rw [intrinsicInterior, intrinsicInterior, â† Ï†.coe_toAffineMap, â† map_span Ï†.toAffineMap s, â† this,
    â† Function.comp.assoc, image_comp, image_comp, f.symm.image_interior, f.image_symm,
    â† preimage_comp, Function.comp.assoc, f.symm_comp_self, AffineIsometry.coe_toAffineMap,
    Function.comp_id, preimage_comp, Ï†.injective.preimage_image]
#align affine_isometry.image_intrinsic_interior AffineIsometry.image_intrinsicInterior

@[simp]
theorem image_intrinsicFrontier (Ï† : P â†’áµƒâ±[ğ•œ] Q) (s : Set P) :
    intrinsicFrontier ğ•œ (Ï† '' s) = Ï† '' intrinsicFrontier ğ•œ s := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· simp
  haveI : Nonempty s := hs.to_subtype
  let f := ((affineSpan ğ•œ s).isometryEquivMap Ï†).toHomeomorph
  have : Ï†.toAffineMap âˆ˜ (â†‘) âˆ˜ f.symm = (â†‘) := funext isometryEquivMap.apply_symm_apply
  rw [intrinsicFrontier, intrinsicFrontier, â† Ï†.coe_toAffineMap, â† map_span Ï†.toAffineMap s, â† this,
    â† Function.comp.assoc, image_comp, image_comp, f.symm.image_frontier, f.image_symm,
    â† preimage_comp, Function.comp.assoc, f.symm_comp_self, AffineIsometry.coe_toAffineMap,
    Function.comp_id, preimage_comp, Ï†.injective.preimage_image]
#align affine_isometry.image_intrinsic_frontier AffineIsometry.image_intrinsicFrontier

@[simp]
theorem image_intrinsicClosure (Ï† : P â†’áµƒâ±[ğ•œ] Q) (s : Set P) :
    intrinsicClosure ğ•œ (Ï† '' s) = Ï† '' intrinsicClosure ğ•œ s := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· simp
  haveI : Nonempty s := hs.to_subtype
  let f := ((affineSpan ğ•œ s).isometryEquivMap Ï†).toHomeomorph
  have : Ï†.toAffineMap âˆ˜ (â†‘) âˆ˜ f.symm = (â†‘) := funext isometryEquivMap.apply_symm_apply
  rw [intrinsicClosure, intrinsicClosure, â† Ï†.coe_toAffineMap, â† map_span Ï†.toAffineMap s, â† this,
    â† Function.comp.assoc, image_comp, image_comp, f.symm.image_closure, f.image_symm,
    â† preimage_comp, Function.comp.assoc, f.symm_comp_self, AffineIsometry.coe_toAffineMap,
    Function.comp_id, preimage_comp, Ï†.injective.preimage_image]
#align affine_isometry.image_intrinsic_closure AffineIsometry.image_intrinsicClosure

end AffineIsometry

section NormedAddTorsor

variable (ğ•œ) [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ] [NormedAddCommGroup V] [NormedSpace ğ•œ V]
  [FiniteDimensional ğ•œ V] [MetricSpace P] [NormedAddTorsor V P] (s : Set P)

@[simp]
theorem intrinsicClosure_eq_closure : intrinsicClosure ğ•œ s = closure s := by
  ext x
  simp only [mem_closure_iff, mem_intrinsicClosure]
  refine âŸ¨?_, fun h => âŸ¨âŸ¨x, _âŸ©, ?_, Subtype.coe_mk _ ?_âŸ©âŸ©
  Â· rintro âŸ¨x, h, rflâŸ© t ht hx
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h _ (continuous_induced_dom.isOpen_preimage t ht) hx
    exact âŸ¨z, hzâ‚, hzâ‚‚âŸ©
  Â· rintro _ âŸ¨t, ht, rflâŸ© hx
    obtain âŸ¨y, hyt, hysâŸ© := h _ ht hx
    exact âŸ¨âŸ¨_, subset_affineSpan ğ•œ s hysâŸ©, hyt, hysâŸ©
  Â· by_contra hc
    obtain âŸ¨z, hzâ‚, hzâ‚‚âŸ© := h _ (affineSpan ğ•œ s).closed_of_finiteDimensional.isOpen_compl hc
    exact hzâ‚ (subset_affineSpan ğ•œ s hzâ‚‚)
#align intrinsic_closure_eq_closure intrinsicClosure_eq_closure

variable {ğ•œ}

@[simp]
theorem closure_diff_intrinsicInterior (s : Set P) :
    closure s \ intrinsicInterior ğ•œ s = intrinsicFrontier ğ•œ s :=
  intrinsicClosure_eq_closure ğ•œ s â–¸ intrinsicClosure_diff_intrinsicInterior s
#align closure_diff_intrinsic_interior closure_diff_intrinsicInterior

@[simp]
theorem closure_diff_intrinsicFrontier (s : Set P) :
    closure s \ intrinsicFrontier ğ•œ s = intrinsicInterior ğ•œ s :=
  intrinsicClosure_eq_closure ğ•œ s â–¸ intrinsicClosure_diff_intrinsicFrontier s
#align closure_diff_intrinsic_frontier closure_diff_intrinsicFrontier

end NormedAddTorsor

private theorem aux {Î± Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²] (Ï† : Î± â‰ƒâ‚œ Î²)
    (s : Set Î²) : (interior s).Nonempty â†” (interior (Ï† â»Â¹' s)).Nonempty := by
  rw [â† Ï†.image_symm, â† Ï†.symm.image_interior, image_nonempty]

variable [NormedAddCommGroup V] [NormedSpace â„ V] [FiniteDimensional â„ V] {s : Set V}

/-- The intrinsic interior of a nonempty convex set is nonempty. -/
protected theorem Set.Nonempty.intrinsicInterior (hscv : Convex â„ s) (hsne : s.Nonempty) :
    (intrinsicInterior â„ s).Nonempty := by
  haveI := hsne.coe_sort
  obtain âŸ¨p, hpâŸ© := hsne
  let p' : _root_.affineSpan â„ s := âŸ¨p, subset_affineSpan _ _ hpâŸ©
  rw [intrinsicInterior, image_nonempty,
    aux (AffineIsometryEquiv.constVSub â„ p').symm.toHomeomorph,
    Convex.interior_nonempty_iff_affineSpan_eq_top, AffineIsometryEquiv.coe_toHomeomorph, â†
    AffineIsometryEquiv.coe_toAffineEquiv, â† comap_span, affineSpan_coe_preimage_eq_top, comap_top]
  exact hscv.affine_preimage
    ((_root_.affineSpan â„ s).subtype.comp
      (AffineIsometryEquiv.constVSub â„ p').symm.toAffineEquiv.toAffineMap)
#align set.nonempty.intrinsic_interior Set.Nonempty.intrinsicInterior

theorem intrinsicInterior_nonempty (hs : Convex â„ s) :
    (intrinsicInterior â„ s).Nonempty â†” s.Nonempty :=
  âŸ¨by simp_rw [nonempty_iff_ne_empty]; rintro h rfl; exact h intrinsicInterior_empty,
    Set.Nonempty.intrinsicInterior hsâŸ©
#align intrinsic_interior_nonempty intrinsicInterior_nonempty
