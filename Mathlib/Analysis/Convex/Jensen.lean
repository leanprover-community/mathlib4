/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudriashov
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Analysis.Convex.Function

#align_import analysis.convex.jensen from "leanprover-community/mathlib"@"bfad3f455b388fbcc14c49d0cac884f774f14d20"

/-!
# Jensen's inequality and maximum principle for convex functions

In this file, we prove the finite Jensen inequality and the finite maximum principle for convex
functions. The integral versions are to be found in `Analysis.Convex.Integral`.

## Main declarations

Jensen's inequalities:
* `ConvexOn.map_centerMass_le`, `ConvexOn.map_sum_le`: Convex Jensen's inequality. The image of a
  convex combination of points under a convex function is less than the convex combination of the
  images.
* `ConcaveOn.le_map_centerMass`, `ConcaveOn.le_map_sum`: Concave Jensen's inequality.

As corollaries, we get:
* `ConvexOn.exists_ge_of_mem_convexHull`: Maximum principle for convex functions.
* `ConcaveOn.exists_le_of_mem_convexHull`: Minimum principle for concave functions.
-/


open Finset LinearMap Set

open BigOperators Classical Convex Pointwise

variable {ğ•œ E F Î² Î¹ : Type*}

/-! ### Jensen's inequality -/


section Jensen

variable [LinearOrderedField ğ•œ] [AddCommGroup E] [OrderedAddCommGroup Î²] [Module ğ•œ E] [Module ğ•œ Î²]
  [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²} {t : Finset Î¹} {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E}

/-- Convex **Jensen's inequality**, `Finset.centerMass` version. -/
theorem ConvexOn.map_centerMass_le (hf : ConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : 0 < âˆ‘ i in t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    f (t.centerMass w p) â‰¤ t.centerMass w (f âˆ˜ p) := by
  have hmem' : âˆ€ i âˆˆ t, (p i, (f âˆ˜ p) i) âˆˆ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2 } := fun i hi =>
    âŸ¨hmem i hi, le_rflâŸ©
  convert (hf.convex_epigraph.centerMass_mem hâ‚€ hâ‚ hmem').2 <;>
    simp only [centerMass, Function.comp, Prod.smul_fst, Prod.fst_sum, Prod.smul_snd, Prod.snd_sum]
#align convex_on.map_center_mass_le ConvexOn.map_centerMass_le

/-- Concave **Jensen's inequality**, `Finset.centerMass` version. -/
theorem ConcaveOn.le_map_centerMass (hf : ConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : 0 < âˆ‘ i in t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    t.centerMass w (f âˆ˜ p) â‰¤ f (t.centerMass w p) :=
  ConvexOn.map_centerMass_le (Î² := Î²áµ’áµˆ) hf hâ‚€ hâ‚ hmem
#align concave_on.le_map_center_mass ConcaveOn.le_map_centerMass

/-- Convex **Jensen's inequality**, `Finset.sum` version. -/
theorem ConvexOn.map_sum_le (hf : ConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i in t, w i = 1)
    (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) : f (âˆ‘ i in t, w i â€¢ p i) â‰¤ âˆ‘ i in t, w i â€¢ f (p i) := by
  simpa only [centerMass, hâ‚, inv_one, one_smul] using
    hf.map_centerMass_le hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hmem
#align convex_on.map_sum_le ConvexOn.map_sum_le

/-- Concave **Jensen's inequality**, `Finset.sum` version. -/
theorem ConcaveOn.le_map_sum (hf : ConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : âˆ‘ i in t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    (âˆ‘ i in t, w i â€¢ f (p i)) â‰¤ f (âˆ‘ i in t, w i â€¢ p i) :=
  ConvexOn.map_sum_le (Î² := Î²áµ’áµˆ) hf hâ‚€ hâ‚ hmem
#align concave_on.le_map_sum ConcaveOn.le_map_sum

end Jensen

/-! ### Maximum principle -/


section MaximumPrinciple

variable [LinearOrderedField ğ•œ] [AddCommGroup E] [LinearOrderedAddCommGroup Î²] [Module ğ•œ E]
  [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²} {t : Finset Î¹} {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E}
  {x : E}

theorem le_sup_of_mem_convexHull {s : Finset E} (hf : ConvexOn ğ•œ (convexHull ğ•œ (s : Set E)) f)
    (hx : x âˆˆ convexHull ğ•œ (s : Set E)) :
    f x â‰¤ s.sup' (coe_nonempty.1 <| convexHull_nonempty_iff.1 âŸ¨x, hxâŸ©) f := by
  obtain âŸ¨w, hwâ‚€, hwâ‚, rflâŸ© := mem_convexHull.1 hx
  exact (hf.map_centerMass_le hwâ‚€ (by positivity) <| subset_convexHull _ _).trans
    (centerMass_le_sup hwâ‚€ <| by positivity)
#align le_sup_of_mem_convex_hull le_sup_of_mem_convexHull

theorem inf_le_of_mem_convexHull {s : Finset E} (hf : ConcaveOn ğ•œ (convexHull ğ•œ (s : Set E)) f)
    (hx : x âˆˆ convexHull ğ•œ (s : Set E)) :
    s.inf' (coe_nonempty.1 <| convexHull_nonempty_iff.1 âŸ¨x, hxâŸ©) f â‰¤ f x :=
  le_sup_of_mem_convexHull hf.dual hx
#align inf_le_of_mem_convex_hull inf_le_of_mem_convexHull

/-- If a function `f` is convex on `s`, then the value it takes at some center of mass of points of
`s` is less than the value it takes on one of those points. -/
theorem ConvexOn.exists_ge_of_centerMass (h : ConvexOn ğ•œ s f) (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hwâ‚ : 0 < âˆ‘ i in t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
    âˆƒ i âˆˆ t, f (t.centerMass w p) â‰¤ f (p i) := by
  set y := t.centerMass w p
  obtain âŸ¨i, hi, hfiâŸ© : âˆƒ i âˆˆ t.filter fun i => w i â‰  0, w i â€¢ f y â‰¤ w i â€¢ (f âˆ˜ p) i
  rotate_left
  Â· rw [mem_filter] at hi
    exact âŸ¨i, hi.1, (smul_le_smul_iff_of_pos <| (hwâ‚€ i hi.1).lt_of_ne hi.2.symm).1 hfiâŸ©
  have hw' : (0 : ğ•œ) < âˆ‘ i in filter (fun i => w i â‰  0) t, w i := by rwa [sum_filter_ne_zero]
  refine' exists_le_of_sum_le (nonempty_of_sum_ne_zero hw'.ne') _
  rw [â† sum_smul, â† smul_le_smul_iff_of_pos (inv_pos.2 hw'), inv_smul_smulâ‚€ hw'.ne', â†
    Finset.centerMass, Finset.centerMass_filter_ne_zero]
  exact h.map_centerMass_le hwâ‚€ hwâ‚ hp
#align convex_on.exists_ge_of_center_mass ConvexOn.exists_ge_of_centerMass

/-- If a function `f` is concave on `s`, then the value it takes at some center of mass of points of
`s` is greater than the value it takes on one of those points. -/
theorem ConcaveOn.exists_le_of_centerMass (h : ConcaveOn ğ•œ s f) (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hwâ‚ : 0 < âˆ‘ i in t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) : âˆƒ i âˆˆ t, f (p i) â‰¤ f (t.centerMass w p) :=
  ConvexOn.exists_ge_of_centerMass (Î² := Î²áµ’áµˆ) h hwâ‚€ hwâ‚ hp
#align concave_on.exists_le_of_center_mass ConcaveOn.exists_le_of_centerMass

/-- Maximum principle for convex functions. If a function `f` is convex on the convex hull of `s`,
then the eventual maximum of `f` on `convexHull ğ•œ s` lies in `s`. -/
theorem ConvexOn.exists_ge_of_mem_convexHull (hf : ConvexOn ğ•œ (convexHull ğ•œ s) f) {x}
    (hx : x âˆˆ convexHull ğ•œ s) : âˆƒ y âˆˆ s, f x â‰¤ f y := by
  rw [_root_.convexHull_eq] at hx
  obtain âŸ¨Î±, t, w, p, hwâ‚€, hwâ‚, hp, rflâŸ© := hx
  rcases hf.exists_ge_of_centerMass hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one) fun i hi =>
      subset_convexHull ğ•œ s (hp i hi) with
    âŸ¨i, hit, HiâŸ©
  exact âŸ¨p i, hp i hit, HiâŸ©
#align convex_on.exists_ge_of_mem_convex_hull ConvexOn.exists_ge_of_mem_convexHull

/-- Minimum principle for concave functions. If a function `f` is concave on the convex hull of `s`,
then the eventual minimum of `f` on `convexHull ğ•œ s` lies in `s`. -/
theorem ConcaveOn.exists_le_of_mem_convexHull (hf : ConcaveOn ğ•œ (convexHull ğ•œ s) f) {x}
    (hx : x âˆˆ convexHull ğ•œ s) : âˆƒ y âˆˆ s, f y â‰¤ f x :=
  ConvexOn.exists_ge_of_mem_convexHull (Î² := Î²áµ’áµˆ) hf hx
#align concave_on.exists_le_of_mem_convex_hull ConcaveOn.exists_le_of_mem_convexHull

end MaximumPrinciple
