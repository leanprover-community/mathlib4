/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Analysis.Convex.Function
import Mathlib.Tactic.FieldSimp

/-!
# Jensen's inequality and maximum principle for convex functions

In this file, we prove the finite Jensen inequality and the finite maximum principle for convex
functions. The integral versions are to be found in `Analysis.Convex.Integral`.

## Main declarations

Jensen's inequalities:
* `ConvexOn.map_centerMass_le`, `ConvexOn.map_sum_le`: Convex Jensen's inequality. The image of a
  convex combination of points under a convex function is less than the convex combination of the
  images.
* `ConcaveOn.le_map_centerMass`, `ConcaveOn.le_map_sum`: Concave Jensen's inequality.
* `StrictConvexOn.map_sum_lt`: Convex strict Jensen inequality.
* `StrictConcaveOn.lt_map_sum`: Concave strict Jensen inequality.

As corollaries, we get:
* `StrictConvexOn.map_sum_eq_iff`: Equality case of the convex Jensen inequality.
* `StrictConcaveOn.map_sum_eq_iff`: Equality case of the concave Jensen inequality.
* `ConvexOn.exists_ge_of_mem_convexHull`: Maximum principle for convex functions.
* `ConcaveOn.exists_le_of_mem_convexHull`: Minimum principle for concave functions.
-/


open Finset LinearMap Set Convex Pointwise

variable {ğ•œ E F Î² Î¹ : Type*}

/-! ### Jensen's inequality -/


section Jensen

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup E]
  [AddCommGroup Î²] [PartialOrder Î²] [IsOrderedAddMonoid Î²] [Module ğ•œ E] [Module ğ•œ Î²]
  [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²} {t : Finset Î¹} {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E} {v : ğ•œ} {q : E}

/-- Convex **Jensen's inequality**, `Finset.centerMass` version. -/
theorem ConvexOn.map_centerMass_le (hf : ConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : 0 < âˆ‘ i âˆˆ t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    f (t.centerMass w p) â‰¤ t.centerMass w (f âˆ˜ p) := by
  have hmem' : âˆ€ i âˆˆ t, (p i, (f âˆ˜ p) i) âˆˆ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2 } := fun i hi =>
    âŸ¨hmem i hi, le_rflâŸ©
  convert (hf.convex_epigraph.centerMass_mem hâ‚€ hâ‚ hmem').2 <;>
    simp only [centerMass, Function.comp, Prod.smul_fst, Prod.fst_sum, Prod.smul_snd, Prod.snd_sum]

/-- Concave **Jensen's inequality**, `Finset.centerMass` version. -/
theorem ConcaveOn.le_map_centerMass (hf : ConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : 0 < âˆ‘ i âˆˆ t, w i) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    t.centerMass w (f âˆ˜ p) â‰¤ f (t.centerMass w p) :=
  ConvexOn.map_centerMass_le (Î² := Î²áµ’áµˆ) hf hâ‚€ hâ‚ hmem

/-- Convex **Jensen's inequality**, `Finset.sum` version. -/
theorem ConvexOn.map_sum_le (hf : ConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i âˆˆ t, w i = 1)
    (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) : f (âˆ‘ i âˆˆ t, w i â€¢ p i) â‰¤ âˆ‘ i âˆˆ t, w i â€¢ f (p i) := by
  simpa only [centerMass, hâ‚, inv_one, one_smul] using
    hf.map_centerMass_le hâ‚€ (hâ‚.symm â–¸ zero_lt_one) hmem

/-- Concave **Jensen's inequality**, `Finset.sum` version. -/
theorem ConcaveOn.le_map_sum (hf : ConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    (âˆ‘ i âˆˆ t, w i â€¢ f (p i)) â‰¤ f (âˆ‘ i âˆˆ t, w i â€¢ p i) :=
  ConvexOn.map_sum_le (Î² := Î²áµ’áµˆ) hf hâ‚€ hâ‚ hmem

/-- Convex **Jensen's inequality** where an element plays a distinguished role. -/
lemma ConvexOn.map_add_sum_le (hf : ConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : v + âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) (hv : 0 â‰¤ v) (hq : q âˆˆ s) :
    f (v â€¢ q + âˆ‘ i âˆˆ t, w i â€¢ p i) â‰¤ v â€¢ f q + âˆ‘ i âˆˆ t, w i â€¢ f (p i) := by
  let W j := Option.elim j v w
  let P j := Option.elim j q p
  have : f (âˆ‘ j âˆˆ insertNone t, W j â€¢ P j) â‰¤ âˆ‘ j âˆˆ insertNone t, W j â€¢ f (P j) :=
    hf.map_sum_le (forall_mem_insertNone.2 âŸ¨hv, hâ‚€âŸ©) (by simpa using hâ‚)
      (forall_mem_insertNone.2 âŸ¨hq, hmemâŸ©)
  simpa using this

/-- Concave **Jensen's inequality** where an element plays a distinguished role. -/
lemma ConcaveOn.map_add_sum_le (hf : ConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : v + âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) (hv : 0 â‰¤ v) (hq : q âˆˆ s) :
    v â€¢ f q + âˆ‘ i âˆˆ t, w i â€¢ f (p i) â‰¤ f (v â€¢ q + âˆ‘ i âˆˆ t, w i â€¢ p i) :=
  hf.dual.map_add_sum_le hâ‚€ hâ‚ hmem hv hq

/-! ### Strict Jensen inequality -/

/-- Convex **strict Jensen inequality**.

If the function is strictly convex, the weights are strictly positive and the indexed family of
points is non-constant, then Jensen's inequality is strict.

See also `StrictConvexOn.map_sum_eq_iff`. -/
lemma StrictConvexOn.map_sum_lt (hf : StrictConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 < w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) (hp : âˆƒ j âˆˆ t, âˆƒ k âˆˆ t, p j â‰  p k) :
    f (âˆ‘ i âˆˆ t, w i â€¢ p i) < âˆ‘ i âˆˆ t, w i â€¢ f (p i) := by
  classical
  obtain âŸ¨j, hj, k, hk, hjkâŸ© := hp
  -- We replace `t` by `t \ {j, k}`
  have : k âˆˆ t.erase j := mem_erase.2 âŸ¨ne_of_apply_ne _ hjk.symm, hkâŸ©
  let u := (t.erase j).erase k
  have hj : j âˆ‰ u := by simp [u]
  have hk : k âˆ‰ u := by simp [u]
  have ht :
      t = (u.cons k hk).cons j (mem_cons.not.2 <| not_or_intro (ne_of_apply_ne _ hjk) hj) := by
    simp [u, insert_erase this, insert_erase â€¹j âˆˆ tâ€º, *]
  clear_value u
  subst ht
  simp only [sum_cons]
  have := hâ‚€ j <| by simp
  have := hâ‚€ k <| by simp
  let c := w j + w k
  have hc : w j / c + w k / c = 1 := by field_simp [c]
  calc f (w j â€¢ p j + (w k â€¢ p k + âˆ‘ x âˆˆ u, w x â€¢ p x))
    _ = f (c â€¢ ((w j / c) â€¢ p j + (w k / c) â€¢ p k) + âˆ‘ x âˆˆ u, w x â€¢ p x) := by
      congrm f ?_
      match_scalars <;> field_simp
    _ â‰¤ c â€¢ f ((w j / c) â€¢ p j + (w k / c) â€¢ p k) + âˆ‘ x âˆˆ u, w x â€¢ f (p x) :=
      -- apply the usual Jensen's inequality wrt the weighted average of the two distinguished
      -- points and all the other points
        hf.convexOn.map_add_sum_le (fun i hi â†¦ (hâ‚€ _ <| by simp [hi]).le)
          (by simpa [-cons_eq_insert, â† add_assoc] using hâ‚)
          (forall_of_forall_cons <| forall_of_forall_cons hmem) (by positivity) <| by
           refine hf.1 (hmem _ <| by simp) (hmem _ <| by simp) ?_ ?_ hc <;> positivity
    _ < c â€¢ ((w j / c) â€¢ f (p j) + (w k / c) â€¢ f (p k)) + âˆ‘ x âˆˆ u, w x â€¢ f (p x) := by
      -- then apply the definition of strict convexity for the two distinguished points
      gcongr; refine hf.2 (hmem _ <| by simp) (hmem _ <| by simp) hjk ?_ ?_ hc <;> positivity
    _ = (w j â€¢ f (p j) + w k â€¢ f (p k)) + âˆ‘ x âˆˆ u, w x â€¢ f (p x) := by
      match_scalars <;> field_simp
    _ = w j â€¢ f (p j) + (w k â€¢ f (p k) + âˆ‘ x âˆˆ u, w x â€¢ f (p x)) := by abel_nf

/-- Concave **strict Jensen inequality**.

If the function is strictly concave, the weights are strictly positive and the indexed family of
points is non-constant, then Jensen's inequality is strict.

See also `StrictConcaveOn.map_sum_eq_iff`. -/
lemma StrictConcaveOn.lt_map_sum (hf : StrictConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 < w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) (hp : âˆƒ j âˆˆ t, âˆƒ k âˆˆ t, p j â‰  p k) :
    âˆ‘ i âˆˆ t, w i â€¢ f (p i) < f (âˆ‘ i âˆˆ t, w i â€¢ p i) := hf.dual.map_sum_lt hâ‚€ hâ‚ hmem hp

/-! ### Equality case of Jensen's inequality -/

/-- A form of the **equality case of Jensen's equality**.

For a strictly convex function `f` and positive weights `w`, if
`f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i)`, then the points `p` are all equal.

See also `StrictConvexOn.map_sum_eq_iff`. -/
lemma StrictConvexOn.eq_of_le_map_sum (hf : StrictConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 < w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s)
    (h_eq : âˆ‘ i âˆˆ t, w i â€¢ f (p i) â‰¤ f (âˆ‘ i âˆˆ t, w i â€¢ p i)) :
    âˆ€ â¦ƒjâ¦„, j âˆˆ t â†’ âˆ€ â¦ƒkâ¦„, k âˆˆ t â†’ p j = p k := by
  by_contra!; exact h_eq.not_gt <| hf.map_sum_lt hâ‚€ hâ‚ hmem this

/-- A form of the **equality case of Jensen's equality**.

For a strictly concave function `f` and positive weights `w`, if
`f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i)`, then the points `p` are all equal.

See also `StrictConcaveOn.map_sum_eq_iff`. -/
lemma StrictConcaveOn.eq_of_map_sum_eq (hf : StrictConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 < w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s)
    (h_eq : f (âˆ‘ i âˆˆ t, w i â€¢ p i) â‰¤ âˆ‘ i âˆˆ t, w i â€¢ f (p i)) :
    âˆ€ â¦ƒjâ¦„, j âˆˆ t â†’ âˆ€ â¦ƒkâ¦„, k âˆˆ t â†’ p j = p k := by
  by_contra!; exact h_eq.not_gt <| hf.lt_map_sum hâ‚€ hâ‚ hmem this

/-- Canonical form of the **equality case of Jensen's equality**.

For a strictly convex function `f` and positive weights `w`, we have
`f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i)` if and only if the points `p` are all equal
(and in fact all equal to their center of mass wrt `w`). -/
lemma StrictConvexOn.map_sum_eq_iff {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E} (hf : StrictConvexOn ğ•œ s f)
    (hâ‚€ : âˆ€ i âˆˆ t, 0 < w i) (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i) â†” âˆ€ j âˆˆ t, p j = âˆ‘ i âˆˆ t, w i â€¢ p i := by
  constructor
  Â· obtain rfl | âŸ¨iâ‚€, hiâ‚€âŸ© := t.eq_empty_or_nonempty
    Â· simp
    intro h_eq i hi
    have H : âˆ€ j âˆˆ t, p j = p iâ‚€ := by
      intro j hj
      apply hf.eq_of_le_map_sum hâ‚€ hâ‚ hmem h_eq.ge hj hiâ‚€
    calc p i = p iâ‚€ := by rw [H _ hi]
      _ = (1 : ğ•œ) â€¢ p iâ‚€ := by simp
      _ = (âˆ‘ j âˆˆ t, w j) â€¢ p iâ‚€ := by rw [hâ‚]
      _ = âˆ‘ j âˆˆ t, (w j â€¢ p iâ‚€) := by rw [sum_smul]
      _ = âˆ‘ j âˆˆ t, (w j â€¢ p j) := by congr! 2 with j hj; rw [â† H _ hj]
  Â· intro h
    have H : âˆ€ j âˆˆ t, w j â€¢ f (p j) = w j â€¢ f (âˆ‘ i âˆˆ t, w i â€¢ p i) := by
      intro j hj
      simp [h j hj]
    rw [sum_congr rfl H, â† sum_smul, hâ‚, one_smul]

/-- Canonical form of the **equality case of Jensen's equality**.

For a strictly concave function `f` and positive weights `w`, we have
`f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i)` if and only if the points `p` are all equal
(and in fact all equal to their center of mass wrt `w`). -/
lemma StrictConcaveOn.map_sum_eq_iff (hf : StrictConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 < w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i) â†” âˆ€ j âˆˆ t, p j = âˆ‘ i âˆˆ t, w i â€¢ p i := by
  simpa using hf.neg.map_sum_eq_iff hâ‚€ hâ‚ hmem

/-- Canonical form of the **equality case of Jensen's equality**.

For a strictly convex function `f` and nonnegative weights `w`, we have
`f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i)` if and only if the points `p` with nonzero
weight are all equal (and in fact all equal to their center of mass wrt `w`). -/
lemma StrictConvexOn.map_sum_eq_iff' (hf : StrictConvexOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i) â†”
      âˆ€ j âˆˆ t, w j â‰  0 â†’ p j = âˆ‘ i âˆˆ t, w i â€¢ p i := by
  have hw (i) (_ : i âˆˆ t) : w i â€¢ p i â‰  0 â†’ w i â‰  0 := by aesop
  have hw' (i) (_ : i âˆˆ t) : w i â€¢ f (p i) â‰  0 â†’ w i â‰  0 := by aesop
  rw [â† sum_filter_of_ne hw, â† sum_filter_of_ne hw', hf.map_sum_eq_iff]
  Â· simp
  Â· simp +contextual [(hâ‚€ _ _).gt_iff_ne]
  Â· rwa [sum_filter_ne_zero]
  Â· simp +contextual [hmem _ _]

/-- Canonical form of the **equality case of Jensen's equality**.

For a strictly concave function `f` and nonnegative weights `w`, we have
`f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i)` if and only if the points `p` with nonzero
weight are all equal (and in fact all equal to their center of mass wrt `w`). -/
lemma StrictConcaveOn.map_sum_eq_iff' (hf : StrictConcaveOn ğ•œ s f) (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i)
    (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (hmem : âˆ€ i âˆˆ t, p i âˆˆ s) :
    f (âˆ‘ i âˆˆ t, w i â€¢ p i) = âˆ‘ i âˆˆ t, w i â€¢ f (p i) â†”
      âˆ€ j âˆˆ t, w j â‰  0 â†’ p j = âˆ‘ i âˆˆ t, w i â€¢ p i := hf.dual.map_sum_eq_iff' hâ‚€ hâ‚ hmem

end Jensen

/-! ### Maximum principle -/


section MaximumPrinciple

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup E]
  [AddCommGroup Î²] [LinearOrder Î²] [IsOrderedAddMonoid Î²] [Module ğ•œ E]
  [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²} {w : Î¹ â†’ ğ•œ} {p : Î¹ â†’ E}
  {x y z : E}

theorem ConvexOn.le_sup_of_mem_convexHull {t : Finset E} (hf : ConvexOn ğ•œ s f) (hts : â†‘t âŠ† s)
    (hx : x âˆˆ convexHull ğ•œ (t : Set E)) :
    f x â‰¤ t.sup' (coe_nonempty.1 <| convexHull_nonempty_iff.1 âŸ¨x, hxâŸ©) f := by
  obtain âŸ¨w, hwâ‚€, hwâ‚, rflâŸ© := mem_convexHull.1 hx
  exact (hf.map_centerMass_le hwâ‚€ (by positivity) hts).trans
    (centerMass_le_sup hwâ‚€ <| by positivity)

theorem ConvexOn.inf_le_of_mem_convexHull {t : Finset E} (hf : ConcaveOn ğ•œ s f) (hts : â†‘t âŠ† s)
    (hx : x âˆˆ convexHull ğ•œ (t : Set E)) :
    t.inf' (coe_nonempty.1 <| convexHull_nonempty_iff.1 âŸ¨x, hxâŸ©) f â‰¤ f x :=
  hf.dual.le_sup_of_mem_convexHull hts hx

/-- If a function `f` is convex on `s`, then the value it takes at some center of mass of points of
`s` is less than the value it takes on one of those points. -/
lemma ConvexOn.exists_ge_of_centerMass {t : Finset Î¹} (h : ConvexOn ğ•œ s f)
    (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i âˆˆ t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
    âˆƒ i âˆˆ t, f (t.centerMass w p) â‰¤ f (p i) := by
  set y := t.centerMass w p
  -- TODO: can `rsuffices` be used to write the `exact` first, then the proof of this obtain?
  obtain âŸ¨i, hi, hfiâŸ© : âˆƒ i âˆˆ {i âˆˆ t | w i â‰  0}, w i â€¢ f y â‰¤ w i â€¢ (f âˆ˜ p) i := by
    have hw' : (0 : ğ•œ) < âˆ‘ i âˆˆ t with w i â‰  0, w i := by rwa [sum_filter_ne_zero]
    refine exists_le_of_sum_le (nonempty_of_sum_ne_zero hw'.ne') ?_
    rw [â† sum_smul, â† smul_le_smul_iff_of_pos_left (inv_pos.2 hw'), inv_smul_smulâ‚€ hw'.ne', â†
      centerMass, centerMass_filter_ne_zero]
    exact h.map_centerMass_le hwâ‚€ hwâ‚ hp
  rw [mem_filter] at hi
  exact âŸ¨i, hi.1, (smul_le_smul_iff_of_pos_left <| (hwâ‚€ i hi.1).lt_of_ne hi.2.symm).1 hfiâŸ©

/-- If a function `f` is concave on `s`, then the value it takes at some center of mass of points of
`s` is greater than the value it takes on one of those points. -/
lemma ConcaveOn.exists_le_of_centerMass {t : Finset Î¹} (h : ConcaveOn ğ•œ s f)
    (hwâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hwâ‚ : 0 < âˆ‘ i âˆˆ t, w i) (hp : âˆ€ i âˆˆ t, p i âˆˆ s) :
    âˆƒ i âˆˆ t, f (p i) â‰¤ f (t.centerMass w p) := h.dual.exists_ge_of_centerMass hwâ‚€ hwâ‚ hp

/-- **Maximum principle** for convex functions. If a function `f` is convex on the convex hull of
`s`, then the eventual maximum of `f` on `convexHull ğ•œ s` lies in `s`. -/
lemma ConvexOn.exists_ge_of_mem_convexHull {t : Set E} (hf : ConvexOn ğ•œ s f) (hts : t âŠ† s)
    (hx : x âˆˆ convexHull ğ•œ t) : âˆƒ y âˆˆ t, f x â‰¤ f y := by
  rw [_root_.convexHull_eq] at hx
  obtain âŸ¨Î±, t, w, p, hwâ‚€, hwâ‚, hp, rflâŸ© := hx
  obtain âŸ¨i, hit, HiâŸ© := hf.exists_ge_of_centerMass hwâ‚€ (hwâ‚.symm â–¸ zero_lt_one)
    fun i hi â†¦ hts (hp i hi)
  exact âŸ¨p i, hp i hit, HiâŸ©

/-- **Minimum principle** for concave functions. If a function `f` is concave on the convex hull of
`s`, then the eventual minimum of `f` on `convexHull ğ•œ s` lies in `s`. -/
lemma ConcaveOn.exists_le_of_mem_convexHull {t : Set E} (hf : ConcaveOn ğ•œ s f) (hts : t âŠ† s)
    (hx : x âˆˆ convexHull ğ•œ t) : âˆƒ y âˆˆ t, f y â‰¤ f x := hf.dual.exists_ge_of_mem_convexHull hts hx

/-- **Maximum principle** for convex functions on a segment. If a function `f` is convex on the
segment `[x, y]`, then the eventual maximum of `f` on `[x, y]` is at `x` or `y`. -/
lemma ConvexOn.le_max_of_mem_segment (hf : ConvexOn ğ•œ s f) (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ [x -[ğ•œ] y]) : f z â‰¤ max (f x) (f y) := by
  rw [â† convexHull_pair] at hz; simpa using hf.exists_ge_of_mem_convexHull (pair_subset hx hy) hz

/-- **Minimum principle** for concave functions on a segment. If a function `f` is concave on the
segment `[x, y]`, then the eventual minimum of `f` on `[x, y]` is at `x` or `y`. -/
lemma ConcaveOn.min_le_of_mem_segment (hf : ConcaveOn ğ•œ s f) (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ [x -[ğ•œ] y]) : min (f x) (f y) â‰¤ f z := hf.dual.le_max_of_mem_segment hx hy hz

/-- **Maximum principle** for convex functions on an interval. If a function `f` is convex on the
interval `[x, y]`, then the eventual maximum of `f` on `[x, y]` is at `x` or `y`. -/
lemma ConvexOn.le_max_of_mem_Icc {s : Set ğ•œ} {f : ğ•œ â†’ Î²} {x y z : ğ•œ} (hf : ConvexOn ğ•œ s f)
    (hx : x âˆˆ s) (hy : y âˆˆ s) (hz : z âˆˆ Icc x y) : f z â‰¤ max (f x) (f y) := by
  rw [â† segment_eq_Icc (hz.1.trans hz.2)] at hz; exact hf.le_max_of_mem_segment hx hy hz

/-- **Minimum principle** for concave functions on an interval. If a function `f` is concave on the
interval `[x, y]`, then the eventual minimum of `f` on `[x, y]` is at `x` or `y`. -/
lemma ConcaveOn.min_le_of_mem_Icc {s : Set ğ•œ} {f : ğ•œ â†’ Î²} {x y z : ğ•œ} (hf : ConcaveOn ğ•œ s f)
    (hx : x âˆˆ s) (hy : y âˆˆ s) (hz : z âˆˆ Icc x y) : min (f x) (f y) â‰¤ f z :=
  hf.dual.le_max_of_mem_Icc hx hy hz

lemma ConvexOn.bddAbove_convexHull {s t : Set E} (hst : s âŠ† t) (hf : ConvexOn ğ•œ t f) :
    BddAbove (f '' s) â†’ BddAbove (f '' convexHull ğ•œ s) := by
  rintro âŸ¨b, hbâŸ©
  refine âŸ¨b, ?_âŸ©
  rintro _ âŸ¨x, hx, rflâŸ©
  obtain âŸ¨y, hy, hxyâŸ© := hf.exists_ge_of_mem_convexHull hst hx
  exact hxy.trans <| hb <| mem_image_of_mem _ hy

lemma ConcaveOn.bddBelow_convexHull {s t : Set E} (hst : s âŠ† t) (hf : ConcaveOn ğ•œ t f) :
    BddBelow (f '' s) â†’ BddBelow (f '' convexHull ğ•œ s) := hf.dual.bddAbove_convexHull hst

end MaximumPrinciple
