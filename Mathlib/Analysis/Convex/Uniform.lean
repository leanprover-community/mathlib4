/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Analysis.Convex.StrictConvexSpace

#align_import analysis.convex.uniform from "leanprover-community/mathlib"@"17ef379e997badd73e5eabb4d38f11919ab3c4b3"

/-!
# Uniformly convex spaces

This file defines uniformly convex spaces, which are real normed vector spaces in which for all
strictly positive `Œµ`, there exists some strictly positive `Œ¥` such that `Œµ ‚â§ ‚Äñx - y‚Äñ` implies
`‚Äñx + y‚Äñ ‚â§ 2 - Œ¥` for all `x` and `y` of norm at most than `1`. This means that the triangle
inequality is strict with a uniform bound, as opposed to strictly convex spaces where the triangle
inequality is strict but not necessarily uniformly (`‚Äñx + y‚Äñ < ‚Äñx‚Äñ + ‚Äñy‚Äñ` for all `x` and `y` not in
the same ray).

## Main declarations

`UniformConvexSpace E` means that `E` is a uniformly convex space.

## TODO

* Milman-Pettis
* Hanner's inequalities

## Tags

convex, uniformly convex
-/


open Set Metric

open Convex Pointwise

/-- A *uniformly convex space* is a real normed space where the triangle inequality is strict with a
uniform bound. Namely, over the `x` and `y` of norm `1`, `‚Äñx + y‚Äñ` is uniformly bounded above
by a constant `< 2` when `‚Äñx - y‚Äñ` is uniformly bounded below by a positive constant. -/
class UniformConvexSpace (E : Type*) [SeminormedAddCommGroup E] : Prop where
  uniform_convex : ‚àÄ ‚¶ÉŒµ : ‚Ñù‚¶Ñ,
    0 < Œµ ‚Üí ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ = 1 ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, ‚Äñy‚Äñ = 1 ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥
#align uniform_convex_space UniformConvexSpace

variable {E : Type*}

section SeminormedAddCommGroup

variable (E) [SeminormedAddCommGroup E] [UniformConvexSpace E] {Œµ : ‚Ñù}

theorem exists_forall_sphere_dist_add_le_two_sub (hŒµ : 0 < Œµ) :
    ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ = 1 ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, ‚Äñy‚Äñ = 1 ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥ :=
  UniformConvexSpace.uniform_convex hŒµ
#align exists_forall_sphere_dist_add_le_two_sub exists_forall_sphere_dist_add_le_two_sub

variable [NormedSpace ‚Ñù E]

theorem exists_forall_closed_ball_dist_add_le_two_sub (hŒµ : 0 < Œµ) :
    ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ 1 ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, ‚Äñy‚Äñ ‚â§ 1 ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥ := by
  have hŒµ' : 0 < Œµ / 3 := div_pos hŒµ zero_lt_three
  -- ‚ä¢ ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ 1 ‚Üí ‚àÄ ‚¶Éy : E‚¶Ñ, ‚Äñy‚Äñ ‚â§ 1 ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚Ä¶
  obtain ‚ü®Œ¥, hŒ¥, h‚ü© := exists_forall_sphere_dist_add_le_two_sub E hŒµ'
  -- ‚ä¢ ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ 1 ‚Üí ‚àÄ ‚¶Éy : E‚¶Ñ, ‚Äñy‚Äñ ‚â§ 1 ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚Ä¶
  set Œ¥' := min (1 / 2) (min (Œµ / 3) <| Œ¥ / 3)
  -- ‚ä¢ ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ 1 ‚Üí ‚àÄ ‚¶Éy : E‚¶Ñ, ‚Äñy‚Äñ ‚â§ 1 ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚Ä¶
  refine' ‚ü®Œ¥', lt_min one_half_pos <| lt_min hŒµ' (div_pos hŒ¥ zero_lt_three), fun x hx y hy hxy => _‚ü©
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥'
  obtain hx' | hx' := le_or_lt ‚Äñx‚Äñ (1 - Œ¥')
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥'
  ¬∑ rw [‚Üê one_add_one_eq_two]
    -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 1 + 1 - Œ¥'
    exact (norm_add_le_of_le hx' hy).trans (sub_add_eq_add_sub _ _ _).le
    -- üéâ no goals
  obtain hy' | hy' := le_or_lt ‚Äñy‚Äñ (1 - Œ¥')
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥'
  ¬∑ rw [‚Üê one_add_one_eq_two]
    -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 1 + 1 - Œ¥'
    exact (norm_add_le_of_le hx hy').trans (add_sub_assoc _ _ _).ge
    -- üéâ no goals
  have hŒ¥' : 0 < 1 - Œ¥' := sub_pos_of_lt (min_lt_of_left_lt one_half_lt_one)
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥'
  have h‚ÇÅ : ‚àÄ z : E, 1 - Œ¥' < ‚Äñz‚Äñ ‚Üí ‚Äñ‚Äñz‚Äñ‚Åª¬π ‚Ä¢ z‚Äñ = 1 := by
    rintro z hz
    rw [norm_smul_of_nonneg (inv_nonneg.2 <| norm_nonneg _), inv_mul_cancel (hŒ¥'.trans hz).ne']
  have h‚ÇÇ : ‚àÄ z : E, ‚Äñz‚Äñ ‚â§ 1 ‚Üí 1 - Œ¥' ‚â§ ‚Äñz‚Äñ ‚Üí ‚Äñ‚Äñz‚Äñ‚Åª¬π ‚Ä¢ z - z‚Äñ ‚â§ Œ¥' := by
    rintro z hz hŒ¥z
    nth_rw 3 [‚Üê one_smul ‚Ñù z]
    rwa [‚Üê sub_smul, norm_smul_of_nonneg (sub_nonneg_of_le <| one_le_inv (hŒ¥'.trans_le hŒ¥z) hz),
      sub_mul, inv_mul_cancel (hŒ¥'.trans_le hŒ¥z).ne', one_mul, sub_le_comm]
  set x' := ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥'
  set y' := ‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 - Œ¥'
  have hxy' : Œµ / 3 ‚â§ ‚Äñx' - y'‚Äñ :=
    calc
      Œµ / 3 = Œµ - (Œµ / 3 + Œµ / 3) := by ring
      _ ‚â§ ‚Äñx - y‚Äñ - (‚Äñx' - x‚Äñ + ‚Äñy' - y‚Äñ) := by
        gcongr
        ¬∑ exact (h‚ÇÇ _ hx hx'.le).trans <| min_le_of_right_le <| min_le_left _ _
        ¬∑ exact (h‚ÇÇ _ hy hy'.le).trans <| min_le_of_right_le <| min_le_left _ _
      _ ‚â§ _ := by
        have : ‚àÄ x' y', x - y = x' - y' + (x - x') + (y' - y) := fun _ _ => by abel
        rw [sub_le_iff_le_add, norm_sub_rev _ x, ‚Üê add_assoc, this]
        exact norm_add‚ÇÉ_le _ _ _
  calc
    ‚Äñx + y‚Äñ ‚â§ ‚Äñx' + y'‚Äñ + ‚Äñx' - x‚Äñ + ‚Äñy' - y‚Äñ := by
      have : ‚àÄ x' y', x + y = x' + y' + (x - x') + (y - y') := fun _ _ => by abel
      rw [norm_sub_rev, norm_sub_rev y', this]
      exact norm_add‚ÇÉ_le _ _ _
    _ ‚â§ 2 - Œ¥ + Œ¥' + Œ¥' :=
      (add_le_add_three (h (h‚ÇÅ _ hx') (h‚ÇÅ _ hy') hxy') (h‚ÇÇ _ hx hx'.le) (h‚ÇÇ _ hy hy'.le))
    _ ‚â§ 2 - Œ¥' := by
      rw [‚Üê le_sub_iff_add_le, ‚Üê le_sub_iff_add_le, sub_sub, sub_sub]
      refine' sub_le_sub_left _ _
      ring_nf
      rw [‚Üê mul_div_cancel' Œ¥ three_ne_zero]
      norm_num -- Porting note: these three extra lines needed to make `exact` work
      have : 3 * (Œ¥ / 3) * (1 / 3) = Œ¥ / 3 := by linarith
      rw [this, mul_comm]
      gcongr
      exact min_le_of_right_le <| min_le_right _ _
#align exists_forall_closed_ball_dist_add_le_two_sub exists_forall_closed_ball_dist_add_le_two_sub

theorem exists_forall_closed_ball_dist_add_le_two_mul_sub (hŒµ : 0 < Œµ) (r : ‚Ñù) :
    ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ r ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, ‚Äñy‚Äñ ‚â§ r ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚â§ 2 * r - Œ¥ := by
  obtain hr | hr := le_or_lt r 0
  -- ‚ä¢ ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ r ‚Üí ‚àÄ ‚¶Éy : E‚¶Ñ, ‚Äñy‚Äñ ‚â§ r ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚Ä¶
  ¬∑ exact ‚ü®1, one_pos, fun x hx y hy h => (hŒµ.not_le <|
      h.trans <| (norm_sub_le _ _).trans <| add_nonpos (hx.trans hr) (hy.trans hr)).elim‚ü©
  obtain ‚ü®Œ¥, hŒ¥, h‚ü© := exists_forall_closed_ball_dist_add_le_two_sub E (div_pos hŒµ hr)
  -- ‚ä¢ ‚àÉ Œ¥, 0 < Œ¥ ‚àß ‚àÄ ‚¶Éx : E‚¶Ñ, ‚Äñx‚Äñ ‚â§ r ‚Üí ‚àÄ ‚¶Éy : E‚¶Ñ, ‚Äñy‚Äñ ‚â§ r ‚Üí Œµ ‚â§ ‚Äñx - y‚Äñ ‚Üí ‚Äñx + y‚Äñ ‚Ä¶
  refine' ‚ü®Œ¥ * r, mul_pos hŒ¥ hr, fun x hx y hy hxy => _‚ü©
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 * r - Œ¥ * r
  rw [‚Üê div_le_one hr, div_eq_inv_mul, ‚Üê norm_smul_of_nonneg (inv_nonneg.2 hr.le)] at hx hy
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 * r - Œ¥ * r
  try infer_instance
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 * r - Œ¥ * r
  have := h hx hy
  -- ‚ä¢ ‚Äñx + y‚Äñ ‚â§ 2 * r - Œ¥ * r
  simp_rw [‚Üê smul_add, ‚Üê smul_sub, norm_smul_of_nonneg (inv_nonneg.2 hr.le), ‚Üê div_eq_inv_mul,
    div_le_div_right hr, div_le_iff hr, sub_mul] at this
  exact this hxy
  -- üéâ no goals
#align exists_forall_closed_ball_dist_add_le_two_mul_sub exists_forall_closed_ball_dist_add_le_two_mul_sub

end SeminormedAddCommGroup

variable [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [UniformConvexSpace E]

-- See note [lower instance priority]
instance (priority := 100) UniformConvexSpace.toStrictConvexSpace : StrictConvexSpace ‚Ñù E :=
  StrictConvexSpace.of_norm_add_ne_two fun _ _ hx hy hxy =>
    let ‚ü®_, hŒ¥, h‚ü© := exists_forall_closed_ball_dist_add_le_two_sub E (norm_sub_pos_iff.2 hxy)
    ((h hx.le hy.le le_rfl).trans_lt <| sub_lt_self _ hŒ¥).ne
#align uniform_convex_space.to_strict_convex_space UniformConvexSpace.toStrictConvexSpace
