/-
Copyright (c) 2022 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.StrictConvexSpace
import Mathlib.Analysis.NormedSpace.HahnBanach.Extension

/-!
# Uniformly convex spaces

This file defines uniformly convex spaces, which are real normed vector spaces in which for all
strictly positive `Îµ`, there exists some strictly positive `Î´` such that `Îµ â‰¤ â€–x - yâ€–` implies
`â€–x + yâ€– â‰¤ 2 - Î´` for all `x` and `y` of norm at most than `1`. This means that the triangle
inequality is strict with a uniform bound, as opposed to strictly convex spaces where the triangle
inequality is strict but not necessarily uniformly (`â€–x + yâ€– < â€–xâ€– + â€–yâ€–` for all `x` and `y` not in
the same ray).

## Main declarations

`UniformConvexSpace E` means that `E` is a uniformly convex space.

## TODO

* Milman-Pettis
* Hanner's inequalities

## Tags

convex, uniformly convex
-/


open Set Metric Filter Topology Uniformity

open Convex Pointwise

theorem norm_tendsto_of_norm_add_of_le {Î¹ E : Type*} [SeminormedAddCommGroup E]
    {a : â„} {ğ“• : Filter Î¹} {f g : Î¹ â†’ E}
    (norm_f : âˆ€á¶  i in ğ“•, â€–f iâ€– â‰¤ a)
    (norm_g : âˆ€á¶  i in ğ“•, â€–g iâ€– â‰¤ a)
    (norm_add : Tendsto (fun i â†¦ â€–f i + g iâ€–) ğ“• (ğ“ (a+a))) :
    Tendsto (fun i â†¦ â€–f iâ€–) ğ“• (ğ“ a) := by
  have : âˆ€á¶  i in ğ“•, â€–f i + g iâ€– - a â‰¤ â€–f iâ€– := by
    filter_upwards [norm_g] with i hgi
    rw [sub_le_iff_le_add]
    exact norm_add_le _ _ |>.trans (add_le_add_left hgi _)
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' ?_ tendsto_const_nhds this norm_f
  simpa only [add_sub_cancel_right a a] using norm_add.sub_const a

theorem normalize_tendsto_uniformity_of_norm_tendsto {Î¹ E : Type*}
    [SeminormedAddCommGroup E] [NormedSpace â„ E]
    {a : â„} {ğ“• : Filter Î¹} {f : Î¹ â†’ E} (ha : a â‰  0)
    (norm_f : Tendsto (fun i â†¦ â€–f iâ€–) ğ“• (ğ“ a)) :
    Tendsto (fun i â†¦ (â€–f iâ€–â»Â¹ â€¢ f i, aâ»Â¹ â€¢ f i)) ğ“• (ğ“¤ E) := by
  simp_rw [uniformity_eq_comap_nhds_zero, tendsto_comap_iff, Function.comp_def, â† sub_smul]
  have : Tendsto (fun i â†¦ aâ»Â¹ - â€–f iâ€–â»Â¹) ğ“• (ğ“ 0) := by
    simpa using (norm_f.invâ‚€ ha).const_sub aâ»Â¹
  exact this.zero_smul_isBoundedUnder_le norm_f.isBoundedUnder_le

/-- A *uniformly convex space* is a real normed space where `â€–x - yâ€–` tends to `0` when
`â€–x+yâ€–` tends to `2` and `â€–xâ€–` and `â€–yâ€–` tend to `1`. This is a strenghtening of strict convexity,
which says that `â€–x - yâ€– = 0` when `â€–xâ€– = â€–yâ€– = 1` and `â€–x + yâ€– = 2`.

A more concrete characterization is given by
`uniformConvexSpace_iff_exists_forall_sphere_norm_add_le_add_sub` : a normed space is
uniformly convex if and only if, over the `x` and `y` of norm `1`,
`â€–x + yâ€–` is uniformly bounded above by a constant `< 2` when `â€–x - yâ€–` is uniformly bounded below
by a positive constant. -/
@[mk_iff uniformConvexSpace_iff_comap_norm_add_le_uniformity]
class UniformConvexSpace (E : Type*) [SeminormedAddCommGroup E] : Prop where
  protected comap_norm_add_le_uniformity : âˆ€ a : â„, 0 < a â†’
    comap (fun xy â†¦ âŸ¨â€–xy.1â€–, â€–xy.2â€–, â€–xy.1 + xy.2â€–âŸ© : E Ã— E â†’ â„ Ã— â„ Ã— â„) (ğ“ âŸ¨a, a, a+aâŸ©) â‰¤ ğ“¤ E

variable {E : Type*}

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup E]

theorem uniformConvexSpace_iff_le_uniformity_of_norm_add' :
    UniformConvexSpace E â†” âˆ€ a : â„, 0 < a â†’ âˆ€ ğ“• : Filter (E Ã— E),
      Tendsto (fun xy â†¦ â€–xy.1â€–) ğ“• (ğ“ a) â†’
      Tendsto (fun xy â†¦ â€–xy.2â€–) ğ“• (ğ“ a) â†’
      Tendsto (fun xy â†¦ â€–xy.1 + xy.2â€–) ğ“• (ğ“ (a+a)) â†’
      ğ“• â‰¤ ğ“¤ E := by
  rw [uniformConvexSpace_iff_comap_norm_add_le_uniformity]
  congrm âˆ€ a a_pos, ?_
  rw [â† forall_le_iff_le]
  congrm âˆ€ ğ“•, ?_
  simp_rw [â† tendsto_iff_comap, nhds_prod_eq, tendsto_prod_iff', and_imp]

theorem tendsto_uniformity_of_norm_add {Î¹ : Type*} [H : UniformConvexSpace E]
    {a : â„} {ğ“• : Filter Î¹} {f g : Î¹ â†’ E} (norm_f : Tendsto (fun i â†¦ â€–f iâ€–) ğ“• (ğ“ a))
    (norm_g : Tendsto (fun i â†¦ â€–g iâ€–) ğ“• (ğ“ a))
    (norm_add : Tendsto (fun i â†¦ â€–f i + g iâ€–) ğ“• (ğ“ (a+a))) :
    Tendsto (fun i â†¦ (f i, g i)) ğ“• (ğ“¤ E) := by
  rcases lt_trichotomy a 0 with (ha|rfl|ha)
  Â· have : ğ“• = âŠ¥ := by
      rw [â† eventually_false_iff_eq_bot]
      filter_upwards [norm_f.eventually_lt_const ha] using fun i â†¦ norm_nonneg _ |>.not_lt
    exact this â–¸ tendsto_bot
  Â· rw [â† tendsto_zero_iff_norm_tendsto_zero] at norm_f norm_g
    exact le_trans (Filter.le_prod.mpr âŸ¨norm_f, norm_gâŸ©)
      (nhds_prod_eq (X := E) (Y := E) â–¸ nhds_le_uniformity (0 : E))
  Â· apply uniformConvexSpace_iff_le_uniformity_of_norm_add'.mp H a ha (map (fun i â†¦ (f i, g i)) ğ“•)
      <;> rwa [tendsto_map'_iff]

theorem tendsto_uniformity_of_norm_add_of_closedBall {Î¹ : Type*}
    [UniformConvexSpace E] {a : â„} {ğ“• : Filter Î¹} {f g : Î¹ â†’ E}
    (norm_f : âˆ€á¶  i in ğ“•, â€–f iâ€– â‰¤ a)
    (norm_g : âˆ€á¶  i in ğ“•, â€–g iâ€– â‰¤ a)
    (norm_add : Tendsto (fun i â†¦ â€–f i + g iâ€–) ğ“• (ğ“ (a+a))) :
    Tendsto (fun i â†¦ (f i, g i)) ğ“• (ğ“¤ E) :=
  tendsto_uniformity_of_norm_add
    (norm_tendsto_of_norm_add_of_le norm_f norm_g norm_add)
    (norm_tendsto_of_norm_add_of_le norm_g norm_f (by simpa [add_comm] using norm_add)) norm_add

theorem tendsto_uniformity_of_norm_add_of_sphere {Î¹ : Type*}
    [UniformConvexSpace E] {a : â„} {ğ“• : Filter Î¹} {f g : Î¹ â†’ E}
    (norm_f : âˆ€á¶  i in ğ“•, â€–f iâ€– = a)
    (norm_g : âˆ€á¶  i in ğ“•, â€–g iâ€– = a)
    (norm_add : Tendsto (fun i â†¦ â€–f i + g iâ€–) ğ“• (ğ“ (a+a))) :
    Tendsto (fun i â†¦ (f i, g i)) ğ“• (ğ“¤ E) :=
  tendsto_uniformity_of_norm_add_of_closedBall
    (norm_f.mono fun _ â†¦ Eq.le) (norm_g.mono fun _ â†¦ Eq.le) norm_add

variable (E) in
theorem exists_forall_closedBall_norm_add_le_add_sub [UniformConvexSpace E]
    (a : â„) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    âˆƒ Î´ > 0, âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– â‰¤ a â†’ âˆ€ â¦ƒyâ¦„, â€–yâ€– â‰¤ a â†’ Îµ â‰¤ â€–x - yâ€– â†’ â€–x + yâ€– â‰¤ (a + a) - Î´ := by
  set ğ“• : Filter (E Ã— E) :=
    comap (fun xy â†¦ â€–xy.1 + xy.2â€–) (ğ“ (a+a)) âŠ“ ğ“Ÿ {xy | â€–xy.1â€– â‰¤ a âˆ§ â€–xy.2â€– â‰¤ a}
  have := tendsto_uniformity_of_norm_add_of_closedBall (E := E) (ğ“• := ğ“•)
    (mem_inf_of_right fun _ â†¦ And.left) (mem_inf_of_right fun _ â†¦ And.right)
    (tendsto_inf_left tendsto_comap) |>.eventually (dist_mem_uniformity Îµ_pos)
  simp_rw [ğ“•, eventually_inf_principal, nhds_basis_ball.comap _ |>.eventually_iff,
    Prod.forall, Real.ball_eq_Ioo, dist_eq_norm] at this
  rcases this with âŸ¨Î´, Î´_pos, hÎ´âŸ©
  exact âŸ¨Î´, Î´_pos, fun _ hxa _ hyb â†¦ le_imp_le_of_lt_imp_lt fun hxy â†¦ hÎ´ _ _
    âŸ¨hxy, lt_add_of_le_of_pos (norm_add_le_of_le hxa hyb) Î´_posâŸ© âŸ¨hxa, hybâŸ©âŸ©

variable (E) in
theorem exists_forall_closedBall_norm_add_le_two_sub [UniformConvexSpace E]
    {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    âˆƒ Î´ > 0, âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– â‰¤ 1 â†’ âˆ€ â¦ƒyâ¦„, â€–yâ€– â‰¤ 1 â†’ Îµ â‰¤ â€–x - yâ€– â†’ â€–x + yâ€– â‰¤ 2 - Î´ := by
  simpa only [one_add_one_eq_two] using exists_forall_closedBall_norm_add_le_add_sub E 1 Îµ_pos

variable (E) in
theorem exists_forall_sphere_norm_add_le_add_sub [UniformConvexSpace E]
    (a : â„) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    âˆƒ Î´ > 0, âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– = a â†’ âˆ€ â¦ƒyâ¦„, â€–yâ€– = a â†’ Îµ â‰¤ â€–x - yâ€– â†’ â€–x + yâ€– â‰¤ (a + a) - Î´ :=
  exists_forall_closedBall_norm_add_le_add_sub (a := a) (E := E) Îµ_pos |>.imp
    fun _ âŸ¨Î´_pos, hÎ´âŸ© â†¦ âŸ¨Î´_pos, fun _ hx _ hy â†¦ hÎ´ hx.le hy.leâŸ©

variable (E) in
theorem exists_forall_sphere_norm_add_le_two_sub [UniformConvexSpace E]
    {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    âˆƒ Î´ > 0, âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– = 1 â†’ âˆ€ â¦ƒyâ¦„, â€–yâ€– = 1 â†’ Îµ â‰¤ â€–x - yâ€– â†’ â€–x + yâ€– â‰¤ 2 - Î´ := by
  simpa only [one_add_one_eq_two] using exists_forall_sphere_norm_add_le_add_sub E 1 Îµ_pos

theorem uniformConvexSpace_iff_le_uniformity_of_norm_add_of_sphere
    [NormedSpace â„ E] {a : â„} (a_pos : a > 0) :
    UniformConvexSpace E â†” âˆ€ ğ“• : Filter (E Ã— E),
      (âˆ€á¶  xy in ğ“•, â€–xy.1â€– = a) â†’
      (âˆ€á¶  xy in ğ“•, â€–xy.2â€– = a) â†’
      Tendsto (fun xy â†¦ â€–xy.1 + xy.2â€–) ğ“• (ğ“ (a+a)) â†’
      ğ“• â‰¤ ğ“¤ E := by
  refine âŸ¨fun H ğ“• â†¦ tendsto_uniformity_of_norm_add_of_sphere,
    fun H â†¦ uniformConvexSpace_iff_le_uniformity_of_norm_add'.mpr
      fun b b_pos ğ“• norm_fst norm_snd norm_add â†¦ ?_âŸ©
  change Tendsto id ğ“• (ğ“¤ E)
  rw [â† smul_tendsto_smul_iffâ‚€ (inv_ne_zero b_pos.ne'), smul_uniformityâ‚€ (inv_ne_zero b_pos.ne')]
  have key_fst := normalize_tendsto_uniformity_of_norm_tendsto b_pos.ne' norm_fst
  have key_snd := normalize_tendsto_uniformity_of_norm_tendsto b_pos.ne' norm_snd
  have key_add : Tendsto (fun xy â†¦ â€–â€–xy.1â€–â»Â¹ â€¢ xy.1 + â€–xy.2â€–â»Â¹ â€¢ xy.2â€–) ğ“• (ğ“ (1 + 1)) := by
    have := Tendsto.comp uniformContinuous_norm (key_fst.uniformity_add key_snd)
    refine .congr_uniformity ?_ this.uniformity_symm
    simpa [â† smul_add, norm_smul_of_nonneg (inv_pos.mpr b_pos).le, mul_add, b_pos.ne'] using
      norm_add.const_mul bâ»Â¹
  refine key_fst.uniformity_symm.uniformity_trans ?_ |>.uniformity_trans key_snd
  rw [â† smul_tendsto_smul_iffâ‚€ a_pos.ne', smul_uniformityâ‚€ a_pos.ne']
  refine H _ ?_ ?_ ?_
  Â· rw [eventually_map]
    filter_upwards [norm_fst.eventually_ne b_pos.ne'] with xy hx
    simpa [norm_smul, hx] using a_pos.le
  Â· rw [eventually_map]
    filter_upwards [norm_snd.eventually_ne b_pos.ne'] with xy hy
    simpa [norm_smul, hy] using a_pos.le
  Â· rw [tendsto_map'_iff]
    simpa [Function.comp_def, â† smul_add, norm_smul_of_nonneg a_pos.le, mul_add]
      using key_add.const_mul a

theorem uniformConvexSpace_iff_le_uniformity_of_norm_add_of_closedBall
    [NormedSpace â„ E] {a : â„} (a_pos : a > 0) :
    UniformConvexSpace E â†” âˆ€ ğ“• : Filter (E Ã— E),
      (âˆ€á¶  xy in ğ“•, â€–xy.1â€– â‰¤ a) â†’
      (âˆ€á¶  xy in ğ“•, â€–xy.2â€– â‰¤ a) â†’
      Tendsto (fun xy â†¦ â€–xy.1 + xy.2â€–) ğ“• (ğ“ (a+a)) â†’
      ğ“• â‰¤ ğ“¤ E := by
  constructor <;> intro H
  Â· exact fun ğ“• â†¦ tendsto_uniformity_of_norm_add_of_closedBall
  Â· rw [uniformConvexSpace_iff_le_uniformity_of_norm_add_of_sphere a_pos]
    exact fun ğ“• norm_fst norm_snd â†¦ H ğ“• (norm_fst.mono fun _ â†¦ Eq.le) (norm_snd.mono fun _ â†¦ Eq.le)

theorem uniformConvexSpace_iff_le_uniformity_of_norm_add [NormedSpace â„ E] {a : â„}
    (a_pos : 0 < a) :
    UniformConvexSpace E â†” âˆ€ ğ“• : Filter (E Ã— E),
      Tendsto (fun xy â†¦ â€–xy.1â€–) ğ“• (ğ“ a) â†’
      Tendsto (fun xy â†¦ â€–xy.2â€–) ğ“• (ğ“ a) â†’
      Tendsto (fun xy â†¦ â€–xy.1 + xy.2â€–) ğ“• (ğ“ (a+a)) â†’
      ğ“• â‰¤ ğ“¤ E := by
  constructor <;> intro H
  Â· exact fun ğ“• â†¦ tendsto_uniformity_of_norm_add
  Â· rw [uniformConvexSpace_iff_le_uniformity_of_norm_add_of_sphere a_pos]
    exact fun ğ“• norm_fst norm_snd â†¦ H ğ“•
      (EventuallyEq.tendsto norm_fst) (EventuallyEq.tendsto norm_snd)

theorem uniformConvexSpace_iff_exists_forall_sphere_norm_add_le_add_sub [NormedSpace â„ E]
    {a : â„} (a_pos : 0 < a) :
    UniformConvexSpace E â†” âˆ€ Îµ > 0,
      âˆƒ Î´ > 0, âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– = a â†’ âˆ€ â¦ƒyâ¦„, â€–yâ€– = a â†’ Îµ â‰¤ â€–x - yâ€– â†’ â€–x + yâ€– â‰¤ (a + a) - Î´ := by
  constructor <;> intro H
  Â· exact fun Îµ â†¦ exists_forall_sphere_norm_add_le_add_sub E a
  Â· simp_rw [uniformConvexSpace_iff_le_uniformity_of_norm_add_of_sphere a_pos]
    intro ğ“• norm_fst norm_snd norm_add
    refine uniformity_basis_dist.ge_iff.mpr fun Îµ Îµ_pos â†¦ ?_
    rcases H Îµ Îµ_pos with âŸ¨Î´, Î´_pos, hÎ´âŸ©
    filter_upwards [norm_fst, norm_snd, norm_add.eventually_const_lt
      (show a + a - Î´ < a + a by linarith)] with p norm_p_fst norm_p_snd
    exact le_imp_le_iff_lt_imp_lt.mp <| dist_eq_norm p.1 p.2 â–¸ hÎ´ norm_p_fst norm_p_snd

theorem uniformConvexSpace_iff_exists_forall_closedBall_norm_add_le_add_sub [NormedSpace â„ E]
    {a : â„} (a_pos : 0 < a) :
    UniformConvexSpace E â†” âˆ€ Îµ > 0,
      âˆƒ Î´ > 0, âˆ€ â¦ƒx : Eâ¦„, â€–xâ€– â‰¤ a â†’ âˆ€ â¦ƒyâ¦„, â€–yâ€– â‰¤ a â†’ Îµ â‰¤ â€–x - yâ€– â†’ â€–x + yâ€– â‰¤ (a + a) - Î´ := by
  constructor <;> intro H
  Â· exact fun Îµ â†¦ exists_forall_closedBall_norm_add_le_add_sub E a
  Â· rw [uniformConvexSpace_iff_exists_forall_sphere_norm_add_le_add_sub a_pos]
    exact fun Îµ Îµ_pos â†¦ (H Îµ Îµ_pos).imp fun Î´ âŸ¨Î´_pos, hÎ´âŸ© â†¦ âŸ¨Î´_pos, fun _ hx _ hy â†¦ hÎ´ hx.le hy.leâŸ©

end SeminormedAddCommGroup

variable [NormedAddCommGroup E] [UniformConvexSpace E]

-- See note [lower instance priority]
instance (priority := 100) UniformConvexSpace.toStrictConvexSpace [NormedSpace â„ E] :
    StrictConvexSpace â„ E :=
  StrictConvexSpace.of_norm_add_ne_two fun _ _ hx hy hxy =>
    let âŸ¨_, hÎ´, hâŸ© := exists_forall_closedBall_norm_add_le_two_sub E (norm_sub_pos_iff.2 hxy)
    ((h hx.le hy.le le_rfl).trans_lt <| sub_lt_self _ hÎ´).ne

/-- In a uniformly convex space over `â„` or `â„‚`, the only obstruction for weak convergence to imply
convergence is the potential loss of mass. More explicitly, if `f i` converges to `x` weakly
and `â€–f iâ€–` converges to `â€–xâ€–`, then `f i` converges to `x` in norm. -/
theorem tendsto_iff_forall_dual_and_norm {ğ•œ Î¹ : Type*} [RCLike ğ•œ] [NormedSpace ğ•œ E]
    {ğ“• : Filter Î¹} {f : Î¹ â†’ E} {x : E} :
    Tendsto f ğ“• (ğ“ x) â†”
      (âˆ€ l : E â†’L[ğ•œ] ğ•œ, Tendsto (l âˆ˜ f) ğ“• (ğ“ (l x))) âˆ§ Tendsto (â€–f Â·â€–) ğ“• (ğ“ â€–xâ€–) := by
  -- The forward direction is obvious
  refine âŸ¨fun H â†¦ âŸ¨fun l â†¦ l.continuous.tendsto _ |>.comp H, H.normâŸ©, fun âŸ¨Hdual, HnormâŸ© â†¦ ?_âŸ©
  -- For the backward direction, the case `x = 0` is trivial.
  rcases eq_or_ne x 0 with (rfl|x_nz)
  Â· rwa [norm_zero, â† tendsto_zero_iff_norm_tendsto_zero] at Hnorm
  -- If `x â‰  0`, pick a linear form `l` with `â€–lâ€– = 1` and `l x = â€–xâ€–`
  Â· rcases exists_dual_vector ğ•œ x x_nz with âŸ¨l, norm_l, l_xâŸ©
    rw [Uniform.tendsto_nhds_left]
  -- By uniform convexity and since `â€–f iâ€–` tends to `â€–xâ€–`,
  -- it suffices to show that `â€–f i + xâ€–` tends to `2 * â€–xâ€–`.
    refine tendsto_uniformity_of_norm_add Hnorm tendsto_const_nhds ?_
  -- This follows from `â€–l (f i + x)â€– â‰¤ â€–f i + xâ€– â‰¤ â€–f iâ€– + â€–xâ€–`.
    refine tendsto_of_tendsto_of_tendsto_of_le_of_le (g := fun i â†¦ â€–l (f i + x)â€–)
      ?_ (Hnorm.add tendsto_const_nhds) (fun i â†¦ ?_) (fun i â†¦ norm_add_le _ _)
    Â· simpa [l_x, â† two_smul â„] using (Hdual l |>.add <| tendsto_const_nhds (x := l x)).norm
    Â· simpa using l.le_of_opNorm_le norm_l.le (f i + x)
