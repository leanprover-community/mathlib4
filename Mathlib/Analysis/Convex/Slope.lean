/-
Copyright (c) 2021 Yury Kudriashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudriashov, Malo JaffrÃ©
-/
import Mathlib.Analysis.Convex.Function
import Mathlib.Tactic.FieldSimp
import Mathlib.Tactic.Linarith

#align_import analysis.convex.slope from "leanprover-community/mathlib"@"a8b2226cfb0a79f5986492053fc49b1a0c6aeffb"

/-!
# Slopes of convex functions

This file relates convexity/concavity of functions in a linearly ordered field and the monotonicity
of their slopes.

The main use is to show convexity/concavity from monotonicity of the derivative.
-/


variable {ğ•œ : Type*} [LinearOrderedField ğ•œ] {s : Set ğ•œ} {f : ğ•œ â†’ ğ•œ}

/-- If `f : ğ•œ â†’ ğ•œ` is convex, then for any three points `x < y < z` the slope of the secant line of
`f` on `[x, y]` is less than the slope of the secant line of `f` on `[x, z]`. -/
theorem ConvexOn.slope_mono_adjacent (hf : ConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x < y) (hyz : y < z) : (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y) := by
  have hxz := hxy.trans hyz
  -- âŠ¢ (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)
  rw [â† sub_pos] at hxy hxz hyz
  -- âŠ¢ (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)
  suffices f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y) by
    ring_nf at this âŠ¢
    linarith
  set a := (z - y) / (z - x)
  -- âŠ¢ f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y)
  set b := (y - x) / (z - x)
  -- âŠ¢ f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y)
  have hy : a â€¢ x + b â€¢ z = y := by field_simp; ring
  -- âŠ¢ f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y)
  have key :=
    hf.2 hx hz (show 0 â‰¤ a by apply div_nonneg <;> linarith)
      (show 0 â‰¤ b by apply div_nonneg <;> linarith)
      (show a + b = 1 by field_simp)
  rw [hy] at key
  -- âŠ¢ f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y)
  replace key := mul_le_mul_of_nonneg_left key hxz.le
  -- âŠ¢ f y / (y - x) + f y / (z - y) â‰¤ f x / (y - x) + f z / (z - y)
  field_simp [mul_comm (z - x) _] at key âŠ¢
  -- âŠ¢ (f y * (z - y) + f y * (y - x)) / ((y - x) * (z - y)) â‰¤ (f x * (z - y) + f z â€¦
  rw [div_le_div_right]
  -- âŠ¢ f y * (z - y) + f y * (y - x) â‰¤ f x * (z - y) + f z * (y - x)
  Â· linarith
    -- ğŸ‰ no goals
  Â· nlinarith
    -- ğŸ‰ no goals
#align convex_on.slope_mono_adjacent ConvexOn.slope_mono_adjacent

/-- If `f : ğ•œ â†’ ğ•œ` is concave, then for any three points `x < y < z` the slope of the secant line of
`f` on `[x, y]` is greater than the slope of the secant line of `f` on `[x, z]`. -/
theorem ConcaveOn.slope_anti_adjacent (hf : ConcaveOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x < y) (hyz : y < z) : (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x) := by
  have := neg_le_neg (ConvexOn.slope_mono_adjacent hf.neg hx hz hxy hyz)
  -- âŠ¢ (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)
  simp only [Pi.neg_apply, â† neg_div, neg_sub', neg_neg] at this
  -- âŠ¢ (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)
  exact this
  -- ğŸ‰ no goals
#align concave_on.slope_anti_adjacent ConcaveOn.slope_anti_adjacent

/-- If `f : ğ•œ â†’ ğ•œ` is strictly convex, then for any three points `x < y < z` the slope of the
secant line of `f` on `[x, y]` is strictly less than the slope of the secant line of `f` on
`[x, z]`. -/
theorem StrictConvexOn.slope_strict_mono_adjacent (hf : StrictConvexOn ğ•œ s f) {x y z : ğ•œ}
    (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) :
    (f y - f x) / (y - x) < (f z - f y) / (z - y) := by
  have hxz := hxy.trans hyz
  -- âŠ¢ (f y - f x) / (y - x) < (f z - f y) / (z - y)
  have hxz' := hxz.ne
  -- âŠ¢ (f y - f x) / (y - x) < (f z - f y) / (z - y)
  rw [â† sub_pos] at hxy hxz hyz
  -- âŠ¢ (f y - f x) / (y - x) < (f z - f y) / (z - y)
  suffices f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y) by
    ring_nf at this âŠ¢
    linarith
  set a := (z - y) / (z - x)
  -- âŠ¢ f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y)
  set b := (y - x) / (z - x)
  -- âŠ¢ f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y)
  have hy : a â€¢ x + b â€¢ z = y := by field_simp; ring
  -- âŠ¢ f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y)
  have key :=
    hf.2 hx hz hxz' (div_pos hyz hxz) (div_pos hxy hxz)
      (show a + b = 1 by field_simp)
  rw [hy] at key
  -- âŠ¢ f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y)
  replace key := mul_lt_mul_of_pos_left key hxz
  -- âŠ¢ f y / (y - x) + f y / (z - y) < f x / (y - x) + f z / (z - y)
  field_simp [mul_comm (z - x) _] at key âŠ¢
  -- âŠ¢ (f y * (z - y) + f y * (y - x)) / ((y - x) * (z - y)) < (f x * (z - y) + f z â€¦
  rw [div_lt_div_right]
  -- âŠ¢ f y * (z - y) + f y * (y - x) < f x * (z - y) + f z * (y - x)
  Â· linarith
    -- ğŸ‰ no goals
  Â· nlinarith
    -- ğŸ‰ no goals
#align strict_convex_on.slope_strict_mono_adjacent StrictConvexOn.slope_strict_mono_adjacent

/-- If `f : ğ•œ â†’ ğ•œ` is strictly concave, then for any three points `x < y < z` the slope of the
secant line of `f` on `[x, y]` is strictly greater than the slope of the secant line of `f` on
`[x, z]`. -/
theorem StrictConcaveOn.slope_anti_adjacent (hf : StrictConcaveOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s)
    (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) : (f z - f y) / (z - y) < (f y - f x) / (y - x) := by
  have := neg_lt_neg (StrictConvexOn.slope_strict_mono_adjacent hf.neg hx hz hxy hyz)
  -- âŠ¢ (f z - f y) / (z - y) < (f y - f x) / (y - x)
  simp only [Pi.neg_apply, â† neg_div, neg_sub', neg_neg] at this
  -- âŠ¢ (f z - f y) / (z - y) < (f y - f x) / (y - x)
  exact this
  -- ğŸ‰ no goals
#align strict_concave_on.slope_anti_adjacent StrictConcaveOn.slope_anti_adjacent

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
less than the slope of the secant line of `f` on `[x, z]`, then `f` is convex. -/
theorem convexOn_of_slope_mono_adjacent (hs : Convex ğ•œ s)
    (hf :
      âˆ€ {x y z : ğ•œ},
        x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y)) :
    ConvexOn ğ•œ s f :=
  LinearOrder.convexOn_of_lt hs fun x hx z hz hxz a b ha hb hab => by
    let y := a * x + b * z
    -- âŠ¢ f (a â€¢ x + b â€¢ z) â‰¤ a â€¢ f x + b â€¢ f z
    have hxy : x < y := by
      rw [â† one_mul x, â† hab, add_mul]
      exact add_lt_add_left ((mul_lt_mul_left hb).2 hxz) _
    have hyz : y < z := by
      rw [â† one_mul z, â† hab, add_mul]
      exact add_lt_add_right ((mul_lt_mul_left ha).2 hxz) _
    have : (f y - f x) * (z - y) â‰¤ (f z - f y) * (y - x) :=
      (div_le_div_iff (sub_pos.2 hxy) (sub_pos.2 hyz)).1 (hf hx hz hxy hyz)
    have hxz : 0 < z - x := sub_pos.2 (hxy.trans hyz)
    -- âŠ¢ f (a â€¢ x + b â€¢ z) â‰¤ a â€¢ f x + b â€¢ f z
    have ha : (z - y) / (z - x) = a := by
      rw [eq_comm, â† sub_eq_iff_eq_add'] at hab
      simp_rw [div_eq_iff hxz.ne', â† hab]
      ring
    have hb : (y - x) / (z - x) = b := by
      rw [eq_comm, â† sub_eq_iff_eq_add] at hab
      simp_rw [div_eq_iff hxz.ne', â† hab]
      ring
    rwa [sub_mul, sub_mul, sub_le_iff_le_add', â† add_sub_assoc, le_sub_iff_add_le, â† mul_add,
      sub_add_sub_cancel, â† le_div_iff hxz, add_div, mul_div_assoc, mul_div_assoc, mul_comm (f x),
      mul_comm (f z), ha, hb] at this
#align convex_on_of_slope_mono_adjacent convexOn_of_slope_mono_adjacent

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
greater than the slope of the secant line of `f` on `[x, z]`, then `f` is concave. -/
theorem concaveOn_of_slope_anti_adjacent (hs : Convex ğ•œ s)
    (hf :
      âˆ€ {x y z : ğ•œ},
        x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x)) :
    ConcaveOn ğ•œ s f := by
  rw [â† neg_convexOn_iff]
  -- âŠ¢ ConvexOn ğ•œ s (-f)
  refine' convexOn_of_slope_mono_adjacent hs fun hx hz hxy hyz => _
  -- âŠ¢ ((-f) yâœ - (-f) xâœ) / (yâœ - xâœ) â‰¤ ((-f) zâœ - (-f) yâœ) / (zâœ - yâœ)
  rw [â† neg_le_neg_iff]
  -- âŠ¢ -(((-f) zâœ - (-f) yâœ) / (zâœ - yâœ)) â‰¤ -(((-f) yâœ - (-f) xâœ) / (yâœ - xâœ))
  simp_rw [â† neg_div, neg_sub, Pi.neg_apply, neg_sub_neg]
  -- âŠ¢ (f zâœ - f yâœ) / (zâœ - yâœ) â‰¤ (f yâœ - f xâœ) / (yâœ - xâœ)
  exact hf hx hz hxy hyz
  -- ğŸ‰ no goals
#align concave_on_of_slope_anti_adjacent concaveOn_of_slope_anti_adjacent

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
strictly less than the slope of the secant line of `f` on `[x, z]`, then `f` is strictly convex. -/
theorem strictConvexOn_of_slope_strict_mono_adjacent (hs : Convex ğ•œ s)
    (hf :
      âˆ€ {x y z : ğ•œ},
        x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f y - f x) / (y - x) < (f z - f y) / (z - y)) :
    StrictConvexOn ğ•œ s f :=
  LinearOrder.strictConvexOn_of_lt hs fun x hx z hz hxz a b ha hb hab => by
    let y := a * x + b * z
    -- âŠ¢ f (a â€¢ x + b â€¢ z) < a â€¢ f x + b â€¢ f z
    have hxy : x < y := by
      rw [â† one_mul x, â† hab, add_mul]
      exact add_lt_add_left ((mul_lt_mul_left hb).2 hxz) _
    have hyz : y < z := by
      rw [â† one_mul z, â† hab, add_mul]
      exact add_lt_add_right ((mul_lt_mul_left ha).2 hxz) _
    have : (f y - f x) * (z - y) < (f z - f y) * (y - x) :=
      (div_lt_div_iff (sub_pos.2 hxy) (sub_pos.2 hyz)).1 (hf hx hz hxy hyz)
    have hxz : 0 < z - x := sub_pos.2 (hxy.trans hyz)
    -- âŠ¢ f (a â€¢ x + b â€¢ z) < a â€¢ f x + b â€¢ f z
    have ha : (z - y) / (z - x) = a := by
      rw [eq_comm, â† sub_eq_iff_eq_add'] at hab
      simp_rw [div_eq_iff hxz.ne', â† hab]
      ring
    have hb : (y - x) / (z - x) = b := by
      rw [eq_comm, â† sub_eq_iff_eq_add] at hab
      simp_rw [div_eq_iff hxz.ne', â† hab]
      ring
    rwa [sub_mul, sub_mul, sub_lt_iff_lt_add', â† add_sub_assoc, lt_sub_iff_add_lt, â† mul_add,
      sub_add_sub_cancel, â† lt_div_iff hxz, add_div, mul_div_assoc, mul_div_assoc, mul_comm (f x),
      mul_comm (f z), ha, hb] at this
#align strict_convex_on_of_slope_strict_mono_adjacent strictConvexOn_of_slope_strict_mono_adjacent

/-- If for any three points `x < y < z`, the slope of the secant line of `f : ğ•œ â†’ ğ•œ` on `[x, y]` is
strictly greater than the slope of the secant line of `f` on `[x, z]`, then `f` is strictly concave.
-/
theorem strictConcaveOn_of_slope_strict_anti_adjacent (hs : Convex ğ•œ s)
    (hf :
      âˆ€ {x y z : ğ•œ},
        x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f z - f y) / (z - y) < (f y - f x) / (y - x)) :
    StrictConcaveOn ğ•œ s f := by
  rw [â† neg_strictConvexOn_iff]
  -- âŠ¢ StrictConvexOn ğ•œ s (-f)
  refine' strictConvexOn_of_slope_strict_mono_adjacent hs fun hx hz hxy hyz => _
  -- âŠ¢ ((-f) yâœ - (-f) xâœ) / (yâœ - xâœ) < ((-f) zâœ - (-f) yâœ) / (zâœ - yâœ)
  rw [â† neg_lt_neg_iff]
  -- âŠ¢ -(((-f) zâœ - (-f) yâœ) / (zâœ - yâœ)) < -(((-f) yâœ - (-f) xâœ) / (yâœ - xâœ))
  simp_rw [â† neg_div, neg_sub, Pi.neg_apply, neg_sub_neg]
  -- âŠ¢ (f zâœ - f yâœ) / (zâœ - yâœ) < (f yâœ - f xâœ) / (yâœ - xâœ)
  exact hf hx hz hxy hyz
  -- ğŸ‰ no goals
#align strict_concave_on_of_slope_strict_anti_adjacent strictConcaveOn_of_slope_strict_anti_adjacent

/-- A function `f : ğ•œ â†’ ğ•œ` is convex iff for any three points `x < y < z` the slope of the secant
line of `f` on `[x, y]` is less than the slope of the secant line of `f` on `[x, z]`. -/
theorem convexOn_iff_slope_mono_adjacent :
    ConvexOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§ âˆ€ â¦ƒx y z : ğ•œâ¦„,
          x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f y - f x) / (y - x) â‰¤ (f z - f y) / (z - y) :=
  âŸ¨fun h => âŸ¨h.1, fun _ _ _ => h.slope_mono_adjacentâŸ©, fun h =>
    convexOn_of_slope_mono_adjacent h.1 (@fun _ _ _ hx hy => h.2 hx hy)âŸ©
#align convex_on_iff_slope_mono_adjacent convexOn_iff_slope_mono_adjacent

/-- A function `f : ğ•œ â†’ ğ•œ` is concave iff for any three points `x < y < z` the slope of the secant
line of `f` on `[x, y]` is greater than the slope of the secant line of `f` on `[x, z]`. -/
theorem concaveOn_iff_slope_anti_adjacent :
    ConcaveOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§
        âˆ€ â¦ƒx y z : ğ•œâ¦„,
          x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f z - f y) / (z - y) â‰¤ (f y - f x) / (y - x) :=
  âŸ¨fun h => âŸ¨h.1, fun _ _ _ => h.slope_anti_adjacentâŸ©, fun h =>
    concaveOn_of_slope_anti_adjacent h.1 (@fun _ _ _ hx hy => h.2 hx hy)âŸ©
#align concave_on_iff_slope_anti_adjacent concaveOn_iff_slope_anti_adjacent

/-- A function `f : ğ•œ â†’ ğ•œ` is strictly convex iff for any three points `x < y < z` the slope of
the secant line of `f` on `[x, y]` is strictly less than the slope of the secant line of `f` on
`[x, z]`. -/
theorem strictConvexOn_iff_slope_strict_mono_adjacent :
    StrictConvexOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§
        âˆ€ â¦ƒx y z : ğ•œâ¦„,
          x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f y - f x) / (y - x) < (f z - f y) / (z - y) :=
  âŸ¨fun h => âŸ¨h.1, fun _ _ _ => h.slope_strict_mono_adjacentâŸ©, fun h =>
    strictConvexOn_of_slope_strict_mono_adjacent h.1 (@fun _ _ _ hx hy => h.2 hx hy)âŸ©
#align strict_convex_on_iff_slope_strict_mono_adjacent strictConvexOn_iff_slope_strict_mono_adjacent

/-- A function `f : ğ•œ â†’ ğ•œ` is strictly concave iff for any three points `x < y < z` the slope of
the secant line of `f` on `[x, y]` is strictly greater than the slope of the secant line of `f` on
`[x, z]`. -/
theorem strictConcaveOn_iff_slope_strict_anti_adjacent :
    StrictConcaveOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§
        âˆ€ â¦ƒx y z : ğ•œâ¦„,
          x âˆˆ s â†’ z âˆˆ s â†’ x < y â†’ y < z â†’ (f z - f y) / (z - y) < (f y - f x) / (y - x) :=
  âŸ¨fun h => âŸ¨h.1, fun _ _ _ => h.slope_anti_adjacentâŸ©, fun h =>
    strictConcaveOn_of_slope_strict_anti_adjacent h.1 (@fun _ _ _ hx hy => h.2 hx hy)âŸ©
#align strict_concave_on_iff_slope_strict_anti_adjacent strictConcaveOn_iff_slope_strict_anti_adjacent

theorem ConvexOn.secant_mono_aux1 (hf : ConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x < y) (hyz : y < z) : (z - x) * f y â‰¤ (z - y) * f x + (y - x) * f z := by
  have hxy' : 0 < y - x := by linarith
  -- âŠ¢ (z - x) * f y â‰¤ (z - y) * f x + (y - x) * f z
  have hyz' : 0 < z - y := by linarith
  -- âŠ¢ (z - x) * f y â‰¤ (z - y) * f x + (y - x) * f z
  have hxz' : 0 < z - x := by linarith
  -- âŠ¢ (z - x) * f y â‰¤ (z - y) * f x + (y - x) * f z
  rw [â† le_div_iff' hxz']
  -- âŠ¢ f y â‰¤ ((z - y) * f x + (y - x) * f z) / (z - x)
  have ha : 0 â‰¤ (z - y) / (z - x) := by positivity
  -- âŠ¢ f y â‰¤ ((z - y) * f x + (y - x) * f z) / (z - x)
  have hb : 0 â‰¤ (y - x) / (z - x) := by positivity
  -- âŠ¢ f y â‰¤ ((z - y) * f x + (y - x) * f z) / (z - x)
  calc
    f y = f ((z - y) / (z - x) * x + (y - x) / (z - x) * z) := ?_
    _ â‰¤ (z - y) / (z - x) * f x + (y - x) / (z - x) * f z := hf.2 hx hz ha hb ?_
    _ = ((z - y) * f x + (y - x) * f z) / (z - x) := ?_
  Â· congr 1
    -- âŠ¢ y = (z - y) / (z - x) * x + (y - x) / (z - x) * z
    field_simp
    -- âŠ¢ y * (z - x) = (z - y) * x + (y - x) * z
    ring
    -- ğŸ‰ no goals
  Â· -- Porting note: this `show` wasn't needed in Lean 3
    show (z - y) / (z - x) + (y - x) / (z - x) = 1
    -- âŠ¢ (z - y) / (z - x) + (y - x) / (z - x) = 1
    field_simp
    -- ğŸ‰ no goals
  Â· field_simp
    -- ğŸ‰ no goals
#align convex_on.secant_mono_aux1 ConvexOn.secant_mono_aux1

theorem ConvexOn.secant_mono_aux2 (hf : ConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x < y) (hyz : y < z) : (f y - f x) / (y - x) â‰¤ (f z - f x) / (z - x) := by
  have hxy' : 0 < y - x := by linarith
  -- âŠ¢ (f y - f x) / (y - x) â‰¤ (f z - f x) / (z - x)
  have hxz' : 0 < z - x := by linarith
  -- âŠ¢ (f y - f x) / (y - x) â‰¤ (f z - f x) / (z - x)
  rw [div_le_div_iff hxy' hxz']
  -- âŠ¢ (f y - f x) * (z - x) â‰¤ (f z - f x) * (y - x)
  linarith only [hf.secant_mono_aux1 hx hz hxy hyz]
  -- ğŸ‰ no goals
#align convex_on.secant_mono_aux2 ConvexOn.secant_mono_aux2

theorem ConvexOn.secant_mono_aux3 (hf : ConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x < y) (hyz : y < z) : (f z - f x) / (z - x) â‰¤ (f z - f y) / (z - y) := by
  have hyz' : 0 < z - y := by linarith
  -- âŠ¢ (f z - f x) / (z - x) â‰¤ (f z - f y) / (z - y)
  have hxz' : 0 < z - x := by linarith
  -- âŠ¢ (f z - f x) / (z - x) â‰¤ (f z - f y) / (z - y)
  rw [div_le_div_iff hxz' hyz']
  -- âŠ¢ (f z - f x) * (z - y) â‰¤ (f z - f y) * (z - x)
  linarith only [hf.secant_mono_aux1 hx hz hxy hyz]
  -- ğŸ‰ no goals
#align convex_on.secant_mono_aux3 ConvexOn.secant_mono_aux3

theorem ConvexOn.secant_mono (hf : ConvexOn ğ•œ s f) {a x y : ğ•œ} (ha : a âˆˆ s) (hx : x âˆˆ s)
    (hy : y âˆˆ s) (hxa : x â‰  a) (hya : y â‰  a) (hxy : x â‰¤ y) :
    (f x - f a) / (x - a) â‰¤ (f y - f a) / (y - a) := by
  rcases eq_or_lt_of_le hxy with (rfl | hxy)
  -- âŠ¢ (f x - f a) / (x - a) â‰¤ (f x - f a) / (x - a)
  Â· simp
    -- ğŸ‰ no goals
  cases' lt_or_gt_of_ne hxa with hxa hxa
  -- âŠ¢ (f x - f a) / (x - a) â‰¤ (f y - f a) / (y - a)
  Â· cases' lt_or_gt_of_ne hya with hya hya
    -- âŠ¢ (f x - f a) / (x - a) â‰¤ (f y - f a) / (y - a)
    Â· convert hf.secant_mono_aux3 hx ha hxy hya using 1 <;> rw [â† neg_div_neg_eq] <;> field_simp
      -- âŠ¢ (f x - f a) / (x - a) = (f a - f x) / (a - x)
                                                            -- âŠ¢ -(f x - f a) / -(x - a) = (f a - f x) / (a - x)
                                                            -- âŠ¢ -(f y - f a) / -(y - a) = (f a - f y) / (a - y)
                                                                                      -- ğŸ‰ no goals
                                                                                      -- ğŸ‰ no goals
    Â· convert hf.slope_mono_adjacent hx hy hxa hya using 1
      -- âŠ¢ (f x - f a) / (x - a) = (f a - f x) / (a - x)
      rw [â† neg_div_neg_eq]; field_simp
      -- âŠ¢ -(f x - f a) / -(x - a) = (f a - f x) / (a - x)
                             -- ğŸ‰ no goals
  Â· exact hf.secant_mono_aux2 ha hy hxa hxy
    -- ğŸ‰ no goals
#align convex_on.secant_mono ConvexOn.secant_mono

theorem StrictConvexOn.secant_strict_mono_aux1 (hf : StrictConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s)
    (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) : (z - x) * f y < (z - y) * f x + (y - x) * f z := by
  have hxy' : 0 < y - x := by linarith
  -- âŠ¢ (z - x) * f y < (z - y) * f x + (y - x) * f z
  have hyz' : 0 < z - y := by linarith
  -- âŠ¢ (z - x) * f y < (z - y) * f x + (y - x) * f z
  have hxz' : 0 < z - x := by linarith
  -- âŠ¢ (z - x) * f y < (z - y) * f x + (y - x) * f z
  rw [â† lt_div_iff' hxz']
  -- âŠ¢ f y < ((z - y) * f x + (y - x) * f z) / (z - x)
  have ha : 0 < (z - y) / (z - x) := by positivity
  -- âŠ¢ f y < ((z - y) * f x + (y - x) * f z) / (z - x)
  have hb : 0 < (y - x) / (z - x) := by positivity
  -- âŠ¢ f y < ((z - y) * f x + (y - x) * f z) / (z - x)
  calc
    f y = f ((z - y) / (z - x) * x + (y - x) / (z - x) * z) := ?_
    _ < (z - y) / (z - x) * f x + (y - x) / (z - x) * f z := (hf.2 hx hz (by linarith) ha hb ?_)
    _ = ((z - y) * f x + (y - x) * f z) / (z - x) := ?_
  Â· congr 1
    -- âŠ¢ y = (z - y) / (z - x) * x + (y - x) / (z - x) * z
    field_simp
    -- âŠ¢ y * (z - x) = (z - y) * x + (y - x) * z
    ring
    -- ğŸ‰ no goals
  Â· -- Porting note: this `show` wasn't needed in Lean 3
    show (z - y) / (z - x) + (y - x) / (z - x) = 1
    -- âŠ¢ (z - y) / (z - x) + (y - x) / (z - x) = 1
    field_simp
    -- ğŸ‰ no goals
  Â· field_simp
    -- ğŸ‰ no goals
#align strict_convex_on.secant_strict_mono_aux1 StrictConvexOn.secant_strict_mono_aux1

theorem StrictConvexOn.secant_strict_mono_aux2 (hf : StrictConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s)
    (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) : (f y - f x) / (y - x) < (f z - f x) / (z - x) := by
  have hxy' : 0 < y - x := by linarith
  -- âŠ¢ (f y - f x) / (y - x) < (f z - f x) / (z - x)
  have hxz' : 0 < z - x := by linarith
  -- âŠ¢ (f y - f x) / (y - x) < (f z - f x) / (z - x)
  rw [div_lt_div_iff hxy' hxz']
  -- âŠ¢ (f y - f x) * (z - x) < (f z - f x) * (y - x)
  linarith only [hf.secant_strict_mono_aux1 hx hz hxy hyz]
  -- ğŸ‰ no goals
#align strict_convex_on.secant_strict_mono_aux2 StrictConvexOn.secant_strict_mono_aux2

theorem StrictConvexOn.secant_strict_mono_aux3 (hf : StrictConvexOn ğ•œ s f) {x y z : ğ•œ} (hx : x âˆˆ s)
    (hz : z âˆˆ s) (hxy : x < y) (hyz : y < z) : (f z - f x) / (z - x) < (f z - f y) / (z - y) := by
  have hyz' : 0 < z - y := by linarith
  -- âŠ¢ (f z - f x) / (z - x) < (f z - f y) / (z - y)
  have hxz' : 0 < z - x := by linarith
  -- âŠ¢ (f z - f x) / (z - x) < (f z - f y) / (z - y)
  rw [div_lt_div_iff hxz' hyz']
  -- âŠ¢ (f z - f x) * (z - y) < (f z - f y) * (z - x)
  linarith only [hf.secant_strict_mono_aux1 hx hz hxy hyz]
  -- ğŸ‰ no goals
#align strict_convex_on.secant_strict_mono_aux3 StrictConvexOn.secant_strict_mono_aux3

theorem StrictConvexOn.secant_strict_mono (hf : StrictConvexOn ğ•œ s f) {a x y : ğ•œ} (ha : a âˆˆ s)
    (hx : x âˆˆ s) (hy : y âˆˆ s) (hxa : x â‰  a) (hya : y â‰  a) (hxy : x < y) :
    (f x - f a) / (x - a) < (f y - f a) / (y - a) := by
  cases' lt_or_gt_of_ne hxa with hxa hxa
  -- âŠ¢ (f x - f a) / (x - a) < (f y - f a) / (y - a)
  Â· cases' lt_or_gt_of_ne hya with hya hya
    -- âŠ¢ (f x - f a) / (x - a) < (f y - f a) / (y - a)
    Â· convert hf.secant_strict_mono_aux3 hx ha hxy hya using 1 <;> rw [â† neg_div_neg_eq] <;>
      -- âŠ¢ (f x - f a) / (x - a) = (f a - f x) / (a - x)
                                                                   -- âŠ¢ -(f x - f a) / -(x - a) = (f a - f x) / (a - x)
                                                                   -- âŠ¢ -(f y - f a) / -(y - a) = (f a - f y) / (a - y)
        field_simp
        -- ğŸ‰ no goals
        -- ğŸ‰ no goals
    Â· convert hf.slope_strict_mono_adjacent hx hy hxa hya using 1
      -- âŠ¢ (f x - f a) / (x - a) = (f a - f x) / (a - x)
      rw [â† neg_div_neg_eq]; field_simp
      -- âŠ¢ -(f x - f a) / -(x - a) = (f a - f x) / (a - x)
                             -- ğŸ‰ no goals
  Â· exact hf.secant_strict_mono_aux2 ha hy hxa hxy
    -- ğŸ‰ no goals
#align strict_convex_on.secant_strict_mono StrictConvexOn.secant_strict_mono

theorem StrictConcaveOn.secant_strict_mono (hf : StrictConcaveOn ğ•œ s f) {a x y : ğ•œ} (ha : a âˆˆ s)
    (hx : x âˆˆ s) (hy : y âˆˆ s) (hxa : x â‰  a) (hya : y â‰  a) (hxy : x < y) :
    (f y - f a) / (y - a) < (f x - f a) / (x - a) := by
  have key := hf.neg.secant_strict_mono ha hx hy hxa hya hxy
  -- âŠ¢ (f y - f a) / (y - a) < (f x - f a) / (x - a)
  simp only [Pi.neg_apply] at key
  -- âŠ¢ (f y - f a) / (y - a) < (f x - f a) / (x - a)
  rw [â† neg_lt_neg_iff]
  -- âŠ¢ -((f x - f a) / (x - a)) < -((f y - f a) / (y - a))
  convert key using 1 <;> field_simp <;> ring
  -- âŠ¢ -((f x - f a) / (x - a)) = (-f x - -f a) / (x - a)
                          -- âŠ¢ (f a - f x) / (x - a) = (-f x + f a) / (x - a)
                          -- âŠ¢ (f a - f y) / (y - a) = (-f y + f a) / (y - a)
                                         -- ğŸ‰ no goals
                                         -- ğŸ‰ no goals
#align strict_concave_on.secant_strict_mono StrictConcaveOn.secant_strict_mono

/-- If `f` is convex on a set `s` in a linearly ordered field, and `f x < f y` for two points
`x < y` in `s`, then `f` is strictly monotone on `s âˆ© [y, âˆ)`. -/
theorem ConvexOn.strict_mono_of_lt (hf : ConvexOn ğ•œ s f) {x y : ğ•œ} (hx : x âˆˆ s) (hxy : x < y)
    (hxy' : f x < f y) : StrictMonoOn f (s âˆ© Set.Ici y) := by
  intro u hu v hv huv
  -- âŠ¢ f u < f v
  have step1 : âˆ€ {z : ğ•œ}, z âˆˆ s âˆ© Set.Ioi y â†’ f y < f z := by
    intros z hz
    refine hf.lt_right_of_left_lt hx hz.1 ?_ hxy'
    rw [openSegment_eq_Ioo (hxy.trans hz.2)]
    exact âŸ¨hxy, hz.2âŸ©
  rcases eq_or_lt_of_le hu.2 with (rfl | hu2)
  -- âŠ¢ f y < f v
  Â· exact step1 âŸ¨hv.1, huvâŸ©
    -- ğŸ‰ no goals
  Â· refine' hf.lt_right_of_left_lt _ hv.1 _ (step1 âŸ¨hu.1, hu2âŸ©)
    -- âŠ¢ y âˆˆ s
    Â· apply hf.1.segment_subset hx hu.1
      -- âŠ¢ y âˆˆ segment ğ•œ x u
      rw [segment_eq_Icc (hxy.le.trans hu.2)]
      -- âŠ¢ y âˆˆ Set.Icc x u
      exact âŸ¨hxy.le, hu.2âŸ©
      -- ğŸ‰ no goals
    Â· rw [openSegment_eq_Ioo (hu2.trans huv)]
      -- âŠ¢ u âˆˆ Set.Ioo y v
      exact âŸ¨hu2, huvâŸ©
      -- ğŸ‰ no goals
#align convex_on.strict_mono_of_lt ConvexOn.strict_mono_of_lt
