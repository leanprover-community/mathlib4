/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.Convex.Combination
import Mathlib.Analysis.Convex.Extreme

/-!
# Convex independence

This file defines convex independent families of points.

Convex independence is closely related to affine independence. In both cases, no point can be
written as a combination of others. When the combination is affine (that is, any coefficients), this
yields affine independence. When the combination is convex (that is, all coefficients are
nonnegative), then this yields convex independence. In particular, affine independence implies
convex independence.

## Main declarations

* `ConvexIndependent p`: Convex independence of the indexed family `p : Î¹ â†’ E`. Every point of the
  family only belongs to convex hulls of sets of the family containing it.
* `convexIndependent_iff_finset`: CarathÃ©odory's theorem allows us to only check finsets to
  conclude convex independence.
* `Convex.convexIndependent_extremePoints`: Extreme points of a convex set are convex independent.

## References

* https://en.wikipedia.org/wiki/Convex_position

## TODO

Prove `AffineIndependent.convexIndependent`. This requires some glue between `affineCombination`
and `Finset.centerMass`.

## Tags

independence, convex position
-/


open Affine Finset Function

variable {ğ•œ E Î¹ : Type*}

section OrderedSemiring

variable (ğ•œ) [Semiring ğ•œ] [PartialOrder ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- An indexed family is said to be convex independent if every point only belongs to convex hulls
of sets containing it. -/
def ConvexIndependent (p : Î¹ â†’ E) : Prop :=
  âˆ€ (s : Set Î¹) (x : Î¹), p x âˆˆ convexHull ğ•œ (p '' s) â†’ x âˆˆ s

variable {ğ•œ}

/-- A family with at most one point is convex independent. -/
theorem Subsingleton.convexIndependent [Subsingleton Î¹] (p : Î¹ â†’ E) : ConvexIndependent ğ•œ p := by
  intro s x hx
  have : (convexHull ğ•œ (p '' s)).Nonempty := âŸ¨p x, hxâŸ©
  rw [convexHull_nonempty_iff, Set.image_nonempty] at this
  rwa [Subsingleton.mem_iff_nonempty]

/-- A convex independent family is injective. -/
protected theorem ConvexIndependent.injective {p : Î¹ â†’ E} (hc : ConvexIndependent ğ•œ p) :
    Function.Injective p := by
  refine fun i j hij â†¦ hc {j} i ?_
  rw [hij, Set.image_singleton, convexHull_singleton]
  exact Set.mem_singleton _

/-- If a family is convex independent, so is any subfamily given by composition of an embedding into
index type with the original family. -/
theorem ConvexIndependent.comp_embedding {Î¹' : Type*} (f : Î¹' â†ª Î¹) {p : Î¹ â†’ E}
    (hc : ConvexIndependent ğ•œ p) : ConvexIndependent ğ•œ (p âˆ˜ f) := by
  intro s x hx
  rw [â† f.injective.mem_set_image]
  exact hc _ _ (by rwa [Set.image_image])

/-- If a family is convex independent, so is any subfamily indexed by a subtype of the index type.
-/
protected theorem ConvexIndependent.subtype {p : Î¹ â†’ E} (hc : ConvexIndependent ğ•œ p) (s : Set Î¹) :
    ConvexIndependent ğ•œ fun i : s â†¦ p i :=
  hc.comp_embedding (Embedding.subtype _)

/-- If an indexed family of points is convex independent, so is the corresponding set of points. -/
protected theorem ConvexIndependent.range {p : Î¹ â†’ E} (hc : ConvexIndependent ğ•œ p) :
    ConvexIndependent ğ•œ ((â†‘) : Set.range p â†’ E) := by
  let f : Set.range p â†’ Î¹ := fun x â†¦ x.property.choose
  have hf : âˆ€ x, p (f x) = x := fun x â†¦ x.property.choose_spec
  let fe : Set.range p â†ª Î¹ := âŸ¨f, fun xâ‚ xâ‚‚ he â†¦ Subtype.ext (hf xâ‚ â–¸ hf xâ‚‚ â–¸ he â–¸ rfl)âŸ©
  convert hc.comp_embedding fe
  ext
  rw [Embedding.coeFn_mk, comp_apply, hf]

/-- A subset of a convex independent set of points is convex independent as well. -/
protected theorem ConvexIndependent.mono {s t : Set E} (hc : ConvexIndependent ğ•œ ((â†‘) : t â†’ E))
    (hs : s âŠ† t) : ConvexIndependent ğ•œ ((â†‘) : s â†’ E) :=
  hc.comp_embedding (s.embeddingOfSubset t hs)

/-- The range of an injective indexed family of points is convex independent iff that family is. -/
theorem Function.Injective.convexIndependent_iff_set {p : Î¹ â†’ E} (hi : Function.Injective p) :
    ConvexIndependent ğ•œ ((â†‘) : Set.range p â†’ E) â†” ConvexIndependent ğ•œ p :=
  âŸ¨fun hc â†¦
    hc.comp_embedding
      (âŸ¨fun i â†¦ âŸ¨p i, Set.mem_range_self _âŸ©, fun _ _ h â†¦ hi (Subtype.mk_eq_mk.1 h)âŸ© :
        Î¹ â†ª Set.range p),
    ConvexIndependent.rangeâŸ©

/-- If a family is convex independent, a point in the family is in the convex hull of some of the
points given by a subset of the index type if and only if the point's index is in this subset. -/
@[simp]
protected theorem ConvexIndependent.mem_convexHull_iff {p : Î¹ â†’ E} (hc : ConvexIndependent ğ•œ p)
    (s : Set Î¹) (i : Î¹) : p i âˆˆ convexHull ğ•œ (p '' s) â†” i âˆˆ s :=
  âŸ¨hc _ _, fun hi â†¦ subset_convexHull ğ•œ _ (Set.mem_image_of_mem p hi)âŸ©

/-- If a family is convex independent, a point in the family is not in the convex hull of the other
points. See `convexIndependent_set_iff_notMem_convexHull_diff` for the `Set` version. -/
theorem convexIndependent_iff_notMem_convexHull_diff {p : Î¹ â†’ E} :
    ConvexIndependent ğ•œ p â†” âˆ€ i s, p i âˆ‰ convexHull ğ•œ (p '' (s \ {i})) := by
  refine âŸ¨fun hc i s h â†¦ ?_, fun h s i hi â†¦ ?_âŸ©
  Â· rw [hc.mem_convexHull_iff] at h
    exact h.2 (Set.mem_singleton _)
  Â· by_contra H
    refine h i s ?_
    rw [Set.diff_singleton_eq_self H]
    exact hi

@[deprecated (since := "2025-05-23")]
alias convexIndependent_iff_not_mem_convexHull_diff := convexIndependent_iff_notMem_convexHull_diff

theorem convexIndependent_set_iff_inter_convexHull_subset {s : Set E} :
    ConvexIndependent ğ•œ ((â†‘) : s â†’ E) â†” âˆ€ t, t âŠ† s â†’ s âˆ© convexHull ğ•œ t âŠ† t := by
  constructor
  Â· rintro hc t h x âŸ¨hxs, hxtâŸ©
    refine hc { x | â†‘x âˆˆ t } âŸ¨x, hxsâŸ© ?_
    rw [Subtype.coe_image_of_subset h]
    exact hxt
  Â· intro hc t x h
    rw [â† Subtype.coe_injective.mem_set_image]
    exact hc (t.image ((â†‘) : s â†’ E)) (Subtype.coe_image_subset s t) âŸ¨x.prop, hâŸ©

/-- If a set is convex independent, a point in the set is not in the convex hull of the other
points. See `convexIndependent_iff_notMem_convexHull_diff` for the indexed family version. -/
theorem convexIndependent_set_iff_notMem_convexHull_diff {s : Set E} :
    ConvexIndependent ğ•œ ((â†‘) : s â†’ E) â†” âˆ€ x âˆˆ s, x âˆ‰ convexHull ğ•œ (s \ {x}) := by
  rw [convexIndependent_set_iff_inter_convexHull_subset]
  constructor
  Â· rintro hs x hxs hx
    exact (hs _ Set.diff_subset âŸ¨hxs, hxâŸ©).2 (Set.mem_singleton _)
  Â· rintro hs t ht x âŸ¨hxs, hxtâŸ©
    by_contra h
    exact hs _ hxs (convexHull_mono (Set.subset_diff_singleton ht h) hxt)

@[deprecated (since := "2025-05-23")]
alias convexIndependent_set_iff_not_mem_convexHull_diff :=
  convexIndependent_set_iff_notMem_convexHull_diff

end OrderedSemiring

section LinearOrderedField

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s : Set E}

open scoped Classical in
/-- To check convex independence, one only has to check finsets thanks to CarathÃ©odory's theorem. -/
theorem convexIndependent_iff_finset {p : Î¹ â†’ E} :
    ConvexIndependent ğ•œ p â†”
      âˆ€ (s : Finset Î¹) (x : Î¹), p x âˆˆ convexHull ğ•œ (s.image p : Set E) â†’ x âˆˆ s := by
  refine âŸ¨fun hc s x hx â†¦ hc s x ?_, fun h s x hx â†¦ ?_âŸ©
  Â· rwa [Finset.coe_image] at hx
  have hp : Injective p := by
    rintro a b hab
    rw [â† mem_singleton]
    refine h {b} a ?_
    rw [hab, image_singleton, coe_singleton, convexHull_singleton]
    exact Set.mem_singleton _
  rw [convexHull_eq_union_convexHull_finite_subsets] at hx
  simp_rw [Set.mem_iUnion] at hx
  obtain âŸ¨t, ht, hxâŸ© := hx
  rw [â† hp.mem_set_image]
  refine ht ?_
  suffices x âˆˆ t.preimage p hp.injOn by rwa [mem_preimage, â† mem_coe] at this
  refine h _ x ?_
  rwa [t.image_preimage p hp.injOn, filter_true_of_mem]
  exact fun y hy â†¦ s.image_subset_range p (ht <| mem_coe.2 hy)

/-! ### Extreme points -/


theorem Convex.convexIndependent_extremePoints (hs : Convex ğ•œ s) :
    ConvexIndependent ğ•œ ((â†‘) : s.extremePoints ğ•œ â†’ E) :=
  convexIndependent_set_iff_notMem_convexHull_diff.2 fun _ hx h â†¦
    (extremePoints_convexHull_subset
          (inter_extremePoints_subset_extremePoints_of_subset
            (convexHull_min (Set.diff_subset.trans extremePoints_subset) hs) âŸ¨h, hxâŸ©)).2
      (Set.mem_singleton _)

end LinearOrderedField
