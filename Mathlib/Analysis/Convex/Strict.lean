/-
Copyright (c) 2021 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Topology.Algebra.Group.Pointwise
import Mathlib.Topology.Order.Basic

/-!
# Strictly convex sets

This file defines strictly convex sets.

A set is strictly convex if the open segment between any two distinct points lies in its interior.
-/


open Set

open Convex Pointwise

variable {ğ•œ ğ• E F Î² : Type*}

open Function Set

open Convex

section OrderedSemiring

/-- A set is strictly convex if the open segment between any two distinct points lies is in its
interior. This basically means "convex and not flat on the boundary". -/
def StrictConvex (ğ•œ : Type*) {E : Type*} [Semiring ğ•œ] [PartialOrder ğ•œ] [TopologicalSpace E]
    [AddCommMonoid E] [SMul ğ•œ E] (s : Set E) : Prop :=
  s.Pairwise fun x y => âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ interior s

variable [Semiring ğ•œ] [PartialOrder ğ•œ] [TopologicalSpace E] [TopologicalSpace F]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F]

section SMul

variable [SMul ğ•œ E] [SMul ğ•œ F] (s : Set E)

variable {s}
variable {x y : E} {a b : ğ•œ}

theorem strictConvex_iff_openSegment_subset :
    StrictConvex ğ•œ s â†” s.Pairwise fun x y => openSegment ğ•œ x y âŠ† interior s :=
  forallâ‚…_congr fun _ _ _ _ _ => (openSegment_subset_iff ğ•œ).symm

theorem StrictConvex.openSegment_subset (hs : StrictConvex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s)
    (h : x â‰  y) : openSegment ğ•œ x y âŠ† interior s :=
  strictConvex_iff_openSegment_subset.1 hs hx hy h

theorem strictConvex_empty : StrictConvex ğ•œ (âˆ… : Set E) :=
  pairwise_empty _

theorem strictConvex_univ : StrictConvex ğ•œ (univ : Set E) := by
  intro x _ y _ _ a b _ _ _
  rw [interior_univ]
  exact mem_univ _

protected nonrec theorem StrictConvex.eq (hs : StrictConvex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s)
    (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) (h : a â€¢ x + b â€¢ y âˆ‰ interior s) : x = y :=
  hs.eq hx hy fun H => h <| H ha hb hab

protected theorem StrictConvex.inter {t : Set E} (hs : StrictConvex ğ•œ s) (ht : StrictConvex ğ•œ t) :
    StrictConvex ğ•œ (s âˆ© t) := by
  intro x hx y hy hxy a b ha hb hab
  rw [interior_inter]
  exact âŸ¨hs hx.1 hy.1 hxy ha hb hab, ht hx.2 hy.2 hxy ha hb habâŸ©

theorem Directed.strictConvex_iUnion {Î¹ : Sort*} {s : Î¹ â†’ Set E} (hdir : Directed (Â· âŠ† Â·) s)
    (hs : âˆ€ â¦ƒi : Î¹â¦„, StrictConvex ğ•œ (s i)) : StrictConvex ğ•œ (â‹ƒ i, s i) := by
  rintro x hx y hy hxy a b ha hb hab
  rw [mem_iUnion] at hx hy
  obtain âŸ¨i, hxâŸ© := hx
  obtain âŸ¨j, hyâŸ© := hy
  obtain âŸ¨k, hik, hjkâŸ© := hdir i j
  exact interior_mono (subset_iUnion s k) (hs (hik hx) (hjk hy) hxy ha hb hab)

theorem DirectedOn.strictConvex_sUnion {S : Set (Set E)} (hdir : DirectedOn (Â· âŠ† Â·) S)
    (hS : âˆ€ s âˆˆ S, StrictConvex ğ•œ s) : StrictConvex ğ•œ (â‹ƒâ‚€ S) := by
  rw [sUnion_eq_iUnion]
  exact (directedOn_iff_directed.1 hdir).strictConvex_iUnion fun s => hS _ s.2

end SMul

section Module

variable [Module ğ•œ E] [Module ğ•œ F] {s : Set E}

protected theorem StrictConvex.convex (hs : StrictConvex ğ•œ s) : Convex ğ•œ s :=
  convex_iff_pairwise_pos.2 fun _ hx _ hy hxy _ _ ha hb hab =>
    interior_subset <| hs hx hy hxy ha hb hab

/-- An open convex set is strictly convex. -/
protected theorem Convex.strictConvex_of_isOpen (h : IsOpen s) (hs : Convex ğ•œ s) :
    StrictConvex ğ•œ s :=
  fun _ hx _ hy _ _ _ ha hb hab => h.interior_eq.symm â–¸ hs hx hy ha.le hb.le hab

theorem IsOpen.strictConvex_iff (h : IsOpen s) : StrictConvex ğ•œ s â†” Convex ğ•œ s :=
  âŸ¨StrictConvex.convex, Convex.strictConvex_of_isOpen hâŸ©

theorem strictConvex_singleton (c : E) : StrictConvex ğ•œ ({c} : Set E) :=
  pairwise_singleton _ _

theorem Set.Subsingleton.strictConvex (hs : s.Subsingleton) : StrictConvex ğ•œ s :=
  hs.pairwise _

theorem StrictConvex.linear_image [Semiring ğ•] [Module ğ• E] [Module ğ• F]
    [LinearMap.CompatibleSMul E F ğ•œ ğ•] (hs : StrictConvex ğ•œ s) (f : E â†’â‚—[ğ•] F) (hf : IsOpenMap f) :
    StrictConvex ğ•œ (f '' s) := by
  rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© hxy a b ha hb hab
  refine hf.image_interior_subset _ âŸ¨a â€¢ x + b â€¢ y, hs hx hy (ne_of_apply_ne _ hxy) ha hb hab, ?_âŸ©
  rw [map_add, f.map_smul_of_tower a, f.map_smul_of_tower b]

theorem StrictConvex.is_linear_image (hs : StrictConvex ğ•œ s) {f : E â†’ F} (h : IsLinearMap ğ•œ f)
    (hf : IsOpenMap f) : StrictConvex ğ•œ (f '' s) :=
  hs.linear_image (h.mk' f) hf

theorem StrictConvex.linear_preimage {s : Set F} (hs : StrictConvex ğ•œ s) (f : E â†’â‚—[ğ•œ] F)
    (hf : Continuous f) (hfinj : Injective f) : StrictConvex ğ•œ (s.preimage f) := by
  intro x hx y hy hxy a b ha hb hab
  refine preimage_interior_subset_interior_preimage hf ?_
  rw [mem_preimage, f.map_add, f.map_smul, f.map_smul]
  exact hs hx hy (hfinj.ne hxy) ha hb hab

theorem StrictConvex.is_linear_preimage {s : Set F} (hs : StrictConvex ğ•œ s) {f : E â†’ F}
    (h : IsLinearMap ğ•œ f) (hf : Continuous f) (hfinj : Injective f) :
    StrictConvex ğ•œ (s.preimage f) :=
  hs.linear_preimage (h.mk' f) hf hfinj

section LinearOrderedCancelAddCommMonoid

variable [TopologicalSpace Î²] [AddCommMonoid Î²] [LinearOrder Î²] [IsOrderedCancelAddMonoid Î²]
  [OrderTopology Î²] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]

protected theorem Set.OrdConnected.strictConvex {s : Set Î²} (hs : OrdConnected s) :
    StrictConvex ğ•œ s := by
  refine strictConvex_iff_openSegment_subset.2 fun x hx y hy hxy => ?_
  rcases hxy.lt_or_gt with hlt | hlt <;> [skip; rw [openSegment_symm]] <;>
    exact
      (openSegment_subset_Ioo hlt).trans
        (isOpen_Ioo.subset_interior_iff.2 <| Ioo_subset_Icc_self.trans <| hs.out â€¹_â€º â€¹_â€º)

theorem strictConvex_Iic (r : Î²) : StrictConvex ğ•œ (Iic r) :=
  ordConnected_Iic.strictConvex

theorem strictConvex_Ici (r : Î²) : StrictConvex ğ•œ (Ici r) :=
  ordConnected_Ici.strictConvex

theorem strictConvex_Iio (r : Î²) : StrictConvex ğ•œ (Iio r) :=
  ordConnected_Iio.strictConvex

theorem strictConvex_Ioi (r : Î²) : StrictConvex ğ•œ (Ioi r) :=
  ordConnected_Ioi.strictConvex

theorem strictConvex_Icc (r s : Î²) : StrictConvex ğ•œ (Icc r s) :=
  ordConnected_Icc.strictConvex

theorem strictConvex_Ioo (r s : Î²) : StrictConvex ğ•œ (Ioo r s) :=
  ordConnected_Ioo.strictConvex

theorem strictConvex_Ico (r s : Î²) : StrictConvex ğ•œ (Ico r s) :=
  ordConnected_Ico.strictConvex

theorem strictConvex_Ioc (r s : Î²) : StrictConvex ğ•œ (Ioc r s) :=
  ordConnected_Ioc.strictConvex

theorem strictConvex_uIcc (r s : Î²) : StrictConvex ğ•œ (uIcc r s) :=
  strictConvex_Icc _ _

theorem strictConvex_uIoc (r s : Î²) : StrictConvex ğ•œ (uIoc r s) :=
  strictConvex_Ioc _ _

end LinearOrderedCancelAddCommMonoid

end Module

end AddCommMonoid

section AddCancelCommMonoid

variable [AddCancelCommMonoid E] [ContinuousAdd E] [Module ğ•œ E] {s : Set E}

/-- The translation of a strictly convex set is also strictly convex. -/
theorem StrictConvex.preimage_add_right (hs : StrictConvex ğ•œ s) (z : E) :
    StrictConvex ğ•œ ((fun x => z + x) â»Â¹' s) := by
  intro x hx y hy hxy a b ha hb hab
  refine preimage_interior_subset_interior_preimage (continuous_add_left _) ?_
  have h := hs hx hy ((add_right_injective _).ne hxy) ha hb hab
  rwa [smul_add, smul_add, add_add_add_comm, â† _root_.add_smul, hab, one_smul] at h

/-- The translation of a strictly convex set is also strictly convex. -/
theorem StrictConvex.preimage_add_left (hs : StrictConvex ğ•œ s) (z : E) :
    StrictConvex ğ•œ ((fun x => x + z) â»Â¹' s) := by
  simpa only [add_comm] using hs.preimage_add_right z

end AddCancelCommMonoid

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F]

section continuous_add

variable [ContinuousAdd E] {s t : Set E}

theorem StrictConvex.add (hs : StrictConvex ğ•œ s) (ht : StrictConvex ğ•œ t) :
    StrictConvex ğ•œ (s + t) := by
  rintro _ âŸ¨v, hv, w, hw, rflâŸ© _ âŸ¨x, hx, y, hy, rflâŸ© h a b ha hb hab
  rw [smul_add, smul_add, add_add_add_comm]
  obtain rfl | hvx := eq_or_ne v x
  Â· refine interior_mono (add_subset_add (singleton_subset_iff.2 hv) Subset.rfl) ?_
    rw [Convex.combo_self hab, singleton_add]
    exact
      (isOpenMap_add_left _).image_interior_subset _
        (mem_image_of_mem _ <| ht hw hy (ne_of_apply_ne _ h) ha hb hab)
  exact
    subset_interior_add_left
      (add_mem_add (hs hv hx hvx ha hb hab) <| ht.convex hw hy ha.le hb.le hab)

theorem StrictConvex.add_left (hs : StrictConvex ğ•œ s) (z : E) :
    StrictConvex ğ•œ ((fun x => z + x) '' s) := by
  simpa only [singleton_add] using (strictConvex_singleton z).add hs

theorem StrictConvex.add_right (hs : StrictConvex ğ•œ s) (z : E) :
    StrictConvex ğ•œ ((fun x => x + z) '' s) := by simpa only [add_comm] using hs.add_left z

/-- The translation of a strictly convex set is also strictly convex. -/
theorem StrictConvex.vadd (hs : StrictConvex ğ•œ s) (x : E) : StrictConvex ğ•œ (x +áµ¥ s) :=
  hs.add_left x

end continuous_add

section ContinuousSMul

variable [Field ğ•] [Module ğ• E] [ContinuousConstSMul ğ• E]
  [LinearMap.CompatibleSMul E E ğ•œ ğ•] {s : Set E} {x : E}

theorem StrictConvex.smul (hs : StrictConvex ğ•œ s) (c : ğ•) : StrictConvex ğ•œ (c â€¢ s) := by
  obtain rfl | hc := eq_or_ne c 0
  Â· exact (subsingleton_zero_smul_set _).strictConvex
  Â· exact hs.linear_image (LinearMap.lsmul _ _ c) (isOpenMap_smulâ‚€ hc)

theorem StrictConvex.affinity [ContinuousAdd E] (hs : StrictConvex ğ•œ s) (z : E) (c : ğ•) :
    StrictConvex ğ•œ (z +áµ¥ c â€¢ s) :=
  (hs.smul c).vadd z

end ContinuousSMul

end AddCommGroup

end OrderedSemiring

section CommSemiring

variable [CommSemiring ğ•œ] [PartialOrder ğ•œ] [TopologicalSpace E]

section AddCommGroup

variable [AddCommGroup E] [Module ğ•œ E] [NoZeroSMulDivisors ğ•œ E] [ContinuousConstSMul ğ•œ E]
  {s : Set E}

theorem StrictConvex.preimage_smul (hs : StrictConvex ğ•œ s) (c : ğ•œ) :
    StrictConvex ğ•œ ((fun z => c â€¢ z) â»Â¹' s) := by
  classical
    obtain rfl | hc := eq_or_ne c 0
    Â· simp_rw [zero_smul, preimage_const]
      split_ifs
      Â· exact strictConvex_univ
      Â· exact strictConvex_empty
    refine hs.linear_preimage (LinearMap.lsmul _ _ c) ?_ (smul_right_injective E hc)
    unfold LinearMap.lsmul LinearMap.mkâ‚‚ LinearMap.mkâ‚‚' LinearMap.mkâ‚‚'â‚›â‚—
    exact continuous_const_smul _

end AddCommGroup

end CommSemiring

section OrderedRing

variable [Ring ğ•œ] [PartialOrder ğ•œ] [TopologicalSpace E] [TopologicalSpace F]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {s t : Set E} {x y : E}

theorem StrictConvex.eq_of_openSegment_subset_frontier
    [IsOrderedRing ğ•œ] [Nontrivial ğ•œ] [DenselyOrdered ğ•œ]
    (hs : StrictConvex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s) (h : openSegment ğ•œ x y âŠ† frontier s) :
    x = y := by
  obtain âŸ¨a, haâ‚€, haâ‚âŸ© := DenselyOrdered.dense (0 : ğ•œ) 1 zero_lt_one
  classical
    by_contra hxy
    exact
      (h âŸ¨a, 1 - a, haâ‚€, sub_pos_of_lt haâ‚, add_sub_cancel _ _, rflâŸ©).2
        (hs hx hy hxy haâ‚€ (sub_pos_of_lt haâ‚) <| add_sub_cancel _ _)

theorem StrictConvex.add_smul_mem [AddRightStrictMono ğ•œ]
    (hs : StrictConvex ğ•œ s) (hx : x âˆˆ s) (hxy : x + y âˆˆ s)
    (hy : y â‰  0) {t : ğ•œ} (htâ‚€ : 0 < t) (htâ‚ : t < 1) : x + t â€¢ y âˆˆ interior s := by
  have h : x + t â€¢ y = (1 - t) â€¢ x + t â€¢ (x + y) := by match_scalars <;> field_simp
  rw [h]
  exact hs hx hxy (fun h => hy <| add_left_cancel (a := x) (by rw [â† h, add_zero]))
    (sub_pos_of_lt htâ‚) htâ‚€ (sub_add_cancel 1 t)

theorem StrictConvex.smul_mem_of_zero_mem [AddRightStrictMono ğ•œ]
    (hs : StrictConvex ğ•œ s) (zero_mem : (0 : E) âˆˆ s)
    (hx : x âˆˆ s) (hxâ‚€ : x â‰  0) {t : ğ•œ} (htâ‚€ : 0 < t) (htâ‚ : t < 1) : t â€¢ x âˆˆ interior s := by
  simpa using hs.add_smul_mem zero_mem (by simpa using hx) hxâ‚€ htâ‚€ htâ‚

theorem StrictConvex.add_smul_sub_mem [AddRightMono ğ•œ]
    (h : StrictConvex ğ•œ s) (hx : x âˆˆ s) (hy : y âˆˆ s) (hxy : x â‰  y)
    {t : ğ•œ} (htâ‚€ : 0 < t) (htâ‚ : t < 1) : x + t â€¢ (y - x) âˆˆ interior s := by
  apply h.openSegment_subset hx hy hxy
  rw [openSegment_eq_image']
  exact mem_image_of_mem _ âŸ¨htâ‚€, htâ‚âŸ©

/-- The preimage of a strictly convex set under an affine map is strictly convex. -/
theorem StrictConvex.affine_preimage {s : Set F} (hs : StrictConvex ğ•œ s) {f : E â†’áµƒ[ğ•œ] F}
    (hf : Continuous f) (hfinj : Injective f) : StrictConvex ğ•œ (f â»Â¹' s) := by
  intro x hx y hy hxy a b ha hb hab
  refine preimage_interior_subset_interior_preimage hf ?_
  rw [mem_preimage, Convex.combo_affine_apply hab]
  exact hs hx hy (hfinj.ne hxy) ha hb hab

/-- The image of a strictly convex set under an affine map is strictly convex. -/
theorem StrictConvex.affine_image (hs : StrictConvex ğ•œ s) {f : E â†’áµƒ[ğ•œ] F} (hf : IsOpenMap f) :
    StrictConvex ğ•œ (f '' s) := by
  rintro _ âŸ¨x, hx, rflâŸ© _ âŸ¨y, hy, rflâŸ© hxy a b ha hb hab
  exact
    hf.image_interior_subset _
      âŸ¨a â€¢ x + b â€¢ y, âŸ¨hs hx hy (ne_of_apply_ne _ hxy) ha hb hab, Convex.combo_affine_apply habâŸ©âŸ©

variable [IsTopologicalAddGroup E]

theorem StrictConvex.neg (hs : StrictConvex ğ•œ s) : StrictConvex ğ•œ (-s) :=
  hs.is_linear_preimage IsLinearMap.isLinearMap_neg continuous_id.neg neg_injective

theorem StrictConvex.sub (hs : StrictConvex ğ•œ s) (ht : StrictConvex ğ•œ t) : StrictConvex ğ•œ (s - t) :=
  (sub_eq_add_neg s t).symm â–¸ hs.add ht.neg

end AddCommGroup

end OrderedRing

section LinearOrderedField

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace E]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F] {s : Set E} {x : E}

/-- Alternative definition of set strict convexity, using division. -/
theorem strictConvex_iff_div :
    StrictConvex ğ•œ s â†”
      s.Pairwise fun x y =>
        âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ (a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y âˆˆ interior s :=
  âŸ¨fun h x hx y hy hxy a b ha hb â†¦ h hx hy hxy (by positivity) (by positivity) (by field_simp),
    fun h x hx y hy hxy a b ha hb hab â†¦ by
    convert h hx hy hxy ha hb <;> rw [hab, div_one]âŸ©

theorem StrictConvex.mem_smul_of_zero_mem (hs : StrictConvex ğ•œ s) (zero_mem : (0 : E) âˆˆ s)
    (hx : x âˆˆ s) (hxâ‚€ : x â‰  0) {t : ğ•œ} (ht : 1 < t) : x âˆˆ t â€¢ interior s := by
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (by positivity)]
  exact hs.smul_mem_of_zero_mem zero_mem hx hxâ‚€ (by positivity) (inv_lt_one_of_one_ltâ‚€ ht)

end AddCommGroup

end LinearOrderedField

/-!
#### Convex sets in an ordered space

Relates `Convex` and `Set.OrdConnected`.
-/


section

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]
  {s : Set ğ•œ}

/-- A set in a linear ordered field is strictly convex if and only if it is convex. -/
@[simp]
theorem strictConvex_iff_convex : StrictConvex ğ•œ s â†” Convex ğ•œ s :=
  âŸ¨StrictConvex.convex, fun hs => hs.ordConnected.strictConvexâŸ©

theorem strictConvex_iff_ordConnected : StrictConvex ğ•œ s â†” s.OrdConnected :=
  strictConvex_iff_convex.trans convex_iff_ordConnected

alias âŸ¨StrictConvex.ordConnected, _âŸ© := strictConvex_iff_ordConnected

end
