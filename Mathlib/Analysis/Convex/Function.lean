/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, FranÃ§ois Dupuis
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Order.Filter.Extr
import Mathlib.Tactic.NormNum

/-!
# Convex and concave functions

This file defines convex and concave functions in vector spaces and proves the finite Jensen
inequality. The integral version can be found in `Analysis.Convex.Integral`.

A function `f : E â†’ Î²` is `ConvexOn` a set `s` if `s` is itself a convex set, and for any two
points `x y âˆˆ s`, the segment joining `(x, f x)` to `(y, f y)` is above the graph of `f`.
Equivalently, `ConvexOn ğ•œ f s` means that the epigraph `{p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2}` is
a convex set.

## Main declarations

* `ConvexOn ğ•œ s f`: The function `f` is convex on `s` with scalars `ğ•œ`.
* `ConcaveOn ğ•œ s f`: The function `f` is concave on `s` with scalars `ğ•œ`.
* `StrictConvexOn ğ•œ s f`: The function `f` is strictly convex on `s` with scalars `ğ•œ`.
* `StrictConcaveOn ğ•œ s f`: The function `f` is strictly concave on `s` with scalars `ğ•œ`.
-/

open LinearMap Set Convex Pointwise

variable {ğ•œ E F Î± Î² Î¹ : Type*}

section OrderedSemiring

variable [Semiring ğ•œ] [PartialOrder ğ•œ]

section AddCommMonoid

variable [AddCommMonoid E] [AddCommMonoid F]

section OrderedAddCommMonoid

variable [AddCommMonoid Î±] [PartialOrder Î±] [AddCommMonoid Î²] [PartialOrder Î²]

section SMul

variable (ğ•œ) [SMul ğ•œ E] [SMul ğ•œ Î±] [SMul ğ•œ Î²] (s : Set E) (f : E â†’ Î²) {g : Î² â†’ Î±}

/-- Convexity of functions -/
def ConvexOn : Prop :=
  Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y

/-- Concavity of functions -/
def ConcaveOn : Prop :=
  Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’
    a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)

/-- Strict convexity of functions -/
def StrictConvexOn : Prop :=
  Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y

/-- Strict concavity of functions -/
def StrictConcaveOn : Prop :=
  Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
    a â€¢ f x + b â€¢ f y < f (a â€¢ x + b â€¢ y)

variable {ğ•œ s f}

open OrderDual (toDual ofDual)

theorem ConvexOn.dual (hf : ConvexOn ğ•œ s f) : ConcaveOn ğ•œ s (toDual âˆ˜ f) := hf

theorem ConcaveOn.dual (hf : ConcaveOn ğ•œ s f) : ConvexOn ğ•œ s (toDual âˆ˜ f) := hf

theorem StrictConvexOn.dual (hf : StrictConvexOn ğ•œ s f) : StrictConcaveOn ğ•œ s (toDual âˆ˜ f) := hf

theorem StrictConcaveOn.dual (hf : StrictConcaveOn ğ•œ s f) : StrictConvexOn ğ•œ s (toDual âˆ˜ f) := hf

theorem convexOn_id {s : Set Î²} (hs : Convex ğ•œ s) : ConvexOn ğ•œ s _root_.id :=
  âŸ¨hs, by
    intros
    rflâŸ©

theorem concaveOn_id {s : Set Î²} (hs : Convex ğ•œ s) : ConcaveOn ğ•œ s _root_.id :=
  âŸ¨hs, by
    intros
    rflâŸ©

section congr

variable {g : E â†’ Î²}

theorem ConvexOn.congr (hf : ConvexOn ğ•œ s f) (hfg : EqOn f g s) : ConvexOn ğ•œ s g :=
  âŸ¨hf.1, fun x hx y hy a b ha hb hab => by
    simpa only [â† hfg hx, â† hfg hy, â† hfg (hf.1 hx hy ha hb hab)] using hf.2 hx hy ha hb habâŸ©

theorem ConcaveOn.congr (hf : ConcaveOn ğ•œ s f) (hfg : EqOn f g s) : ConcaveOn ğ•œ s g :=
  âŸ¨hf.1, fun x hx y hy a b ha hb hab => by
    simpa only [â† hfg hx, â† hfg hy, â† hfg (hf.1 hx hy ha hb hab)] using hf.2 hx hy ha hb habâŸ©

theorem StrictConvexOn.congr (hf : StrictConvexOn ğ•œ s f) (hfg : EqOn f g s) :
    StrictConvexOn ğ•œ s g :=
  âŸ¨hf.1, fun x hx y hy hxy a b ha hb hab => by
    simpa only [â† hfg hx, â† hfg hy, â† hfg (hf.1 hx hy ha.le hb.le hab)] using
      hf.2 hx hy hxy ha hb habâŸ©

theorem StrictConcaveOn.congr (hf : StrictConcaveOn ğ•œ s f) (hfg : EqOn f g s) :
    StrictConcaveOn ğ•œ s g :=
  âŸ¨hf.1, fun x hx y hy hxy a b ha hb hab => by
    simpa only [â† hfg hx, â† hfg hy, â† hfg (hf.1 hx hy ha.le hb.le hab)] using
      hf.2 hx hy hxy ha hb habâŸ©

end congr

theorem ConvexOn.subset {t : Set E} (hf : ConvexOn ğ•œ t f) (hst : s âŠ† t) (hs : Convex ğ•œ s) :
    ConvexOn ğ•œ s f :=
  âŸ¨hs, fun _ hx _ hy => hf.2 (hst hx) (hst hy)âŸ©

theorem ConcaveOn.subset {t : Set E} (hf : ConcaveOn ğ•œ t f) (hst : s âŠ† t) (hs : Convex ğ•œ s) :
    ConcaveOn ğ•œ s f :=
  âŸ¨hs, fun _ hx _ hy => hf.2 (hst hx) (hst hy)âŸ©

theorem StrictConvexOn.subset {t : Set E} (hf : StrictConvexOn ğ•œ t f) (hst : s âŠ† t)
    (hs : Convex ğ•œ s) : StrictConvexOn ğ•œ s f :=
  âŸ¨hs, fun _ hx _ hy => hf.2 (hst hx) (hst hy)âŸ©

theorem StrictConcaveOn.subset {t : Set E} (hf : StrictConcaveOn ğ•œ t f) (hst : s âŠ† t)
    (hs : Convex ğ•œ s) : StrictConcaveOn ğ•œ s f :=
  âŸ¨hs, fun _ hx _ hy => hf.2 (hst hx) (hst hy)âŸ©

theorem ConvexOn.comp (hg : ConvexOn ğ•œ (f '' s) g) (hf : ConvexOn ğ•œ s f)
    (hg' : MonotoneOn g (f '' s)) : ConvexOn ğ•œ s (g âˆ˜ f) :=
  âŸ¨hf.1, fun _ hx _ hy _ _ ha hb hab =>
    (hg' (mem_image_of_mem f <| hf.1 hx hy ha hb hab)
            (hg.1 (mem_image_of_mem f hx) (mem_image_of_mem f hy) ha hb hab) <|
          hf.2 hx hy ha hb hab).trans <|
      hg.2 (mem_image_of_mem f hx) (mem_image_of_mem f hy) ha hb habâŸ©

theorem ConcaveOn.comp (hg : ConcaveOn ğ•œ (f '' s) g) (hf : ConcaveOn ğ•œ s f)
    (hg' : MonotoneOn g (f '' s)) : ConcaveOn ğ•œ s (g âˆ˜ f) :=
  âŸ¨hf.1, fun _ hx _ hy _ _ ha hb hab =>
    (hg.2 (mem_image_of_mem f hx) (mem_image_of_mem f hy) ha hb hab).trans <|
      hg' (hg.1 (mem_image_of_mem f hx) (mem_image_of_mem f hy) ha hb hab)
          (mem_image_of_mem f <| hf.1 hx hy ha hb hab) <|
        hf.2 hx hy ha hb habâŸ©

theorem ConvexOn.comp_concaveOn (hg : ConvexOn ğ•œ (f '' s) g) (hf : ConcaveOn ğ•œ s f)
    (hg' : AntitoneOn g (f '' s)) : ConvexOn ğ•œ s (g âˆ˜ f) :=
  hg.dual.comp hf hg'

theorem ConcaveOn.comp_convexOn (hg : ConcaveOn ğ•œ (f '' s) g) (hf : ConvexOn ğ•œ s f)
    (hg' : AntitoneOn g (f '' s)) : ConcaveOn ğ•œ s (g âˆ˜ f) :=
  hg.dual.comp hf hg'

theorem StrictConvexOn.comp (hg : StrictConvexOn ğ•œ (f '' s) g) (hf : StrictConvexOn ğ•œ s f)
    (hg' : StrictMonoOn g (f '' s)) (hf' : s.InjOn f) : StrictConvexOn ğ•œ s (g âˆ˜ f) :=
  âŸ¨hf.1, fun _ hx _ hy hxy _ _ ha hb hab =>
    (hg' (mem_image_of_mem f <| hf.1 hx hy ha.le hb.le hab)
            (hg.1 (mem_image_of_mem f hx) (mem_image_of_mem f hy) ha.le hb.le hab) <|
          hf.2 hx hy hxy ha hb hab).trans <|
      hg.2 (mem_image_of_mem f hx) (mem_image_of_mem f hy) (mt (hf' hx hy) hxy) ha hb habâŸ©

theorem StrictConcaveOn.comp (hg : StrictConcaveOn ğ•œ (f '' s) g) (hf : StrictConcaveOn ğ•œ s f)
    (hg' : StrictMonoOn g (f '' s)) (hf' : s.InjOn f) : StrictConcaveOn ğ•œ s (g âˆ˜ f) :=
  âŸ¨hf.1, fun _ hx _ hy hxy _ _ ha hb hab =>
    (hg.2 (mem_image_of_mem f hx) (mem_image_of_mem f hy) (mt (hf' hx hy) hxy) ha hb hab).trans <|
      hg' (hg.1 (mem_image_of_mem f hx) (mem_image_of_mem f hy) ha.le hb.le hab)
          (mem_image_of_mem f <| hf.1 hx hy ha.le hb.le hab) <|
        hf.2 hx hy hxy ha hb habâŸ©

theorem StrictConvexOn.comp_strictConcaveOn (hg : StrictConvexOn ğ•œ (f '' s) g)
    (hf : StrictConcaveOn ğ•œ s f) (hg' : StrictAntiOn g (f '' s)) (hf' : s.InjOn f) :
    StrictConvexOn ğ•œ s (g âˆ˜ f) :=
  hg.dual.comp hf hg' hf'

theorem StrictConcaveOn.comp_strictConvexOn (hg : StrictConcaveOn ğ•œ (f '' s) g)
    (hf : StrictConvexOn ğ•œ s f) (hg' : StrictAntiOn g (f '' s)) (hf' : s.InjOn f) :
    StrictConcaveOn ğ•œ s (g âˆ˜ f) :=
  hg.dual.comp hf hg' hf'

end SMul

section DistribMulAction

variable [IsOrderedAddMonoid Î²] [SMul ğ•œ E] [DistribMulAction ğ•œ Î²] {s : Set E} {f g : E â†’ Î²}

theorem ConvexOn.add (hf : ConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) : ConvexOn ğ•œ s (f + g) :=
  âŸ¨hf.1, fun x hx y hy a b ha hb hab =>
    calc
      f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y + (a â€¢ g x + b â€¢ g y) :=
        add_le_add (hf.2 hx hy ha hb hab) (hg.2 hx hy ha hb hab)
      _ = a â€¢ (f x + g x) + b â€¢ (f y + g y) := by rw [smul_add, smul_add, add_add_add_comm]
      âŸ©

theorem ConcaveOn.add (hf : ConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) : ConcaveOn ğ•œ s (f + g) :=
  hf.dual.add hg

end DistribMulAction

section Module

variable [SMul ğ•œ E] [Module ğ•œ Î²] {s : Set E} {f : E â†’ Î²}

theorem convexOn_const (c : Î²) (hs : Convex ğ•œ s) : ConvexOn ğ•œ s fun _ : E => c :=
  âŸ¨hs, fun _ _ _ _ _ _ _ _ hab => (Convex.combo_self hab c).geâŸ©

theorem concaveOn_const (c : Î²) (hs : Convex ğ•œ s) : ConcaveOn ğ•œ s fun _ => c :=
  convexOn_const (Î² := Î²áµ’áµˆ) _ hs

theorem ConvexOn.add_const [IsOrderedAddMonoid Î²] (hf : ConvexOn ğ•œ s f) (b : Î²) :
    ConvexOn ğ•œ s (f + fun _ => b) :=
  hf.add (convexOn_const _ hf.1)

theorem ConcaveOn.add_const [IsOrderedAddMonoid Î²] (hf : ConcaveOn ğ•œ s f) (b : Î²) :
    ConcaveOn ğ•œ s (f + fun _ => b) :=
  hf.add (concaveOn_const _ hf.1)

theorem convexOn_of_convex_epigraph (h : Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2 }) :
    ConvexOn ğ•œ s f :=
  âŸ¨fun x hx y hy a b ha hb hab => (@h (x, f x) âŸ¨hx, le_rflâŸ© (y, f y) âŸ¨hy, le_rflâŸ© a b ha hb hab).1,
    fun x hx y hy a b ha hb hab => (@h (x, f x) âŸ¨hx, le_rflâŸ© (y, f y) âŸ¨hy, le_rflâŸ© a b ha hb hab).2âŸ©

theorem concaveOn_of_convex_hypograph (h : Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1 }) :
    ConcaveOn ğ•œ s f :=
  convexOn_of_convex_epigraph (Î² := Î²áµ’áµˆ) h

end Module

section OrderedSMul

variable [IsOrderedAddMonoid Î²] [SMul ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²}

theorem ConvexOn.convex_le (hf : ConvexOn ğ•œ s f) (r : Î²) : Convex ğ•œ ({ x âˆˆ s | f x â‰¤ r }) :=
  fun x hx y hy a b ha hb hab =>
  âŸ¨hf.1 hx.1 hy.1 ha hb hab,
    calc
      f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.2 hx.1 hy.1 ha hb hab
      _ â‰¤ a â€¢ r + b â€¢ r := by
        gcongr
        Â· exact hx.2
        Â· exact hy.2
      _ = r := Convex.combo_self hab r
      âŸ©

theorem ConcaveOn.convex_ge (hf : ConcaveOn ğ•œ s f) (r : Î²) : Convex ğ•œ ({ x âˆˆ s | r â‰¤ f x }) :=
  hf.dual.convex_le r

theorem ConvexOn.convex_epigraph (hf : ConvexOn ğ•œ s f) :
    Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2 } := by
  rintro âŸ¨x, râŸ© âŸ¨hx, hrâŸ© âŸ¨y, tâŸ© âŸ¨hy, htâŸ© a b ha hb hab
  refine âŸ¨hf.1 hx hy ha hb hab, ?_âŸ©
  calc
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.2 hx hy ha hb hab
    _ â‰¤ a â€¢ r + b â€¢ t := by gcongr

theorem ConcaveOn.convex_hypograph (hf : ConcaveOn ğ•œ s f) :
    Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1 } :=
  hf.dual.convex_epigraph

theorem convexOn_iff_convex_epigraph :
    ConvexOn ğ•œ s f â†” Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 â‰¤ p.2 } :=
  âŸ¨ConvexOn.convex_epigraph, convexOn_of_convex_epigraphâŸ©

theorem concaveOn_iff_convex_hypograph :
    ConcaveOn ğ•œ s f â†” Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 â‰¤ f p.1 } :=
  convexOn_iff_convex_epigraph (Î² := Î²áµ’áµˆ)

end OrderedSMul

section Module

variable [Module ğ•œ E] [SMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²}

/-- Right translation preserves convexity. -/
theorem ConvexOn.translate_right (hf : ConvexOn ğ•œ s f) (c : E) :
    ConvexOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => c + z) :=
  âŸ¨hf.1.translate_preimage_right _, fun x hx y hy a b ha hb hab =>
    calc
      f (c + (a â€¢ x + b â€¢ y)) = f (a â€¢ (c + x) + b â€¢ (c + y)) := by
        rw [smul_add, smul_add, add_add_add_comm, Convex.combo_self hab]
      _ â‰¤ a â€¢ f (c + x) + b â€¢ f (c + y) := hf.2 hx hy ha hb hab
      âŸ©

/-- Right translation preserves concavity. -/
theorem ConcaveOn.translate_right (hf : ConcaveOn ğ•œ s f) (c : E) :
    ConcaveOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => c + z) :=
  hf.dual.translate_right _

/-- Left translation preserves convexity. -/
theorem ConvexOn.translate_left (hf : ConvexOn ğ•œ s f) (c : E) :
    ConvexOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => z + c) := by
  simpa only [add_comm c] using hf.translate_right c

/-- Left translation preserves concavity. -/
theorem ConcaveOn.translate_left (hf : ConcaveOn ğ•œ s f) (c : E) :
    ConcaveOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => z + c) :=
  hf.dual.translate_left _

end Module

section Module

variable [Module ğ•œ E] [Module ğ•œ Î²]

theorem convexOn_iff_forall_pos {s : Set E} {f : E â†’ Î²} :
    ConvexOn ğ•œ s f â†” Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’
      a + b = 1 â†’ f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := by
  refine and_congr_right'
    âŸ¨fun h x hx y hy a b ha hb hab => h hx hy ha.le hb.le hab, fun h x hx y hy a b ha hb hab => ?_âŸ©
  obtain rfl | ha' := ha.eq_or_lt
  Â· rw [zero_add] at hab
    subst b
    simp_rw [zero_smul, zero_add, one_smul, le_rfl]
  obtain rfl | hb' := hb.eq_or_lt
  Â· rw [add_zero] at hab
    subst a
    simp_rw [zero_smul, add_zero, one_smul, le_rfl]
  exact h hx hy ha' hb' hab

theorem concaveOn_iff_forall_pos {s : Set E} {f : E â†’ Î²} :
    ConcaveOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
        a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y) :=
  convexOn_iff_forall_pos (Î² := Î²áµ’áµˆ)

theorem convexOn_iff_pairwise_pos {s : Set E} {f : E â†’ Î²} :
    ConvexOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§
        s.Pairwise fun x y =>
          âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := by
  rw [convexOn_iff_forall_pos]
  refine
    and_congr_right'
      âŸ¨fun h x hx y hy _ a b ha hb hab => h hx hy ha hb hab, fun h x hx y hy a b ha hb hab => ?_âŸ©
  obtain rfl | hxy := eq_or_ne x y
  Â· rw [Convex.combo_self hab, Convex.combo_self hab]
  exact h hx hy hxy ha hb hab

theorem concaveOn_iff_pairwise_pos {s : Set E} {f : E â†’ Î²} :
    ConcaveOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§
        s.Pairwise fun x y =>
          âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y) :=
  convexOn_iff_pairwise_pos (Î² := Î²áµ’áµˆ)

/-- A linear map is convex. -/
theorem LinearMap.convexOn (f : E â†’â‚—[ğ•œ] Î²) {s : Set E} (hs : Convex ğ•œ s) : ConvexOn ğ•œ s f :=
  âŸ¨hs, fun _ _ _ _ _ _ _ _ _ => by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

/-- A linear map is concave. -/
theorem LinearMap.concaveOn (f : E â†’â‚—[ğ•œ] Î²) {s : Set E} (hs : Convex ğ•œ s) : ConcaveOn ğ•œ s f :=
  âŸ¨hs, fun _ _ _ _ _ _ _ _ _ => by rw [f.map_add, f.map_smul, f.map_smul]âŸ©

theorem StrictConvexOn.convexOn {s : Set E} {f : E â†’ Î²} (hf : StrictConvexOn ğ•œ s f) :
    ConvexOn ğ•œ s f :=
  convexOn_iff_pairwise_pos.mpr
    âŸ¨hf.1, fun _ hx _ hy hxy _ _ ha hb hab => (hf.2 hx hy hxy ha hb hab).leâŸ©

theorem StrictConcaveOn.concaveOn {s : Set E} {f : E â†’ Î²} (hf : StrictConcaveOn ğ•œ s f) :
    ConcaveOn ğ•œ s f :=
  hf.dual.convexOn

section OrderedSMul

variable [IsOrderedAddMonoid Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²}

theorem StrictConvexOn.convex_lt (hf : StrictConvexOn ğ•œ s f) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | f x < r }) :=
  convex_iff_pairwise_pos.2 fun x hx y hy hxy a b ha hb hab =>
    âŸ¨hf.1 hx.1 hy.1 ha.le hb.le hab,
      calc
        f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y := hf.2 hx.1 hy.1 hxy ha hb hab
        _ â‰¤ a â€¢ r + b â€¢ r := by
          gcongr
          Â· exact hx.2.le
          Â· exact hy.2.le
        _ = r := Convex.combo_self hab r
        âŸ©

theorem StrictConcaveOn.convex_gt (hf : StrictConcaveOn ğ•œ s f) (r : Î²) :
    Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  hf.dual.convex_lt r

end OrderedSMul

section LinearOrder

variable [LinearOrder E] {s : Set E} {f : E â†’ Î²}

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is convex, it suffices to
verify the inequality `f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y` only for `x < y` and positive `a`,
`b`. The main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order.
-/
theorem LinearOrder.convexOn_of_lt (hs : Convex ğ•œ s)
    (hf : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
      f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y) :
    ConvexOn ğ•œ s f := by
  refine convexOn_iff_pairwise_pos.2 âŸ¨hs, fun x hx y hy hxy a b ha hb hab => ?_âŸ©
  wlog h : x < y
  Â· rw [add_comm (a â€¢ x), add_comm (a â€¢ f x)]
    rw [add_comm] at hab
    exact this hs hf y hy x hx hxy.symm b a hb ha hab (hxy.lt_or_gt.resolve_left h)
  exact hf hx hy h ha hb hab

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is concave it suffices to
verify the inequality `a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)` for `x < y` and positive `a`, `b`. The
main use case is `E = â„` however one can apply it, e.g., to `â„^n` with lexicographic order. -/
theorem LinearOrder.concaveOn_of_lt (hs : Convex ğ•œ s)
    (hf : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
      a â€¢ f x + b â€¢ f y â‰¤ f (a â€¢ x + b â€¢ y)) :
    ConcaveOn ğ•œ s f :=
  LinearOrder.convexOn_of_lt (Î² := Î²áµ’áµˆ) hs hf

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is strictly convex, it suffices
to verify the inequality `f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y` for `x < y` and positive `a`, `b`.
The main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order. -/
theorem LinearOrder.strictConvexOn_of_lt (hs : Convex ğ•œ s)
    (hf : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
      f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y) :
    StrictConvexOn ğ•œ s f := by
  refine âŸ¨hs, fun x hx y hy hxy a b ha hb hab => ?_âŸ©
  wlog h : x < y
  Â· rw [add_comm (a â€¢ x), add_comm (a â€¢ f x)]
    rw [add_comm] at hab
    exact this hs hf y hy x hx hxy.symm b a hb ha hab (hxy.lt_or_gt.resolve_left h)
  exact hf hx hy h ha hb hab

/-- For a function on a convex set in a linearly ordered space (where the order and the algebraic
structures aren't necessarily compatible), in order to prove that it is strictly concave it suffices
to verify the inequality `a â€¢ f x + b â€¢ f y < f (a â€¢ x + b â€¢ y)` for `x < y` and positive `a`, `b`.
The main use case is `E = ğ•œ` however one can apply it, e.g., to `ğ•œ^n` with lexicographic order. -/
theorem LinearOrder.strictConcaveOn_of_lt (hs : Convex ğ•œ s)
    (hf : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x < y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’ a + b = 1 â†’
      a â€¢ f x + b â€¢ f y < f (a â€¢ x + b â€¢ y)) :
    StrictConcaveOn ğ•œ s f :=
  LinearOrder.strictConvexOn_of_lt (Î² := Î²áµ’áµˆ) hs hf

end LinearOrder

end Module

section Module

variable [Module ğ•œ E] [Module ğ•œ F] [SMul ğ•œ Î²]

/-- If `f` is convex on `s`, so is `(f âˆ˜ g)` on `g â»Â¹' s` for a linear `g`. -/
theorem ConvexOn.comp_linearMap {f : F â†’ Î²} {s : Set F} (hf : ConvexOn ğ•œ s f) (g : E â†’â‚—[ğ•œ] F) :
    ConvexOn ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
  âŸ¨hf.1.linear_preimage _, fun x hx y hy a b ha hb hab =>
    calc
      f (g (a â€¢ x + b â€¢ y)) = f (a â€¢ g x + b â€¢ g y) := by rw [g.map_add, g.map_smul, g.map_smul]
      _ â‰¤ a â€¢ f (g x) + b â€¢ f (g y) := hf.2 hx hy ha hb habâŸ©

/-- If `f` is concave on `s`, so is `(g âˆ˜ f)` on `g â»Â¹' s` for a linear `g`. -/
theorem ConcaveOn.comp_linearMap {f : F â†’ Î²} {s : Set F} (hf : ConcaveOn ğ•œ s f) (g : E â†’â‚—[ğ•œ] F) :
    ConcaveOn ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
  hf.dual.comp_linearMap g

end Module

end OrderedAddCommMonoid

section OrderedCancelAddCommMonoid

variable [AddCommMonoid Î²] [PartialOrder Î²] [IsOrderedCancelAddMonoid Î²]

section DistribMulAction

variable [SMul ğ•œ E] [DistribMulAction ğ•œ Î²] {s : Set E} {f g : E â†’ Î²}

theorem StrictConvexOn.add_convexOn (hf : StrictConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f + g) :=
  âŸ¨hf.1, fun x hx y hy hxy a b ha hb hab =>
    calc
      f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y + (a â€¢ g x + b â€¢ g y) :=
        add_lt_add_of_lt_of_le (hf.2 hx hy hxy ha hb hab) (hg.2 hx hy ha.le hb.le hab)
      _ = a â€¢ (f x + g x) + b â€¢ (f y + g y) := by rw [smul_add, smul_add, add_add_add_comm]âŸ©

theorem ConvexOn.add_strictConvexOn (hf : ConvexOn ğ•œ s f) (hg : StrictConvexOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f + g) :=
  add_comm g f â–¸ hg.add_convexOn hf

theorem StrictConvexOn.add (hf : StrictConvexOn ğ•œ s f) (hg : StrictConvexOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f + g) :=
  âŸ¨hf.1, fun x hx y hy hxy a b ha hb hab =>
    calc
      f (a â€¢ x + b â€¢ y) + g (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y + (a â€¢ g x + b â€¢ g y) :=
        add_lt_add (hf.2 hx hy hxy ha hb hab) (hg.2 hx hy hxy ha hb hab)
      _ = a â€¢ (f x + g x) + b â€¢ (f y + g y) := by rw [smul_add, smul_add, add_add_add_comm]âŸ©

theorem StrictConcaveOn.add_concaveOn (hf : StrictConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f + g) :=
  hf.dual.add_convexOn hg.dual

theorem ConcaveOn.add_strictConcaveOn (hf : ConcaveOn ğ•œ s f) (hg : StrictConcaveOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f + g) :=
  hf.dual.add_strictConvexOn hg.dual

theorem StrictConcaveOn.add (hf : StrictConcaveOn ğ•œ s f) (hg : StrictConcaveOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f + g) :=
  hf.dual.add hg

theorem StrictConvexOn.add_const {Î³ : Type*} {f : E â†’ Î³}
    [AddCommMonoid Î³] [PartialOrder Î³] [IsOrderedCancelAddMonoid Î³]
    [Module ğ•œ Î³] (hf : StrictConvexOn ğ•œ s f) (b : Î³) : StrictConvexOn ğ•œ s (f + fun _ => b) :=
  hf.add_convexOn (convexOn_const _ hf.1)

theorem StrictConcaveOn.add_const {Î³ : Type*} {f : E â†’ Î³}
    [AddCommMonoid Î³] [PartialOrder Î³] [IsOrderedCancelAddMonoid Î³]
    [Module ğ•œ Î³] (hf : StrictConcaveOn ğ•œ s f) (b : Î³) : StrictConcaveOn ğ•œ s (f + fun _ => b) :=
  hf.add_concaveOn (concaveOn_const _ hf.1)

end DistribMulAction

section Module

variable [Module ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²}

theorem ConvexOn.convex_lt (hf : ConvexOn ğ•œ s f) (r : Î²) : Convex ğ•œ ({ x âˆˆ s | f x < r }) :=
  convex_iff_forall_pos.2 fun x hx y hy a b ha hb hab =>
    âŸ¨hf.1 hx.1 hy.1 ha.le hb.le hab,
      calc
        f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.2 hx.1 hy.1 ha.le hb.le hab
        _ < a â€¢ r + b â€¢ r :=
          (add_lt_add_of_lt_of_le (smul_lt_smul_of_pos_left hx.2 ha)
            (smul_le_smul_of_nonneg_left hy.2.le hb.le))
        _ = r := Convex.combo_self hab _âŸ©

theorem ConcaveOn.convex_gt (hf : ConcaveOn ğ•œ s f) (r : Î²) : Convex ğ•œ ({ x âˆˆ s | r < f x }) :=
  hf.dual.convex_lt r

theorem ConvexOn.openSegment_subset_strict_epigraph (hf : ConvexOn ğ•œ s f) (p q : E Ã— Î²)
    (hp : p.1 âˆˆ s âˆ§ f p.1 < p.2) (hq : q.1 âˆˆ s âˆ§ f q.1 â‰¤ q.2) :
    openSegment ğ•œ p q âŠ† { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 < p.2 } := by
  rintro _ âŸ¨a, b, ha, hb, hab, rflâŸ©
  refine âŸ¨hf.1 hp.1 hq.1 ha.le hb.le hab, ?_âŸ©
  calc
    f (a â€¢ p.1 + b â€¢ q.1) â‰¤ a â€¢ f p.1 + b â€¢ f q.1 := hf.2 hp.1 hq.1 ha.le hb.le hab
    _ < a â€¢ p.2 + b â€¢ q.2 := add_lt_add_of_lt_of_le
       (smul_lt_smul_of_pos_left hp.2 ha) (smul_le_smul_of_nonneg_left hq.2 hb.le)

theorem ConcaveOn.openSegment_subset_strict_hypograph (hf : ConcaveOn ğ•œ s f) (p q : E Ã— Î²)
    (hp : p.1 âˆˆ s âˆ§ p.2 < f p.1) (hq : q.1 âˆˆ s âˆ§ q.2 â‰¤ f q.1) :
    openSegment ğ•œ p q âŠ† { p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 < f p.1 } :=
  hf.dual.openSegment_subset_strict_epigraph p q hp hq

theorem ConvexOn.convex_strict_epigraph [ZeroLEOneClass ğ•œ] (hf : ConvexOn ğ•œ s f) :
    Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ f p.1 < p.2 } :=
  convex_iff_openSegment_subset.mpr fun p hp q hq =>
    hf.openSegment_subset_strict_epigraph p q hp âŸ¨hq.1, hq.2.leâŸ©

theorem ConcaveOn.convex_strict_hypograph [ZeroLEOneClass ğ•œ] (hf : ConcaveOn ğ•œ s f) :
    Convex ğ•œ { p : E Ã— Î² | p.1 âˆˆ s âˆ§ p.2 < f p.1 } :=
  hf.dual.convex_strict_epigraph

end Module

end OrderedCancelAddCommMonoid

section LinearOrderedAddCommMonoid

variable [AddCommMonoid Î²] [LinearOrder Î²] [IsOrderedAddMonoid Î²]
  [SMul ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E}
  {f g : E â†’ Î²}

/-- The pointwise maximum of convex functions is convex. -/
theorem ConvexOn.sup (hf : ConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) : ConvexOn ğ•œ s (f âŠ” g) := by
  refine âŸ¨hf.left, fun x hx y hy a b ha hb hab => sup_le ?_ ?_âŸ©
  Â· calc
      f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.right hx hy ha hb hab
      _ â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) := by gcongr <;> apply le_sup_left
  Â· calc
      g (a â€¢ x + b â€¢ y) â‰¤ a â€¢ g x + b â€¢ g y := hg.right hx hy ha hb hab
      _ â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) := by gcongr <;> apply le_sup_right

/-- The pointwise minimum of concave functions is concave. -/
theorem ConcaveOn.inf (hf : ConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) : ConcaveOn ğ•œ s (f âŠ“ g) :=
  hf.dual.sup hg

/-- The pointwise maximum of strictly convex functions is strictly convex. -/
theorem StrictConvexOn.sup (hf : StrictConvexOn ğ•œ s f) (hg : StrictConvexOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f âŠ” g) :=
  âŸ¨hf.left, fun x hx y hy hxy a b ha hb hab =>
    max_lt
      (calc
        f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y := hf.2 hx hy hxy ha hb hab
        _ â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) := by gcongr <;> apply le_sup_left)
      (calc
        g (a â€¢ x + b â€¢ y) < a â€¢ g x + b â€¢ g y := hg.2 hx hy hxy ha hb hab
        _ â‰¤ a â€¢ (f x âŠ” g x) + b â€¢ (f y âŠ” g y) := by gcongr <;> apply le_sup_right)âŸ©

/-- The pointwise minimum of strictly concave functions is strictly concave. -/
theorem StrictConcaveOn.inf (hf : StrictConcaveOn ğ•œ s f) (hg : StrictConcaveOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f âŠ“ g) :=
  hf.dual.sup hg

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
theorem ConvexOn.le_on_segment' (hf : ConvexOn ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) {a b : ğ•œ}
    (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) : f (a â€¢ x + b â€¢ y) â‰¤ max (f x) (f y) :=
  calc
    f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.2 hx hy ha hb hab
    _ â‰¤ a â€¢ max (f x) (f y) + b â€¢ max (f x) (f y) := by
      gcongr
      Â· apply le_max_left
      Â· apply le_max_right
    _ = max (f x) (f y) := Convex.combo_self hab _

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
theorem ConcaveOn.ge_on_segment' (hf : ConcaveOn ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) : min (f x) (f y) â‰¤ f (a â€¢ x + b â€¢ y) :=
  hf.dual.le_on_segment' hx hy ha hb hab

/-- A convex function on a segment is upper-bounded by the max of its endpoints. -/
theorem ConvexOn.le_on_segment (hf : ConvexOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ [x -[ğ•œ] y]) : f z â‰¤ max (f x) (f y) :=
  let âŸ¨_, _, ha, hb, hab, hzâŸ© := hz
  hz â–¸ hf.le_on_segment' hx hy ha hb hab

/-- A concave function on a segment is lower-bounded by the min of its endpoints. -/
theorem ConcaveOn.ge_on_segment (hf : ConcaveOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ [x -[ğ•œ] y]) : min (f x) (f y) â‰¤ f z :=
  hf.dual.le_on_segment hx hy hz

/-- A strictly convex function on an open segment is strictly upper-bounded by the max of its
endpoints. -/
theorem StrictConvexOn.lt_on_open_segment' (hf : StrictConvexOn ğ•œ s f) {x y : E} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (hxy : x â‰  y) {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) :
    f (a â€¢ x + b â€¢ y) < max (f x) (f y) :=
  calc
    f (a â€¢ x + b â€¢ y) < a â€¢ f x + b â€¢ f y := hf.2 hx hy hxy ha hb hab
    _ â‰¤ a â€¢ max (f x) (f y) + b â€¢ max (f x) (f y) := by
      gcongr
      Â· apply le_max_left
      Â· apply le_max_right
    _ = max (f x) (f y) := Convex.combo_self hab _

/-- A strictly concave function on an open segment is strictly lower-bounded by the min of its
endpoints. -/
theorem StrictConcaveOn.lt_on_open_segment' (hf : StrictConcaveOn ğ•œ s f) {x y : E} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (hxy : x â‰  y) {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) :
    min (f x) (f y) < f (a â€¢ x + b â€¢ y) :=
  hf.dual.lt_on_open_segment' hx hy hxy ha hb hab

/-- A strictly convex function on an open segment is strictly upper-bounded by the max of its
endpoints. -/
theorem StrictConvexOn.lt_on_openSegment (hf : StrictConvexOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (hxy : x â‰  y) (hz : z âˆˆ openSegment ğ•œ x y) : f z < max (f x) (f y) :=
  let âŸ¨_, _, ha, hb, hab, hzâŸ© := hz
  hz â–¸ hf.lt_on_open_segment' hx hy hxy ha hb hab

/-- A strictly concave function on an open segment is strictly lower-bounded by the min of its
endpoints. -/
theorem StrictConcaveOn.lt_on_openSegment (hf : StrictConcaveOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (hxy : x â‰  y) (hz : z âˆˆ openSegment ğ•œ x y) : min (f x) (f y) < f z :=
  hf.dual.lt_on_openSegment hx hy hxy hz

end LinearOrderedAddCommMonoid

section LinearOrderedCancelAddCommMonoid

variable [AddCommMonoid Î²] [LinearOrder Î²] [IsOrderedCancelAddMonoid Î²]

section OrderedSMul

variable [SMul ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f g : E â†’ Î²}

theorem ConvexOn.le_left_of_right_le' (hf : ConvexOn ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) (hfy : f y â‰¤ f (a â€¢ x + b â€¢ y)) :
    f (a â€¢ x + b â€¢ y) â‰¤ f x :=
  le_of_not_gt fun h â†¦ lt_irrefl (f (a â€¢ x + b â€¢ y)) <|
    calc
      f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.2 hx hy ha.le hb hab
      _ < a â€¢ f (a â€¢ x + b â€¢ y) + b â€¢ f (a â€¢ x + b â€¢ y) := add_lt_add_of_lt_of_le
          (smul_lt_smul_of_pos_left h ha) (smul_le_smul_of_nonneg_left hfy hb)
      _ = f (a â€¢ x + b â€¢ y) := Convex.combo_self hab _

theorem ConcaveOn.left_le_of_le_right' (hf : ConcaveOn ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) (hfy : f (a â€¢ x + b â€¢ y) â‰¤ f y) :
    f x â‰¤ f (a â€¢ x + b â€¢ y) :=
  hf.dual.le_left_of_right_le' hx hy ha hb hab hfy

theorem ConvexOn.le_right_of_left_le' (hf : ConvexOn ğ•œ s f) {x y : E} {a b : ğ•œ} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) (hfx : f x â‰¤ f (a â€¢ x + b â€¢ y)) :
    f (a â€¢ x + b â€¢ y) â‰¤ f y := by
  rw [add_comm] at hab hfx âŠ¢
  exact hf.le_left_of_right_le' hy hx hb ha hab hfx

theorem ConcaveOn.right_le_of_le_left' (hf : ConcaveOn ğ•œ s f) {x y : E} {a b : ğ•œ} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) (hfx : f (a â€¢ x + b â€¢ y) â‰¤ f x) :
    f y â‰¤ f (a â€¢ x + b â€¢ y) :=
  hf.dual.le_right_of_left_le' hx hy ha hb hab hfx

theorem ConvexOn.le_left_of_right_le (hf : ConvexOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hyz : f y â‰¤ f z) : f z â‰¤ f x := by
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz
  exact hf.le_left_of_right_le' hx hy ha hb.le hab hyz

theorem ConcaveOn.left_le_of_le_right (hf : ConcaveOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hyz : f z â‰¤ f y) : f x â‰¤ f z :=
  hf.dual.le_left_of_right_le hx hy hz hyz

theorem ConvexOn.le_right_of_left_le (hf : ConvexOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hxz : f x â‰¤ f z) : f z â‰¤ f y := by
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz
  exact hf.le_right_of_left_le' hx hy ha.le hb hab hxz

theorem ConcaveOn.right_le_of_le_left (hf : ConcaveOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hxz : f z â‰¤ f x) : f y â‰¤ f z :=
  hf.dual.le_right_of_left_le hx hy hz hxz

end OrderedSMul

section Module

variable [Module ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f g : E â†’ Î²}

/- The following lemmas don't require `Module ğ•œ E` if you add the hypothesis `x â‰  y`. At the time of
the writing, we decided the resulting lemmas wouldn't be useful. Feel free to reintroduce them. -/
theorem ConvexOn.lt_left_of_right_lt' (hf : ConvexOn ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) (hfy : f y < f (a â€¢ x + b â€¢ y)) :
    f (a â€¢ x + b â€¢ y) < f x :=
  not_le.1 fun h â†¦ lt_irrefl (f (a â€¢ x + b â€¢ y)) <|
    calc
      f (a â€¢ x + b â€¢ y) â‰¤ a â€¢ f x + b â€¢ f y := hf.2 hx hy ha.le hb.le hab
      _ < a â€¢ f (a â€¢ x + b â€¢ y) + b â€¢ f (a â€¢ x + b â€¢ y) := add_lt_add_of_le_of_lt
          (smul_le_smul_of_nonneg_left h ha.le) (smul_lt_smul_of_pos_left hfy hb)
      _ = f (a â€¢ x + b â€¢ y) := Convex.combo_self hab _

theorem ConcaveOn.left_lt_of_lt_right' (hf : ConcaveOn ğ•œ s f) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    {a b : ğ•œ} (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) (hfy : f (a â€¢ x + b â€¢ y) < f y) :
    f x < f (a â€¢ x + b â€¢ y) :=
  hf.dual.lt_left_of_right_lt' hx hy ha hb hab hfy

theorem ConvexOn.lt_right_of_left_lt' (hf : ConvexOn ğ•œ s f) {x y : E} {a b : ğ•œ} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) (hfx : f x < f (a â€¢ x + b â€¢ y)) :
    f (a â€¢ x + b â€¢ y) < f y := by
  rw [add_comm] at hab hfx âŠ¢
  exact hf.lt_left_of_right_lt' hy hx hb ha hab hfx

theorem ConcaveOn.lt_right_of_left_lt' (hf : ConcaveOn ğ•œ s f) {x y : E} {a b : ğ•œ} (hx : x âˆˆ s)
    (hy : y âˆˆ s) (ha : 0 < a) (hb : 0 < b) (hab : a + b = 1) (hfx : f (a â€¢ x + b â€¢ y) < f x) :
    f y < f (a â€¢ x + b â€¢ y) :=
  hf.dual.lt_right_of_left_lt' hx hy ha hb hab hfx

theorem ConvexOn.lt_left_of_right_lt (hf : ConvexOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hyz : f y < f z) : f z < f x := by
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz
  exact hf.lt_left_of_right_lt' hx hy ha hb hab hyz

theorem ConcaveOn.left_lt_of_lt_right (hf : ConcaveOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hyz : f z < f y) : f x < f z :=
  hf.dual.lt_left_of_right_lt hx hy hz hyz

theorem ConvexOn.lt_right_of_left_lt (hf : ConvexOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hxz : f x < f z) : f z < f y := by
  obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := hz
  exact hf.lt_right_of_left_lt' hx hy ha hb hab hxz

theorem ConcaveOn.lt_right_of_left_lt (hf : ConcaveOn ğ•œ s f) {x y z : E} (hx : x âˆˆ s) (hy : y âˆˆ s)
    (hz : z âˆˆ openSegment ğ•œ x y) (hxz : f z < f x) : f y < f z :=
  hf.dual.lt_right_of_left_lt hx hy hz hxz

end Module

end LinearOrderedCancelAddCommMonoid

section OrderedAddCommGroup

variable [AddCommGroup Î²] [PartialOrder Î²] [IsOrderedAddMonoid Î²] [SMul ğ•œ E] [Module ğ•œ Î²]
  {s : Set E} {f g : E â†’ Î²}

/-- A function `-f` is convex iff `f` is concave. -/
@[simp]
theorem neg_convexOn_iff : ConvexOn ğ•œ s (-f) â†” ConcaveOn ğ•œ s f := by
  constructor
  Â· rintro âŸ¨hconv, hâŸ©
    refine âŸ¨hconv, fun x hx y hy a b ha hb hab => ?_âŸ©
    simp? [neg_apply, neg_le, add_comm] at h says
      simp only [Pi.neg_apply, smul_neg, le_add_neg_iff_add_le, add_comm,
        add_neg_le_iff_le_add] at h
    exact h hx hy ha hb hab
  Â· rintro âŸ¨hconv, hâŸ©
    refine âŸ¨hconv, fun x hx y hy a b ha hb hab => ?_âŸ©
    rw [â† neg_le_neg_iff]
    simp_rw [neg_add, Pi.neg_apply, smul_neg, neg_neg]
    exact h hx hy ha hb hab

/-- A function `-f` is concave iff `f` is convex. -/
@[simp]
theorem neg_concaveOn_iff : ConcaveOn ğ•œ s (-f) â†” ConvexOn ğ•œ s f := by
  rw [â† neg_convexOn_iff, neg_neg f]

/-- A function `-f` is strictly convex iff `f` is strictly concave. -/
@[simp]
theorem neg_strictConvexOn_iff : StrictConvexOn ğ•œ s (-f) â†” StrictConcaveOn ğ•œ s f := by
  constructor
  Â· rintro âŸ¨hconv, hâŸ©
    refine âŸ¨hconv, fun x hx y hy hxy a b ha hb hab => ?_âŸ©
    simp only [ne_eq, Pi.neg_apply, smul_neg, lt_add_neg_iff_add_lt, add_comm,
      add_neg_lt_iff_lt_add] at h
    exact h hx hy hxy ha hb hab
  Â· rintro âŸ¨hconv, hâŸ©
    refine âŸ¨hconv, fun x hx y hy hxy a b ha hb hab => ?_âŸ©
    rw [â† neg_lt_neg_iff]
    simp_rw [neg_add, Pi.neg_apply, smul_neg, neg_neg]
    exact h hx hy hxy ha hb hab

/-- A function `-f` is strictly concave iff `f` is strictly convex. -/
@[simp]
theorem neg_strictConcaveOn_iff : StrictConcaveOn ğ•œ s (-f) â†” StrictConvexOn ğ•œ s f := by
  rw [â† neg_strictConvexOn_iff, neg_neg f]

alias âŸ¨_, ConcaveOn.negâŸ© := neg_convexOn_iff

alias âŸ¨_, ConvexOn.negâŸ© := neg_concaveOn_iff

alias âŸ¨_, StrictConcaveOn.negâŸ© := neg_strictConvexOn_iff

alias âŸ¨_, StrictConvexOn.negâŸ© := neg_strictConcaveOn_iff

theorem ConvexOn.sub (hf : ConvexOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) : ConvexOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

theorem ConcaveOn.sub (hf : ConcaveOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) : ConcaveOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

theorem StrictConvexOn.sub (hf : StrictConvexOn ğ•œ s f) (hg : StrictConcaveOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

theorem StrictConcaveOn.sub (hf : StrictConcaveOn ğ•œ s f) (hg : StrictConvexOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add hg.neg

theorem ConvexOn.sub_strictConcaveOn (hf : ConvexOn ğ•œ s f) (hg : StrictConcaveOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add_strictConvexOn hg.neg

theorem ConcaveOn.sub_strictConvexOn (hf : ConcaveOn ğ•œ s f) (hg : StrictConvexOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add_strictConcaveOn hg.neg

theorem StrictConvexOn.sub_concaveOn (hf : StrictConvexOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) :
    StrictConvexOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add_convexOn hg.neg

theorem StrictConcaveOn.sub_convexOn (hf : StrictConcaveOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) :
    StrictConcaveOn ğ•œ s (f - g) :=
  (sub_eq_add_neg f g).symm â–¸ hf.add_concaveOn hg.neg

end OrderedAddCommGroup

end AddCommMonoid

section AddCancelCommMonoid

variable [AddCancelCommMonoid E] [AddCommMonoid Î²] [PartialOrder Î²] [Module ğ•œ E] [SMul ğ•œ Î²]
  {s : Set E}
  {f : E â†’ Î²}

/-- Right translation preserves strict convexity. -/
theorem StrictConvexOn.translate_right (hf : StrictConvexOn ğ•œ s f) (c : E) :
    StrictConvexOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => c + z) :=
  âŸ¨hf.1.translate_preimage_right _, fun x hx y hy hxy a b ha hb hab =>
    calc
      f (c + (a â€¢ x + b â€¢ y)) = f (a â€¢ (c + x) + b â€¢ (c + y)) := by
        rw [smul_add, smul_add, add_add_add_comm, Convex.combo_self hab]
      _ < a â€¢ f (c + x) + b â€¢ f (c + y) := hf.2 hx hy ((add_right_injective c).ne hxy) ha hb habâŸ©

/-- Right translation preserves strict concavity. -/
theorem StrictConcaveOn.translate_right (hf : StrictConcaveOn ğ•œ s f) (c : E) :
    StrictConcaveOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => c + z) :=
  hf.dual.translate_right _

/-- Left translation preserves strict convexity. -/
theorem StrictConvexOn.translate_left (hf : StrictConvexOn ğ•œ s f) (c : E) :
    StrictConvexOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => z + c) := by
  simpa only [add_comm] using hf.translate_right c

/-- Left translation preserves strict concavity. -/
theorem StrictConcaveOn.translate_left (hf : StrictConcaveOn ğ•œ s f) (c : E) :
    StrictConcaveOn ğ•œ ((fun z => c + z) â»Â¹' s) (f âˆ˜ fun z => z + c) := by
  simpa only [add_comm] using hf.translate_right c

end AddCancelCommMonoid

end OrderedSemiring

section OrderedCommSemiring

variable [CommSemiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E]

section OrderedAddCommMonoid

variable [AddCommMonoid Î²] [PartialOrder Î²]

section Module

variable [SMul ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²] {s : Set E} {f : E â†’ Î²}

theorem ConvexOn.smul {c : ğ•œ} (hc : 0 â‰¤ c) (hf : ConvexOn ğ•œ s f) : ConvexOn ğ•œ s fun x => c â€¢ f x :=
  âŸ¨hf.1, fun x hx y hy a b ha hb hab =>
    calc
      c â€¢ f (a â€¢ x + b â€¢ y) â‰¤ c â€¢ (a â€¢ f x + b â€¢ f y) :=
        smul_le_smul_of_nonneg_left (hf.2 hx hy ha hb hab) hc
      _ = a â€¢ c â€¢ f x + b â€¢ c â€¢ f y := by rw [smul_add, smul_comm c, smul_comm c]âŸ©

theorem ConcaveOn.smul {c : ğ•œ} (hc : 0 â‰¤ c) (hf : ConcaveOn ğ•œ s f) :
    ConcaveOn ğ•œ s fun x => c â€¢ f x :=
  hf.dual.smul hc

end Module

end OrderedAddCommMonoid

end OrderedCommSemiring

section OrderedRing

variable [Field ğ•œ] [LinearOrder ğ•œ] [AddCommGroup E] [AddCommGroup F]

section OrderedAddCommMonoid

variable [AddCommMonoid Î²] [PartialOrder Î²]

section Module

variable [Module ğ•œ E] [Module ğ•œ F] [SMul ğ•œ Î²]

/-- If a function is convex on `s`, it remains convex when precomposed by an affine map. -/
theorem ConvexOn.comp_affineMap {f : F â†’ Î²} (g : E â†’áµƒ[ğ•œ] F) {s : Set F} (hf : ConvexOn ğ•œ s f) :
    ConvexOn ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
  âŸ¨hf.1.affine_preimage _, fun x hx y hy a b ha hb hab =>
    calc
      (f âˆ˜ g) (a â€¢ x + b â€¢ y) = f (g (a â€¢ x + b â€¢ y)) := rfl
      _ = f (a â€¢ g x + b â€¢ g y) := by rw [Convex.combo_affine_apply hab]
      _ â‰¤ a â€¢ f (g x) + b â€¢ f (g y) := hf.2 hx hy ha hb habâŸ©

/-- If a function is concave on `s`, it remains concave when precomposed by an affine map. -/
theorem ConcaveOn.comp_affineMap {f : F â†’ Î²} (g : E â†’áµƒ[ğ•œ] F) {s : Set F} (hf : ConcaveOn ğ•œ s f) :
    ConcaveOn ğ•œ (g â»Â¹' s) (f âˆ˜ g) :=
  hf.dual.comp_affineMap g

end Module

end OrderedAddCommMonoid

end OrderedRing

section LinearOrderedField

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [AddCommMonoid E]

section OrderedAddCommMonoid

variable [AddCommMonoid Î²] [PartialOrder Î²]

section SMul

variable [SMul ğ•œ E] [SMul ğ•œ Î²] {s : Set E}

theorem convexOn_iff_div {f : E â†’ Î²} :
    ConvexOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’
        f ((a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y) â‰¤ (a / (a + b)) â€¢ f x + (b / (a + b)) â€¢ f y :=
  and_congr Iff.rfl âŸ¨by
    intro h x hx y hy a b ha hb hab
    apply h hx hy (div_nonneg ha hab.le) (div_nonneg hb hab.le)
    rw [â† add_div, div_self hab.ne'], by
    intro h x hx y hy a b ha hb hab
    simpa [hab, zero_lt_one] using h hx hy ha hbâŸ©

theorem concaveOn_iff_div {f : E â†’ Î²} :
    ConcaveOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 â‰¤ a â†’ 0 â‰¤ b â†’ 0 < a + b â†’
        (a / (a + b)) â€¢ f x + (b / (a + b)) â€¢ f y â‰¤ f ((a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y) :=
  convexOn_iff_div (Î² := Î²áµ’áµˆ)

theorem strictConvexOn_iff_div {f : E â†’ Î²} :
    StrictConvexOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’
        f ((a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y) < (a / (a + b)) â€¢ f x + (b / (a + b)) â€¢ f y :=
  and_congr Iff.rfl âŸ¨by
    intro h x hx y hy hxy a b ha hb
    have hab := add_pos ha hb
    apply h hx hy hxy (div_pos ha hab) (div_pos hb hab)
    rw [â† add_div, div_self hab.ne'], by
    intro h x hx y hy hxy a b ha hb hab
    simpa [hab, zero_lt_one] using h hx hy hxy ha hbâŸ©

theorem strictConcaveOn_iff_div {f : E â†’ Î²} :
    StrictConcaveOn ğ•œ s f â†”
      Convex ğ•œ s âˆ§ âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ x â‰  y â†’ âˆ€ â¦ƒa b : ğ•œâ¦„, 0 < a â†’ 0 < b â†’
        (a / (a + b)) â€¢ f x + (b / (a + b)) â€¢ f y < f ((a / (a + b)) â€¢ x + (b / (a + b)) â€¢ y) :=
  strictConvexOn_iff_div (Î² := Î²áµ’áµˆ)

end SMul

end OrderedAddCommMonoid

end LinearOrderedField

section OrderIso

variable [Semiring ğ•œ] [PartialOrder ğ•œ]
  [AddCommMonoid Î±] [PartialOrder Î±] [SMul ğ•œ Î±]
  [AddCommMonoid Î²] [PartialOrder Î²] [SMul ğ•œ Î²]

theorem OrderIso.strictConvexOn_symm (f : Î± â‰ƒo Î²) (hf : StrictConcaveOn ğ•œ univ f) :
    StrictConvexOn ğ•œ univ f.symm := by
  refine âŸ¨convex_univ, fun x _ y _ hxy a b ha hb hab => ?_âŸ©
  obtain âŸ¨x', hx''âŸ© := f.surjective.exists.mp âŸ¨x, rflâŸ©
  obtain âŸ¨y', hy''âŸ© := f.surjective.exists.mp âŸ¨y, rflâŸ©
  have hxy' : x' â‰  y' := by rw [â† f.injective.ne_iff, â† hx'', â† hy'']; exact hxy
  simp only [hx'', hy'', OrderIso.symm_apply_apply, gt_iff_lt]
  rw [â† f.lt_iff_lt, OrderIso.apply_symm_apply]
  exact hf.2 (by simp : x' âˆˆ univ) (by simp : y' âˆˆ univ) hxy' ha hb hab

theorem OrderIso.convexOn_symm (f : Î± â‰ƒo Î²) (hf : ConcaveOn ğ•œ univ f) :
    ConvexOn ğ•œ univ f.symm := by
  refine âŸ¨convex_univ, fun x _ y _ a b ha hb hab => ?_âŸ©
  obtain âŸ¨x', hx''âŸ© := f.surjective.exists.mp âŸ¨x, rflâŸ©
  obtain âŸ¨y', hy''âŸ© := f.surjective.exists.mp âŸ¨y, rflâŸ©
  simp only [hx'', hy'', OrderIso.symm_apply_apply]
  rw [â† f.le_iff_le, OrderIso.apply_symm_apply]
  exact hf.2 (by simp : x' âˆˆ univ) (by simp : y' âˆˆ univ) ha hb hab

theorem OrderIso.strictConcaveOn_symm (f : Î± â‰ƒo Î²) (hf : StrictConvexOn ğ•œ univ f) :
    StrictConcaveOn ğ•œ univ f.symm := by
  refine âŸ¨convex_univ, fun x _ y _ hxy a b ha hb hab => ?_âŸ©
  obtain âŸ¨x', hx''âŸ© := f.surjective.exists.mp âŸ¨x, rflâŸ©
  obtain âŸ¨y', hy''âŸ© := f.surjective.exists.mp âŸ¨y, rflâŸ©
  have hxy' : x' â‰  y' := by rw [â† f.injective.ne_iff, â† hx'', â† hy'']; exact hxy
  simp only [hx'', hy'', OrderIso.symm_apply_apply, gt_iff_lt]
  rw [â† f.lt_iff_lt, OrderIso.apply_symm_apply]
  exact hf.2 (by simp : x' âˆˆ univ) (by simp : y' âˆˆ univ) hxy' ha hb hab

theorem OrderIso.concaveOn_symm (f : Î± â‰ƒo Î²) (hf : ConvexOn ğ•œ univ f) :
    ConcaveOn ğ•œ univ f.symm := by
  refine âŸ¨convex_univ, fun x _ y _ a b ha hb hab => ?_âŸ©
  obtain âŸ¨x', hx''âŸ© := f.surjective.exists.mp âŸ¨x, rflâŸ©
  obtain âŸ¨y', hy''âŸ© := f.surjective.exists.mp âŸ¨y, rflâŸ©
  simp only [hx'', hy'', OrderIso.symm_apply_apply]
  rw [â† f.le_iff_le, OrderIso.apply_symm_apply]
  exact hf.2 (by simp : x' âˆˆ univ) (by simp : y' âˆˆ univ) ha hb hab

end OrderIso


section LinearOrderedField
variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

section OrderedAddCommMonoid
variable [AddCommMonoid Î²] [PartialOrder Î²] [IsOrderedAddMonoid Î²]
  [AddCommMonoid E] [SMul ğ•œ E] [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]
  {f : E â†’ Î²} {s : Set E} {x y : E}

/-- A strictly convex function admits at most one global minimum. -/
lemma StrictConvexOn.eq_of_isMinOn (hf : StrictConvexOn ğ•œ s f) (hfx : IsMinOn f s x)
    (hfy : IsMinOn f s y) (hx : x âˆˆ s) (hy : y âˆˆ s) : x = y := by
  by_contra hxy
  let z := (2 : ğ•œ)â»Â¹ â€¢ x + (2 : ğ•œ)â»Â¹ â€¢ y
  have hz : z âˆˆ s := hf.1 hx hy (by simp) (by simp) <| by norm_num
  refine lt_irrefl (f z) ?_
  calc
    f z < _ := hf.2 hx hy hxy (by norm_num) (by norm_num) <| by norm_num
    _ â‰¤ (2 : ğ•œ)â»Â¹ â€¢ f z + (2 : ğ•œ)â»Â¹ â€¢ f z := by gcongr; exacts [hfx hz, hfy hz]
    _ = f z := by rw [â† _root_.add_smul]; norm_num

/-- A strictly concave function admits at most one global maximum. -/
lemma StrictConcaveOn.eq_of_isMaxOn (hf : StrictConcaveOn ğ•œ s f) (hfx : IsMaxOn f s x)
    (hfy : IsMaxOn f s y) (hx : x âˆˆ s) (hy : y âˆˆ s) : x = y :=
  hf.dual.eq_of_isMinOn hfx hfy hx hy

end OrderedAddCommMonoid

section LinearOrderedCancelAddCommMonoid
variable [AddCommMonoid Î²] [LinearOrder Î²] [IsOrderedCancelAddMonoid Î²]
  [Module ğ•œ Î²] [OrderedSMul ğ•œ Î²]
  {x y z : ğ•œ} {s : Set ğ•œ} {f : ğ•œ â†’ Î²}

theorem ConvexOn.le_right_of_left_le'' (hf : ConvexOn ğ•œ s f) (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x < y)
    (hyz : y â‰¤ z) (h : f x â‰¤ f y) : f y â‰¤ f z :=
  hyz.eq_or_lt.elim (fun hyz => (congr_arg f hyz).le) fun hyz =>
    hf.le_right_of_left_le hx hz (Ioo_subset_openSegment âŸ¨hxy, hyzâŸ©) h

theorem ConvexOn.le_left_of_right_le'' (hf : ConvexOn ğ•œ s f) (hx : x âˆˆ s) (hz : z âˆˆ s) (hxy : x â‰¤ y)
    (hyz : y < z) (h : f z â‰¤ f y) : f y â‰¤ f x :=
  hxy.eq_or_lt.elim (fun hxy => (congr_arg f hxy).ge) fun hxy =>
    hf.le_left_of_right_le hx hz (Ioo_subset_openSegment âŸ¨hxy, hyzâŸ©) h

theorem ConcaveOn.right_le_of_le_left'' (hf : ConcaveOn ğ•œ s f) (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x < y) (hyz : y â‰¤ z) (h : f y â‰¤ f x) : f z â‰¤ f y :=
  hf.dual.le_right_of_left_le'' hx hz hxy hyz h

theorem ConcaveOn.left_le_of_le_right'' (hf : ConcaveOn ğ•œ s f) (hx : x âˆˆ s) (hz : z âˆˆ s)
    (hxy : x â‰¤ y) (hyz : y < z) (h : f y â‰¤ f z) : f x â‰¤ f y :=
  hf.dual.le_left_of_right_le'' hx hz hxy hyz h

end LinearOrderedCancelAddCommMonoid
end LinearOrderedField
