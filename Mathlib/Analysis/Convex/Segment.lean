/-
Copyright (c) 2019 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov, YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.Invertible
import Mathlib.Algebra.Order.Module.OrderedSMul
import Mathlib.LinearAlgebra.AffineSpace.Midpoint
import Mathlib.LinearAlgebra.Ray
import Mathlib.Tactic.GCongr

#align_import analysis.convex.segment from "leanprover-community/mathlib"@"c5773405394e073885e2a144c9ca14637e8eb963"

/-!
# Segments in vector spaces

In a ğ•œ-vector space, we define the following objects and properties.
* `segment ğ•œ x y`: Closed segment joining `x` and `y`.
* `openSegment ğ•œ x y`: Open segment joining `x` and `y`.

## Notations

We provide the following notation:
* `[x -[ğ•œ] y] = segment ğ•œ x y` in locale `Convex`

## TODO

Generalize all this file to affine spaces.

Should we rename `segment` and `openSegment` to `convex.Icc` and `convex.Ioo`? Should we also
define `clopenSegment`/`convex.Ico`/`convex.Ioc`?
-/


variable {ğ•œ E F G Î¹ : Type*} {Ï€ : Î¹ â†’ Type*}

open Function Set

open Pointwise Convex

section OrderedSemiring

variable [OrderedSemiring ğ•œ] [AddCommMonoid E]

section SMul

variable (ğ•œ) [SMul ğ•œ E] {s : Set E} {x y : E}

/-- Segments in a vector space. -/
def segment (x y : E) : Set E :=
  { z : E | âˆƒ a b : ğ•œ, 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a â€¢ x + b â€¢ y = z }
#align segment segment

/-- Open segment in a vector space. Note that `openSegment ğ•œ x x = {x}` instead of being `âˆ…` when
the base semiring has some element between `0` and `1`. -/
def openSegment (x y : E) : Set E :=
  { z : E | âˆƒ a b : ğ•œ, 0 < a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a â€¢ x + b â€¢ y = z }
#align open_segment openSegment

@[inherit_doc] scoped[Convex] notation (priority := high) "[" x "-[" ğ•œ "]" y "]" => segment ğ•œ x y

theorem segment_eq_imageâ‚‚ (x y : E) :
    [x -[ğ•œ] y] =
      (fun p : ğ•œ Ã— ğ•œ => p.1 â€¢ x + p.2 â€¢ y) '' { p | 0 â‰¤ p.1 âˆ§ 0 â‰¤ p.2 âˆ§ p.1 + p.2 = 1 } := by
  simp only [segment, image, Prod.exists, mem_setOf_eq, exists_prop, and_assoc]
#align segment_eq_imageâ‚‚ segment_eq_imageâ‚‚

theorem openSegment_eq_imageâ‚‚ (x y : E) :
    openSegment ğ•œ x y =
      (fun p : ğ•œ Ã— ğ•œ => p.1 â€¢ x + p.2 â€¢ y) '' { p | 0 < p.1 âˆ§ 0 < p.2 âˆ§ p.1 + p.2 = 1 } := by
  simp only [openSegment, image, Prod.exists, mem_setOf_eq, exists_prop, and_assoc]
#align open_segment_eq_imageâ‚‚ openSegment_eq_imageâ‚‚

theorem segment_symm (x y : E) : [x -[ğ•œ] y] = [y -[ğ•œ] x] :=
  Set.ext fun _ =>
    âŸ¨fun âŸ¨a, b, ha, hb, hab, HâŸ© => âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
      fun âŸ¨a, b, ha, hb, hab, HâŸ© =>
      âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©
#align segment_symm segment_symm

theorem openSegment_symm (x y : E) : openSegment ğ•œ x y = openSegment ğ•œ y x :=
  Set.ext fun _ =>
    âŸ¨fun âŸ¨a, b, ha, hb, hab, HâŸ© => âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©,
      fun âŸ¨a, b, ha, hb, hab, HâŸ© =>
      âŸ¨b, a, hb, ha, (add_comm _ _).trans hab, (add_comm _ _).trans HâŸ©âŸ©
#align open_segment_symm openSegment_symm

theorem openSegment_subset_segment (x y : E) : openSegment ğ•œ x y âŠ† [x -[ğ•œ] y] :=
  fun _ âŸ¨a, b, ha, hb, hab, hzâŸ© => âŸ¨a, b, ha.le, hb.le, hab, hzâŸ©
#align open_segment_subset_segment openSegment_subset_segment

theorem segment_subset_iff :
    [x -[ğ•œ] y] âŠ† s â†” âˆ€ a b : ğ•œ, 0 â‰¤ a â†’ 0 â‰¤ b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
  âŸ¨fun H a b ha hb hab => H âŸ¨a, b, ha, hb, hab, rflâŸ©, fun H _ âŸ¨a, b, ha, hb, hab, hzâŸ© =>
    hz â–¸ H a b ha hb habâŸ©
#align segment_subset_iff segment_subset_iff

theorem openSegment_subset_iff :
    openSegment ğ•œ x y âŠ† s â†” âˆ€ a b : ğ•œ, 0 < a â†’ 0 < b â†’ a + b = 1 â†’ a â€¢ x + b â€¢ y âˆˆ s :=
  âŸ¨fun H a b ha hb hab => H âŸ¨a, b, ha, hb, hab, rflâŸ©, fun H _ âŸ¨a, b, ha, hb, hab, hzâŸ© =>
    hz â–¸ H a b ha hb habâŸ©
#align open_segment_subset_iff openSegment_subset_iff

end SMul

open Convex

section MulActionWithZero

variable (ğ•œ)
variable [MulActionWithZero ğ•œ E]


theorem left_mem_segment (x y : E) : x âˆˆ [x -[ğ•œ] y] :=
  âŸ¨1, 0, zero_le_one, le_refl 0, add_zero 1, by rw [zero_smul, one_smul, add_zero]âŸ©
#align left_mem_segment left_mem_segment

theorem right_mem_segment (x y : E) : y âˆˆ [x -[ğ•œ] y] :=
  segment_symm ğ•œ y x â–¸ left_mem_segment ğ•œ y x
#align right_mem_segment right_mem_segment

end MulActionWithZero

section Module

variable (ğ•œ)
variable [Module ğ•œ E] {s : Set E} {x y z : E}

@[simp]
theorem segment_same (x : E) : [x -[ğ•œ] x] = {x} :=
  Set.ext fun z =>
    âŸ¨fun âŸ¨a, b, _, _, hab, hzâŸ© => by
      simpa only [(add_smul _ _ _).symm, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
      fun h => mem_singleton_iff.1 h â–¸ left_mem_segment ğ•œ z zâŸ©
#align segment_same segment_same

theorem insert_endpoints_openSegment (x y : E) :
    insert x (insert y (openSegment ğ•œ x y)) = [x -[ğ•œ] y] := by
  simp only [subset_antisymm_iff, insert_subset_iff, left_mem_segment, right_mem_segment,
    openSegment_subset_segment, true_and_iff]
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©
  refine hb.eq_or_gt.imp ?_ fun hb' => ha.eq_or_gt.imp ?_ fun ha' => ?_
  Â· rintro rfl
    rw [â† add_zero a, hab, one_smul, zero_smul, add_zero]
  Â· rintro rfl
    rw [â† zero_add b, hab, one_smul, zero_smul, zero_add]
  Â· exact âŸ¨a, b, ha', hb', hab, rflâŸ©
#align insert_endpoints_open_segment insert_endpoints_openSegment

variable {ğ•œ}

theorem mem_openSegment_of_ne_left_right (hx : x â‰  z) (hy : y â‰  z) (hz : z âˆˆ [x -[ğ•œ] y]) :
    z âˆˆ openSegment ğ•œ x y := by
  rw [â† insert_endpoints_openSegment] at hz
  exact (hz.resolve_left hx.symm).resolve_left hy.symm
#align mem_open_segment_of_ne_left_right mem_openSegment_of_ne_left_right

theorem openSegment_subset_iff_segment_subset (hx : x âˆˆ s) (hy : y âˆˆ s) :
    openSegment ğ•œ x y âŠ† s â†” [x -[ğ•œ] y] âŠ† s := by
  simp only [â† insert_endpoints_openSegment, insert_subset_iff, *, true_and_iff]
#align open_segment_subset_iff_segment_subset openSegment_subset_iff_segment_subset

end Module

end OrderedSemiring

open Convex

section OrderedRing

variable (ğ•œ) [OrderedRing ğ•œ] [AddCommGroup E] [AddCommGroup F] [AddCommGroup G] [Module ğ•œ E]
  [Module ğ•œ F]

section DenselyOrdered

variable [Nontrivial ğ•œ] [DenselyOrdered ğ•œ]

@[simp]
theorem openSegment_same (x : E) : openSegment ğ•œ x x = {x} :=
  Set.ext fun z =>
    âŸ¨fun âŸ¨a, b, _, _, hab, hzâŸ© => by
      simpa only [â† add_smul, mem_singleton_iff, hab, one_smul, eq_comm] using hz,
    fun h : z = x => by
      obtain âŸ¨a, haâ‚€, haâ‚âŸ© := DenselyOrdered.dense (0 : ğ•œ) 1 zero_lt_one
      refine âŸ¨a, 1 - a, haâ‚€, sub_pos_of_lt haâ‚, add_sub_cancel _ _, ?_âŸ©
      rw [â† add_smul, add_sub_cancel, one_smul, h]âŸ©
#align open_segment_same openSegment_same

end DenselyOrdered

theorem segment_eq_image (x y : E) :
    [x -[ğ•œ] y] = (fun Î¸ : ğ•œ => (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Icc (0 : ğ•œ) 1 :=
  Set.ext fun z =>
    âŸ¨fun âŸ¨a, b, ha, hb, hab, hzâŸ© =>
      âŸ¨b, âŸ¨hb, hab â–¸ le_add_of_nonneg_left haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel_right]âŸ©,
      fun âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ© => âŸ¨1 - Î¸, Î¸, sub_nonneg.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©
#align segment_eq_image segment_eq_image

theorem openSegment_eq_image (x y : E) :
    openSegment ğ•œ x y = (fun Î¸ : ğ•œ => (1 - Î¸) â€¢ x + Î¸ â€¢ y) '' Ioo (0 : ğ•œ) 1 :=
  Set.ext fun z =>
    âŸ¨fun âŸ¨a, b, ha, hb, hab, hzâŸ© =>
      âŸ¨b, âŸ¨hb, hab â–¸ lt_add_of_pos_left _ haâŸ©, hab â–¸ hz â–¸ by simp only [add_sub_cancel_right]âŸ©,
      fun âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, hzâŸ© => âŸ¨1 - Î¸, Î¸, sub_pos.2 hÎ¸â‚, hÎ¸â‚€, sub_add_cancel _ _, hzâŸ©âŸ©
#align open_segment_eq_image openSegment_eq_image

theorem segment_eq_image' (x y : E) :
    [x -[ğ•œ] y] = (fun Î¸ : ğ•œ => x + Î¸ â€¢ (y - x)) '' Icc (0 : ğ•œ) 1 := by
  convert segment_eq_image ğ•œ x y using 2
  simp only [smul_sub, sub_smul, one_smul]
  abel
#align segment_eq_image' segment_eq_image'

theorem openSegment_eq_image' (x y : E) :
    openSegment ğ•œ x y = (fun Î¸ : ğ•œ => x + Î¸ â€¢ (y - x)) '' Ioo (0 : ğ•œ) 1 := by
  convert openSegment_eq_image ğ•œ x y using 2
  simp only [smul_sub, sub_smul, one_smul]
  abel
#align open_segment_eq_image' openSegment_eq_image'

theorem segment_eq_image_lineMap (x y : E) : [x -[ğ•œ] y] =
    AffineMap.lineMap x y '' Icc (0 : ğ•œ) 1 := by
  convert segment_eq_image ğ•œ x y using 2
  exact AffineMap.lineMap_apply_module _ _ _
#align segment_eq_image_line_map segment_eq_image_lineMap

theorem openSegment_eq_image_lineMap (x y : E) :
    openSegment ğ•œ x y = AffineMap.lineMap x y '' Ioo (0 : ğ•œ) 1 := by
  convert openSegment_eq_image ğ•œ x y using 2
  exact AffineMap.lineMap_apply_module _ _ _
#align open_segment_eq_image_line_map openSegment_eq_image_lineMap

@[simp]
theorem image_segment (f : E â†’áµƒ[ğ•œ] F) (a b : E) : f '' [a -[ğ•œ] b] = [f a -[ğ•œ] f b] :=
  Set.ext fun x => by
    simp_rw [segment_eq_image_lineMap, mem_image, exists_exists_and_eq_and, AffineMap.apply_lineMap]
#align image_segment image_segment

@[simp]
theorem image_openSegment (f : E â†’áµƒ[ğ•œ] F) (a b : E) :
    f '' openSegment ğ•œ a b = openSegment ğ•œ (f a) (f b) :=
  Set.ext fun x => by
    simp_rw [openSegment_eq_image_lineMap, mem_image, exists_exists_and_eq_and,
      AffineMap.apply_lineMap]
#align image_open_segment image_openSegment

@[simp]
theorem vadd_segment [AddTorsor G E] [VAddCommClass G E E] (a : G) (b c : E) :
    a +áµ¥ [b -[ğ•œ] c] = [a +áµ¥ b -[ğ•œ] a +áµ¥ c] :=
  image_segment ğ•œ âŸ¨_, LinearMap.id, fun _ _ => vadd_comm _ _ _âŸ© b c
#align vadd_segment vadd_segment

@[simp]
theorem vadd_openSegment [AddTorsor G E] [VAddCommClass G E E] (a : G) (b c : E) :
    a +áµ¥ openSegment ğ•œ b c = openSegment ğ•œ (a +áµ¥ b) (a +áµ¥ c) :=
  image_openSegment ğ•œ âŸ¨_, LinearMap.id, fun _ _ => vadd_comm _ _ _âŸ© b c
#align vadd_open_segment vadd_openSegment

@[simp]
theorem mem_segment_translate (a : E) {x b c} : a + x âˆˆ [a + b -[ğ•œ] a + c] â†” x âˆˆ [b -[ğ•œ] c] := by
  simp_rw [â† vadd_eq_add, â† vadd_segment, vadd_mem_vadd_set_iff]
#align mem_segment_translate mem_segment_translate

@[simp]
theorem mem_openSegment_translate (a : E) {x b c : E} :
    a + x âˆˆ openSegment ğ•œ (a + b) (a + c) â†” x âˆˆ openSegment ğ•œ b c := by
  simp_rw [â† vadd_eq_add, â† vadd_openSegment, vadd_mem_vadd_set_iff]
#align mem_open_segment_translate mem_openSegment_translate

theorem segment_translate_preimage (a b c : E) :
    (fun x => a + x) â»Â¹' [a + b -[ğ•œ] a + c] = [b -[ğ•œ] c] :=
  Set.ext fun _ => mem_segment_translate ğ•œ a
#align segment_translate_preimage segment_translate_preimage

theorem openSegment_translate_preimage (a b c : E) :
    (fun x => a + x) â»Â¹' openSegment ğ•œ (a + b) (a + c) = openSegment ğ•œ b c :=
  Set.ext fun _ => mem_openSegment_translate ğ•œ a
#align open_segment_translate_preimage openSegment_translate_preimage

theorem segment_translate_image (a b c : E) : (fun x => a + x) '' [b -[ğ•œ] c] = [a + b -[ğ•œ] a + c] :=
  segment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ <| add_left_surjective a
#align segment_translate_image segment_translate_image

theorem openSegment_translate_image (a b c : E) :
    (fun x => a + x) '' openSegment ğ•œ b c = openSegment ğ•œ (a + b) (a + c) :=
  openSegment_translate_preimage ğ•œ a b c â–¸ image_preimage_eq _ <| add_left_surjective a
#align open_segment_translate_image openSegment_translate_image

lemma segment_inter_eq_endpoint_of_linearIndependent_sub
    {c x y : E} (h : LinearIndependent ğ•œ ![x - c, y - c]) :
    [c -[ğ•œ] x] âˆ© [c -[ğ•œ] y] = {c} := by
  apply Subset.antisymm; swap
  Â· simp [singleton_subset_iff, left_mem_segment]
  intro z âŸ¨hzt, hzsâŸ©
  rw [segment_eq_image, mem_image] at hzt hzs
  rcases hzt with âŸ¨p, âŸ¨p0, p1âŸ©, rflâŸ©
  rcases hzs with âŸ¨q, âŸ¨q0, q1âŸ©, HâŸ©
  have Hx : x = (x - c) + c := by abel
  have Hy : y = (y - c) + c := by abel
  rw [Hx, Hy, smul_add, smul_add] at H
  have : c + q â€¢ (y - c) = c + p â€¢ (x - c) := by
    convert H using 1 <;> simp [sub_smul]
  obtain âŸ¨rfl, rflâŸ© : p = 0 âˆ§ q = 0 := h.eq_zero_of_pair' ((add_right_inj c).1 this).symm
  simp

end OrderedRing

theorem sameRay_of_mem_segment [StrictOrderedCommRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] {x y z : E}
    (h : x âˆˆ [y -[ğ•œ] z]) : SameRay ğ•œ (x - y) (z - x) := by
  rw [segment_eq_image'] at h
  rcases h with âŸ¨Î¸, âŸ¨hÎ¸â‚€, hÎ¸â‚âŸ©, rflâŸ©
  simpa only [add_sub_cancel_left, â† sub_sub, sub_smul, one_smul] using
    (SameRay.sameRay_nonneg_smul_left (z - y) hÎ¸â‚€).nonneg_smul_right (sub_nonneg.2 hÎ¸â‚)
#align same_ray_of_mem_segment sameRay_of_mem_segment

lemma segment_inter_eq_endpoint_of_linearIndependent_of_ne [OrderedCommRing ğ•œ] [NoZeroDivisors ğ•œ]
    [AddCommGroup E] [Module ğ•œ E]
    {x y : E} (h : LinearIndependent ğ•œ ![x, y]) {s t : ğ•œ} (hs : s â‰  t) (c : E) :
    [c + x -[ğ•œ] c + t â€¢ y] âˆ© [c + x -[ğ•œ] c + s â€¢ y] = {c + x} := by
  apply segment_inter_eq_endpoint_of_linearIndependent_sub
  simp only [add_sub_add_left_eq_sub]
  suffices H : LinearIndependent ğ•œ ![(-1 : ğ•œ) â€¢ x + t â€¢ y, (-1 : ğ•œ) â€¢ x + s â€¢ y] by
    convert H using 1; simp only [neg_smul, one_smul]; abel_nf
  apply h.linear_combination_pair_of_det_ne_zero
  contrapose! hs
  apply Eq.symm
  simpa [neg_mul, one_mul, mul_neg, mul_one, sub_neg_eq_add, add_comm _ t,
    â† sub_eq_add_neg, sub_eq_zero] using hs

section LinearOrderedRing

variable [LinearOrderedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] {x y : E}

theorem midpoint_mem_segment [Invertible (2 : ğ•œ)] (x y : E) : midpoint ğ•œ x y âˆˆ [x -[ğ•œ] y] := by
  rw [segment_eq_image_lineMap]
  exact âŸ¨â…Ÿ 2, âŸ¨invOf_nonneg.mpr zero_le_two, invOf_le_one one_le_twoâŸ©, rflâŸ©
#align midpoint_mem_segment midpoint_mem_segment

theorem mem_segment_sub_add [Invertible (2 : ğ•œ)] (x y : E) : x âˆˆ [x - y -[ğ•œ] x + y] := by
  convert @midpoint_mem_segment ğ•œ _ _ _ _ _ (x - y) (x + y)
  rw [midpoint_sub_add]
#align mem_segment_sub_add mem_segment_sub_add

theorem mem_segment_add_sub [Invertible (2 : ğ•œ)] (x y : E) : x âˆˆ [x + y -[ğ•œ] x - y] := by
  convert @midpoint_mem_segment ğ•œ _ _ _ _ _ (x + y) (x - y)
  rw [midpoint_add_sub]
#align mem_segment_add_sub mem_segment_add_sub

@[simp]
theorem left_mem_openSegment_iff [DenselyOrdered ğ•œ] [NoZeroSMulDivisors ğ•œ E] :
    x âˆˆ openSegment ğ•œ x y â†” x = y := by
  constructor
  Â· rintro âŸ¨a, b, _, hb, hab, hxâŸ©
    refine smul_right_injective _ hb.ne' ((add_right_inj (a â€¢ x)).1 ?_)
    rw [hx, â† add_smul, hab, one_smul]
  Â· rintro rfl
    rw [openSegment_same]
    exact mem_singleton _
#align left_mem_open_segment_iff left_mem_openSegment_iff

@[simp]
theorem right_mem_openSegment_iff [DenselyOrdered ğ•œ] [NoZeroSMulDivisors ğ•œ E] :
    y âˆˆ openSegment ğ•œ x y â†” x = y := by rw [openSegment_symm, left_mem_openSegment_iff, eq_comm]
#align right_mem_open_segment_iff right_mem_openSegment_iff

end LinearOrderedRing

section LinearOrderedSemifield

variable [LinearOrderedSemifield ğ•œ] [AddCommGroup E] [Module ğ•œ E] {x y z : E}

theorem mem_segment_iff_div :
    x âˆˆ [y -[ğ•œ] z] â†”
      âˆƒ a b : ğ•œ, 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ 0 < a + b âˆ§ (a / (a + b)) â€¢ y + (b / (a + b)) â€¢ z = x := by
  constructor
  Â· rintro âŸ¨a, b, ha, hb, hab, rflâŸ©
    use a, b, ha, hb
    simp [*]
  Â· rintro âŸ¨a, b, ha, hb, hab, rflâŸ©
    refine âŸ¨a / (a + b), b / (a + b), by positivity, by positivity, ?_, rflâŸ©
    rw [â† add_div, div_self hab.ne']
#align mem_segment_iff_div mem_segment_iff_div

theorem mem_openSegment_iff_div : x âˆˆ openSegment ğ•œ y z â†”
    âˆƒ a b : ğ•œ, 0 < a âˆ§ 0 < b âˆ§ (a / (a + b)) â€¢ y + (b / (a + b)) â€¢ z = x := by
  constructor
  Â· rintro âŸ¨a, b, ha, hb, hab, rflâŸ©
    use a, b, ha, hb
    rw [hab, div_one, div_one]
  Â· rintro âŸ¨a, b, ha, hb, rflâŸ©
    have hab : 0 < a + b := by positivity
    refine âŸ¨a / (a + b), b / (a + b), by positivity, by positivity, ?_, rflâŸ©
    rw [â† add_div, div_self hab.ne']
#align mem_open_segment_iff_div mem_openSegment_iff_div

end LinearOrderedSemifield

section LinearOrderedField

variable [LinearOrderedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] {x y z : E}

theorem mem_segment_iff_sameRay : x âˆˆ [y -[ğ•œ] z] â†” SameRay ğ•œ (x - y) (z - x) := by
  refine âŸ¨sameRay_of_mem_segment, fun h => ?_âŸ©
  rcases h.exists_eq_smul_add with âŸ¨a, b, ha, hb, hab, hxy, hzxâŸ©
  rw [add_comm, sub_add_sub_cancel] at hxy hzx
  rw [â† mem_segment_translate _ (-x), neg_add_self]
  refine âŸ¨b, a, hb, ha, add_comm a b â–¸ hab, ?_âŸ©
  rw [â† sub_eq_neg_add, â† neg_sub, hxy, â† sub_eq_neg_add, hzx, smul_neg, smul_comm, neg_add_self]
#align mem_segment_iff_same_ray mem_segment_iff_sameRay

open AffineMap

/-- If `z = lineMap x y c` is a point on the line passing through `x` and `y`, then the open
segment `openSegment ğ•œ x y` is included in the union of the open segments `openSegment ğ•œ x z`,
`openSegment ğ•œ z y`, and the point `z`. Informally, `(x, y) âŠ† {z} âˆª (x, z) âˆª (z, y)`. -/
theorem openSegment_subset_union (x y : E) {z : E} (hz : z âˆˆ range (lineMap x y : ğ•œ â†’ E)) :
    openSegment ğ•œ x y âŠ† insert z (openSegment ğ•œ x z âˆª openSegment ğ•œ z y) := by
  rcases hz with âŸ¨c, rflâŸ©
  simp only [openSegment_eq_image_lineMap, â† mapsTo']
  rintro a âŸ¨hâ‚€, hâ‚âŸ©
  rcases lt_trichotomy a c with (hac | rfl | hca)
  Â· right
    left
    have hc : 0 < c := hâ‚€.trans hac
    refine âŸ¨a / c, âŸ¨div_pos hâ‚€ hc, (div_lt_one hc).2 hacâŸ©, ?_âŸ©
    simp only [â† homothety_eq_lineMap, â† homothety_mul_apply, div_mul_cancelâ‚€ _ hc.ne']
  Â· left
    rfl
  Â· right
    right
    have hc : 0 < 1 - c := sub_pos.2 (hca.trans hâ‚)
    simp only [â† lineMap_apply_one_sub y]
    refine
      âŸ¨(a - c) / (1 - c), âŸ¨div_pos (sub_pos.2 hca) hc, (div_lt_one hc).2 <| sub_lt_sub_right hâ‚ _âŸ©,
        ?_âŸ©
    simp only [â† homothety_eq_lineMap, â† homothety_mul_apply, sub_mul, one_mul,
      div_mul_cancelâ‚€ _ hc.ne', sub_sub_sub_cancel_right]
#align open_segment_subset_union openSegment_subset_union

end LinearOrderedField

/-!
#### Segments in an ordered space

Relates `segment`, `openSegment` and `Set.Icc`, `Set.Ico`, `Set.Ioc`, `Set.Ioo`
-/


section OrderedSemiring

variable [OrderedSemiring ğ•œ]

section OrderedAddCommMonoid

variable [OrderedAddCommMonoid E] [Module ğ•œ E] [OrderedSMul ğ•œ E] {x y : E}

theorem segment_subset_Icc (h : x â‰¤ y) : [x -[ğ•œ] y] âŠ† Icc x y := by
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©
  constructor
  Â· calc
      x = a â€¢ x + b â€¢ x := (Convex.combo_self hab _).symm
      _ â‰¤ a â€¢ x + b â€¢ y := by gcongr
  Â· calc
      a â€¢ x + b â€¢ y â‰¤ a â€¢ y + b â€¢ y := by gcongr
      _ = y := Convex.combo_self hab _
#align segment_subset_Icc segment_subset_Icc

end OrderedAddCommMonoid

section OrderedCancelAddCommMonoid

variable [OrderedCancelAddCommMonoid E] [Module ğ•œ E] [OrderedSMul ğ•œ E] {x y : E}

theorem openSegment_subset_Ioo (h : x < y) : openSegment ğ•œ x y âŠ† Ioo x y := by
  rintro z âŸ¨a, b, ha, hb, hab, rflâŸ©
  constructor
  Â· calc
      x = a â€¢ x + b â€¢ x := (Convex.combo_self hab _).symm
      _ < a â€¢ x + b â€¢ y := by gcongr
  Â· calc
      a â€¢ x + b â€¢ y < a â€¢ y + b â€¢ y := by gcongr
      _ = y := Convex.combo_self hab _
#align open_segment_subset_Ioo openSegment_subset_Ioo

end OrderedCancelAddCommMonoid

section LinearOrderedAddCommMonoid

variable [LinearOrderedAddCommMonoid E] [Module ğ•œ E] [OrderedSMul ğ•œ E] {a b : ğ•œ}

theorem segment_subset_uIcc (x y : E) : [x -[ğ•œ] y] âŠ† uIcc x y := by
  rcases le_total x y with h | h
  Â· rw [uIcc_of_le h]
    exact segment_subset_Icc h
  Â· rw [uIcc_of_ge h, segment_symm]
    exact segment_subset_Icc h
#align segment_subset_uIcc segment_subset_uIcc

theorem Convex.min_le_combo (x y : E) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
    min x y â‰¤ a â€¢ x + b â€¢ y :=
  (segment_subset_uIcc x y âŸ¨_, _, ha, hb, hab, rflâŸ©).1
#align convex.min_le_combo Convex.min_le_combo

theorem Convex.combo_le_max (x y : E) (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
    a â€¢ x + b â€¢ y â‰¤ max x y :=
  (segment_subset_uIcc x y âŸ¨_, _, ha, hb, hab, rflâŸ©).2
#align convex.combo_le_max Convex.combo_le_max

end LinearOrderedAddCommMonoid

end OrderedSemiring

section LinearOrderedField

variable [LinearOrderedField ğ•œ] {x y z : ğ•œ}

theorem Icc_subset_segment : Icc x y âŠ† [x -[ğ•œ] y] := by
  rintro z âŸ¨hxz, hyzâŸ©
  obtain rfl | h := (hxz.trans hyz).eq_or_lt
  Â· rw [segment_same]
    exact hyz.antisymm hxz
  rw [â† sub_nonneg] at hxz hyz
  rw [â† sub_pos] at h
  refine âŸ¨(y - z) / (y - x), (z - x) / (y - x), div_nonneg hyz h.le, div_nonneg hxz h.le, ?_, ?_âŸ©
  Â· rw [â† add_div, sub_add_sub_cancel, div_self h.ne']
  Â· rw [smul_eq_mul, smul_eq_mul, â† mul_div_right_comm, â† mul_div_right_comm, â† add_div,
      div_eq_iff h.ne', add_comm, sub_mul, sub_mul, mul_comm x, sub_add_sub_cancel, mul_sub]
#align Icc_subset_segment Icc_subset_segment

@[simp]
theorem segment_eq_Icc (h : x â‰¤ y) : [x -[ğ•œ] y] = Icc x y :=
  (segment_subset_Icc h).antisymm Icc_subset_segment
#align segment_eq_Icc segment_eq_Icc

theorem Ioo_subset_openSegment : Ioo x y âŠ† openSegment ğ•œ x y := fun _ hz =>
  mem_openSegment_of_ne_left_right hz.1.ne hz.2.ne' <| Icc_subset_segment <| Ioo_subset_Icc_self hz
#align Ioo_subset_open_segment Ioo_subset_openSegment

@[simp]
theorem openSegment_eq_Ioo (h : x < y) : openSegment ğ•œ x y = Ioo x y :=
  (openSegment_subset_Ioo h).antisymm Ioo_subset_openSegment
#align open_segment_eq_Ioo openSegment_eq_Ioo

theorem segment_eq_Icc' (x y : ğ•œ) : [x -[ğ•œ] y] = Icc (min x y) (max x y) := by
  rcases le_total x y with h | h
  Â· rw [segment_eq_Icc h, max_eq_right h, min_eq_left h]
  Â· rw [segment_symm, segment_eq_Icc h, max_eq_left h, min_eq_right h]
#align segment_eq_Icc' segment_eq_Icc'

theorem openSegment_eq_Ioo' (hxy : x â‰  y) : openSegment ğ•œ x y = Ioo (min x y) (max x y) := by
  cases' hxy.lt_or_lt with h h
  Â· rw [openSegment_eq_Ioo h, max_eq_right h.le, min_eq_left h.le]
  Â· rw [openSegment_symm, openSegment_eq_Ioo h, max_eq_left h.le, min_eq_right h.le]
#align open_segment_eq_Ioo' openSegment_eq_Ioo'

theorem segment_eq_uIcc (x y : ğ•œ) : [x -[ğ•œ] y] = uIcc x y :=
  segment_eq_Icc' _ _
#align segment_eq_uIcc segment_eq_uIcc

/-- A point is in an `Icc` iff it can be expressed as a convex combination of the endpoints. -/
theorem Convex.mem_Icc (h : x â‰¤ y) :
    z âˆˆ Icc x y â†” âˆƒ a b, 0 â‰¤ a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z := by
  rw [â† segment_eq_Icc h]
  rfl
#align convex.mem_Icc Convex.mem_Icc

/-- A point is in an `Ioo` iff it can be expressed as a strict convex combination of the endpoints.
-/
theorem Convex.mem_Ioo (h : x < y) :
    z âˆˆ Ioo x y â†” âˆƒ a b, 0 < a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z := by
  rw [â† openSegment_eq_Ioo h]
  rfl
#align convex.mem_Ioo Convex.mem_Ioo

/-- A point is in an `Ioc` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
theorem Convex.mem_Ioc (h : x < y) :
    z âˆˆ Ioc x y â†” âˆƒ a b, 0 â‰¤ a âˆ§ 0 < b âˆ§ a + b = 1 âˆ§ a * x + b * y = z := by
  refine âŸ¨fun hz => ?_, ?_âŸ©
  Â· obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (Convex.mem_Icc h.le).1 (Ioc_subset_Icc_self hz)
    obtain rfl | hb' := hb.eq_or_lt
    Â· rw [add_zero] at hab
      rw [hab, one_mul, zero_mul, add_zero] at hz
      exact (hz.1.ne rfl).elim
    Â· exact âŸ¨a, b, ha, hb', hab, rflâŸ©
  Â· rintro âŸ¨a, b, ha, hb, hab, rflâŸ©
    obtain rfl | ha' := ha.eq_or_lt
    Â· rw [zero_add] at hab
      rwa [hab, one_mul, zero_mul, zero_add, right_mem_Ioc]
    Â· exact Ioo_subset_Ioc_self ((Convex.mem_Ioo h).2 âŸ¨a, b, ha', hb, hab, rflâŸ©)
#align convex.mem_Ioc Convex.mem_Ioc

/-- A point is in an `Ico` iff it can be expressed as a semistrict convex combination of the
endpoints. -/
theorem Convex.mem_Ico (h : x < y) :
    z âˆˆ Ico x y â†” âˆƒ a b, 0 < a âˆ§ 0 â‰¤ b âˆ§ a + b = 1 âˆ§ a * x + b * y = z := by
  refine âŸ¨fun hz => ?_, ?_âŸ©
  Â· obtain âŸ¨a, b, ha, hb, hab, rflâŸ© := (Convex.mem_Icc h.le).1 (Ico_subset_Icc_self hz)
    obtain rfl | ha' := ha.eq_or_lt
    Â· rw [zero_add] at hab
      rw [hab, one_mul, zero_mul, zero_add] at hz
      exact (hz.2.ne rfl).elim
    Â· exact âŸ¨a, b, ha', hb, hab, rflâŸ©
  Â· rintro âŸ¨a, b, ha, hb, hab, rflâŸ©
    obtain rfl | hb' := hb.eq_or_lt
    Â· rw [add_zero] at hab
      rwa [hab, one_mul, zero_mul, add_zero, left_mem_Ico]
    Â· exact Ioo_subset_Ico_self ((Convex.mem_Ioo h).2 âŸ¨a, b, ha, hb', hab, rflâŸ©)
#align convex.mem_Ico Convex.mem_Ico

end LinearOrderedField

namespace Prod

variable [OrderedSemiring ğ•œ] [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F]

theorem segment_subset (x y : E Ã— F) : segment ğ•œ x y âŠ† segment ğ•œ x.1 y.1 Ã—Ë¢ segment ğ•œ x.2 y.2 := by
  rintro z âŸ¨a, b, ha, hb, hab, hzâŸ©
  exact âŸ¨âŸ¨a, b, ha, hb, hab, congr_arg Prod.fst hzâŸ©, a, b, ha, hb, hab, congr_arg Prod.snd hzâŸ©
#align prod.segment_subset Prod.segment_subset

theorem openSegment_subset (x y : E Ã— F) :
    openSegment ğ•œ x y âŠ† openSegment ğ•œ x.1 y.1 Ã—Ë¢ openSegment ğ•œ x.2 y.2 := by
  rintro z âŸ¨a, b, ha, hb, hab, hzâŸ©
  exact âŸ¨âŸ¨a, b, ha, hb, hab, congr_arg Prod.fst hzâŸ©, a, b, ha, hb, hab, congr_arg Prod.snd hzâŸ©
#align prod.open_segment_subset Prod.openSegment_subset

theorem image_mk_segment_left (xâ‚ xâ‚‚ : E) (y : F) :
    (fun x => (x, y)) '' [xâ‚ -[ğ•œ] xâ‚‚] = [(xâ‚, y) -[ğ•œ] (xâ‚‚, y)] := by
  rw [segment_eq_imageâ‚‚, segment_eq_imageâ‚‚, image_image]
  refine EqOn.image_eq fun a ha â†¦ ?_
  simp [Convex.combo_self ha.2.2]
#align prod.image_mk_segment_left Prod.image_mk_segment_left

theorem image_mk_segment_right (x : E) (yâ‚ yâ‚‚ : F) :
    (fun y => (x, y)) '' [yâ‚ -[ğ•œ] yâ‚‚] = [(x, yâ‚) -[ğ•œ] (x, yâ‚‚)] := by
  rw [segment_eq_imageâ‚‚, segment_eq_imageâ‚‚, image_image]
  refine EqOn.image_eq fun a ha â†¦ ?_
  simp [Convex.combo_self ha.2.2]
#align prod.image_mk_segment_right Prod.image_mk_segment_right

theorem image_mk_openSegment_left (xâ‚ xâ‚‚ : E) (y : F) :
    (fun x => (x, y)) '' openSegment ğ•œ xâ‚ xâ‚‚ = openSegment ğ•œ (xâ‚, y) (xâ‚‚, y) := by
  rw [openSegment_eq_imageâ‚‚, openSegment_eq_imageâ‚‚, image_image]
  refine EqOn.image_eq fun a ha â†¦ ?_
  simp [Convex.combo_self ha.2.2]
#align prod.image_mk_open_segment_left Prod.image_mk_openSegment_left

@[simp]
theorem image_mk_openSegment_right (x : E) (yâ‚ yâ‚‚ : F) :
    (fun y => (x, y)) '' openSegment ğ•œ yâ‚ yâ‚‚ = openSegment ğ•œ (x, yâ‚) (x, yâ‚‚) := by
  rw [openSegment_eq_imageâ‚‚, openSegment_eq_imageâ‚‚, image_image]
  refine EqOn.image_eq fun a ha â†¦ ?_
  simp [Convex.combo_self ha.2.2]
#align prod.image_mk_open_segment_right Prod.image_mk_openSegment_right

end Prod

namespace Pi

variable [OrderedSemiring ğ•œ] [âˆ€ i, AddCommMonoid (Ï€ i)] [âˆ€ i, Module ğ•œ (Ï€ i)] {s : Set Î¹}

theorem segment_subset (x y : âˆ€ i, Ï€ i) : segment ğ•œ x y âŠ† s.pi fun i => segment ğ•œ (x i) (y i) := by
  rintro z âŸ¨a, b, ha, hb, hab, hzâŸ© i -
  exact âŸ¨a, b, ha, hb, hab, congr_fun hz iâŸ©
#align pi.segment_subset Pi.segment_subset

theorem openSegment_subset (x y : âˆ€ i, Ï€ i) :
    openSegment ğ•œ x y âŠ† s.pi fun i => openSegment ğ•œ (x i) (y i) := by
  rintro z âŸ¨a, b, ha, hb, hab, hzâŸ© i -
  exact âŸ¨a, b, ha, hb, hab, congr_fun hz iâŸ©
#align pi.open_segment_subset Pi.openSegment_subset

variable [DecidableEq Î¹]

theorem image_update_segment (i : Î¹) (xâ‚ xâ‚‚ : Ï€ i) (y : âˆ€ i, Ï€ i) :
    update y i '' [xâ‚ -[ğ•œ] xâ‚‚] = [update y i xâ‚ -[ğ•œ] update y i xâ‚‚] := by
  rw [segment_eq_imageâ‚‚, segment_eq_imageâ‚‚, image_image]
  refine EqOn.image_eq fun a ha â†¦ ?_
  simp only [â† update_smul, â† update_add, Convex.combo_self ha.2.2]
#align pi.image_update_segment Pi.image_update_segment

theorem image_update_openSegment (i : Î¹) (xâ‚ xâ‚‚ : Ï€ i) (y : âˆ€ i, Ï€ i) :
    update y i '' openSegment ğ•œ xâ‚ xâ‚‚ = openSegment ğ•œ (update y i xâ‚) (update y i xâ‚‚) := by
  rw [openSegment_eq_imageâ‚‚, openSegment_eq_imageâ‚‚, image_image]
  refine EqOn.image_eq fun a ha â†¦ ?_
  simp only [â† update_smul, â† update_add, Convex.combo_self ha.2.2]
#align pi.image_update_open_segment Pi.image_update_openSegment

end Pi
