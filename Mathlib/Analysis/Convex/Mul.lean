/-
Copyright (c) 2023 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Analysis.Convex.Function
import Mathlib.Algebra.Order.Monovary
import Mathlib.Tactic.FieldSimp

/-!
# Product of convex functions

This file proves that the product of convex functions is convex, provided they monovary.

As corollaries, we also prove that `x ‚Ü¶ x ^ n` is convex
* `Even.convexOn_pow`: for even `n : ‚Ñï`.
* `convexOn_pow`: over $[0, +‚àû)$ for `n : ‚Ñï`.
* `convexOn_zpow`: over $[0, +‚àû)$ For `n : ‚Ñ§`.
-/

open Set

variable {ùïú E F : Type*}

section LinearOrderedCommRing
variable [LinearOrderedCommRing ùïú] [LinearOrderedCommRing E]
  [LinearOrderedAddCommGroup F] [Module ùïú E] [Module ùïú F] [Module E F] [IsScalarTower ùïú E F]
  [SMulCommClass ùïú E F] [OrderedSMul ùïú E] [OrderedSMul ùïú F] [OrderedSMul E F] {s : Set ùïú}
  {f : ùïú ‚Üí E} {g : ùïú ‚Üí F}

lemma ConvexOn.smul' (hf : ConvexOn ùïú s f) (hg : ConvexOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : MonovaryOn f g s) : ConvexOn ùïú s (f ‚Ä¢ g) := by
  refine ‚ü®hf.1, fun x hx y hy a b ha hb hab ‚Ü¶ ?_‚ü©
  dsimp
  refine (smul_le_smul (hg.2 hx hy ha hb hab) (hf.2 hx hy ha hb hab) (hg‚ÇÄ $ hf.1 hx hy ha hb hab) $
    add_nonneg (smul_nonneg ha $ hf‚ÇÄ hx) $ smul_nonneg hb $ hf‚ÇÄ hy).trans ?_
  calc
      _ = (a * a) ‚Ä¢ (f x ‚Ä¢ g x) + (b * b) ‚Ä¢ (f y ‚Ä¢ g y) + (a * b) ‚Ä¢ (f x ‚Ä¢ g y + f y ‚Ä¢ g x)
        := ?_
    _ ‚â§ (a * a) ‚Ä¢ (f x ‚Ä¢ g x) + (b * b) ‚Ä¢ (f y ‚Ä¢ g y) + (a * b) ‚Ä¢ (f x ‚Ä¢ g x + f y ‚Ä¢ g y)
        := add_le_add_left (smul_le_smul_of_nonneg (hfg.smul_add_smul_le_smul_add_smul hx hy) $
            mul_nonneg ha hb) _
    _ = (a * (a + b)) ‚Ä¢ (f x ‚Ä¢ g x) + (b * (a + b)) ‚Ä¢ (f y ‚Ä¢ g y)
        := by simp only [mul_add, add_smul, smul_add, mul_comm _ a]; abel
    _ = _ := by simp_rw [hab, mul_one]
  simp only [mul_add, add_smul, smul_add]
  rw [‚Üêsmul_smul_smul_comm a, ‚Üêsmul_smul_smul_comm b, ‚Üêsmul_smul_smul_comm a b,
    ‚Üêsmul_smul_smul_comm b b, smul_eq_mul, smul_eq_mul, smul_eq_mul, smul_eq_mul, mul_comm b,
    add_comm _ ((b * b) ‚Ä¢ f y ‚Ä¢ g y), add_add_add_comm, add_comm ((a * b) ‚Ä¢ f y ‚Ä¢ g x)]

lemma ConcaveOn.smul' (hf : ConcaveOn ùïú s f) (hg : ConcaveOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : AntivaryOn f g s) : ConcaveOn ùïú s (f ‚Ä¢ g) := by
  refine ‚ü®hf.1, fun x hx y hy a b ha hb hab ‚Ü¶ ?_‚ü©
  dsimp
  refine (smul_le_smul (hg.2 hx hy ha hb hab) (hf.2 hx hy ha hb hab) (add_nonneg
    (smul_nonneg ha $ hg‚ÇÄ hx) $ smul_nonneg hb $ hg‚ÇÄ hy) $ hf‚ÇÄ $ hf.1 hx hy ha hb hab).trans' ?_
  calc a ‚Ä¢ f x ‚Ä¢ g x + b ‚Ä¢ f y ‚Ä¢ g y
        = (a * (a + b)) ‚Ä¢ (f x ‚Ä¢ g x) + (b * (a + b)) ‚Ä¢ (f y ‚Ä¢ g y)
        := by simp_rw [hab, mul_one]
    _ = (a * a) ‚Ä¢ (f x ‚Ä¢ g x) + (b * b) ‚Ä¢ (f y ‚Ä¢ g y) + (a * b) ‚Ä¢ (f x ‚Ä¢ g x + f y ‚Ä¢ g y)
        := by simp only [mul_add, add_smul, smul_add, mul_comm _ a]; abel
    _ ‚â§ (a * a) ‚Ä¢ (f x ‚Ä¢ g x) + (b * b) ‚Ä¢ (f y ‚Ä¢ g y) + (a * b) ‚Ä¢ (f x ‚Ä¢ g y + f y ‚Ä¢ g x)
        := add_le_add_left (smul_le_smul_of_nonneg (hfg.smul_add_smul_le_smul_add_smul hx hy) $
            mul_nonneg ha hb) _
    _ = _ := ?_
  simp only [mul_add, add_smul, smul_add]
  rw [‚Üêsmul_smul_smul_comm a, ‚Üêsmul_smul_smul_comm b, ‚Üêsmul_smul_smul_comm a b,
    ‚Üêsmul_smul_smul_comm b b, smul_eq_mul, smul_eq_mul, smul_eq_mul, smul_eq_mul, mul_comm b a,
    add_comm ((a * b) ‚Ä¢ f x ‚Ä¢ g y), add_comm ((a * b) ‚Ä¢ f x ‚Ä¢ g y), add_add_add_comm]

lemma ConvexOn.smul'' (hf : ConvexOn ùïú s f) (hg : ConvexOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : AntivaryOn f g s) : ConcaveOn ùïú s (f ‚Ä¢ g) := by
  rw [‚Üêneg_smul_neg]
  exact hf.neg.smul' hg.neg (fun x hx ‚Ü¶ neg_nonneg.2 $ hf‚ÇÄ hx) (fun x hx ‚Ü¶ neg_nonneg.2 $ hg‚ÇÄ hx) hfg.neg

lemma ConcaveOn.smul'' (hf : ConcaveOn ùïú s f) (hg : ConcaveOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : MonovaryOn f g s) : ConvexOn ùïú s (f ‚Ä¢ g) := by
  rw [‚Üêneg_smul_neg]
  exact hf.neg.smul' hg.neg (fun x hx ‚Ü¶ neg_nonneg.2 $ hf‚ÇÄ hx) (fun x hx ‚Ü¶ neg_nonneg.2 $ hg‚ÇÄ hx)
    hfg.neg

lemma ConvexOn.smul_concaveOn (hf : ConvexOn ùïú s f) (hg : ConcaveOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ùïú s (f ‚Ä¢ g) := by
  rw [‚Üêneg_convexOn_iff, ‚Üêsmul_neg]
  exact hf.smul' hg.neg hf‚ÇÄ (fun x hx ‚Ü¶ neg_nonneg.2 $ hg‚ÇÄ hx) hfg.neg_right

lemma ConcaveOn.smul_convexOn (hf : ConcaveOn ùïú s f) (hg : ConvexOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : MonovaryOn f g s) :
    ConvexOn ùïú s (f ‚Ä¢ g) := by
  rw [‚Üêneg_concaveOn_iff, ‚Üêsmul_neg]
  exact hf.smul' hg.neg hf‚ÇÄ (fun x hx ‚Ü¶ neg_nonneg.2 $ hg‚ÇÄ hx) hfg.neg_right

lemma ConvexOn.smul_concaveOn' (hf : ConvexOn ùïú s f) (hg : ConcaveOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : MonovaryOn f g s) :
    ConvexOn ùïú s (f ‚Ä¢ g) := by
  rw [‚Üêneg_concaveOn_iff, ‚Üêsmul_neg]
  exact hf.smul'' hg.neg hf‚ÇÄ (fun x hx ‚Ü¶ neg_nonpos.2 $ hg‚ÇÄ hx) hfg.neg_right

lemma ConcaveOn.smul_convexOn' (hf : ConcaveOn ùïú s f) (hg : ConvexOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ùïú s (f ‚Ä¢ g) := by
  rw [‚Üêneg_convexOn_iff, ‚Üêsmul_neg]
  exact hf.smul'' hg.neg hf‚ÇÄ (fun x hx ‚Ü¶ neg_nonpos.2 $ hg‚ÇÄ hx) hfg.neg_right

variable [IsScalarTower ùïú E E] [SMulCommClass ùïú E E] {f g : ùïú ‚Üí E}

lemma ConvexOn.mul (hf : ConvexOn ùïú s f) (hg : ConvexOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : MonovaryOn f g s) :
    ConvexOn ùïú s (f * g) := hf.smul' hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConcaveOn.mul (hf : ConcaveOn ùïú s f) (hg : ConcaveOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ùïú s (f * g) := hf.smul' hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConvexOn.mul' (hf : ConvexOn ùïú s f) (hg : ConvexOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ùïú s (f * g) := hf.smul'' hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConcaveOn.mul' (hf : ConcaveOn ùïú s f) (hg : ConcaveOn ùïú s g) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0)
    (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : MonovaryOn f g s) :
    ConvexOn ùïú s (f * g) := hf.smul'' hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConvexOn.mul_concaveOn (hf : ConvexOn ùïú s f) (hg : ConcaveOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ùïú s (f * g) := hf.smul_concaveOn hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConcaveOn.mul_convexOn (hf : ConcaveOn ùïú s f) (hg : ConvexOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí g x ‚â§ 0) (hfg : MonovaryOn f g s) :
    ConvexOn ùïú s (f * g) := hf.smul_convexOn hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConvexOn.mul_concaveOn' (hf : ConvexOn ùïú s f) (hg : ConcaveOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : MonovaryOn f g s) :
    ConvexOn ùïú s (f * g) := hf.smul_concaveOn' hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConcaveOn.mul_convexOn' (hf : ConcaveOn ùïú s f) (hg : ConvexOn ùïú s g)
    (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí f x ‚â§ 0) (hg‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ùïú s (f ‚Ä¢ g) := hf.smul_convexOn' hg hf‚ÇÄ hg‚ÇÄ hfg

lemma ConvexOn.pow (hf : ConvexOn ùïú s f) (hf‚ÇÄ : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí 0 ‚â§ f x) :
    ‚àÄ n, ConvexOn ùïú s (f ^ n)
  | 0 => by simpa using convexOn_const 1 hf.1
  | n + 1 => by rw [pow_succ]; exact hf.mul (hf.pow hf‚ÇÄ _) hf‚ÇÄ (fun x hx ‚Ü¶ pow_nonneg (hf‚ÇÄ hx) _) $
      (monovaryOn_self f s).pow_right‚ÇÄ hf‚ÇÄ n

/-- `x^n`, `n : ‚Ñï` is convex on `[0, +‚àû)` for all `n`. -/
lemma convexOn_pow : ‚àÄ n, ConvexOn ùïú (Ici 0) (fun x : ùïú ‚Ü¶ x ^ n) :=
  (convexOn_id $ convex_Ici _).pow fun _ ‚Ü¶ id

/-- `x^n`, `n : ‚Ñï` is convex on the whole real line whenever `n` is even. -/
protected lemma Even.convexOn_pow {n : ‚Ñï} (hn : Even n) : ConvexOn ùïú univ fun x : ùïú ‚Ü¶ x ^ n := by
  obtain ‚ü®n, rfl‚ü© := hn
  simp_rw [‚Üêtwo_mul, pow_mul]
  refine ConvexOn.pow ‚ü®convex_univ, fun x _ y _ a b ha hb hab ‚Ü¶ sub_nonneg.1 ?_‚ü©
    (fun _ _ ‚Ü¶ by positivity) _
  calc
    (0 : ùïú) ‚â§ (a * b) * (x - y) ^ 2 := by positivity
    _ = _ := by obtain rfl := eq_sub_of_add_eq hab; simp only [smul_eq_mul]; ring

end LinearOrderedCommRing

section LinearOrderedField
variable [LinearOrderedField ùïú]

open Int in
/-- `x^m`, `m : ‚Ñ§` is convex on `(0, +‚àû)` for all `m`. -/
lemma convexOn_zpow : ‚àÄ n : ‚Ñ§, ConvexOn ùïú (Ioi 0) fun x : ùïú ‚Ü¶ x ^ n
  | (n : ‚Ñï) => by
    simp_rw [zpow_ofNat]
    exact (convexOn_pow n).subset Ioi_subset_Ici_self (convex_Ioi _)
  | -[n+1] => by
    simp_rw [zpow_negSucc, ‚Üêinv_pow]
    refine (convexOn_iff_forall_pos.2 ‚ü®convex_Ioi _, ?_‚ü©).pow (fun x (hx : 0 < x) ‚Ü¶ by positivity) _
    rintro x (hx : 0 < x) y (hy : 0 < y) a b ha hb hab
    field_simp
    rw [div_le_div_iff, ‚Üêsub_nonneg]
    calc
      0 ‚â§ a * b * (x - y) ^ 2 := by positivity
      _ = _ := by obtain rfl := eq_sub_of_add_eq hab; ring
    all_goals positivity
