/-
Copyright (c) 2023 YaÃ«l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies
-/
import Mathlib.Algebra.Order.Monovary
import Mathlib.Algebra.Order.Ring.Basic
import Mathlib.Analysis.Convex.Function
import Mathlib.Tactic.FieldSimp

/-!
# Product of convex functions

This file proves that the product of convex functions is convex, provided they monovary.

As corollaries, we also prove that `x â†¦ x ^ n` is convex
* `Even.convexOn_pow`: for even `n : â„•`.
* `convexOn_pow`: over $[0, +âˆ)$ for `n : â„•`.
* `convexOn_zpow`: over $(0, +âˆ)$ For `n : â„¤`.
-/

open Set

variable {ğ•œ E F G : Type*}

section LinearOrderedCommRing
variable [CommRing ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [CommRing E] [LinearOrder E] [IsStrictOrderedRing E]
  [AddCommGroup F] [LinearOrder F] [IsOrderedAddMonoid F]
  [AddCommGroup G] [Module ğ•œ G]
  [Module ğ•œ E] [Module ğ•œ F] [Module E F] [IsScalarTower ğ•œ E F] [SMulCommClass ğ•œ E F]
  [IsOrderedModule ğ•œ F] [IsStrictOrderedModule E F] {s : Set G} {f : G â†’ E} {g : G â†’ F}

lemma ConvexOn.smul' (hf : ConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x)
    (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : MonovaryOn f g s) : ConvexOn ğ•œ s (f â€¢ g) := by
  refine âŸ¨hf.1, fun x hx y hy a b ha hb hab â†¦ ?_âŸ©
  dsimp
  refine
    (smul_le_smul (hf.2 hx hy ha hb hab) (hg.2 hx hy ha hb hab) (hfâ‚€ <| hf.1 hx hy ha hb hab) <|
      add_nonneg (smul_nonneg ha <| hgâ‚€ hx) <| smul_nonneg hb <| hgâ‚€ hy).trans ?_
  calc
      _ = (a * a) â€¢ (f x â€¢ g x) + (b * b) â€¢ (f y â€¢ g y) + (a * b) â€¢ (f x â€¢ g y + f y â€¢ g x) := ?_
    _ â‰¤ (a * a) â€¢ (f x â€¢ g x) + (b * b) â€¢ (f y â€¢ g y) + (a * b) â€¢ (f x â€¢ g x + f y â€¢ g y) := by
        gcongr _ + (a * b) â€¢ ?_; exact hfg.smul_add_smul_le_smul_add_smul hx hy
    _ = (a * (a + b)) â€¢ (f x â€¢ g x) + (b * (a + b)) â€¢ (f y â€¢ g y) := by
        simp only [mul_add, add_smul, smul_add, mul_comm _ a]; abel
    _ = _ := by simp_rw [hab, mul_one]
  simp only [add_smul, smul_add]
  rw [â† smul_smul_smul_comm a, â† smul_smul_smul_comm b, â† smul_smul_smul_comm a b,
    â† smul_smul_smul_comm b b, smul_eq_mul, smul_eq_mul, smul_eq_mul, smul_eq_mul, mul_comm b,
    add_comm _ ((b * b) â€¢ f y â€¢ g y), add_add_add_comm, add_comm ((a * b) â€¢ f y â€¢ g x)]

lemma ConcaveOn.smul' [IsOrderedModule ğ•œ E] (hf : ConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f â€¢ g) := by
  refine âŸ¨hf.1, fun x hx y hy a b ha hb hab â†¦ ?_âŸ©
  dsimp
  refine (smul_le_smul (hf.2 hx hy ha hb hab) (hg.2 hx hy ha hb hab)
    (add_nonneg (smul_nonneg ha <| hfâ‚€ hx) <| smul_nonneg hb <| hfâ‚€ hy)
    (hgâ‚€ <| hf.1 hx hy ha hb hab)).trans' ?_
  calc a â€¢ f x â€¢ g x + b â€¢ f y â€¢ g y
        = (a * (a + b)) â€¢ (f x â€¢ g x) + (b * (a + b)) â€¢ (f y â€¢ g y) := by simp_rw [hab, mul_one]
    _ = (a * a) â€¢ (f x â€¢ g x) + (b * b) â€¢ (f y â€¢ g y) + (a * b) â€¢ (f x â€¢ g x + f y â€¢ g y) := by
        simp only [mul_add, add_smul, smul_add, mul_comm _ a]; abel
    _ â‰¤ (a * a) â€¢ (f x â€¢ g x) + (b * b) â€¢ (f y â€¢ g y) + (a * b) â€¢ (f x â€¢ g y + f y â€¢ g x) := by
        gcongr _ + (a * b) â€¢ ?_; exact hfg.smul_add_smul_le_smul_add_smul hx hy
    _ = _ := ?_
  simp only [add_smul, smul_add]
  rw [â† smul_smul_smul_comm a, â† smul_smul_smul_comm b, â† smul_smul_smul_comm a b,
    â† smul_smul_smul_comm b b, smul_eq_mul, smul_eq_mul, smul_eq_mul, smul_eq_mul, mul_comm b a,
    add_comm ((a * b) â€¢ f x â€¢ g y), add_comm ((a * b) â€¢ f x â€¢ g y), add_add_add_comm]

lemma ConvexOn.smul'' [IsOrderedModule ğ•œ E] (hf : ConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f â€¢ g) := by
  rw [â† neg_smul_neg]
  exact hf.neg.smul' hg.neg (fun x hx â†¦ neg_nonneg.2 <| hfâ‚€ hx) (fun x hx â†¦ neg_nonneg.2 <| hgâ‚€ hx)
    hfg.neg

lemma ConcaveOn.smul'' (hf : ConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0)
    (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : MonovaryOn f g s) : ConvexOn ğ•œ s (f â€¢ g) := by
  rw [â† neg_smul_neg]
  exact hf.neg.smul' hg.neg (fun x hx â†¦ neg_nonneg.2 <| hfâ‚€ hx) (fun x hx â†¦ neg_nonneg.2 <| hgâ‚€ hx)
    hfg.neg

lemma ConvexOn.smul_concaveOn (hf : ConvexOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f â€¢ g) := by
  rw [â† neg_convexOn_iff, â† smul_neg]
  exact hf.smul' hg.neg hfâ‚€ (fun x hx â†¦ neg_nonneg.2 <| hgâ‚€ hx) hfg.neg_right

lemma ConcaveOn.smul_convexOn [IsOrderedModule ğ•œ E] (hf : ConcaveOn ğ•œ s f) (hg : ConvexOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : MonovaryOn f g s) :
    ConvexOn ğ•œ s (f â€¢ g) := by
  rw [â† neg_concaveOn_iff, â† smul_neg]
  exact hf.smul' hg.neg hfâ‚€ (fun x hx â†¦ neg_nonneg.2 <| hgâ‚€ hx) hfg.neg_right

lemma ConvexOn.smul_concaveOn' [IsOrderedModule ğ•œ E] (hf : ConvexOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : MonovaryOn f g s) :
    ConvexOn ğ•œ s (f â€¢ g) := by
  rw [â† neg_concaveOn_iff, â† smul_neg]
  exact hf.smul'' hg.neg hfâ‚€ (fun x hx â†¦ neg_nonpos.2 <| hgâ‚€ hx) hfg.neg_right

lemma ConcaveOn.smul_convexOn' (hf : ConcaveOn ğ•œ s f) (hg : ConvexOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f â€¢ g) := by
  rw [â† neg_convexOn_iff, â† smul_neg]
  exact hf.smul'' hg.neg hfâ‚€ (fun x hx â†¦ neg_nonpos.2 <| hgâ‚€ hx) hfg.neg_right

variable [IsOrderedModule ğ•œ E] [IsScalarTower ğ•œ E E] [SMulCommClass ğ•œ E E] {f g : G â†’ E}

lemma ConvexOn.mul (hf : ConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x)
    (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : MonovaryOn f g s) :
    ConvexOn ğ•œ s (f * g) := hf.smul' hg hfâ‚€ hgâ‚€ hfg

lemma ConcaveOn.mul (hf : ConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f * g) := hf.smul' hg hfâ‚€ hgâ‚€ hfg

lemma ConvexOn.mul' (hf : ConvexOn ğ•œ s f) (hg : ConvexOn ğ•œ s g) (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0)
    (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f * g) := hf.smul'' hg hfâ‚€ hgâ‚€ hfg

lemma ConcaveOn.mul' (hf : ConcaveOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g) (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0)
    (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : MonovaryOn f g s) :
    ConvexOn ğ•œ s (f * g) := hf.smul'' hg hfâ‚€ hgâ‚€ hfg

lemma ConvexOn.mul_concaveOn (hf : ConvexOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f * g) := hf.smul_concaveOn hg hfâ‚€ hgâ‚€ hfg

lemma ConcaveOn.mul_convexOn (hf : ConcaveOn ğ•œ s f) (hg : ConvexOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ g x â‰¤ 0) (hfg : MonovaryOn f g s) :
    ConvexOn ğ•œ s (f * g) := hf.smul_convexOn hg hfâ‚€ hgâ‚€ hfg

lemma ConvexOn.mul_concaveOn' (hf : ConvexOn ğ•œ s f) (hg : ConcaveOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : MonovaryOn f g s) :
    ConvexOn ğ•œ s (f * g) := hf.smul_concaveOn' hg hfâ‚€ hgâ‚€ hfg

lemma ConcaveOn.mul_convexOn' (hf : ConcaveOn ğ•œ s f) (hg : ConvexOn ğ•œ s g)
    (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ f x â‰¤ 0) (hgâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ g x) (hfg : AntivaryOn f g s) :
    ConcaveOn ğ•œ s (f â€¢ g) := hf.smul_convexOn' hg hfâ‚€ hgâ‚€ hfg

lemma ConvexOn.pow (hf : ConvexOn ğ•œ s f) (hfâ‚€ : âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ 0 â‰¤ f x) :
    âˆ€ n, ConvexOn ğ•œ s (f ^ n)
  | 0 => by simpa using convexOn_const 1 hf.1
  | n + 1 => by
    rw [pow_succ']
    exact hf.mul (hf.pow hfâ‚€ _) hfâ‚€ (fun x hx â†¦ pow_nonneg (hfâ‚€ hx) _) <|
      (monovaryOn_self f s).pow_rightâ‚€ hfâ‚€ n

/-- `x^n`, `n : â„•` is convex on `[0, +âˆ)` for all `n`. -/
lemma convexOn_pow : âˆ€ n, ConvexOn ğ•œ (Ici 0) fun x : ğ•œ â†¦ x ^ n :=
  (convexOn_id <| convex_Ici _).pow fun _ â†¦ id

/-- `x^n`, `n : â„•` is convex on the whole real line whenever `n` is even. -/
protected lemma Even.convexOn_pow {n : â„•} (hn : Even n) : ConvexOn ğ•œ univ fun x : ğ•œ â†¦ x ^ n := by
  obtain âŸ¨n, rflâŸ© := hn
  simp_rw [â† two_mul, pow_mul]
  refine ConvexOn.pow âŸ¨convex_univ, fun x _ y _ a b ha hb hab â†¦ sub_nonneg.1 ?_âŸ©
    (fun _ _ â†¦ by positivity) _
  calc
    (0 : ğ•œ) â‰¤ (a * b) * (x - y) ^ 2 := by positivity
    _ = _ := by obtain rfl := eq_sub_of_add_eq hab; simp only [smul_eq_mul]; ring

end LinearOrderedCommRing

section LinearOrderedField
variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

open Int in
/-- `x^m`, `m : â„¤` is convex on `(0, +âˆ)` for all `m`. -/
lemma convexOn_zpow : âˆ€ n : â„¤, ConvexOn ğ•œ (Ioi 0) fun x : ğ•œ â†¦ x ^ n
  | (n : â„•) => by
    simp_rw [zpow_natCast]
    exact (convexOn_pow n).subset Ioi_subset_Ici_self (convex_Ioi _)
  | -[n+1] => by
    simp_rw [zpow_negSucc, â† inv_pow]
    refine (convexOn_iff_forall_pos.2 âŸ¨convex_Ioi _, ?_âŸ©).pow (fun x (hx : 0 < x) â†¦ by positivity) _
    rintro x (hx : 0 < x) y (hy : 0 < y) a b ha hb hab
    simp only [smul_eq_mul]
    field_simp
    rw [div_le_div_iffâ‚€, â† sub_nonneg]
    Â· calc
        0 â‰¤ a * b * (x - y) ^ 2 := by positivity
        _ = _ := by obtain rfl := eq_sub_of_add_eq hab; ring
    all_goals positivity

end LinearOrderedField
