/-
Copyright (c) 2025 Bolton Bailey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bolton Bailey
-/
module

public import Mathlib.Analysis.Convex.Combination
public import Mathlib.Analysis.Convex.SimplicialComplex.Basic
public import Mathlib.LinearAlgebra.Finsupp.VectorSpace
public import Mathlib.Combinatorics.SimpleGraph.Basic

/-!
# Simplicial complexes from affinely independent points

This file provides constructions for simplicial complexes where the vertices
are affinely independent.

## Main declarations

* `Geometry.SimplicialComplex.ofAffineIndependent`: Construct a simplicial complex from a
  downward-closed set of faces whose union of vertices is affinely independent.
* `Geometry.SimplicialComplex.onFinsupp`: Construct a simplicial complex on `Î¹ â†’â‚€ ğ•œ` from a
  downward-closed set of finite subsets of `Î¹`, using the standard basis vectors.
* `Geometry.SimplicialComplex.ofSimpleGraph`: Construct a simplicial complex from a simple graph,
  where vertices of the graph are 0-simplices and edges are 1-simplices.
-/

@[expose] public section

open Finset Set

namespace Geometry

namespace AbstractSimplicialComplex

/--
Construct an abstract simplicial complex from a simple graph, where vertices of the graph
are 0-simplices and edges are 1-simplices.
-/
def ofSimpleGraph {Î¹ : Type*} [DecidableEq Î¹] (G : SimpleGraph Î¹) :
    AbstractSimplicialComplex Î¹ where
  faces := ((fun v => ({v} : Finset Î¹)) '' (Set.univ (Î± := Î¹))) âˆª Sym2.toFinset '' G.edgeSet
  empty_notMem := by
    simp only [Set.mem_union, Set.mem_image, Set.mem_univ, true_and, Finset.singleton_ne_empty,
      exists_false, false_or, not_exists, not_and]
    exact fun _ _ h => Finset.ne_empty_of_mem (Sym2.mem_toFinset.mpr (Sym2.out_fst_mem _)) h
  down_closed := by
    simp only [Set.mem_union, Set.mem_image, Set.mem_univ, true_and]
    intro s t hs hts ht
    rcases hs with âŸ¨v, rflâŸ© | âŸ¨e, he, rflâŸ©
    Â· simp only [Finset.subset_singleton_iff] at hts
      rcases hts with rfl | rfl
      Â· exact ht.ne_empty rfl |>.elim
      Â· exact Or.inl âŸ¨v, rflâŸ©
    Â· by_cases hc : t.card â‰¤ 1
      Â· left
        obtain âŸ¨x, hxâŸ© := ht
        exact âŸ¨x, (Finset.eq_singleton_iff_unique_mem.mpr
          âŸ¨hx, fun y hy => Finset.card_le_one.mp hc y hy x hxâŸ©).symmâŸ©
      Â· right
        push_neg at hc
        have hle : e.toFinset.card â‰¤ t.card := by
          have := Sym2.card_toFinset e
          split_ifs at this <;> omega
        exact âŸ¨e, he, (Finset.eq_of_subset_of_card_le hts hle).symmâŸ©

end AbstractSimplicialComplex

namespace SimplicialComplex

/--
Construct a simplicial complex from a downward-closed set of faces
with defining points affinely independent.
-/
def ofAffineIndependent {ğ•œ E}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [DecidableEq E] [AddCommGroup E] [Module ğ•œ E]
    (faces : Set (Finset E)) (empty_notMem : âˆ… âˆ‰ faces)
    (down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t.Nonempty â†’ t âˆˆ faces)
    (indep : AffineIndependent ğ•œ (Subtype.val : (â‹ƒ s âˆˆ faces, (s : Set E)) â†’ E)) :
    SimplicialComplex ğ•œ E where
  faces := faces
  empty_notMem := empty_notMem
  indep {s} hs := indep.mono (Set.subset_biUnion_of_mem hs)
  down_closed := down_closed
  inter_subset_convexHull {s t} hs ht := by
    apply subset_of_eq
    rw [AffineIndependent.convexHull_inter (R := ğ•œ) (s := s âˆª t)]
    Â· apply indep.mono
      simp only [Finset.coe_union]
      exact Set.union_subset (Set.subset_biUnion_of_mem hs) (Set.subset_biUnion_of_mem ht)
    Â· exact Finset.subset_union_left
    Â· exact Finset.subset_union_right

/--
Construct a simplicial complex from an abstract simplicial complex on a set of points
over the `ğ•œ`-module of finitely supported functions on those points.
-/
noncomputable def onFinsupp {ğ•œ Î¹ : Type*} [DecidableEq Î¹]
    [DecidableEq ğ•œ] [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    (abstract : AbstractSimplicialComplex Î¹) :
    SimplicialComplex ğ•œ (Î¹ â†’â‚€ ğ•œ) :=
  ofAffineIndependent (ğ•œ := ğ•œ) (E := Î¹ â†’â‚€ ğ•œ)
    (abstract.faces.image (fun x => x.image (fun i => Finsupp.single i (1 : ğ•œ))))
    (by
      simp only [Set.mem_image, Finset.image_eq_empty]
      rintro âŸ¨s, hs, rflâŸ©
      exact abstract.empty_notMem hs)
    (by
      simp only [Set.mem_image]
      rintro _ t âŸ¨s', hs', rflâŸ© hts ht
      rw [Finset.subset_image_iff] at hts
      obtain âŸ¨t', ht', rflâŸ© := hts
      exact âŸ¨t', abstract.down_closed hs' ht' (Finset.image_nonempty.mp ht), rflâŸ©)
    (by
      refine (Finsupp.linearIndependent_single_one ğ•œ Î¹).affineIndependent.range.mono fun x hx => ?_
      simp only [Set.mem_iUnion, Set.mem_image, Finset.mem_coe] at hx
      obtain âŸ¨_, âŸ¨_, _, rflâŸ©, hxâŸ© := hx
      exact Finset.mem_image.mp hx |>.choose_spec.2 â–¸ Set.mem_range_self _)

/--
The simplicial complex associated to a simple graph, where vertices of the graph
are 0-simplices and edges are 1-simplices. The complex is constructed over the
`ğ•œ`-module of finitely supported functions on the vertex type.
-/
noncomputable def ofSimpleGraph {ğ•œ V : Type*} [DecidableEq V] [DecidableEq ğ•œ]
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    (G : SimpleGraph V) :
    SimplicialComplex ğ•œ (V â†’â‚€ ğ•œ) :=
  onFinsupp (AbstractSimplicialComplex.ofSimpleGraph G)

end SimplicialComplex

end Geometry
