/-
Copyright (c) 2025 Bolton Bailey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bolton Bailey
-/
module

public import Mathlib.Analysis.Convex.Combination
public import Mathlib.Analysis.Convex.SimplicialComplex.Basic
public import Mathlib.LinearAlgebra.Finsupp.VectorSpace
public import Mathlib.Combinatorics.SimpleGraph.Basic

/-!
# Simplicial complexes from affinely independent points

This file provides constructions for simplicial complexes where the vertices
are affinely independent.

## Main declarations

* `Geometry.SimplicialComplex.ofAffineIndependent`: Construct a simplicial complex from a
  downward-closed set of faces whose union of vertices is affinely independent.
* `Geometry.SimplicialComplex.onFinsupp`: Construct a simplicial complex on `Œπ ‚Üí‚ÇÄ ùïú` from a
  downward-closed set of finite subsets of `Œπ`, using the standard basis vectors.
-/

@[expose] public section

open Finset Set

namespace Geometry

namespace SimplicialComplex

/--
Construct a simplicial complex from a downward-closed set of faces
with defining points affinely independent.
-/
def ofAffineIndependent {ùïú E}
    [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú] [DecidableEq E] [AddCommGroup E] [Module ùïú E]
    (faces : Set (Finset E)) (empty_notMem : ‚àÖ ‚àâ faces)
    (down_closed : ‚àÄ {s t}, s ‚àà faces ‚Üí t ‚äÜ s ‚Üí t.Nonempty ‚Üí t ‚àà faces)
    (indep : AffineIndependent ùïú (Subtype.val : (‚ãÉ s ‚àà faces, (s : Set E)) ‚Üí E)) :
    SimplicialComplex ùïú E where
  faces := faces
  empty_notMem := empty_notMem
  indep {s} hs := indep.mono (Set.subset_biUnion_of_mem hs)
  down_closed := down_closed
  inter_subset_convexHull {s t} hs ht := by
    apply subset_of_eq
    rw [AffineIndependent.convexHull_inter (R := ùïú) (s := s ‚à™ t)]
    ¬∑ apply indep.mono
      simp only [Finset.coe_union]
      exact Set.union_subset (Set.subset_biUnion_of_mem hs) (Set.subset_biUnion_of_mem ht)
    ¬∑ exact Finset.subset_union_left
    ¬∑ exact Finset.subset_union_right

/--
Construct a simplicial complex from a downward-closed set of points
over the `ùïú`-module of finitely supported functions on those points.
-/
noncomputable def onFinsupp {ùïú Œπ : Type*} [DecidableEq Œπ]
    [DecidableEq ùïú] [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú]
    (faces : Set (Finset Œπ))
    (empty_notMem : ‚àÖ ‚àâ faces)
    (down_closed : ‚àÄ {s t}, s ‚àà faces ‚Üí t ‚äÜ s ‚Üí t.Nonempty ‚Üí t ‚àà faces) :
    SimplicialComplex ùïú (Œπ ‚Üí‚ÇÄ ùïú) :=
  ofAffineIndependent (ùïú := ùïú) (E := Œπ ‚Üí‚ÇÄ ùïú)
    (faces.image (fun x => x.image (fun i => Finsupp.single i (1 : ùïú))))
    (by
      simp only [Set.mem_image, Finset.image_eq_empty]
      rintro ‚ü®s, hs, rfl‚ü©
      exact empty_notMem hs)
    (by
      simp only [Set.mem_image]
      rintro _ t ‚ü®s', hs', rfl‚ü© hts ht
      rw [Finset.subset_image_iff] at hts
      obtain ‚ü®t', ht', rfl‚ü© := hts
      exact ‚ü®t', down_closed hs' ht' (Finset.image_nonempty.mp ht), rfl‚ü©)
    (by
      refine (Finsupp.linearIndependent_single_one ùïú Œπ).affineIndependent.range.mono fun x hx => ?_
      simp only [Set.mem_iUnion, Set.mem_image, Finset.mem_coe] at hx
      obtain ‚ü®_, ‚ü®_, _, rfl‚ü©, hx‚ü© := hx
      exact Finset.mem_image.mp hx |>.choose_spec.2 ‚ñ∏ Set.mem_range_self _)

/--
The simplicial complex associated to a simple graph, where vertices of the graph
are 0-simplices and edges are 1-simplices. The complex is constructed over the
`ùïú`-module of finitely supported functions on the vertex type.
-/
noncomputable def ofSimpleGraph {ùïú V : Type*} [DecidableEq V] [DecidableEq ùïú]
    [Field ùïú] [LinearOrder ùïú] [IsStrictOrderedRing ùïú]
    (G : SimpleGraph V) :
    SimplicialComplex ùïú (V ‚Üí‚ÇÄ ùïú) :=
  onFinsupp
    (faces := ((fun v => ({v} : Finset V)) '' (Set.univ (Œ± := V))) ‚à™ Sym2.toFinset '' G.edgeSet)
    (empty_notMem := by
      simp only [Set.mem_union, Set.mem_image, Set.mem_univ, true_and, Finset.singleton_ne_empty,
        exists_false, false_or, not_exists, not_and]
      exact fun _ _ h => Finset.ne_empty_of_mem (Sym2.mem_toFinset.mpr (Sym2.out_fst_mem _)) h)
    (down_closed := by
      simp only [Set.mem_union, Set.mem_image, Set.mem_univ, true_and]
      intro s t hs hts ht
      rcases hs with ‚ü®v, rfl‚ü© | ‚ü®e, he, rfl‚ü©
      ¬∑ simp only [Finset.subset_singleton_iff] at hts
        rcases hts with rfl | rfl
        ¬∑ exact ht.ne_empty rfl |>.elim
        ¬∑ exact Or.inl ‚ü®v, rfl‚ü©
      ¬∑ by_cases hc : t.card ‚â§ 1
        ¬∑ left
          obtain ‚ü®x, hx‚ü© := ht
          exact ‚ü®x, (Finset.eq_singleton_iff_unique_mem.mpr
            ‚ü®hx, fun y hy => Finset.card_le_one.mp hc y hy x hx‚ü©).symm‚ü©
        ¬∑ right
          push_neg at hc
          have hle : e.toFinset.card ‚â§ t.card := by
            have := Sym2.card_toFinset e
            split_ifs at this <;> omega
          exact ‚ü®e, he, (Finset.eq_of_subset_of_card_le hts hle).symm‚ü©)

end SimplicialComplex

end Geometry
