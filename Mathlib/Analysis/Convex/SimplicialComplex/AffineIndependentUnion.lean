/-
Copyright (c) 2025 Bolton Bailey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bolton Bailey
-/
module

public import Mathlib.Analysis.Convex.Combination
public import Mathlib.Analysis.Convex.SimplicialComplex.Basic
public import Mathlib.LinearAlgebra.Finsupp.VectorSpace

/-!
# Simplicial complexes from affinely independent points

This file provides constructions for simplicial complexes where the vertices
are affinely independent.

## Main declarations

* `Geometry.SimplicialComplex.ofAffineIndependent`: Construct a simplicial complex from a
  downward-closed set of faces whose union of vertices is affinely independent.
* `Geometry.SimplicialComplex.onFinsupp`: Construct a simplicial complex on `Î¹ â†’â‚€ ð•œ` from a
  downward-closed set of finite subsets of `Î¹`, using the standard basis vectors.
-/

@[expose] public section

open Finset Set

-- TODO find home
open Classical in
theorem AffineIndependent.Finsupp {ð•œ Î¹ : Type*} [inst : Ring ð•œ] :
    AffineIndependent ð•œ (V := Î¹ â†’â‚€ ð•œ) (P := Î¹ â†’â‚€ ð•œ) fun i â†¦ Finsupp.single i 1 := by
  intro s w hw0 hwv i hi
  rw [Finset.weightedVSub_eq_weightedVSubOfPoint_of_sum_eq_zero _ _ _ hw0 0,
    Finset.weightedVSubOfPoint_apply] at hwv
  simp only [vsub_eq_sub, sub_zero] at hwv
  exact (linearIndependent_iff'.mp (Finsupp.linearIndependent_single_one ð•œ Î¹)) s w hwv i hi


namespace Geometry

namespace SimplicialComplex

open Classical in
/--
Construct a simplicial complex from a downward-closed set of faces
with defining points affinely independent.
-/
def ofAffineIndependent {ð•œ E}
    [Field ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ] [AddCommGroup E] [Module ð•œ E]
    (faces : Set (Finset E)) (empty_notMem : âˆ… âˆ‰ faces)
    (down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t.Nonempty â†’ t âˆˆ faces)
    (indep : AffineIndependent ð•œ (Subtype.val : (â‹ƒ s âˆˆ faces, (s : Set E)) â†’ E)) :
    SimplicialComplex ð•œ E where
  faces := faces
  empty_notMem := empty_notMem
  indep {s} hs := indep.mono (Set.subset_biUnion_of_mem hs)
  down_closed := down_closed
  inter_subset_convexHull {s t} hs ht := by
    apply subset_of_eq
    rw [AffineIndependent.convexHull_inter (R := ð•œ) (s := s âˆª t)]
    Â· apply indep.mono
      simp only [Finset.coe_union]
      exact Set.union_subset (Set.subset_biUnion_of_mem hs) (Set.subset_biUnion_of_mem ht)
    Â· exact Finset.subset_union_left
    Â· exact Finset.subset_union_right

open Classical in
/--
Construct a simplicial complex from a downward-closed set of points
over the `ð•œ`-module of finitely supported functions on those points.
-/
def onFinsupp {ð•œ Î¹ : Type*} [Field ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ]
    (faces : Set (Finset Î¹))
    (empty_notMem : âˆ… âˆ‰ faces)
    (down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t.Nonempty â†’ t âˆˆ faces) :
    SimplicialComplex ð•œ (Î¹ â†’â‚€ ð•œ) :=
  ofAffineIndependent (ð•œ := ð•œ) (E := Î¹ â†’â‚€ ð•œ)
    (faces.image (fun x => x.image (fun i => Finsupp.single i (1 : ð•œ))))
    (by
      simp only [Set.mem_image, Finset.image_eq_empty]
      rintro âŸ¨s, hs, rflâŸ©
      exact empty_notMem hs)
    (by
      simp only [Set.mem_image]
      rintro _ t âŸ¨s', hs', rflâŸ© hts ht
      rw [Finset.subset_image_iff] at hts
      obtain âŸ¨t', ht', rflâŸ© := hts
      exact âŸ¨t', down_closed hs' ht' (Finset.image_nonempty.mp ht), rflâŸ©)
    (by
      refine AffineIndependent.Finsupp.range.mono fun x hx => ?_
      simp only [Set.mem_iUnion, Set.mem_image, Finset.mem_coe] at hx
      obtain âŸ¨_, âŸ¨_, _, rflâŸ©, hxâŸ© := hx
      exact Finset.mem_image.mp hx |>.choose_spec.2 â–¸ Set.mem_range_self _)

end SimplicialComplex

end Geometry
