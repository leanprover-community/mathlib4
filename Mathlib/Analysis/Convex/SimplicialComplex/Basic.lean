/-
Copyright (c) 2021 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Analysis.Convex.Hull
import Mathlib.LinearAlgebra.AffineSpace.Independent

/-!
# Simplicial complexes

In this file, we define simplicial complexes in `ğ•œ`-modules. A simplicial complex is a collection
of simplices closed by inclusion (of vertices) and intersection (of underlying sets).

We model them by a downward-closed set of affine independent finite sets whose convex hulls "glue
nicely", each finite set and its convex hull corresponding respectively to the vertices and the
underlying set of a simplex.

## Main declarations

* `SimplicialComplex ğ•œ E`: A simplicial complex in the `ğ•œ`-module `E`.
* `SimplicialComplex.vertices`: The zero-dimensional faces of a simplicial complex.
* `SimplicialComplex.facets`: The maximal faces of a simplicial complex.

## Notation

`s âˆˆ K` means that `s` is a face of `K`.

`K â‰¤ L` means that the faces of `K` are faces of `L`.

## Implementation notes

"glue nicely" usually means that the intersection of two faces (as sets in the ambient space) is a
face. Given that we store the vertices, not the faces, this would be a bit awkward to spell.
Instead, `SimplicialComplex.inter_subset_convexHull` is an equivalent condition which works on the
vertices.

## TODO

Simplicial complexes can be generalized to affine spaces once `ConvexHull` has been ported.
-/


open Finset Set

variable (ğ•œ E : Type*) [Ring ğ•œ] [PartialOrder ğ•œ] [AddCommGroup E] [Module ğ•œ E]

namespace Geometry

-- TODO: update to new binder order? not sure what binder order is correct for `down_closed`.
/-- A simplicial complex in a `ğ•œ`-module is a collection of simplices which glue nicely together.
Note that the textbook meaning of "glue nicely" is given in
`Geometry.SimplicialComplex.disjoint_or_exists_inter_eq_convexHull`. It is mostly useless, as
`Geometry.SimplicialComplex.convexHull_inter_convexHull` is enough for all purposes. -/
@[ext]
structure SimplicialComplex where
  /-- the faces of this simplicial complex: currently, given by their spanning vertices -/
  faces : Set (Finset E)
  /-- the empty set is not a face: hence, all faces are non-empty -/
  empty_notMem : âˆ… âˆ‰ faces
  /-- the vertices in each face are affine independent: this is an implementation detail -/
  indep : âˆ€ {s}, s âˆˆ faces â†’ AffineIndependent ğ•œ ((â†‘) : s â†’ E)
  /-- faces are downward closed: a non-empty subset of its spanning vertices spans another face -/
  down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t â‰  âˆ… â†’ t âˆˆ faces
  inter_subset_convexHull : âˆ€ {s t}, s âˆˆ faces â†’ t âˆˆ faces â†’
    convexHull ğ•œ â†‘s âˆ© convexHull ğ•œ â†‘t âŠ† convexHull ğ•œ (s âˆ© t : Set E)

namespace SimplicialComplex

@[deprecated (since := "2025-05-23")]
alias not_empty_mem := empty_notMem

variable {ğ•œ E}
variable {K : SimplicialComplex ğ•œ E} {s t : Finset E} {x : E}

/-- A `Finset` belongs to a `SimplicialComplex` if it's a face of it. -/
instance : Membership (Finset E) (SimplicialComplex ğ•œ E) :=
  âŸ¨fun K s => s âˆˆ K.facesâŸ©

lemma nonempty_of_mem_faces (hs : s âˆˆ K.faces) : s.Nonempty := by
  rw [Finset.nonempty_iff_ne_empty]; rintro rfl; exact K.empty_notMem hs

/-- The underlying space of a simplicial complex is the union of its faces. -/
def space (K : SimplicialComplex ğ•œ E) : Set E :=
  â‹ƒ s âˆˆ K.faces, convexHull ğ•œ (s : Set E)

theorem mem_space_iff : x âˆˆ K.space â†” âˆƒ s âˆˆ K.faces, x âˆˆ convexHull ğ•œ (s : Set E) := by
  simp [space]

theorem convexHull_subset_space (hs : s âˆˆ K.faces) : convexHull ğ•œ â†‘s âŠ† K.space := by
  convert subset_biUnion_of_mem hs
  rfl

protected theorem subset_space (hs : s âˆˆ K.faces) : (s : Set E) âŠ† K.space :=
  (subset_convexHull ğ•œ _).trans <| convexHull_subset_space hs

theorem convexHull_inter_convexHull (hs : s âˆˆ K.faces) (ht : t âˆˆ K.faces) :
    convexHull ğ•œ â†‘s âˆ© convexHull ğ•œ â†‘t = convexHull ğ•œ (s âˆ© t : Set E) :=
  (K.inter_subset_convexHull hs ht).antisymm <|
    subset_inter (convexHull_mono Set.inter_subset_left) <|
      convexHull_mono Set.inter_subset_right

/-- The conclusion is the usual meaning of "glue nicely" in textbooks. It turns out to be quite
unusable, as it's about faces as sets in space rather than simplices. Further, additional structure
on `ğ•œ` means the only choice of `u` is `s âˆ© t` (but it's hard to prove). -/
theorem disjoint_or_exists_inter_eq_convexHull (hs : s âˆˆ K.faces) (ht : t âˆˆ K.faces) :
    Disjoint (convexHull ğ•œ (s : Set E)) (convexHull ğ•œ â†‘t) âˆ¨
      âˆƒ u âˆˆ K.faces, convexHull ğ•œ (s : Set E) âˆ© convexHull ğ•œ â†‘t = convexHull ğ•œ â†‘u := by
  classical
  by_contra! h
  refine h.2 (s âˆ© t) (K.down_closed hs inter_subset_left fun hst => h.1 <|
    disjoint_iff_inf_le.mpr <| (K.inter_subset_convexHull hs ht).trans ?_) ?_
  Â· rw [â† coe_inter, hst, coe_empty, convexHull_empty]
    rfl
  Â· rw [coe_inter, convexHull_inter_convexHull hs ht]

/-- Construct a simplicial complex by removing the empty face for you. -/
@[simps]
def ofErase (faces : Set (Finset E)) (indep : âˆ€ s âˆˆ faces, AffineIndependent ğ•œ ((â†‘) : s â†’ E))
    (down_closed : âˆ€ s âˆˆ faces, âˆ€ t âŠ† s, t âˆˆ faces)
    (inter_subset_convexHull : âˆ€áµ‰ (s âˆˆ faces) (t âˆˆ faces),
      convexHull ğ•œ â†‘s âˆ© convexHull ğ•œ â†‘t âŠ† convexHull ğ•œ (s âˆ© t : Set E)) :
    SimplicialComplex ğ•œ E where
  faces := faces \ {âˆ…}
  empty_notMem h := h.2 (mem_singleton _)
  indep hs := indep _ hs.1
  down_closed hs hts ht := âŸ¨down_closed _ hs.1 _ hts, htâŸ©
  inter_subset_convexHull hs ht := inter_subset_convexHull _ hs.1 _ ht.1

/-- Construct a simplicial complex as a subset of a given simplicial complex. -/
@[simps]
def ofSubcomplex (K : SimplicialComplex ğ•œ E) (faces : Set (Finset E)) (subset : faces âŠ† K.faces)
    (down_closed : âˆ€ {s t}, s âˆˆ faces â†’ t âŠ† s â†’ t âˆˆ faces) : SimplicialComplex ğ•œ E :=
  { faces
    empty_notMem := fun h => K.empty_notMem (subset h)
    indep := fun hs => K.indep (subset hs)
    down_closed := fun hs hts _ => down_closed hs hts
    inter_subset_convexHull := fun hs ht => K.inter_subset_convexHull (subset hs) (subset ht) }

/-! ### Vertices -/


/-- The vertices of a simplicial complex are its zero-dimensional faces. -/
def vertices (K : SimplicialComplex ğ•œ E) : Set E :=
  { x | {x} âˆˆ K.faces }

theorem mem_vertices : x âˆˆ K.vertices â†” {x} âˆˆ K.faces := Iff.rfl

theorem vertices_eq : K.vertices = â‹ƒ k âˆˆ K.faces, (k : Set E) := by
  ext x
  refine âŸ¨fun h => mem_biUnion h <| mem_coe.2 <| mem_singleton_self x, fun h => ?_âŸ©
  obtain âŸ¨s, hs, hxâŸ© := mem_iUnionâ‚‚.1 h
  exact K.down_closed hs (Finset.singleton_subset_iff.2 <| mem_coe.1 hx) (singleton_ne_empty _)

theorem vertices_subset_space : K.vertices âŠ† K.space :=
  vertices_eq.subset.trans <| iUnionâ‚‚_mono fun x _ => subset_convexHull ğ•œ (x : Set E)

theorem vertex_mem_convexHull_iff (hx : x âˆˆ K.vertices) (hs : s âˆˆ K.faces) :
    x âˆˆ convexHull ğ•œ (s : Set E) â†” x âˆˆ s := by
  refine âŸ¨fun h => ?_, fun h => subset_convexHull ğ•œ _ hâŸ©
  classical
  have h := K.inter_subset_convexHull hx hs âŸ¨by simp, hâŸ©
  by_contra H
  rwa [â† coe_inter, Finset.disjoint_iff_inter_eq_empty.1 (Finset.disjoint_singleton_right.2 H).symm,
    coe_empty, convexHull_empty] at h

/-- A face is a subset of another one iff its vertices are. -/
theorem face_subset_face_iff (hs : s âˆˆ K.faces) (ht : t âˆˆ K.faces) :
    convexHull ğ•œ (s : Set E) âŠ† convexHull ğ•œ â†‘t â†” s âŠ† t :=
  âŸ¨fun h _ hxs =>
    (vertex_mem_convexHull_iff
          (K.down_closed hs (Finset.singleton_subset_iff.2 hxs) <| singleton_ne_empty _) ht).1
      (h (subset_convexHull ğ•œ (E := E) s hxs)),
    convexHull_monoâŸ©

/-! ### Facets -/


/-- A facet of a simplicial complex is a maximal face. -/
def facets (K : SimplicialComplex ğ•œ E) : Set (Finset E) :=
  { s âˆˆ K.faces | âˆ€ â¦ƒtâ¦„, t âˆˆ K.faces â†’ s âŠ† t â†’ s = t }

theorem mem_facets : s âˆˆ K.facets â†” s âˆˆ K.faces âˆ§ âˆ€ t âˆˆ K.faces, s âŠ† t â†’ s = t :=
  mem_sep_iff

theorem facets_subset : K.facets âŠ† K.faces := fun _ hs => hs.1

theorem not_facet_iff_subface (hs : s âˆˆ K.faces) : s âˆ‰ K.facets â†” âˆƒ t, t âˆˆ K.faces âˆ§ s âŠ‚ t := by
  refine âŸ¨fun hs' : Â¬(_ âˆ§ _) => ?_, ?_âŸ©
  Â· push_neg at hs'
    obtain âŸ¨t, htâŸ© := hs' hs
    exact âŸ¨t, ht.1, âŸ¨ht.2.1, fun hts => ht.2.2 (Subset.antisymm ht.2.1 hts)âŸ©âŸ©
  Â· rintro âŸ¨t, htâŸ© âŸ¨hs, hs'âŸ©
    have := hs' ht.1 ht.2.1
    rw [this] at ht
    exact ht.2.2 (Subset.refl t)

/-!
### The semilattice of simplicial complexes

`K â‰¤ L` means that `K.faces âŠ† L.faces`.
-/


-- `HasSSubset.SSubset.ne` would be handy here
variable (ğ•œ E)

/-- The complex consisting of only the faces present in both of its arguments. -/
instance : Min (SimplicialComplex ğ•œ E) :=
  âŸ¨fun K L =>
    { faces := K.faces âˆ© L.faces
      empty_notMem := fun h => K.empty_notMem (Set.inter_subset_left h)
      indep := fun hs => K.indep hs.1
      down_closed := fun hs hst ht => âŸ¨K.down_closed hs.1 hst ht, L.down_closed hs.2 hst htâŸ©
      inter_subset_convexHull := fun hs ht => K.inter_subset_convexHull hs.1 ht.1 }âŸ©

instance : SemilatticeInf (SimplicialComplex ğ•œ E) :=
  { PartialOrder.lift faces (fun _ _ => SimplicialComplex.ext) with
    min := (Â· âŠ“ Â·)
    inf_le_left := fun _ _ _ hs => hs.1
    inf_le_right := fun _ _ _ hs => hs.2
    le_inf := fun _ _ _ hKL hKM _ hs => âŸ¨hKL hs, hKM hsâŸ© }

instance hasBot : Bot (SimplicialComplex ğ•œ E) :=
  âŸ¨{  faces := âˆ…
      empty_notMem := Set.notMem_empty âˆ…
      indep := fun hs => (Set.notMem_empty _ hs).elim
      down_closed := fun hs => (Set.notMem_empty _ hs).elim
      inter_subset_convexHull := fun hs => (Set.notMem_empty _ hs).elim }âŸ©

instance : OrderBot (SimplicialComplex ğ•œ E) :=
  { SimplicialComplex.hasBot ğ•œ E with bot_le := fun _ => Set.empty_subset _ }

instance : Inhabited (SimplicialComplex ğ•œ E) :=
  âŸ¨âŠ¥âŸ©

variable {ğ•œ E}

theorem faces_bot : (âŠ¥ : SimplicialComplex ğ•œ E).faces = âˆ… := rfl

theorem space_bot : (âŠ¥ : SimplicialComplex ğ•œ E).space = âˆ… :=
  Set.biUnion_empty _

theorem facets_bot : (âŠ¥ : SimplicialComplex ğ•œ E).facets = âˆ… :=
  eq_empty_of_subset_empty facets_subset

end SimplicialComplex

end Geometry
