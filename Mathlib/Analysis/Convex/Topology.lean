/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Convex.Strict
public import Mathlib.Analysis.Convex.StdSimplex
public import Mathlib.Topology.Algebra.Affine
public import Mathlib.Topology.Algebra.Module.Basic

/-!
# Topological properties of convex sets

We prove the following facts:

* `Convex.interior` : interior of a convex set is convex;
* `Convex.closure` : closure of a convex set is convex;
* `closedConvexHull_closure_eq_closedConvexHull` : the closed convex hull of the closure of a set is
  equal to the closed convex hull of the set;
* `Set.Finite.isCompact_convexHull` : convex hull of a finite set is compact;
* `Set.Finite.isClosed_convexHull` : convex hull of a finite set is closed.
-/

@[expose] public section

assert_not_exists Cardinal Norm

open Metric Bornology Set Pointwise Convex

variable {Î¹ ğ•œ E : Type*}

namespace Real
variable {s : Set â„} {r Îµ : â„}

lemma closedBall_eq_segment (hÎµ : 0 â‰¤ Îµ) : closedBall r Îµ = segment â„ (r - Îµ) (r + Îµ) := by
  rw [closedBall_eq_Icc, segment_eq_Icc ((sub_le_self _ hÎµ).trans <| le_add_of_nonneg_right hÎµ)]

lemma ball_eq_openSegment (hÎµ : 0 < Îµ) : ball r Îµ = openSegment â„ (r - Îµ) (r + Îµ) := by
  rw [ball_eq_Ioo, openSegment_eq_Ioo ((sub_lt_self _ hÎµ).trans <| lt_add_of_pos_right _ hÎµ)]

theorem convex_iff_isPreconnected : Convex â„ s â†” IsPreconnected s :=
  convex_iff_ordConnected.trans isPreconnected_iff_ordConnected.symm

end Real

alias âŸ¨_, IsPreconnected.convexâŸ© := Real.convex_iff_isPreconnected

/-! ### Topological vector spaces -/
section TopologicalSpace

variable [Ring ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [DenselyOrdered ğ•œ]
  [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [ContinuousAdd E] [Module ğ•œ E] [ContinuousSMul ğ•œ E]
  {x y : E}

theorem segment_subset_closure_openSegment : [x -[ğ•œ] y] âŠ† closure (openSegment ğ•œ x y) := by
  rw [segment_eq_image, openSegment_eq_image, â† closure_Ioo (zero_ne_one' ğ•œ)]
  exact image_closure_subset_closure_image (by fun_prop)

end TopologicalSpace

section PseudoMetricSpace

variable [Ring ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [DenselyOrdered ğ•œ]
  [PseudoMetricSpace ğ•œ] [OrderTopology ğ•œ]
  [ProperSpace ğ•œ] [CompactIccSpace ğ•œ] [AddCommGroup E] [TopologicalSpace E] [T2Space E]
  [ContinuousAdd E] [Module ğ•œ E] [ContinuousSMul ğ•œ E]

@[simp]
theorem closure_openSegment (x y : E) : closure (openSegment ğ•œ x y) = [x -[ğ•œ] y] := by
  rw [segment_eq_image, openSegment_eq_image, â† closure_Ioo (zero_ne_one' ğ•œ)]
  exact (image_closure_of_isCompact (isBounded_Ioo _ _).isCompact_closure <|
    Continuous.continuousOn <| by fun_prop).symm

end PseudoMetricSpace

section ContinuousConstSMul

variable [Field ğ•œ] [PartialOrder ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [IsTopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]

/-- If `s` is a convex set, then `a â€¢ interior s + b â€¢ closure s âŠ† interior s` for all `0 < a`,
`0 â‰¤ b`, `a + b = 1`. See also `Convex.combo_interior_self_subset_interior` for a weaker version. -/
theorem Convex.combo_interior_closure_subset_interior {s : Set E} (hs : Convex ğ•œ s) {a b : ğ•œ}
    (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) : a â€¢ interior s + b â€¢ closure s âŠ† interior s :=
  interior_smulâ‚€ ha.ne' s â–¸
    calc
      interior (a â€¢ s) + b â€¢ closure s âŠ† interior (a â€¢ s) + closure (b â€¢ s) :=
        add_subset_add Subset.rfl (smul_closure_subset b s)
      _ = interior (a â€¢ s) + b â€¢ s := by rw [isOpen_interior.add_closure (b â€¢ s)]
      _ âŠ† interior (a â€¢ s + b â€¢ s) := subset_interior_add_left
      _ âŠ† interior s := interior_mono <| hs.set_combo_subset ha.le hb hab

/-- If `s` is a convex set, then `a â€¢ interior s + b â€¢ s âŠ† interior s` for all `0 < a`, `0 â‰¤ b`,
`a + b = 1`. See also `Convex.combo_interior_closure_subset_interior` for a stronger version. -/
theorem Convex.combo_interior_self_subset_interior {s : Set E} (hs : Convex ğ•œ s) {a b : ğ•œ}
    (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) : a â€¢ interior s + b â€¢ s âŠ† interior s :=
  calc
    a â€¢ interior s + b â€¢ s âŠ† a â€¢ interior s + b â€¢ closure s :=
      add_subset_add Subset.rfl <| image_mono subset_closure
    _ âŠ† interior s := hs.combo_interior_closure_subset_interior ha hb hab

/-- If `s` is a convex set, then `a â€¢ closure s + b â€¢ interior s âŠ† interior s` for all `0 â‰¤ a`,
`0 < b`, `a + b = 1`. See also `Convex.combo_self_interior_subset_interior` for a weaker version. -/
theorem Convex.combo_closure_interior_subset_interior {s : Set E} (hs : Convex ğ•œ s) {a b : ğ•œ}
    (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) : a â€¢ closure s + b â€¢ interior s âŠ† interior s := by
  rw [add_comm]
  exact hs.combo_interior_closure_subset_interior hb ha (add_comm a b â–¸ hab)

/-- If `s` is a convex set, then `a â€¢ s + b â€¢ interior s âŠ† interior s` for all `0 â‰¤ a`, `0 < b`,
`a + b = 1`. See also `Convex.combo_closure_interior_subset_interior` for a stronger version. -/
theorem Convex.combo_self_interior_subset_interior {s : Set E} (hs : Convex ğ•œ s) {a b : ğ•œ}
    (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) : a â€¢ s + b â€¢ interior s âŠ† interior s := by
  rw [add_comm]
  exact hs.combo_interior_self_subset_interior hb ha (add_comm a b â–¸ hab)

theorem Convex.combo_interior_closure_mem_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ interior s) (hy : y âˆˆ closure s) {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b)
    (hab : a + b = 1) : a â€¢ x + b â€¢ y âˆˆ interior s :=
  hs.combo_interior_closure_subset_interior ha hb hab <|
    add_mem_add (smul_mem_smul_set hx) (smul_mem_smul_set hy)

theorem Convex.combo_interior_self_mem_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ interior s) (hy : y âˆˆ s) {a b : ğ•œ} (ha : 0 < a) (hb : 0 â‰¤ b) (hab : a + b = 1) :
    a â€¢ x + b â€¢ y âˆˆ interior s :=
  hs.combo_interior_closure_mem_interior hx (subset_closure hy) ha hb hab

theorem Convex.combo_closure_interior_mem_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ closure s) (hy : y âˆˆ interior s) {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 < b)
    (hab : a + b = 1) : a â€¢ x + b â€¢ y âˆˆ interior s :=
  hs.combo_closure_interior_subset_interior ha hb hab <|
    add_mem_add (smul_mem_smul_set hx) (smul_mem_smul_set hy)

theorem Convex.combo_self_interior_mem_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s)
    (hy : y âˆˆ interior s) {a b : ğ•œ} (ha : 0 â‰¤ a) (hb : 0 < b) (hab : a + b = 1) :
    a â€¢ x + b â€¢ y âˆˆ interior s :=
  hs.combo_closure_interior_mem_interior (subset_closure hx) hy ha hb hab

theorem Convex.openSegment_interior_closure_subset_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ interior s) (hy : y âˆˆ closure s) : openSegment ğ•œ x y âŠ† interior s := by
  rintro _ âŸ¨a, b, ha, hb, hab, rflâŸ©
  exact hs.combo_interior_closure_mem_interior hx hy ha hb.le hab

theorem Convex.openSegment_interior_self_subset_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ interior s) (hy : y âˆˆ s) : openSegment ğ•œ x y âŠ† interior s :=
  hs.openSegment_interior_closure_subset_interior hx (subset_closure hy)

theorem Convex.openSegment_closure_interior_subset_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ closure s) (hy : y âˆˆ interior s) : openSegment ğ•œ x y âŠ† interior s := by
  rintro _ âŸ¨a, b, ha, hb, hab, rflâŸ©
  exact hs.combo_closure_interior_mem_interior hx hy ha.le hb hab

theorem Convex.openSegment_self_interior_subset_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ s) (hy : y âˆˆ interior s) : openSegment ğ•œ x y âŠ† interior s :=
  hs.openSegment_closure_interior_subset_interior (subset_closure hx) hy

section

variable [AddRightMono ğ•œ]

/-- If `x âˆˆ closure s` and `y âˆˆ interior s`, then the segment `(x, y]` is included in `interior s`.
-/
theorem Convex.add_smul_sub_mem_interior' {s : Set E} (hs : Convex ğ•œ s) {x y : E}
    (hx : x âˆˆ closure s) (hy : y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) :
    x + t â€¢ (y - x) âˆˆ interior s := by
  simpa only [sub_smul, smul_sub, one_smul, add_sub, add_comm] using
    hs.combo_interior_closure_mem_interior hy hx ht.1 (sub_nonneg.mpr ht.2)
      (add_sub_cancel _ _)

/-- If `x âˆˆ s` and `y âˆˆ interior s`, then the segment `(x, y]` is included in `interior s`. -/
theorem Convex.add_smul_sub_mem_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s)
    (hy : y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) : x + t â€¢ (y - x) âˆˆ interior s :=
  hs.add_smul_sub_mem_interior' (subset_closure hx) hy ht

/-- If `x âˆˆ closure s` and `x + y âˆˆ interior s`, then `x + t y âˆˆ interior s` for `t âˆˆ (0, 1]`. -/
theorem Convex.add_smul_mem_interior' {s : Set E} (hs : Convex ğ•œ s) {x y : E} (hx : x âˆˆ closure s)
    (hy : x + y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ interior s := by
  simpa only [add_sub_cancel_left] using hs.add_smul_sub_mem_interior' hx hy ht

/-- If `x âˆˆ s` and `x + y âˆˆ interior s`, then `x + t y âˆˆ interior s` for `t âˆˆ (0, 1]`. -/
theorem Convex.add_smul_mem_interior {s : Set E} (hs : Convex ğ•œ s) {x y : E} (hx : x âˆˆ s)
    (hy : x + y âˆˆ interior s) {t : ğ•œ} (ht : t âˆˆ Ioc (0 : ğ•œ) 1) : x + t â€¢ y âˆˆ interior s :=
  hs.add_smul_mem_interior' (subset_closure hx) hy ht

end

/-- In a topological vector space, the interior of a convex set is convex. -/
protected theorem Convex.interior [ZeroLEOneClass ğ•œ] {s : Set E} (hs : Convex ğ•œ s) :
    Convex ğ•œ (interior s) :=
  convex_iff_openSegment_subset.mpr fun _ hx _ hy =>
    hs.openSegment_closure_interior_subset_interior (interior_subset_closure hx) hy

/-- In a topological vector space, the closure of a convex set is convex. -/
protected theorem Convex.closure {s : Set E} (hs : Convex ğ•œ s) : Convex ğ•œ (closure s) :=
  fun x hx y hy a b ha hb hab =>
  let f : E â†’ E â†’ E := fun x' y' => a â€¢ x' + b â€¢ y'
  have hf : Continuous (Function.uncurry f) :=
    (continuous_fst.const_smul _).add (continuous_snd.const_smul _)
  show f x y âˆˆ closure s from map_mem_closureâ‚‚ hf hx hy fun _ hx' _ hy' => hs hx' hy' ha hb hab

end ContinuousConstSMul

section ContinuousConstSMul

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [IsTopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]

open AffineMap

/-- A convex set `s` is strictly convex provided that for any two distinct points of
`s \ interior s`, the line passing through these points has nonempty intersection with
`interior s`. -/
protected theorem Convex.strictConvex' {s : Set E} (hs : Convex ğ•œ s)
    (h : (s \ interior s).Pairwise fun x y => âˆƒ c : ğ•œ, lineMap x y c âˆˆ interior s) :
    StrictConvex ğ•œ s := by
  refine strictConvex_iff_openSegment_subset.2 ?_
  intro x hx y hy hne
  by_cases hx' : x âˆˆ interior s
  Â· exact hs.openSegment_interior_self_subset_interior hx' hy
  by_cases hy' : y âˆˆ interior s
  Â· exact hs.openSegment_self_interior_subset_interior hx hy'
  rcases h âŸ¨hx, hx'âŸ© âŸ¨hy, hy'âŸ© hne with âŸ¨c, hcâŸ©
  refine (openSegment_subset_union x y âŸ¨c, rflâŸ©).trans
    (insert_subset_iff.2 âŸ¨hc, union_subset ?_ ?_âŸ©)
  exacts [hs.openSegment_self_interior_subset_interior hx hc,
    hs.openSegment_interior_self_subset_interior hc hy]

/-- A convex set `s` is strictly convex provided that for any two distinct points `x`, `y` of
`s \ interior s`, the segment with endpoints `x`, `y` has nonempty intersection with
`interior s`. -/
protected theorem Convex.strictConvex {s : Set E} (hs : Convex ğ•œ s)
    (h : (s \ interior s).Pairwise fun x y => ([x -[ğ•œ] y] \ frontier s).Nonempty) :
    StrictConvex ğ•œ s := by
  refine hs.strictConvex' <| h.imp_on fun x hx y hy _ => ?_
  simp only [segment_eq_image_lineMap, â† self_diff_frontier]
  rintro âŸ¨_, âŸ¨âŸ¨c, hc, rflâŸ©, hcsâŸ©âŸ©
  refine âŸ¨c, hs.segment_subset hx.1 hy.1 ?_, hcsâŸ©
  exact (segment_eq_image_lineMap ğ•œ x y).symm â–¸ mem_image_of_mem _ hc

end ContinuousConstSMul

section ContinuousSMul

variable [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [IsTopologicalAddGroup E] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] [ContinuousSMul ğ•œ E]

theorem Convex.closure_interior_eq_closure_of_nonempty_interior {s : Set E} (hs : Convex ğ•œ s)
    (hs' : (interior s).Nonempty) : closure (interior s) = closure s :=
  subset_antisymm (closure_mono interior_subset)
    fun _ h â†¦ closure_mono (hs.openSegment_interior_closure_subset_interior hs'.choose_spec h)
      (segment_subset_closure_openSegment (right_mem_segment ..))

theorem Convex.interior_closure_eq_interior_of_nonempty_interior {s : Set E} (hs : Convex ğ•œ s)
    (hs' : (interior s).Nonempty) : interior (closure s) = interior s := by
  refine subset_antisymm ?_ (interior_mono subset_closure)
  intro y hy
  rcases hs' with âŸ¨x, hxâŸ©
  have h := AffineMap.lineMap_apply_one (k := ğ•œ) x y
  obtain âŸ¨t, ht1, htâŸ© := AffineMap.lineMap_continuous.tendsto' _ _ h |>.eventually_mem
    (mem_interior_iff_mem_nhds.1 hy) |>.exists_gt
  apply hs.openSegment_interior_closure_subset_interior hx ht
  nth_rw 1 [â† AffineMap.lineMap_apply_zero (k := ğ•œ) x y, â† image_openSegment]
  exact âŸ¨1, Ioo_subset_openSegment âŸ¨zero_lt_one, ht1âŸ©, hâŸ©

end ContinuousSMul

section TopologicalSpace

variable [Semiring ğ•œ] [PartialOrder ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]

theorem convex_closed_sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, Convex ğ•œ s âˆ§ IsClosed s) :
    Convex ğ•œ (â‹‚â‚€ S) âˆ§ IsClosed (â‹‚â‚€ S) :=
  âŸ¨fun _ hx => starConvex_sInter fun _ hs => (h _ hs).1 <| hx _ hs,
    isClosed_sInter fun _ hs => (h _ hs).2âŸ©

variable (ğ•œ) in
/-- The convex closed hull of a set `s` is the minimal convex closed set that includes `s`. -/
@[simps! isClosed]
def closedConvexHull : ClosureOperator (Set E) := .ofCompletePred (fun s => Convex ğ•œ s âˆ§ IsClosed s)
  fun _ â†¦ convex_closed_sInter

theorem convex_closedConvexHull {s : Set E} :
    Convex ğ•œ (closedConvexHull ğ•œ s) := ((closedConvexHull ğ•œ).isClosed_closure s).1

theorem isClosed_closedConvexHull {s : Set E} :
    IsClosed (closedConvexHull ğ•œ s) := ((closedConvexHull ğ•œ).isClosed_closure s).2

theorem subset_closedConvexHull {s : Set E} : s âŠ† closedConvexHull ğ•œ s :=
  (closedConvexHull ğ•œ).le_closure s

theorem closure_subset_closedConvexHull {s : Set E} : closure s âŠ† closedConvexHull ğ•œ s :=
  closure_minimal subset_closedConvexHull isClosed_closedConvexHull

theorem closedConvexHull_min {s t : Set E} (hst : s âŠ† t) (h_conv : Convex ğ•œ t)
    (h_closed : IsClosed t) : closedConvexHull ğ•œ s âŠ† t :=
  (closedConvexHull ğ•œ).closure_min hst âŸ¨h_conv, h_closedâŸ©

theorem convexHull_subset_closedConvexHull {s : Set E} :
    (convexHull ğ•œ) s âŠ† (closedConvexHull ğ•œ) s :=
  convexHull_min subset_closedConvexHull convex_closedConvexHull

@[simp]
theorem closedConvexHull_closure_eq_closedConvexHull {s : Set E} :
    closedConvexHull ğ•œ (closure s) = closedConvexHull ğ•œ s :=
  subset_antisymm (by
    simpa using ((closedConvexHull ğ•œ).monotone (closure_subset_closedConvexHull (ğ•œ := ğ•œ) (E := E))))
    ((closedConvexHull ğ•œ).monotone subset_closure)

end TopologicalSpace

section ContinuousConstSMul

variable [Field ğ•œ] [PartialOrder ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [IsTopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]

theorem closedConvexHull_eq_closure_convexHull {s : Set E} :
    closedConvexHull ğ•œ s = closure (convexHull ğ•œ s) := subset_antisymm
  (closedConvexHull_min (subset_trans (subset_convexHull ğ•œ s) subset_closure)
    (Convex.closure (convex_convexHull ğ•œ s)) isClosed_closure)
  (closure_minimal convexHull_subset_closedConvexHull isClosed_closedConvexHull)

end ContinuousConstSMul

section ContinuousSMul

variable [AddCommGroup E] [Module â„ E] [TopologicalSpace E] [IsTopologicalAddGroup E]
  [ContinuousSMul â„ E]

/-- Convex hull of a finite set is compact. -/
theorem Set.Finite.isCompact_convexHull {s : Set E} (hs : s.Finite) :
    IsCompact (convexHull â„ s) := by
  rw [hs.convexHull_eq_image]
  apply (@isCompact_stdSimplex _ hs.fintype).image
  haveI := hs.fintype
  apply LinearMap.continuous_on_pi

/-- Convex hull of a finite set is closed. -/
theorem Set.Finite.isClosed_convexHull [T2Space E] {s : Set E} (hs : s.Finite) :
    IsClosed (convexHull â„ s) :=
  hs.isCompact_convexHull.isClosed

open AffineMap

/-- If we dilate the interior of a convex set about a point in its interior by a scale `t > 1`,
the result includes the closure of the original set.

TODO Generalise this from convex sets to sets that are balanced / star-shaped about `x`. -/
theorem Convex.closure_subset_image_homothety_interior_of_one_lt {s : Set E} (hs : Convex â„ s)
    {x : E} (hx : x âˆˆ interior s) (t : â„) (ht : 1 < t) :
    closure s âŠ† homothety x t '' interior s := by
  intro y hy
  have hne : t â‰  0 := (one_pos.trans ht).ne'
  refine
    âŸ¨homothety x tâ»Â¹ y, hs.openSegment_interior_closure_subset_interior hx hy ?_,
      (AffineEquiv.homothetyUnitsMulHom x (Units.mk0 t hne)).apply_symm_apply yâŸ©
  rw [openSegment_eq_image_lineMap, â† inv_one, â† inv_Ioiâ‚€ (zero_lt_one' â„), â† image_inv_eq_inv,
    image_image, homothety_eq_lineMap]
  exact mem_image_of_mem _ ht

/-- If we dilate a convex set about a point in its interior by a scale `t > 1`, the interior of
the result includes the closure of the original set.

TODO Generalise this from convex sets to sets that are balanced / star-shaped about `x`. -/
theorem Convex.closure_subset_interior_image_homothety_of_one_lt {s : Set E} (hs : Convex â„ s)
    {x : E} (hx : x âˆˆ interior s) (t : â„) (ht : 1 < t) :
    closure s âŠ† interior (homothety x t '' s) :=
  (hs.closure_subset_image_homothety_interior_of_one_lt hx t ht).trans <|
    (homothety_isOpenMap x t (one_pos.trans ht).ne').image_interior_subset _

/-- If we dilate a convex set about a point in its interior by a scale `t > 1`, the interior of
the result includes the closure of the original set.

TODO Generalise this from convex sets to sets that are balanced / star-shaped about `x`. -/
theorem Convex.subset_interior_image_homothety_of_one_lt {s : Set E} (hs : Convex â„ s) {x : E}
    (hx : x âˆˆ interior s) (t : â„) (ht : 1 < t) : s âŠ† interior (homothety x t '' s) :=
  subset_closure.trans <| hs.closure_subset_interior_image_homothety_of_one_lt hx t ht

end ContinuousSMul

section LinearOrderedField

variable {ğ•œ : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
  [TopologicalSpace ğ•œ] [OrderTopology ğ•œ]

open scoped Topology
open Filter

theorem Convex.nontrivial_iff_nonempty_interior {s : Set ğ•œ} (hs : Convex ğ•œ s) :
    s.Nontrivial â†” (interior s).Nonempty := by
  constructor
  Â· rintro âŸ¨x, hx, y, hy, hâŸ©
    have hs' := Nonempty.mono <| interior_mono <| hs.segment_subset hx hy
    rw [segment_eq_Icc', interior_Icc, nonempty_Ioo, inf_lt_sup] at hs'
    exact hs' h
  Â· rintro âŸ¨x, hxâŸ©
    rcases eq_singleton_or_nontrivial (interior_subset hx) with rfl | h
    Â· rw [interior_singleton] at hx
      exact hx.elim
    Â· exact h

lemma Convex.Ioo_subset_of_mem_closure {s : Set ğ•œ} (hs : Convex ğ•œ s) {a b : ğ•œ}
    (has : a âˆˆ closure s) (hbs : b âˆˆ closure s) :
    Ioo a b âŠ† s := by
  cases subsingleton_or_nontrivial s with
  | inl hs_sub =>
    simp only [subsingleton_coe] at hs_sub
    simp [hs_sub.closure has hbs]
  | inr h' =>
    simp only [nontrivial_coe_sort] at h'
    calc Ioo a b
    _ = interior (Ioo a b) := interior_Ioo.symm
    _ âŠ† interior (openSegment ğ•œ a b) := interior_mono <| Ioo_subset_openSegment
    _ âŠ† interior (closure s) := interior_mono <| hs.closure.openSegment_subset has hbs
    _ = interior s := hs.interior_closure_eq_interior_of_nonempty_interior <|
      hs.nontrivial_iff_nonempty_interior.1 h'
    _ âŠ† s := interior_subset

lemma Convex.nhdsWithin_inter_Iio_eq_nhdsLT {s : Set ğ•œ} (hs : Convex ğ•œ s) {a : ğ•œ}
    (has : a âˆˆ closure s) (h' : (s âˆ© Iio a).Nonempty) :
    ğ“[s âˆ© Iio a] a = ğ“[<] a := by
  obtain âŸ¨b, hbs, hbaâŸ© := h'
  refine nhdsWithin_inter_of_mem (mem_nhdsLT_iff_exists_Ioo_subset.2 âŸ¨b, hba, ?_âŸ©)
  exact hs.Ioo_subset_of_mem_closure (subset_closure hbs) has

lemma Convex.nhdsWithin_inter_Ioi_eq_nhdsGT {s : Set ğ•œ} (hs : Convex ğ•œ s) {a : ğ•œ}
    (has : a âˆˆ closure s) (h' : (s âˆ© Ioi a).Nonempty) :
    ğ“[s âˆ© Ioi a] a = ğ“[>] a := by
  obtain âŸ¨b, hbs, hbaâŸ© := h'
  refine nhdsWithin_inter_of_mem (mem_nhdsGT_iff_exists_Ioo_subset.2 âŸ¨b, hba, ?_âŸ©)
  exact hs.Ioo_subset_of_mem_closure has (subset_closure hbs)

lemma Convex.nhdsWithin_diff_eq_nhdsNE {s : Set ğ•œ} (hs : Convex ğ•œ s) {a : ğ•œ}
    (has : a âˆˆ closure s) (h_Iio : (s âˆ© Iio a).Nonempty) (h_Ioi : (s âˆ© Ioi a).Nonempty) :
    ğ“[s \ {a}] a = ğ“[â‰ ] a := by
  rw [diff_eq, â† Iio_union_Ioi, inter_union_distrib_left, nhdsWithin_union, nhdsWithin_union]
  simp [hs.nhdsWithin_inter_Ioi_eq_nhdsGT has h_Ioi, hs.nhdsWithin_inter_Iio_eq_nhdsLT has h_Iio]

lemma Convex.nhdsWithin_diff_eq_nhdsLT {s : Set ğ•œ} (hs : Convex ğ•œ s) {a : ğ•œ}
    (has : a âˆˆ closure s) (h_Iio : (s âˆ© Iio a).Nonempty) (h_Ioi : s âˆ© Ioi a = âˆ…) :
    ğ“[s \ {a}] a = ğ“[<] a := by
  rw [diff_eq, â† Iio_union_Ioi, inter_union_distrib_left, nhdsWithin_union]
  simp [h_Ioi, hs.nhdsWithin_inter_Iio_eq_nhdsLT has h_Iio]

lemma Convex.nhdsWithin_diff_eq_nhdsGT {s : Set ğ•œ} (hs : Convex ğ•œ s) {a : ğ•œ}
    (has : a âˆˆ closure s) (h_Iio : s âˆ© Iio a = âˆ…) (h_Ioi : (s âˆ© Ioi a).Nonempty) :
    ğ“[s \ {a}] a = ğ“[>] a := by
  rw [diff_eq, â† Iio_union_Ioi, inter_union_distrib_left, nhdsWithin_union]
  simp [h_Iio, hs.nhdsWithin_inter_Ioi_eq_nhdsGT has h_Ioi]

omit [Field ğ•œ] [IsStrictOrderedRing ğ•œ] in
private lemma diff_singleton_eventually_mem_nhds_left {s : Set ğ•œ} {a : ğ•œ}
    (h : âˆ€ x âˆˆ closure s, Ioo x a âŠ† s) : âˆ€á¶  (x : ğ•œ) in ğ“[s âˆ© Iio a] a, s \ {a} âˆˆ ğ“ x := by
  rcases eq_empty_or_nonempty (s âˆ© Iio a) with hs' | âŸ¨b, hbs, hbaâŸ©
  Â· simp [hs']
  have : Ioo b a âŠ† s := h b (subset_closure hbs)
  apply eventually_of_mem (U := Ioo b a) ?_ fun x hx â†¦ ?_
  Â· exact mem_nhdsWithin.2 âŸ¨Ioi b, isOpen_Ioi, hba, fun _ âŸ¨hâ‚, _, hâ‚‚âŸ© â†¦ âŸ¨hâ‚, hâ‚‚âŸ©âŸ©
  Â· exact mem_nhds_iff.2 âŸ¨Ioo b a, subset_diff_singleton this right_notMem_Ioo, isOpen_Ioo, hxâŸ©

theorem Convex.diff_singleton_eventually_mem_nhds {s : Set ğ•œ} (hs : Convex ğ•œ s) (a : ğ•œ) :
    âˆ€á¶  x in ğ“[s \ {a}] a, s \ {a} âˆˆ ğ“ x := by
  rcases eq_or_neBot (ğ“[s \ {a}] a) with h | has
  Â· rw [h]
    exact eventually_bot
  replace has := closure_mono diff_subset (mem_closure_iff_nhdsWithin_neBot.2 has)
  conv in ğ“[s \ {a}] a => rw [diff_eq, â† Iio_union_Ioi, inter_union_distrib_left]
  rw [nhdsWithin_union, eventually_sup]
  exact âŸ¨diff_singleton_eventually_mem_nhds_left fun x hx â†¦ hs.Ioo_subset_of_mem_closure hx has,
    diff_singleton_eventually_mem_nhds_left (ğ•œ := ğ•œáµ’áµˆ) fun x hx z hz â†¦
      hs.Ioo_subset_of_mem_closure has hx hz.symmâŸ©

end LinearOrderedField
