/-
Copyright (c) 2022 Ya√´l Dillies. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ya√´l Dillies
-/
import Mathlib.Analysis.Convex.Exposed
import Mathlib.Analysis.NormedSpace.HahnBanach.Separation

#align_import analysis.convex.krein_milman from "leanprover-community/mathlib"@"279297937dede7b1b3451b7b0f1786352ad011fa"

/-!
# The Krein-Milman theorem

This file proves the Krein-Milman lemma and the Krein-Milman theorem.

## The lemma

The lemma states that a nonempty compact set `s` has an extreme point. The proof goes:
1. Using Zorn's lemma, find a minimal nonempty closed `t` that is an extreme subset of `s`. We will
  show that `t` is a singleton, thus corresponding to an extreme point.
2. By contradiction, `t` contains two distinct points `x` and `y`.
3. With the (geometric) Hahn-Banach theorem, find a hyperplane that separates `x` and `y`.
4. Look at the extreme (actually exposed) subset of `t` obtained by going the furthest away from
  the separating hyperplane in the direction of `x`. It is nonempty, closed and an extreme subset
  of `s`.
5. It is a strict subset of `t` (`y` isn't in it), so `t` isn't minimal. Absurd.

## The theorem

The theorem states that a compact convex set `s` is the closure of the convex hull of its extreme
points. It is an almost immediate strengthening of the lemma. The proof goes:
1. By contradiction, `s \ closure (convexHull ‚Ñù (extremePoints ‚Ñù s))` is nonempty, say with `x`.
2. With the (geometric) Hahn-Banach theorem, find a hyperplane that separates `x` from
  `closure (convexHull ‚Ñù (extremePoints ‚Ñù s))`.
3. Look at the extreme (actually exposed) subset of
  `s \ closure (convexHull ‚Ñù (extremePoints ‚Ñù s))` obtained by going the furthest away from the
  separating hyperplane. It is nonempty by assumption of nonemptiness and compactness, so by the
  lemma it has an extreme point.
4. This point is also an extreme point of `s`. Absurd.

## Related theorems

When the space is finite dimensional, the `closure` can be dropped to strengthen the result of the
Krein-Milman theorem. This leads to the Minkowski-Carath√©odory theorem (currently not in mathlib).
Birkhoff's theorem is the Minkowski-Carath√©odory theorem applied to the set of bistochastic
matrices, permutation matrices being the extreme points.

## References

See chapter 8 of [Barry Simon, *Convexity*][simon2011]

-/


open Set

open Classical

variable {E : Type*} [AddCommGroup E] [Module ‚Ñù E] [TopologicalSpace E] [T2Space E]
  [TopologicalAddGroup E] [ContinuousSMul ‚Ñù E] [LocallyConvexSpace ‚Ñù E] {s : Set E}

/-- **Krein-Milman lemma**: In a LCTVS, any nonempty compact set has an extreme point. -/
theorem IsCompact.has_extreme_point (hscomp : IsCompact s) (hsnemp : s.Nonempty) :
    (s.extremePoints ‚Ñù).Nonempty := by
  let S : Set (Set E) := { t | t.Nonempty ‚àß IsClosed t ‚àß IsExtreme ‚Ñù s t }
  -- ‚ä¢ Set.Nonempty (extremePoints ‚Ñù s)
  rsuffices ‚ü®t, ‚ü®‚ü®x, hxt‚ü©, htclos, hst‚ü©, hBmin‚ü© : ‚àÉ t ‚àà S, ‚àÄ u ‚àà S, u ‚äÜ t ‚Üí u = t
  -- ‚ä¢ Set.Nonempty (extremePoints ‚Ñù s)
  ¬∑ refine' ‚ü®x, mem_extremePoints_iff_extreme_singleton.2 _‚ü©
    -- ‚ä¢ IsExtreme ‚Ñù s {x}
    rwa [‚Üê eq_singleton_iff_unique_mem.2 ‚ü®hxt, fun y hyB => ?_‚ü©]
    -- ‚ä¢ y = x
    by_contra hyx
    -- ‚ä¢ False
    obtain ‚ü®l, hl‚ü© := geometric_hahn_banach_point_point hyx
    -- ‚ä¢ False
    obtain ‚ü®z, hzt, hz‚ü© :=
      (isCompact_of_isClosed_subset hscomp htclos hst.1).exists_forall_ge ‚ü®x, hxt‚ü©
        l.continuous.continuousOn
    have h : IsExposed ‚Ñù t ({ z ‚àà t | ‚àÄ w ‚àà t, l w ‚â§ l z }) := fun _ => ‚ü®l, rfl‚ü©
    -- ‚ä¢ False
    rw [‚Üê hBmin ({ z ‚àà t | ‚àÄ w ‚àà t, l w ‚â§ l z })
      ‚ü®‚ü®z, hzt, hz‚ü©, h.isClosed htclos, hst.trans h.isExtreme‚ü© (t.sep_subset _)] at hyB
    exact hl.not_le (hyB.2 x hxt)
    -- üéâ no goals
  refine' zorn_superset _ fun F hFS hF => _
  -- ‚ä¢ ‚àÉ lb, lb ‚àà S ‚àß ‚àÄ (s : Set E), s ‚àà F ‚Üí lb ‚äÜ s
  obtain rfl | hFnemp := F.eq_empty_or_nonempty
  -- ‚ä¢ ‚àÉ lb, lb ‚àà S ‚àß ‚àÄ (s : Set E), s ‚àà ‚àÖ ‚Üí lb ‚äÜ s
  ¬∑ exact ‚ü®s, ‚ü®hsnemp, hscomp.isClosed, IsExtreme.rfl‚ü©, fun _ => False.elim‚ü©
    -- üéâ no goals
  refine' ‚ü®‚ãÇ‚ÇÄ F, ‚ü®_, isClosed_sInter fun t ht => (hFS ht).2.1,
    isExtreme_sInter hFnemp fun t ht => (hFS ht).2.2‚ü©, fun t ht => sInter_subset_of_mem ht‚ü©
  haveI : Nonempty (‚Ü•F) := hFnemp.to_subtype
  -- ‚ä¢ Set.Nonempty (‚ãÇ‚ÇÄ F)
  rw [sInter_eq_iInter]
  -- ‚ä¢ Set.Nonempty (‚ãÇ (i : ‚ÜëF), ‚Üëi)
  refine' IsCompact.nonempty_iInter_of_directed_nonempty_compact_closed _ (fun t u => _)
    (fun t => (hFS t.mem).1)
    (fun t => isCompact_of_isClosed_subset hscomp (hFS t.mem).2.1 (hFS t.mem).2.2.1) fun t =>
      (hFS t.mem).2.1
  obtain htu | hut := hF.total t.mem u.mem
  -- ‚ä¢ ‚àÉ z, (fun x x_1 => x ‚äá x_1) ‚Üët ‚Üëz ‚àß (fun x x_1 => x ‚äá x_1) ‚Üëu ‚Üëz
  exacts [‚ü®t, Subset.rfl, htu‚ü©, ‚ü®u, hut, Subset.rfl‚ü©]
  -- üéâ no goals
#align is_compact.has_extreme_point IsCompact.has_extreme_point

/-- **Krein-Milman theorem**: In a LCTVS, any compact convex set is the closure of the convex hull
    of its extreme points. -/
theorem closure_convexHull_extremePoints (hscomp : IsCompact s) (hAconv : Convex ‚Ñù s) :
    closure (convexHull ‚Ñù <| s.extremePoints ‚Ñù) = s := by
  apply (closure_minimal (convexHull_min extremePoints_subset hAconv) hscomp.isClosed).antisymm
  -- ‚ä¢ s ‚äÜ closure (‚Üë(convexHull ‚Ñù) (extremePoints ‚Ñù s))
  by_contra hs
  -- ‚ä¢ False
  obtain ‚ü®x, hxA, hxt‚ü© := not_subset.1 hs
  -- ‚ä¢ False
  obtain ‚ü®l, r, hlr, hrx‚ü© :=
    geometric_hahn_banach_closed_point (convex_convexHull _ _).closure isClosed_closure hxt
  have h : IsExposed ‚Ñù s ({ y ‚àà s | ‚àÄ z ‚àà s, l z ‚â§ l y }) := fun _ => ‚ü®l, rfl‚ü©
  -- ‚ä¢ False
  obtain ‚ü®z, hzA, hz‚ü© := hscomp.exists_forall_ge ‚ü®x, hxA‚ü© l.continuous.continuousOn
  -- ‚ä¢ False
  obtain ‚ü®y, hy‚ü© := (h.isCompact hscomp).has_extreme_point ‚ü®z, hzA, hz‚ü©
  -- ‚ä¢ False
  linarith [hlr _ (subset_closure <| subset_convexHull _ _ <|
    h.isExtreme.extremePoints_subset_extremePoints hy), hy.1.2 x hxA]
#align closure_convex_hull_extreme_points closure_convexHull_extremePoints
