/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov
-/
import Mathlib.Analysis.Convex.Between
import Mathlib.Analysis.Convex.Jensen
import Mathlib.Analysis.Convex.Topology
import Mathlib.Analysis.Normed.Group.Pointwise
import Mathlib.Analysis.Normed.Affine.AddTorsor
import Mathlib.Analysis.Normed.Affine.AddTorsorBases

/-!
# Topological and metric properties of convex sets in normed spaces

We prove the following facts:

* `convexOn_norm`, `convexOn_dist` : norm and distance to a fixed point is convex on any convex
  set;
* `convexOn_univ_norm`, `convexOn_univ_dist` : norm and distance to a fixed point is convex on
  the whole space;
* `convexHull_ediam`, `convexHull_diam` : convex hull of a set has the same (e)metric diameter
  as the original set;
* `bounded_convexHull` : convex hull of a set is bounded if and only if the original set
  is bounded.
-/

variable {E P : Type*}

open AffineBasis Module Metric Set
open scoped Convex Pointwise Topology

section SeminormedAddCommGroup
variable [SeminormedAddCommGroup E] [NormedSpace â„ E] [PseudoMetricSpace P] [NormedAddTorsor E P]
variable {s : Set E}

/-- The norm on a real normed space is convex on any convex set. See also `Seminorm.convexOn`
and `convexOn_univ_norm`. -/
theorem convexOn_norm (hs : Convex â„ s) : ConvexOn â„ s norm :=
  âŸ¨hs, fun x _ y _ a b ha hb _ =>
    calc
      â€–a â€¢ x + b â€¢ yâ€– â‰¤ â€–a â€¢ xâ€– + â€–b â€¢ yâ€– := norm_add_le _ _
      _ = a * â€–xâ€– + b * â€–yâ€– := by
        rw [norm_smul, norm_smul, Real.norm_of_nonneg ha, Real.norm_of_nonneg hb]âŸ©

/-- The norm on a real normed space is convex on the whole space. See also `Seminorm.convexOn`
and `convexOn_norm`. -/
theorem convexOn_univ_norm : ConvexOn â„ univ (norm : E â†’ â„) :=
  convexOn_norm convex_univ

theorem convexOn_dist (z : E) (hs : Convex â„ s) : ConvexOn â„ s fun z' => dist z' z := by
  simpa [dist_eq_norm, preimage_preimage] using
    (convexOn_norm (hs.translate (-z))).comp_affineMap (AffineMap.id â„ E - AffineMap.const â„ E z)

theorem convexOn_univ_dist (z : E) : ConvexOn â„ univ fun z' => dist z' z :=
  convexOn_dist z convex_univ

theorem convex_ball (a : E) (r : â„) : Convex â„ (Metric.ball a r) := by
  simpa only [Metric.ball, sep_univ] using (convexOn_univ_dist a).convex_lt r

theorem convex_closedBall (a : E) (r : â„) : Convex â„ (Metric.closedBall a r) := by
  simpa only [Metric.closedBall, sep_univ] using (convexOn_univ_dist a).convex_le r

theorem Convex.thickening (hs : Convex â„ s) (Î´ : â„) : Convex â„ (thickening Î´ s) := by
  rw [â† add_ball_zero]
  exact hs.add (convex_ball 0 _)

theorem Convex.cthickening (hs : Convex â„ s) (Î´ : â„) : Convex â„ (cthickening Î´ s) := by
  obtain hÎ´ | hÎ´ := le_total 0 Î´
  Â· rw [cthickening_eq_iInter_thickening hÎ´]
    exact convex_iInterâ‚‚ fun _ _ => hs.thickening _
  Â· rw [cthickening_of_nonpos hÎ´]
    exact hs.closure

/-- Given a point `x` in the convex hull of `s` and a point `y`, there exists a point
of `s` at distance at least `dist x y` from `y`. -/
theorem convexHull_exists_dist_ge {s : Set E} {x : E} (hx : x âˆˆ convexHull â„ s) (y : E) :
    âˆƒ x' âˆˆ s, dist x y â‰¤ dist x' y :=
  (convexOn_dist y (convex_convexHull â„ _)).exists_ge_of_mem_convexHull (subset_convexHull ..) hx

/-- Given a point `x` in the convex hull of `s` and a point `y` in the convex hull of `t`,
there exist points `x' âˆˆ s` and `y' âˆˆ t` at distance at least `dist x y`. -/
theorem convexHull_exists_dist_ge2 {s t : Set E} {x y : E} (hx : x âˆˆ convexHull â„ s)
    (hy : y âˆˆ convexHull â„ t) : âˆƒ x' âˆˆ s, âˆƒ y' âˆˆ t, dist x y â‰¤ dist x' y' := by
  rcases convexHull_exists_dist_ge hx y with âŸ¨x', hx', Hx'âŸ©
  rcases convexHull_exists_dist_ge hy x' with âŸ¨y', hy', Hy'âŸ©
  use x', hx', y', hy'
  exact le_trans Hx' (dist_comm y x' â–¸ dist_comm y' x' â–¸ Hy')

/-- Emetric diameter of the convex hull of a set `s` equals the emetric diameter of `s`. -/
@[simp]
theorem convexHull_ediam (s : Set E) : EMetric.diam (convexHull â„ s) = EMetric.diam s := by
  refine (EMetric.diam_le fun x hx y hy => ?_).antisymm (EMetric.diam_mono <| subset_convexHull â„ s)
  rcases convexHull_exists_dist_ge2 hx hy with âŸ¨x', hx', y', hy', HâŸ©
  rw [edist_dist]
  apply le_trans (ENNReal.ofReal_le_ofReal H)
  rw [â† edist_dist]
  exact EMetric.edist_le_diam_of_mem hx' hy'

/-- Diameter of the convex hull of a set `s` equals the emetric diameter of `s`. -/
@[simp]
theorem convexHull_diam (s : Set E) : Metric.diam (convexHull â„ s) = Metric.diam s := by
  simp only [Metric.diam, convexHull_ediam]

/-- Convex hull of `s` is bounded if and only if `s` is bounded. -/
@[simp]
theorem isBounded_convexHull {s : Set E} :
    Bornology.IsBounded (convexHull â„ s) â†” Bornology.IsBounded s := by
  simp only [Metric.isBounded_iff_ediam_ne_top, convexHull_ediam]

instance (priority := 100) NormedSpace.instPathConnectedSpace : PathConnectedSpace E :=
  IsTopologicalAddGroup.pathConnectedSpace

theorem Wbtw.dist_add_dist {x y z : P} (h : Wbtw â„ x y z) :
    dist x y + dist y z = dist x z := by
  obtain âŸ¨a, âŸ¨haâ‚€, haâ‚âŸ©, rflâŸ© := h
  simp [abs_of_nonneg, haâ‚€, haâ‚, sub_mul]

theorem dist_add_dist_of_mem_segment {x y z : E} (h : y âˆˆ [x -[â„] z]) :
    dist x y + dist y z = dist x z :=
  (mem_segment_iff_wbtw.1 h).dist_add_dist

/-- The set of vectors in the same ray as `x` is connected. -/
theorem isConnected_setOf_sameRay (x : E) : IsConnected { y | SameRay â„ x y } := by
  by_cases hx : x = 0; Â· simpa [hx] using isConnected_univ (Î± := E)
  simp_rw [â† exists_nonneg_left_iff_sameRay hx]
  exact isConnected_Ici.image _ (continuous_id.smul continuous_const).continuousOn

/-- The set of nonzero vectors in the same ray as the nonzero vector `x` is connected. -/
theorem isConnected_setOf_sameRay_and_ne_zero {x : E} (hx : x â‰  0) :
    IsConnected { y | SameRay â„ x y âˆ§ y â‰  0 } := by
  simp_rw [â† exists_pos_left_iff_sameRay_and_ne_zero hx]
  exact isConnected_Ioi.image _ (continuous_id.smul continuous_const).continuousOn

end SeminormedAddCommGroup

section NormedAddCommGroup
variable [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E] {s : Set E} {x : E}

/-- We can intercalate a simplex between a point and one of its neighborhoods. -/
lemma exists_mem_interior_convexHull_affineBasis (hs : s âˆˆ ð“ x) :
    âˆƒ b : AffineBasis (Fin (finrank â„ E + 1)) â„ E,
      x âˆˆ interior (convexHull â„ (range b)) âˆ§ convexHull â„ (range b) âŠ† s := by
  classical
  -- By translating, WLOG `x` is the origin.
  wlog hx : x = 0
  Â· obtain âŸ¨b, hbâŸ© := this (s := -x +áµ¥ s) (by simpa using vadd_mem_nhds_vadd (-x) hs) rfl
    use x +áµ¥ b
    simpa [subset_vadd_set_iff, mem_vadd_set_iff_neg_vadd_mem, convexHull_vadd, interior_vadd,
      Pi.vadd_def, -vadd_eq_add, vadd_eq_add (a := -x), â† Set.vadd_set_range] using hb
  subst hx
  -- The strategy is now to find an arbitrary maximal spanning simplex (aka an affine basis)...
  obtain âŸ¨bâŸ© := exists_affineBasis_of_finiteDimensional
    (Î¹ := Fin (finrank â„ E + 1)) (k := â„) (P := E) (by simp)
  -- ... translate it to contain the origin...
  set c : AffineBasis (Fin (finrank â„ E + 1)) â„ E := -Finset.univ.centroid â„ b +áµ¥ b
  have hcâ‚€ : 0 âˆˆ interior (convexHull â„ (range c) : Set E) := by
    simpa [c, convexHull_vadd, interior_vadd, range_add, Pi.vadd_def, mem_vadd_set_iff_neg_vadd_mem]
      using b.centroid_mem_interior_convexHull
  set cnorm := Finset.univ.sup' Finset.univ_nonempty (fun i â†¦ â€–c iâ€–)
  have hcnorm : range c âŠ† closedBall 0 (cnorm + 1) := by
    simpa only [cnorm, subset_def, Finset.mem_coe, mem_closedBall, dist_zero_right,
      â† sub_le_iff_le_add, Finset.le_sup'_iff, forall_mem_range] using fun i â†¦ âŸ¨i, by simpâŸ©
  -- ... and finally scale it to fit inside the neighborhood `s`.
  obtain âŸ¨Îµ, hÎµ, hÎµsâŸ© := Metric.mem_nhds_iff.1 hs
  set Îµ' : â„ := Îµ / 2 / (cnorm + 1)
  have hc' : 0 < cnorm + 1 := by
    have : 0 â‰¤ cnorm := Finset.le_sup'_of_le _ (Finset.mem_univ 0) (norm_nonneg _)
    positivity
  have hÎµ' : 0 < Îµ' := by positivity
  set d : AffineBasis (Fin (finrank â„ E + 1)) â„ E := Units.mk0 Îµ' hÎµ'.ne' â€¢ c
  have hÎµâ‚€ : 0 < Îµ / 2 := by positivity
  have hdnorm : (range d : Set E) âŠ† closedBall 0 (Îµ / 2) := by
    simp [d, Set.smul_set_subset_iffâ‚€ hÎµ'.ne', hÎµâ‚€.le, _root_.smul_closedBall, abs_of_nonneg hÎµ'.le,
      range_subset_iff, norm_smul]
    simpa [Îµ', hÎµâ‚€.ne', range_subset_iff, â† mul_div_right_comm (Îµ / 2), div_le_iffâ‚€ hc',
      mul_le_mul_left hÎµâ‚€] using hcnorm
  refine âŸ¨d, ?_, ?_âŸ©
  Â· simpa [d, Pi.smul_def, range_smul, interior_smulâ‚€, convexHull_smul, zero_mem_smul_set_iff,
      hÎµ'.ne']
  Â· calc
      convexHull â„ (range d) âŠ† closedBall 0 (Îµ / 2) := convexHull_min hdnorm (convex_closedBall ..)
      _ âŠ† ball 0 Îµ := closedBall_subset_ball (by linarith)
      _ âŠ† s := hÎµs

end NormedAddCommGroup
