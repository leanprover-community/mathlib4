/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Order.Closure

/-!
# Convex hull

This file defines the convex hull of a set `s` in a module. `convexHull ğ•œ s` is the smallest convex
set containing `s`. In order theory speak, this is a closure operator.

## Implementation notes

`convexHull` is defined as a closure operator. This gives access to the `ClosureOperator` API
while the impact on writing code is minimal as `convexHull ğ•œ s` is automatically elaborated as
`(convexHull ğ•œ) s`.
-/


open Set

open Pointwise

variable {ğ•œ E F : Type*}

section convexHull

section OrderedSemiring

variable [Semiring ğ•œ] [PartialOrder ğ•œ]

section AddCommMonoid

variable (ğ•œ)
variable [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F]

/-- The convex hull of a set `s` is the minimal convex set that includes `s`. -/
@[simps! isClosed]
def convexHull : ClosureOperator (Set E) := .ofCompletePred (Convex ğ•œ) fun _ â†¦ convex_sInter

variable (s : Set E)

theorem subset_convexHull : s âŠ† convexHull ğ•œ s :=
  (convexHull ğ•œ).le_closure s

theorem convex_convexHull : Convex ğ•œ (convexHull ğ•œ s) := (convexHull ğ•œ).isClosed_closure s

theorem convexHull_eq_iInter : convexHull ğ•œ s = â‹‚ (t : Set E) (_ : s âŠ† t) (_ : Convex ğ•œ t), t := by
  simp [convexHull, iInter_subtype, iInter_and]

variable {ğ•œ s} {t : Set E} {x y : E}

theorem mem_convexHull_iff : x âˆˆ convexHull ğ•œ s â†” âˆ€ t, s âŠ† t â†’ Convex ğ•œ t â†’ x âˆˆ t := by
  simp_rw [convexHull_eq_iInter, mem_iInter]

theorem convexHull_min : s âŠ† t â†’ Convex ğ•œ t â†’ convexHull ğ•œ s âŠ† t := (convexHull ğ•œ).closure_min

theorem Convex.convexHull_subset_iff (ht : Convex ğ•œ t) : convexHull ğ•œ s âŠ† t â†” s âŠ† t :=
  (show (convexHull ğ•œ).IsClosed t from ht).closure_le_iff

@[mono, gcongr]
theorem convexHull_mono (hst : s âŠ† t) : convexHull ğ•œ s âŠ† convexHull ğ•œ t :=
  (convexHull ğ•œ).monotone hst

lemma convexHull_eq_self : convexHull ğ•œ s = s â†” Convex ğ•œ s := (convexHull ğ•œ).isClosed_iff.symm

alias âŸ¨_, Convex.convexHull_eqâŸ© := convexHull_eq_self

@[simp]
theorem convexHull_univ : convexHull ğ•œ (univ : Set E) = univ :=
  ClosureOperator.closure_top (convexHull ğ•œ)

@[simp]
theorem convexHull_empty : convexHull ğ•œ (âˆ… : Set E) = âˆ… :=
  convex_empty.convexHull_eq

@[simp]
theorem convexHull_eq_empty : convexHull ğ•œ s = âˆ… â†” s = âˆ… := by
  constructor
  Â· intro h
    rw [â† Set.subset_empty_iff, â† h]
    exact subset_convexHull ğ•œ _
  Â· rintro rfl
    exact convexHull_empty

@[deprecated (since := "2025-08-09")] alias convexHull_empty_iff := convexHull_eq_empty

@[simp]
theorem convexHull_nonempty_iff : (convexHull ğ•œ s).Nonempty â†” s.Nonempty := by
  rw [nonempty_iff_ne_empty, nonempty_iff_ne_empty, Ne, Ne]
  exact not_congr convexHull_eq_empty

protected alias âŸ¨_, Set.Nonempty.convexHullâŸ© := convexHull_nonempty_iff

theorem segment_subset_convexHull (hx : x âˆˆ s) (hy : y âˆˆ s) : segment ğ•œ x y âŠ† convexHull ğ•œ s :=
  (convex_convexHull _ _).segment_subset (subset_convexHull _ _ hx) (subset_convexHull _ _ hy)

@[simp]
theorem convexHull_singleton (x : E) : convexHull ğ•œ ({x} : Set E) = {x} :=
  (convex_singleton x).convexHull_eq

@[simp] lemma convexHull_eq_singleton : convexHull ğ•œ s = {x} â†” s = {x} where
  mp hs := by
    rw [â† Set.Nonempty.subset_singleton_iff, â† hs]
    Â· exact subset_convexHull ..
    Â· by_contra! hs
      simp_all [eq_comm (a := âˆ…)]
  mpr hs := by simp [hs]

@[simp]
theorem convexHull_zero : convexHull ğ•œ (0 : Set E) = 0 :=
  convexHull_singleton 0

@[simp] lemma convexHull_eq_zero : convexHull ğ•œ s = 0 â†” s = 0 := convexHull_eq_singleton

@[simp]
theorem convexHull_pair [IsOrderedRing ğ•œ] (x y : E) : convexHull ğ•œ {x, y} = segment ğ•œ x y := by
  refine (convexHull_min ?_ <| convex_segment _ _).antisymm
    (segment_subset_convexHull (mem_insert _ _) <| subset_insert _ _ <| mem_singleton _)
  rw [insert_subset_iff, singleton_subset_iff]
  exact âŸ¨left_mem_segment _ _ _, right_mem_segment _ _ _âŸ©

theorem convexHull_convexHull_union_left (s t : Set E) :
    convexHull ğ•œ (convexHull ğ•œ s âˆª t) = convexHull ğ•œ (s âˆª t) :=
  ClosureOperator.closure_sup_closure_left _ _ _

theorem convexHull_convexHull_union_right (s t : Set E) :
    convexHull ğ•œ (s âˆª convexHull ğ•œ t) = convexHull ğ•œ (s âˆª t) :=
  ClosureOperator.closure_sup_closure_right _ _ _

theorem Convex.convex_remove_iff_notMem_convexHull_remove {s : Set E} (hs : Convex ğ•œ s) (x : E) :
    Convex ğ•œ (s \ {x}) â†” x âˆ‰ convexHull ğ•œ (s \ {x}) := by
  constructor
  Â· rintro hsx hx
    rw [hsx.convexHull_eq] at hx
    exact hx.2 (mem_singleton _)
  rintro hx
  suffices h : s \ {x} = convexHull ğ•œ (s \ {x}) by
    rw [h]
    exact convex_convexHull ğ•œ _
  exact
    Subset.antisymm (subset_convexHull ğ•œ _) fun y hy =>
      âŸ¨convexHull_min diff_subset hs hy, by
        rintro (rfl : y = x)
        exact hx hyâŸ©

@[deprecated (since := "2025-05-23")]
alias Convex.convex_remove_iff_not_mem_convexHull_remove :=
  Convex.convex_remove_iff_notMem_convexHull_remove

theorem IsLinearMap.image_convexHull {f : E â†’ F} (hf : IsLinearMap ğ•œ f) (s : Set E) :
    f '' convexHull ğ•œ s = convexHull ğ•œ (f '' s) :=
  Set.Subset.antisymm
    (image_subset_iff.2 <|
      convexHull_min (image_subset_iff.1 <| subset_convexHull ğ•œ _)
        ((convex_convexHull ğ•œ _).is_linear_preimage hf))
    (convexHull_min (image_mono (subset_convexHull ğ•œ s)) <|
      (convex_convexHull ğ•œ s).is_linear_image hf)

theorem LinearMap.image_convexHull (f : E â†’â‚—[ğ•œ] F) (s : Set E) :
    f '' convexHull ğ•œ s = convexHull ğ•œ (f '' s) :=
  f.isLinear.image_convexHull s

theorem convexHull_add_subset {s t : Set E} :
    convexHull ğ•œ (s + t) âŠ† convexHull ğ•œ s + convexHull ğ•œ t :=
  convexHull_min (add_subset_add (subset_convexHull _ _) (subset_convexHull _ _))
    (Convex.add (convex_convexHull ğ•œ s) (convex_convexHull ğ•œ t))

end AddCommMonoid

end OrderedSemiring

section CommSemiring

variable [CommSemiring ğ•œ] [PartialOrder ğ•œ] [AddCommMonoid E] [Module ğ•œ E]

theorem convexHull_smul (a : ğ•œ) (s : Set E) : convexHull ğ•œ (a â€¢ s) = a â€¢ convexHull ğ•œ s :=
  (LinearMap.lsmul _ _ a).image_convexHull _ |>.symm

end CommSemiring

section OrderedRing

variable [Ring ğ•œ] [PartialOrder ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F]

theorem AffineMap.image_convexHull (f : E â†’áµƒ[ğ•œ] F) (s : Set E) :
    f '' convexHull ğ•œ s = convexHull ğ•œ (f '' s) := by
  apply Set.Subset.antisymm
  Â· rw [Set.image_subset_iff]
    refine convexHull_min ?_ ((convex_convexHull ğ•œ (f '' s)).affine_preimage f)
    rw [â† Set.image_subset_iff]
    exact subset_convexHull ğ•œ (f '' s)
  Â· exact convexHull_min (Set.image_mono (subset_convexHull ğ•œ s))
      ((convex_convexHull ğ•œ s).affine_image f)

theorem convexHull_subset_affineSpan (s : Set E) : convexHull ğ•œ s âŠ† (affineSpan ğ•œ s : Set E) :=
  convexHull_min (subset_affineSpan ğ•œ s) (affineSpan ğ•œ s).convex

@[simp]
theorem affineSpan_convexHull (s : Set E) : affineSpan ğ•œ (convexHull ğ•œ s) = affineSpan ğ•œ s := by
  refine le_antisymm ?_ (affineSpan_mono ğ•œ (subset_convexHull ğ•œ s))
  rw [affineSpan_le]
  exact convexHull_subset_affineSpan s

theorem convexHull_neg (s : Set E) : convexHull ğ•œ (-s) = -convexHull ğ•œ s := by
  simp_rw [â† image_neg_eq_neg]
  exact AffineMap.image_convexHull (-1) _ |>.symm

lemma convexHull_vadd (x : E) (s : Set E) : convexHull ğ•œ (x +áµ¥ s) = x +áµ¥ convexHull ğ•œ s :=
  (AffineEquiv.constVAdd ğ•œ _ x).toAffineMap.image_convexHull s |>.symm

end AddCommGroup

end OrderedRing

end convexHull
