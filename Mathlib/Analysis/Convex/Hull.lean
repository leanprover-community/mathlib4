/-
Copyright (c) 2020 Yury Kudriashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudriashov, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Order.Closure

#align_import analysis.convex.hull from "leanprover-community/mathlib"@"92bd7b1ffeb306a89f450bee126ddd8a284c259d"

/-!
# Convex hull

This file defines the convex hull of a set `s` in a module. `convexHull ğ•œ s` is the smallest convex
set containing `s`. In order theory speak, this is a closure operator.

## Implementation notes

`convexHull` is defined as a closure operator. This gives access to the `ClosureOperator` API
while the impact on writing code is minimal as `convexHull ğ•œ s` is automatically elaborated as
`(convexHull ğ•œ) s`.
-/


open Set

open Pointwise

variable {ğ•œ E F : Type*}

section convexHull

section OrderedSemiring

variable [OrderedSemiring ğ•œ]

section AddCommMonoid

variable (ğ•œ)
variable [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F]

/-- The convex hull of a set `s` is the minimal convex set that includes `s`. -/
@[simps! isClosed]
def convexHull : ClosureOperator (Set E) := .ofCompletePred (Convex ğ•œ) fun _ â†¦ convex_sInter
#align convex_hull convexHull

variable (s : Set E)

theorem subset_convexHull : s âŠ† convexHull ğ•œ s :=
  (convexHull ğ•œ).le_closure s
#align subset_convex_hull subset_convexHull

theorem convex_convexHull : Convex ğ•œ (convexHull ğ•œ s) := (convexHull ğ•œ).isClosed_closure s
#align convex_convex_hull convex_convexHull

theorem convexHull_eq_iInter : convexHull ğ•œ s = â‹‚ (t : Set E) (_ : s âŠ† t) (_ : Convex ğ•œ t), t := by
  simp [convexHull, iInter_subtype, iInter_and]
#align convex_hull_eq_Inter convexHull_eq_iInter

variable {ğ•œ s} {t : Set E} {x y : E}

theorem mem_convexHull_iff : x âˆˆ convexHull ğ•œ s â†” âˆ€ t, s âŠ† t â†’ Convex ğ•œ t â†’ x âˆˆ t := by
  simp_rw [convexHull_eq_iInter, mem_iInter]
#align mem_convex_hull_iff mem_convexHull_iff

theorem convexHull_min : s âŠ† t â†’ Convex ğ•œ t â†’ convexHull ğ•œ s âŠ† t := (convexHull ğ•œ).closure_min
#align convex_hull_min convexHull_min

theorem Convex.convexHull_subset_iff (ht : Convex ğ•œ t) : convexHull ğ•œ s âŠ† t â†” s âŠ† t :=
  (show (convexHull ğ•œ).IsClosed t from ht).closure_le_iff
#align convex.convex_hull_subset_iff Convex.convexHull_subset_iff

@[mono, gcongr]
theorem convexHull_mono (hst : s âŠ† t) : convexHull ğ•œ s âŠ† convexHull ğ•œ t :=
  (convexHull ğ•œ).monotone hst
#align convex_hull_mono convexHull_mono

lemma convexHull_eq_self : convexHull ğ•œ s = s â†” Convex ğ•œ s := (convexHull ğ•œ).isClosed_iff.symm

alias âŸ¨_, Convex.convexHull_eqâŸ© := convexHull_eq_self
#align convex.convex_hull_eq Convex.convexHull_eq

@[simp]
theorem convexHull_univ : convexHull ğ•œ (univ : Set E) = univ :=
  ClosureOperator.closure_top (convexHull ğ•œ)
#align convex_hull_univ convexHull_univ

@[simp]
theorem convexHull_empty : convexHull ğ•œ (âˆ… : Set E) = âˆ… :=
  convex_empty.convexHull_eq
#align convex_hull_empty convexHull_empty

@[simp]
theorem convexHull_empty_iff : convexHull ğ•œ s = âˆ… â†” s = âˆ… := by
  constructor
  Â· intro h
    rw [â† Set.subset_empty_iff, â† h]
    exact subset_convexHull ğ•œ _
  Â· rintro rfl
    exact convexHull_empty
#align convex_hull_empty_iff convexHull_empty_iff

@[simp]
theorem convexHull_nonempty_iff : (convexHull ğ•œ s).Nonempty â†” s.Nonempty := by
  rw [nonempty_iff_ne_empty, nonempty_iff_ne_empty, Ne, Ne]
  exact not_congr convexHull_empty_iff
#align convex_hull_nonempty_iff convexHull_nonempty_iff

protected alias âŸ¨_, Set.Nonempty.convexHullâŸ© := convexHull_nonempty_iff
#align set.nonempty.convex_hull Set.Nonempty.convexHull

theorem segment_subset_convexHull (hx : x âˆˆ s) (hy : y âˆˆ s) : segment ğ•œ x y âŠ† convexHull ğ•œ s :=
  (convex_convexHull _ _).segment_subset (subset_convexHull _ _ hx) (subset_convexHull _ _ hy)
#align segment_subset_convex_hull segment_subset_convexHull

@[simp]
theorem convexHull_singleton (x : E) : convexHull ğ•œ ({x} : Set E) = {x} :=
  (convex_singleton x).convexHull_eq
#align convex_hull_singleton convexHull_singleton

@[simp]
theorem convexHull_zero : convexHull ğ•œ (0 : Set E) = 0 :=
  convexHull_singleton 0
#align convex_hull_zero convexHull_zero

@[simp]
theorem convexHull_pair (x y : E) : convexHull ğ•œ {x, y} = segment ğ•œ x y := by
  refine (convexHull_min ?_ <| convex_segment _ _).antisymm
    (segment_subset_convexHull (mem_insert _ _) <| subset_insert _ _ <| mem_singleton _)
  rw [insert_subset_iff, singleton_subset_iff]
  exact âŸ¨left_mem_segment _ _ _, right_mem_segment _ _ _âŸ©
#align convex_hull_pair convexHull_pair

theorem convexHull_convexHull_union_left (s t : Set E) :
    convexHull ğ•œ (convexHull ğ•œ s âˆª t) = convexHull ğ•œ (s âˆª t) :=
  ClosureOperator.closure_sup_closure_left _ _ _
#align convex_hull_convex_hull_union_left convexHull_convexHull_union_left

theorem convexHull_convexHull_union_right (s t : Set E) :
    convexHull ğ•œ (s âˆª convexHull ğ•œ t) = convexHull ğ•œ (s âˆª t) :=
  ClosureOperator.closure_sup_closure_right _ _ _
#align convex_hull_convex_hull_union_right convexHull_convexHull_union_right

theorem Convex.convex_remove_iff_not_mem_convexHull_remove {s : Set E} (hs : Convex ğ•œ s) (x : E) :
    Convex ğ•œ (s \ {x}) â†” x âˆ‰ convexHull ğ•œ (s \ {x}) := by
  constructor
  Â· rintro hsx hx
    rw [hsx.convexHull_eq] at hx
    exact hx.2 (mem_singleton _)
  rintro hx
  suffices h : s \ {x} = convexHull ğ•œ (s \ {x}) by
    rw [h]
    exact convex_convexHull ğ•œ _
  exact
    Subset.antisymm (subset_convexHull ğ•œ _) fun y hy =>
      âŸ¨convexHull_min diff_subset hs hy, by
        rintro (rfl : y = x)
        exact hx hyâŸ©
#align convex.convex_remove_iff_not_mem_convex_hull_remove Convex.convex_remove_iff_not_mem_convexHull_remove

theorem IsLinearMap.image_convexHull {f : E â†’ F} (hf : IsLinearMap ğ•œ f) (s : Set E) :
    f '' convexHull ğ•œ s = convexHull ğ•œ (f '' s) :=
  Set.Subset.antisymm
    (image_subset_iff.2 <|
      convexHull_min (image_subset_iff.1 <| subset_convexHull ğ•œ _)
        ((convex_convexHull ğ•œ _).is_linear_preimage hf))
    (convexHull_min (image_subset _ (subset_convexHull ğ•œ s)) <|
      (convex_convexHull ğ•œ s).is_linear_image hf)
#align is_linear_map.convex_hull_image IsLinearMap.image_convexHull

theorem LinearMap.image_convexHull (f : E â†’â‚—[ğ•œ] F) (s : Set E) :
    f '' convexHull ğ•œ s = convexHull ğ•œ (f '' s) :=
  f.isLinear.image_convexHull s
#align linear_map.convex_hull_image LinearMap.image_convexHull

end AddCommMonoid

end OrderedSemiring

section OrderedCommSemiring

variable [OrderedCommSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E]

theorem convexHull_smul (a : ğ•œ) (s : Set E) : convexHull ğ•œ (a â€¢ s) = a â€¢ convexHull ğ•œ s :=
  (LinearMap.lsmul _ _ a).image_convexHull _ |>.symm
#align convex_hull_smul convexHull_smul

end OrderedCommSemiring

section OrderedRing

variable [OrderedRing ğ•œ]

section AddCommGroup

variable [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F]

theorem AffineMap.image_convexHull (f : E â†’áµƒ[ğ•œ] F) (s : Set E) :
    f '' convexHull ğ•œ s = convexHull ğ•œ (f '' s) := by
  apply Set.Subset.antisymm
  Â· rw [Set.image_subset_iff]
    refine convexHull_min ?_ ((convex_convexHull ğ•œ (f '' s)).affine_preimage f)
    rw [â† Set.image_subset_iff]
    exact subset_convexHull ğ•œ (f '' s)
  Â· exact convexHull_min (Set.image_subset _ (subset_convexHull ğ•œ s))
      ((convex_convexHull ğ•œ s).affine_image f)
#align affine_map.image_convex_hull AffineMap.image_convexHull

theorem convexHull_subset_affineSpan (s : Set E) : convexHull ğ•œ s âŠ† (affineSpan ğ•œ s : Set E) :=
  convexHull_min (subset_affineSpan ğ•œ s) (affineSpan ğ•œ s).convex
#align convex_hull_subset_affine_span convexHull_subset_affineSpan

@[simp]
theorem affineSpan_convexHull (s : Set E) : affineSpan ğ•œ (convexHull ğ•œ s) = affineSpan ğ•œ s := by
  refine le_antisymm ?_ (affineSpan_mono ğ•œ (subset_convexHull ğ•œ s))
  rw [affineSpan_le]
  exact convexHull_subset_affineSpan s
#align affine_span_convex_hull affineSpan_convexHull

theorem convexHull_neg (s : Set E) : convexHull ğ•œ (-s) = -convexHull ğ•œ s := by
  simp_rw [â† image_neg]
  exact AffineMap.image_convexHull (-1) _ |>.symm
#align convex_hull_neg convexHull_neg

lemma convexHull_vadd (x : E) (s : Set E) : convexHull ğ•œ (x +áµ¥ s) = x +áµ¥ convexHull ğ•œ s :=
  (AffineEquiv.constVAdd ğ•œ _ x).toAffineMap.image_convexHull s |>.symm

end AddCommGroup

end OrderedRing

end convexHull
