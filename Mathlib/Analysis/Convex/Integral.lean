/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Convex.Function
import Mathlib.Analysis.Convex.StrictConvexSpace
import Mathlib.MeasureTheory.Function.AEEqOfIntegral
import Mathlib.MeasureTheory.Integral.Average

/-!
# Jensen's inequality for integrals

In this file we prove several forms of Jensen's inequality for integrals.

- for convex sets: `Convex.average_mem`, `Convex.set_average_mem`, `Convex.integral_mem`;

- for convex functions: `ConvexOn.average_mem_epigraph`, `ConvexOn.map_average_le`,
  `ConvexOn.set_average_mem_epigraph`, `ConvexOn.map_set_average_le`, `ConvexOn.map_integral_le`;

- for strictly convex sets: `StrictConvex.ae_eq_const_or_average_mem_interior`;

- for a closed ball in a strictly convex normed space:
  `ae_eq_const_or_norm_integral_lt_of_norm_le_const`;

- for strictly convex functions: `StrictConvexOn.ae_eq_const_or_map_average_lt`.

## TODO

- Use a typeclass for strict convexity of a closed ball.

## Tags

convex, integral, center mass, average value, Jensen's inequality
-/


open MeasureTheory MeasureTheory.Measure Metric Set Filter TopologicalSpace Function

open scoped Topology ENNReal Convex

variable {Î± E : Type*} {m0 : MeasurableSpace Î±} [NormedAddCommGroup E] [NormedSpace â„ E]
  [CompleteSpace E] {Î¼ : Measure Î±} {s : Set E} {t : Set Î±} {f : Î± â†’ E} {g : E â†’ â„} {C : â„}

/-!
### Non-strict Jensen's inequality
-/


/-- If `Î¼` is a probability measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the expected value of `f` belongs to `s`:
`âˆ« x, f x âˆ‚Î¼ âˆˆ s`. See also `Convex.sum_mem` for a finite sum version of this lemma. -/
theorem Convex.integral_mem [IsProbabilityMeasure Î¼] (hs : Convex â„ s) (hsc : IsClosed s)
    (hf : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) : (âˆ« x, f x âˆ‚Î¼) âˆˆ s := by
  borelize E
  rcases hfi.aestronglyMeasurable with âŸ¨g, hgm, hfgâŸ©
  haveI : SeparableSpace (range g âˆ© s : Set E) :=
    (hgm.isSeparable_range.mono inter_subset_left).separableSpace
  obtain âŸ¨yâ‚€, hâ‚€âŸ© : (range g âˆ© s).Nonempty := by
    rcases (hf.and hfg).exists with âŸ¨xâ‚€, hâ‚€âŸ©
    exact âŸ¨f xâ‚€, by simp only [hâ‚€.2, mem_range_self], hâ‚€.1âŸ©
  rw [integral_congr_ae hfg]; rw [integrable_congr hfg] at hfi
  have hg : âˆ€áµ x âˆ‚Î¼, g x âˆˆ closure (range g âˆ© s) := by
    filter_upwards [hfg.rw (fun _ y => y âˆˆ s) hf] with x hx
    apply subset_closure
    exact âŸ¨mem_range_self _, hxâŸ©
  set G : â„• â†’ SimpleFunc Î± E := SimpleFunc.approxOn _ hgm.measurable (range g âˆ© s) yâ‚€ hâ‚€
  have : Tendsto (fun n => (G n).integral Î¼) atTop (ğ“ <| âˆ« x, g x âˆ‚Î¼) :=
    tendsto_integral_approxOn_of_measurable hfi _ hg _ (integrable_const _)
  refine hsc.mem_of_tendsto this (Eventually.of_forall fun n => hs.sum_mem ?_ ?_ ?_)
  Â· exact fun _ _ => ENNReal.toReal_nonneg
  Â· simp_rw [measureReal_def]
    rw [â† ENNReal.toReal_sum, (G n).sum_range_measure_preimage_singleton, measure_univ,
      ENNReal.toReal_one]
    finiteness
  Â· simp only [SimpleFunc.mem_range, forall_mem_range]
    intro x
    apply (range g).inter_subset_right
    exact SimpleFunc.approxOn_mem hgm.measurable hâ‚€ _ _

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`â¨ x, f x âˆ‚Î¼ âˆˆ s`. See also `Convex.centerMass_mem` for a finite sum version of this lemma. -/
theorem Convex.average_mem [IsFiniteMeasure Î¼] [NeZero Î¼] (hs : Convex â„ s) (hsc : IsClosed s)
    (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) : (â¨ x, f x âˆ‚Î¼) âˆˆ s :=
  hs.integral_mem hsc (ae_mono' smul_absolutelyContinuous hfs) hfi.to_average

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex closed set in `E`, and `f` is an
integrable function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `s`:
`â¨ x, f x âˆ‚Î¼ âˆˆ s`. See also `Convex.centerMass_mem` for a finite sum version of this lemma. -/
theorem Convex.set_average_mem (hs : Convex â„ s) (hsc : IsClosed s) (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ)
    (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : IntegrableOn f t Î¼) : (â¨ x in t, f x âˆ‚Î¼) âˆˆ s :=
  have := Fact.mk ht.lt_top
  have := NeZero.mk h0
  hs.average_mem hsc hfs hfi

/-- If `Î¼` is a non-zero finite measure on `Î±`, `s` is a convex set in `E`, and `f` is an integrable
function sending `Î¼`-a.e. points to `s`, then the average value of `f` belongs to `closure s`:
`â¨ x, f x âˆ‚Î¼ âˆˆ s`. See also `Convex.centerMass_mem` for a finite sum version of this lemma. -/
theorem Convex.set_average_mem_closure (hs : Convex â„ s) (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ)
    (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s) (hfi : IntegrableOn f t Î¼) :
    (â¨ x in t, f x âˆ‚Î¼) âˆˆ closure s :=
  hs.closure.set_average_mem isClosed_closure h0 ht (hfs.mono fun _ hx => subset_closure hx) hfi

theorem ConvexOn.average_mem_epigraph [IsFiniteMeasure Î¼] [NeZero Î¼] (hg : ConvexOn â„ s g)
    (hgc : ContinuousOn g s) (hsc : IsClosed s) (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s)
    (hfi : Integrable f Î¼) (hgi : Integrable (g âˆ˜ f) Î¼) :
    (â¨ x, f x âˆ‚Î¼, â¨ x, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2} := by
  have ht_mem : âˆ€áµ x âˆ‚Î¼, (f x, g (f x)) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2} :=
    hfs.mono fun x hx => âŸ¨hx, le_rflâŸ©
  exact average_pair hfi hgi â–¸
    hg.convex_epigraph.average_mem (hsc.epigraph hgc) ht_mem (hfi.prodMk hgi)

theorem ConcaveOn.average_mem_hypograph [IsFiniteMeasure Î¼] [NeZero Î¼] (hg : ConcaveOn â„ s g)
    (hgc : ContinuousOn g s) (hsc : IsClosed s) (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s)
    (hfi : Integrable f Î¼) (hgi : Integrable (g âˆ˜ f) Î¼) :
    (â¨ x, f x âˆ‚Î¼, â¨ x, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ p.2 â‰¤ g p.1} := by
  simpa only [mem_setOf_eq, Pi.neg_apply, average_neg, neg_le_neg_iff] using
    hg.neg.average_mem_epigraph hgc.neg hsc hfs hfi hgi.neg

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is convex and continuous on a convex closed
set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending
`Î¼`-a.e. points to `s`, then the value of `g` at the average value of `f` is less than or equal to
the average value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`ConvexOn.map_centerMass_le` for a finite sum version of this lemma. -/
theorem ConvexOn.map_average_le [IsFiniteMeasure Î¼] [NeZero Î¼]
    (hg : ConvexOn â„ s g) (hgc : ContinuousOn g s) (hsc : IsClosed s)
    (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) (hgi : Integrable (g âˆ˜ f) Î¼) :
    g (â¨ x, f x âˆ‚Î¼) â‰¤ â¨ x, g (f x) âˆ‚Î¼ :=
  (hg.average_mem_epigraph hgc hsc hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is concave and continuous on a convex closed
set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending
`Î¼`-a.e. points to `s`, then the average value of `g âˆ˜ f` is less than or equal to the value of `g`
at the average value of `f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`ConcaveOn.le_map_centerMass` for a finite sum version of this lemma. -/
theorem ConcaveOn.le_map_average [IsFiniteMeasure Î¼] [NeZero Î¼]
    (hg : ConcaveOn â„ s g) (hgc : ContinuousOn g s) (hsc : IsClosed s)
    (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) (hgi : Integrable (g âˆ˜ f) Î¼) :
    (â¨ x, g (f x) âˆ‚Î¼) â‰¤ g (â¨ x, f x âˆ‚Î¼) :=
  (hg.average_mem_hypograph hgc hsc hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is convex and continuous on a convex closed
set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending
`Î¼`-a.e. points of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is
less than or equal to the average value of `g âˆ˜ f` over `t` provided that both `f` and `g âˆ˜ f` are
integrable. -/
theorem ConvexOn.set_average_mem_epigraph (hg : ConvexOn â„ s g) (hgc : ContinuousOn g s)
    (hsc : IsClosed s) (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ) (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s)
    (hfi : IntegrableOn f t Î¼) (hgi : IntegrableOn (g âˆ˜ f) t Î¼) :
    (â¨ x in t, f x âˆ‚Î¼, â¨ x in t, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ g p.1 â‰¤ p.2} :=
  have := Fact.mk ht.lt_top
  have := NeZero.mk h0
  hg.average_mem_epigraph hgc hsc hfs hfi hgi

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is concave and continuous on a convex closed
set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending
`Î¼`-a.e. points of a set `t` to `s`, then the average value of `g âˆ˜ f` over `t` is less than or
equal to the value of `g` at the average value of `f` over `t` provided that both `f` and `g âˆ˜ f`
are integrable. -/
theorem ConcaveOn.set_average_mem_hypograph (hg : ConcaveOn â„ s g) (hgc : ContinuousOn g s)
    (hsc : IsClosed s) (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ) (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s)
    (hfi : IntegrableOn f t Î¼) (hgi : IntegrableOn (g âˆ˜ f) t Î¼) :
    (â¨ x in t, f x âˆ‚Î¼, â¨ x in t, g (f x) âˆ‚Î¼) âˆˆ {p : E Ã— â„ | p.1 âˆˆ s âˆ§ p.2 â‰¤ g p.1} := by
  simpa only [mem_setOf_eq, Pi.neg_apply, average_neg, neg_le_neg_iff] using
    hg.neg.set_average_mem_epigraph hgc.neg hsc h0 ht hfs hfi hgi.neg

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is convex and continuous on a convex closed
set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending
`Î¼`-a.e. points of a set `t` to `s`, then the value of `g` at the average value of `f` over `t` is
less than or equal to the average value of `g âˆ˜ f` over `t` provided that both `f` and `g âˆ˜ f` are
integrable. -/
theorem ConvexOn.map_set_average_le (hg : ConvexOn â„ s g) (hgc : ContinuousOn g s)
    (hsc : IsClosed s) (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ) (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s)
    (hfi : IntegrableOn f t Î¼) (hgi : IntegrableOn (g âˆ˜ f) t Î¼) :
    g (â¨ x in t, f x âˆ‚Î¼) â‰¤ â¨ x in t, g (f x) âˆ‚Î¼ :=
  (hg.set_average_mem_epigraph hgc hsc h0 ht hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is concave and continuous on a convex closed
set `s`, `Î¼` is a finite non-zero measure on `Î±`, and `f : Î± â†’ E` is a function sending
`Î¼`-a.e. points of a set `t` to `s`, then the average value of `g âˆ˜ f` over `t` is less than or
equal to the value of `g` at the average value of `f` over `t` provided that both `f` and `g âˆ˜ f`
are integrable. -/
theorem ConcaveOn.le_map_set_average (hg : ConcaveOn â„ s g) (hgc : ContinuousOn g s)
    (hsc : IsClosed s) (h0 : Î¼ t â‰  0) (ht : Î¼ t â‰  âˆ) (hfs : âˆ€áµ x âˆ‚Î¼.restrict t, f x âˆˆ s)
    (hfi : IntegrableOn f t Î¼) (hgi : IntegrableOn (g âˆ˜ f) t Î¼) :
    (â¨ x in t, g (f x) âˆ‚Î¼) â‰¤ g (â¨ x in t, f x âˆ‚Î¼) :=
  (hg.set_average_mem_hypograph hgc hsc h0 ht hfs hfi hgi).2

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is convex and continuous on a convex closed
set `s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e.  points
to `s`, then the value of `g` at the expected value of `f` is less than or equal to the expected
value of `g âˆ˜ f` provided that both `f` and `g âˆ˜ f` are integrable. See also
`ConvexOn.map_centerMass_le` for a finite sum version of this lemma. -/
theorem ConvexOn.map_integral_le [IsProbabilityMeasure Î¼] (hg : ConvexOn â„ s g)
    (hgc : ContinuousOn g s) (hsc : IsClosed s) (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼)
    (hgi : Integrable (g âˆ˜ f) Î¼) : g (âˆ« x, f x âˆ‚Î¼) â‰¤ âˆ« x, g (f x) âˆ‚Î¼ := by
  simpa only [average_eq_integral] using hg.map_average_le hgc hsc hfs hfi hgi

/-- **Jensen's inequality**: if a function `g : E â†’ â„` is concave and continuous on a convex closed
set `s`, `Î¼` is a probability measure on `Î±`, and `f : Î± â†’ E` is a function sending `Î¼`-a.e.  points
to `s`, then the expected value of `g âˆ˜ f` is less than or equal to the value of `g` at the expected
value of `f` provided that both `f` and `g âˆ˜ f` are integrable. -/
theorem ConcaveOn.le_map_integral [IsProbabilityMeasure Î¼] (hg : ConcaveOn â„ s g)
    (hgc : ContinuousOn g s) (hsc : IsClosed s) (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼)
    (hgi : Integrable (g âˆ˜ f) Î¼) : (âˆ« x, g (f x) âˆ‚Î¼) â‰¤ g (âˆ« x, f x âˆ‚Î¼) := by
  simpa only [average_eq_integral] using hg.le_map_average hgc hsc hfs hfi hgi

/-!
### Strict Jensen's inequality
-/


/-- If `f : Î± â†’ E` is an integrable function, then either it is a.e. equal to the constant
`â¨ x, f x âˆ‚Î¼` or there exists a measurable set such that `Î¼ t â‰  0`, `Î¼ tá¶œ â‰  0`, and the average
values of `f` over `t` and `tá¶œ` are different. -/
theorem ae_eq_const_or_exists_average_ne_compl [IsFiniteMeasure Î¼] (hfi : Integrable f Î¼) :
    f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨
      âˆƒ t, MeasurableSet t âˆ§ Î¼ t â‰  0 âˆ§ Î¼ tá¶œ â‰  0 âˆ§ (â¨ x in t, f x âˆ‚Î¼) â‰  â¨ x in tá¶œ, f x âˆ‚Î¼ := by
  refine or_iff_not_imp_right.mpr fun H => ?_; push_neg at H
  refine hfi.ae_eq_of_forall_setIntegral_eq _ _ (integrable_const _) fun t ht ht' => ?_; clear ht'
  simp only [const_apply, setIntegral_const]
  by_cases hâ‚€ : Î¼ t = 0
  Â· rw [restrict_eq_zero.2 hâ‚€, integral_zero_measure, measureReal_def, hâ‚€,
      ENNReal.toReal_zero, zero_smul]
  by_cases hâ‚€' : Î¼ tá¶œ = 0
  Â· rw [â† ae_eq_univ] at hâ‚€'
    rw [restrict_congr_set hâ‚€', restrict_univ, measureReal_congr hâ‚€', measure_smul_average]
  have := average_mem_openSegment_compl_self ht.nullMeasurableSet hâ‚€ hâ‚€' hfi
  rw [â† H t ht hâ‚€ hâ‚€', openSegment_same, mem_singleton_iff] at this
  rw [this, measure_smul_setAverage _ (by finiteness)]

/-- If an integrable function `f : Î± â†’ E` takes values in a convex set `s` and for some set `t` of
positive measure, the average value of `f` over `t` belongs to the interior of `s`, then the average
of `f` over the whole space belongs to the interior of `s`. -/
theorem Convex.average_mem_interior_of_set [IsFiniteMeasure Î¼] (hs : Convex â„ s) (h0 : Î¼ t â‰  0)
    (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) (ht : (â¨ x in t, f x âˆ‚Î¼) âˆˆ interior s) :
    (â¨ x, f x âˆ‚Î¼) âˆˆ interior s := by
  rw [â† measure_toMeasurable] at h0; rw [â† restrict_toMeasurable (by finiteness)] at ht
  by_cases h0' : Î¼ (toMeasurable Î¼ t)á¶œ = 0
  Â· rw [â† ae_eq_univ] at h0'
    rwa [restrict_congr_set h0', restrict_univ] at ht
  exact hs.openSegment_interior_closure_subset_interior ht
      (hs.set_average_mem_closure h0' (by finiteness) (ae_restrict_of_ae hfs) hfi.integrableOn)
      (average_mem_openSegment_compl_self (measurableSet_toMeasurable Î¼ t).nullMeasurableSet h0
        h0' hfi)

/-- If an integrable function `f : Î± â†’ E` takes values in a strictly convex closed set `s`, then
either it is a.e. equal to its average value, or its average value belongs to the interior of
`s`. -/
theorem StrictConvex.ae_eq_const_or_average_mem_interior [IsFiniteMeasure Î¼] (hs : StrictConvex â„ s)
    (hsc : IsClosed s) (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) :
    f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ (â¨ x, f x âˆ‚Î¼) âˆˆ interior s := by
  have : âˆ€ {t}, Î¼ t â‰  0 â†’ (â¨ x in t, f x âˆ‚Î¼) âˆˆ s := fun ht =>
    hs.convex.set_average_mem hsc ht (by finiteness) (ae_restrict_of_ae hfs) hfi.integrableOn
  refine (ae_eq_const_or_exists_average_ne_compl hfi).imp_right ?_
  rintro âŸ¨t, hm, hâ‚€, hâ‚€', hneâŸ©
  exact
    hs.openSegment_subset (this hâ‚€) (this hâ‚€') hne
      (average_mem_openSegment_compl_self hm.nullMeasurableSet hâ‚€ hâ‚€' hfi)

/-- **Jensen's inequality**, strict version: if an integrable function `f : Î± â†’ E` takes values in a
convex closed set `s`, and `g : E â†’ â„` is continuous and strictly convex on `s`, then
either `f` is a.e. equal to its average value, or `g (â¨ x, f x âˆ‚Î¼) < â¨ x, g (f x) âˆ‚Î¼`. -/
theorem StrictConvexOn.ae_eq_const_or_map_average_lt [IsFiniteMeasure Î¼] (hg : StrictConvexOn â„ s g)
    (hgc : ContinuousOn g s) (hsc : IsClosed s) (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼)
    (hgi : Integrable (g âˆ˜ f) Î¼) :
    f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ g (â¨ x, f x âˆ‚Î¼) < â¨ x, g (f x) âˆ‚Î¼ := by
  have : âˆ€ {t}, Î¼ t â‰  0 â†’ (â¨ x in t, f x âˆ‚Î¼) âˆˆ s âˆ§ g (â¨ x in t, f x âˆ‚Î¼) â‰¤ â¨ x in t, g (f x) âˆ‚Î¼ :=
    fun ht =>
    hg.convexOn.set_average_mem_epigraph hgc hsc ht (by finiteness) (ae_restrict_of_ae hfs)
      hfi.integrableOn hgi.integrableOn
  refine (ae_eq_const_or_exists_average_ne_compl hfi).imp_right ?_
  rintro âŸ¨t, hm, hâ‚€, hâ‚€', hneâŸ©
  rcases average_mem_openSegment_compl_self hm.nullMeasurableSet hâ‚€ hâ‚€' (hfi.prodMk hgi) with
    âŸ¨a, b, ha, hb, hab, h_avgâŸ©
  rw [average_pair hfi hgi, average_pair hfi.integrableOn hgi.integrableOn,
    average_pair hfi.integrableOn hgi.integrableOn, Prod.smul_mk,
    Prod.smul_mk, Prod.mk_add_mk, Prod.mk_inj] at h_avg
  simp only [Function.comp] at h_avg
  rw [â† h_avg.1, â† h_avg.2]
  calc
    g ((a â€¢ â¨ x in t, f x âˆ‚Î¼) + b â€¢ â¨ x in tá¶œ, f x âˆ‚Î¼) <
        a * g (â¨ x in t, f x âˆ‚Î¼) + b * g (â¨ x in tá¶œ, f x âˆ‚Î¼) :=
      hg.2 (this hâ‚€).1 (this hâ‚€').1 hne ha hb hab
    _ â‰¤ (a * â¨ x in t, g (f x) âˆ‚Î¼) + b * â¨ x in tá¶œ, g (f x) âˆ‚Î¼ := by
      gcongr
      exacts [(this hâ‚€).2, (this hâ‚€').2]

/-- **Jensen's inequality**, strict version: if an integrable function `f : Î± â†’ E` takes values in a
convex closed set `s`, and `g : E â†’ â„` is continuous and strictly concave on `s`, then
either `f` is a.e. equal to its average value, or `â¨ x, g (f x) âˆ‚Î¼ < g (â¨ x, f x âˆ‚Î¼)`. -/
theorem StrictConcaveOn.ae_eq_const_or_lt_map_average [IsFiniteMeasure Î¼]
    (hg : StrictConcaveOn â„ s g) (hgc : ContinuousOn g s) (hsc : IsClosed s)
    (hfs : âˆ€áµ x âˆ‚Î¼, f x âˆˆ s) (hfi : Integrable f Î¼) (hgi : Integrable (g âˆ˜ f) Î¼) :
    f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ (â¨ x, g (f x) âˆ‚Î¼) < g (â¨ x, f x âˆ‚Î¼) := by
  simpa only [Pi.neg_apply, average_neg, neg_lt_neg_iff] using
    hg.neg.ae_eq_const_or_map_average_lt hgc.neg hsc hfs hfi hgi.neg

/-- If `E` is a strictly convex normed space and `f : Î± â†’ E` is a function such that `â€–f xâ€– â‰¤ C`
a.e., then either this function is a.e. equal to its average value, or the norm of its average value
is strictly less than `C`. -/
theorem ae_eq_const_or_norm_average_lt_of_norm_le_const [StrictConvexSpace â„ E]
    (h_le : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ C) : f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ â€–â¨ x, f x âˆ‚Î¼â€– < C := by
  rcases le_or_gt C 0 with hC0 | hC0
  Â· have : f =áµ[Î¼] 0 := h_le.mono fun x hx => norm_le_zero_iff.1 (hx.trans hC0)
    simp only [average_congr this, Pi.zero_apply, average_zero]
    exact Or.inl this
  by_cases hfi : Integrable f Î¼; swap
  Â· simp [average_eq, integral_undef hfi, hC0]
  rcases (le_top : Î¼ univ â‰¤ âˆ).eq_or_lt with hÎ¼t | hÎ¼t
  Â· simp [average_eq, measureReal_def, hÎ¼t, hC0]
  haveI : IsFiniteMeasure Î¼ := âŸ¨hÎ¼tâŸ©
  replace h_le : âˆ€áµ x âˆ‚Î¼, f x âˆˆ closedBall (0 : E) C := by simpa only [mem_closedBall_zero_iff]
  simpa only [interior_closedBall _ hC0.ne', mem_ball_zero_iff] using
    (strictConvex_closedBall â„ (0 : E) C).ae_eq_const_or_average_mem_interior isClosed_closedBall
      h_le hfi

/-- If `E` is a strictly convex normed space and `f : Î± â†’ E` is a function such that `â€–f xâ€– â‰¤ C`
a.e., then either this function is a.e. equal to its average value, or the norm of its integral is
strictly less than `Î¼.real univ * C`. -/
theorem ae_eq_const_or_norm_integral_lt_of_norm_le_const [StrictConvexSpace â„ E] [IsFiniteMeasure Î¼]
    (h_le : âˆ€áµ x âˆ‚Î¼, â€–f xâ€– â‰¤ C) :
    f =áµ[Î¼] const Î± (â¨ x, f x âˆ‚Î¼) âˆ¨ â€–âˆ« x, f x âˆ‚Î¼â€– < Î¼.real univ * C := by
  rcases eq_or_ne Î¼ 0 with hâ‚€ | hâ‚€; Â· simp [hâ‚€, EventuallyEq]
  have hÎ¼ : 0 < Î¼.real univ := by
    simp [measureReal_def, ENNReal.toReal_pos_iff, pos_iff_ne_zero, hâ‚€, measure_lt_top]
  refine (ae_eq_const_or_norm_average_lt_of_norm_le_const h_le).imp_right fun H => ?_
  rwa [average_eq, norm_smul, norm_inv, Real.norm_eq_abs, abs_of_pos hÎ¼, â† div_eq_inv_mul,
    div_lt_iffâ‚€' hÎ¼] at H

/-- If `E` is a strictly convex normed space and `f : Î± â†’ E` is a function such that `â€–f xâ€– â‰¤ C`
a.e. on a set `t` of finite measure, then either this function is a.e. equal to its average value on
`t`, or the norm of its integral over `t` is strictly less than `Î¼.real t * C`. -/
theorem ae_eq_const_or_norm_setIntegral_lt_of_norm_le_const [StrictConvexSpace â„ E] (ht : Î¼ t â‰  âˆ)
    (h_le : âˆ€áµ x âˆ‚Î¼.restrict t, â€–f xâ€– â‰¤ C) :
    f =áµ[Î¼.restrict t] const Î± (â¨ x in t, f x âˆ‚Î¼) âˆ¨ â€–âˆ« x in t, f x âˆ‚Î¼â€– < Î¼.real t * C := by
  haveI := Fact.mk ht.lt_top
  rw [â† measureReal_restrict_apply_univ]
  exact ae_eq_const_or_norm_integral_lt_of_norm_le_const h_le
