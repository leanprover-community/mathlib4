/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Order.Filter.AtTopBot.Archimedean
import Mathlib.Order.Filter.AtTopBot.Finite
import Mathlib.Order.Filter.AtTopBot.Prod
import Mathlib.Topology.Algebra.Ring.Real

/-!
# Convergence of subadditive sequences

A subadditive sequence `u : â„• â†’ â„` is a sequence satisfying `u (m + n) â‰¤ u m + u n` for all `m, n`.
We define this notion as `Subadditive u`, and prove in `Subadditive.tendsto_lim` that, if `u n / n`
is bounded below, then it converges to a limit (that we denote by `Subadditive.lim` for
convenience). This result is known as Fekete's lemma in the literature.

## TODO

Define a bundled `SubadditiveHom`, use it.
-/

noncomputable section

open Set Filter Topology

/-- A real-valued sequence is subadditive if it satisfies the inequality `u (m + n) â‰¤ u m + u n`
for all `m, n`. -/
def Subadditive (u : â„• â†’ â„) : Prop :=
  âˆ€ m n, u (m + n) â‰¤ u m + u n

namespace Subadditive

variable {u : â„• â†’ â„} (h : Subadditive u)

/-- The limit of a bounded-below subadditive sequence. The fact that the sequence indeed tends to
this limit is given in `Subadditive.tendsto_lim` -/
@[nolint unusedArguments, irreducible]
protected def lim (_h : Subadditive u) :=
  sInf ((fun n : â„• => u n / n) '' Ici 1)

theorem lim_le_div (hbdd : BddBelow (range fun n => u n / n)) {n : â„•} (hn : n â‰  0) :
    h.lim â‰¤ u n / n := by
  rw [Subadditive.lim]
  exact csInf_le (hbdd.mono <| image_subset_range _ _) âŸ¨n, hn.bot_lt, rflâŸ©

include h in
theorem apply_mul_add_le (k n r) : u (k * n + r) â‰¤ k * u n + u r := by
  induction k with
  | zero => simp only [Nat.cast_zero, zero_mul, zero_add]; rfl
  | succ k IH =>
    calc
      u ((k + 1) * n + r) = u (n + (k * n + r)) := by congr 1; ring
      _ â‰¤ u n + u (k * n + r) := h _ _
      _ â‰¤ u n + (k * u n + u r) := by grw [IH]
      _ = (k + 1 : â„•) * u n + u r := by simp; ring

include h in
theorem eventually_div_lt_of_div_lt {L : â„} {n : â„•} (hn : n â‰  0) (hL : u n / n < L) :
    âˆ€á¶  p in atTop, u p / p < L := by
  /- It suffices to prove the statement for each arithmetic progression `(n * Â· + r)`. -/
  refine .atTop_of_arithmetic hn fun r _ => ?_
  /- `(k * u n + u r) / (k * n + r)` tends to `u n / n < L`, hence
  `(k * u n + u r) / (k * n + r) < L` for sufficiently large `k`. -/
  have A : Tendsto (fun x : â„ => (u n + u r / x) / (n + r / x)) atTop (ğ“ ((u n + 0) / (n + 0))) :=
    (tendsto_const_nhds.add <| tendsto_const_nhds.div_atTop tendsto_id).div
      (tendsto_const_nhds.add <| tendsto_const_nhds.div_atTop tendsto_id) <| by simpa
  have B : Tendsto (fun x => (x * u n + u r) / (x * n + r)) atTop (ğ“ (u n / n)) := by
    rw [add_zero, add_zero] at A
    refine A.congr' <| (eventually_ne_atTop 0).mono fun x hx => ?_
    simp only [add_div' _ _ _ hx, div_div_div_cancel_rightâ‚€ hx, mul_comm]
  refine ((B.comp tendsto_natCast_atTop_atTop).eventually (gt_mem_nhds hL)).mono fun k hk => ?_
  /- Finally, we use an upper estimate on `u (k * n + r)` to get an estimate on
  `u (k * n + r) / (k * n + r)`. -/
  rw [mul_comm]
  refine lt_of_le_of_lt ?_ hk
  simp only [(Â· âˆ˜ Â·), â† Nat.cast_add, â† Nat.cast_mul]
  gcongr
  apply h.apply_mul_add_le

/-- Fekete's lemma: a subadditive sequence which is bounded below converges. -/
theorem tendsto_lim (hbdd : BddBelow (range fun n => u n / n)) :
    Tendsto (fun n => u n / n) atTop (ğ“ h.lim) := by
  refine tendsto_order.2 âŸ¨fun l hl => ?_, fun L hL => ?_âŸ©
  Â· refine eventually_atTop.2
      âŸ¨1, fun n hn => hl.trans_le (h.lim_le_div hbdd (zero_lt_one.trans_le hn).ne')âŸ©
  Â· obtain âŸ¨n, npos, hnâŸ© : âˆƒ n : â„•, 0 < n âˆ§ u n / n < L := by
      rw [Subadditive.lim] at hL
      rcases exists_lt_of_csInf_lt (by simp) hL with âŸ¨x, hx, xLâŸ©
      rcases (mem_image _ _ _).1 hx with âŸ¨n, hn, rflâŸ©
      exact âŸ¨n, zero_lt_one.trans_le hn, xLâŸ©
    exact h.eventually_div_lt_of_div_lt npos.ne' hn

end Subadditive
