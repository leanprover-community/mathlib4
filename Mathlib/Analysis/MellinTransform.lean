/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.MeasureTheory.Measure.Haar.NormedSpace

#align_import analysis.mellin_transform from "leanprover-community/mathlib"@"917c3c072e487b3cccdbfeff17e75b40e45f66cb"

/-! # The Mellin transform

We define the Mellin transform of a locally integrable function on `Ioi 0`, and show it is
differentiable in a suitable vertical strip.

## Main statements

- `mellin` : the Mellin transform `‚à´ (t : ‚Ñù) in Ioi 0, t ^ (s - 1) ‚Ä¢ f t`,
  where `s` is a complex number.
- `HasMellin`: shorthand asserting that the Mellin transform exists and has a given value
  (analogous to `HasSum`).
- `mellin_differentiableAt_of_isBigO_rpow` : if `f` is `O(x ^ (-a))` at infinity, and
  `O(x ^ (-b))` at 0, then `mellin f` is holomorphic on the domain `b < re s < a`.

-/

open MeasureTheory Set Filter Asymptotics TopologicalSpace

namespace Complex

-- Porting note: move this to `Mathlib.Analysis.SpecialFunctions.Pow.Complex`
theorem cpow_mul_ofReal_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) (z : ‚ÑÇ) :
    (x : ‚ÑÇ) ^ (‚Üëy * z) = (‚Üë(x ^ y) : ‚ÑÇ) ^ z := by
  rw [cpow_mul, ofReal_cpow hx]
  ¬∑ rw [‚Üê ofReal_log hx, ‚Üê ofReal_mul, ofReal_im, neg_lt_zero]; exact Real.pi_pos
  ¬∑ rw [‚Üê ofReal_log hx, ‚Üê ofReal_mul, ofReal_im]; exact Real.pi_pos.le
#align complex.cpow_mul_of_real_nonneg Complex.cpow_mul_ofReal_nonneg

end Complex

open Real

open Complex hiding exp log abs_of_nonneg

open scoped Topology

noncomputable section

section Defs

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]

/-- Predicate on `f` and `s` asserting that the Mellin integral is well-defined. -/
def MellinConvergent (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) : Prop :=
  IntegrableOn (fun t : ‚Ñù => (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f t) (Ioi 0)
#align mellin_convergent MellinConvergent

theorem MellinConvergent.const_smul {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ} (hf : MellinConvergent f s) {ùïú : Type*}
    [NontriviallyNormedField ùïú] [NormedSpace ùïú E] [SMulCommClass ‚ÑÇ ùïú E] (c : ùïú) :
    MellinConvergent (fun t => c ‚Ä¢ f t) s := by
  simpa only [MellinConvergent, smul_comm] using hf.smul c
#align mellin_convergent.const_smul MellinConvergent.const_smul

theorem MellinConvergent.cpow_smul {f : ‚Ñù ‚Üí E} {s a : ‚ÑÇ} :
    MellinConvergent (fun t => (t : ‚ÑÇ) ^ a ‚Ä¢ f t) s ‚Üî MellinConvergent f (s + a) := by
  refine' integrableOn_congr_fun (fun t ht => _) measurableSet_Ioi
  simp_rw [‚Üê sub_add_eq_add_sub, cpow_add _ _ (ofReal_ne_zero.2 <| ne_of_gt ht), mul_smul]
#align mellin_convergent.cpow_smul MellinConvergent.cpow_smul

nonrec theorem MellinConvergent.div_const {f : ‚Ñù ‚Üí ‚ÑÇ} {s : ‚ÑÇ} (hf : MellinConvergent f s) (a : ‚ÑÇ) :
    MellinConvergent (fun t => f t / a) s := by
  simpa only [MellinConvergent, smul_eq_mul, ‚Üê mul_div_assoc] using hf.div_const a
#align mellin_convergent.div_const MellinConvergent.div_const

theorem MellinConvergent.comp_mul_left {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ} {a : ‚Ñù} (ha : 0 < a) :
    MellinConvergent (fun t => f (a * t)) s ‚Üî MellinConvergent f s := by
  have := integrableOn_Ioi_comp_mul_left_iff (fun t : ‚Ñù => (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f t) 0 ha
  rw [mul_zero] at this
  have h1 : EqOn (fun t : ‚Ñù => (‚Üë(a * t) : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f (a * t))
      ((a : ‚ÑÇ) ^ (s - 1) ‚Ä¢ fun t : ‚Ñù => (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f (a * t)) (Ioi 0) := fun t ht ‚Ü¶ by
    simp only [ofReal_mul, mul_cpow_ofReal_nonneg ha.le (le_of_lt ht), mul_smul, Pi.smul_apply]
  have h2 : (a : ‚ÑÇ) ^ (s - 1) ‚â† 0 := by
    rw [Ne.def, cpow_eq_zero_iff, not_and_or, ofReal_eq_zero]
    exact Or.inl ha.ne'
  rw [MellinConvergent, MellinConvergent, ‚Üê this, integrableOn_congr_fun h1 measurableSet_Ioi,
    IntegrableOn, IntegrableOn, integrable_smul_iff h2]
#align mellin_convergent.comp_mul_left MellinConvergent.comp_mul_left

theorem MellinConvergent.comp_rpow {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ} {a : ‚Ñù} (ha : a ‚â† 0) :
    MellinConvergent (fun t => f (t ^ a)) s ‚Üî MellinConvergent f (s / a) := by
  refine Iff.trans ?_ (integrableOn_Ioi_comp_rpow_iff' _ ha)
  rw [MellinConvergent]
  refine' integrableOn_congr_fun (fun t ht => _) measurableSet_Ioi
  dsimp only [Pi.smul_apply]
  rw [‚Üê Complex.coe_smul (t ^ (a - 1)), ‚Üê mul_smul, ‚Üê cpow_mul_ofReal_nonneg (le_of_lt ht),
    ofReal_cpow (le_of_lt ht), ‚Üê cpow_add _ _ (ofReal_ne_zero.mpr (ne_of_gt ht)), ofReal_sub,
    ofReal_one, mul_sub, mul_div_cancel' _ (ofReal_ne_zero.mpr ha), mul_one, add_comm, ‚Üê
    add_sub_assoc, sub_add_cancel]
#align mellin_convergent.comp_rpow MellinConvergent.comp_rpow

/-- The Mellin transform of a function `f` (for a complex exponent `s`), defined as the integral of
`t ^ (s - 1) ‚Ä¢ f` over `Ioi 0`. -/
def mellin (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) : E :=
  ‚à´ t : ‚Ñù in Ioi 0, (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f t
#align mellin mellin

-- next few lemmas don't require convergence of the Mellin transform (they are just 0 = 0 otherwise)
theorem mellin_cpow_smul (f : ‚Ñù ‚Üí E) (s a : ‚ÑÇ) :
    mellin (fun t => (t : ‚ÑÇ) ^ a ‚Ä¢ f t) s = mellin f (s + a) := by
  refine' set_integral_congr measurableSet_Ioi fun t ht => _
  simp_rw [‚Üê sub_add_eq_add_sub, cpow_add _ _ (ofReal_ne_zero.2 <| ne_of_gt ht), mul_smul]
#align mellin_cpow_smul mellin_cpow_smul

theorem mellin_const_smul (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) {ùïú : Type*} [NontriviallyNormedField ùïú]
    [NormedSpace ùïú E] [SMulCommClass ‚ÑÇ ùïú E] (c : ùïú) :
    mellin (fun t => c ‚Ä¢ f t) s = c ‚Ä¢ mellin f s := by simp only [mellin, smul_comm, integral_smul]
#align mellin_const_smul mellin_const_smul

theorem mellin_div_const (f : ‚Ñù ‚Üí ‚ÑÇ) (s a : ‚ÑÇ) : mellin (fun t => f t / a) s = mellin f s / a := by
  simp_rw [mellin, smul_eq_mul, ‚Üê mul_div_assoc, integral_div]
#align mellin_div_const mellin_div_const

theorem mellin_comp_rpow (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) (a : ‚Ñù) :
    mellin (fun t => f (t ^ a)) s = |a|‚Åª¬π ‚Ä¢ mellin f (s / a) := by
  /- This is true for `a = 0` as all sides are undefined but turn out to vanish thanks to our
  convention. The interesting case is `a ‚â† 0` -/
  rcases eq_or_ne a 0 with rfl|ha
  ¬∑ by_cases hE : CompleteSpace E
    ¬∑ simp [integral_smul_const, mellin, setIntegral_Ioi_zero_cpow]
    ¬∑ simp [integral, mellin, hE]
  simp_rw [mellin]
  conv_rhs => rw [‚Üê integral_comp_rpow_Ioi _ ha, ‚Üê integral_smul]
  refine' set_integral_congr measurableSet_Ioi fun t ht => _
  dsimp only
  rw [‚Üê mul_smul, ‚Üê mul_assoc, inv_mul_cancel (mt abs_eq_zero.1 ha), one_mul, ‚Üê smul_assoc,
    real_smul]
  rw [ofReal_cpow (le_of_lt ht), ‚Üê cpow_mul_ofReal_nonneg (le_of_lt ht), ‚Üê
    cpow_add _ _ (ofReal_ne_zero.mpr <| ne_of_gt ht), ofReal_sub, ofReal_one, mul_sub,
    mul_div_cancel' _ (ofReal_ne_zero.mpr ha), add_comm, ‚Üê add_sub_assoc, mul_one, sub_add_cancel]
#align mellin_comp_rpow mellin_comp_rpow

theorem mellin_comp_mul_left (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) {a : ‚Ñù} (ha : 0 < a) :
    mellin (fun t => f (a * t)) s = (a : ‚ÑÇ) ^ (-s) ‚Ä¢ mellin f s := by
  simp_rw [mellin]
  have : EqOn (fun t : ‚Ñù => (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f (a * t))
      (fun t : ‚Ñù => (a : ‚ÑÇ) ^ (1 - s) ‚Ä¢ (fun u : ‚Ñù => (u : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f u) (a * t))
        (Ioi 0) := fun t ht ‚Ü¶ by
    dsimp only
    rw [ofReal_mul, mul_cpow_ofReal_nonneg ha.le (le_of_lt ht), ‚Üê mul_smul,
      (by ring : 1 - s = -(s - 1)), cpow_neg, inv_mul_cancel_left‚ÇÄ]
    rw [Ne.def, cpow_eq_zero_iff, ofReal_eq_zero, not_and_or]
    exact Or.inl ha.ne'
  rw [set_integral_congr measurableSet_Ioi this, integral_smul,
    integral_comp_mul_left_Ioi (fun u ‚Ü¶ (u : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f u) _ ha,
    mul_zero, ‚Üê Complex.coe_smul, ‚Üê mul_smul, sub_eq_add_neg,
    cpow_add _ _ (ofReal_ne_zero.mpr ha.ne'), cpow_one, abs_of_pos (inv_pos.mpr ha), ofReal_inv,
    mul_assoc, mul_comm, inv_mul_cancel_right‚ÇÄ (ofReal_ne_zero.mpr ha.ne')]
#align mellin_comp_mul_left mellin_comp_mul_left

theorem mellin_comp_mul_right (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) {a : ‚Ñù} (ha : 0 < a) :
    mellin (fun t => f (t * a)) s = (a : ‚ÑÇ) ^ (-s) ‚Ä¢ mellin f s := by
  simpa only [mul_comm] using mellin_comp_mul_left f s ha
#align mellin_comp_mul_right mellin_comp_mul_right

theorem mellin_comp_inv (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) : mellin (fun t => f t‚Åª¬π) s = mellin f (-s) := by
  simp_rw [‚Üê rpow_neg_one, mellin_comp_rpow _ _ _, abs_neg, abs_one,
    inv_one, one_smul, ofReal_neg, ofReal_one, div_neg, div_one]
#align mellin_comp_inv mellin_comp_inv

/-- Predicate standing for "the Mellin transform of `f` is defined at `s` and equal to `m`". This
shortens some arguments. -/
def HasMellin (f : ‚Ñù ‚Üí E) (s : ‚ÑÇ) (m : E) : Prop :=
  MellinConvergent f s ‚àß mellin f s = m
#align has_mellin HasMellin

theorem hasMellin_add {f g : ‚Ñù ‚Üí E} {s : ‚ÑÇ} (hf : MellinConvergent f s)
    (hg : MellinConvergent g s) : HasMellin (fun t => f t + g t) s (mellin f s + mellin g s) :=
  ‚ü®by simpa only [MellinConvergent, smul_add] using hf.add hg, by
    simpa only [mellin, smul_add] using integral_add hf hg‚ü©
#align has_mellin_add hasMellin_add

theorem hasMellin_sub {f g : ‚Ñù ‚Üí E} {s : ‚ÑÇ} (hf : MellinConvergent f s)
    (hg : MellinConvergent g s) : HasMellin (fun t => f t - g t) s (mellin f s - mellin g s) :=
  ‚ü®by simpa only [MellinConvergent, smul_sub] using hf.sub hg, by
    simpa only [mellin, smul_sub] using integral_sub hf hg‚ü©
#align has_mellin_sub hasMellin_sub

end Defs

variable {E : Type*} [NormedAddCommGroup E]

section MellinConvergent

/-! ## Convergence of Mellin transform integrals -/

/-- Auxiliary lemma to reduce convergence statements from vector-valued functions to real
scalar-valued functions. -/
theorem mellin_convergent_iff_norm [NormedSpace ‚ÑÇ E] {f : ‚Ñù ‚Üí E} {T : Set ‚Ñù} (hT : T ‚äÜ Ioi 0)
    (hT' : MeasurableSet T) (hfc : AEStronglyMeasurable f <| volume.restrict <| Ioi 0) {s : ‚ÑÇ} :
    IntegrableOn (fun t : ‚Ñù => (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f t) T ‚Üî
      IntegrableOn (fun t : ‚Ñù => t ^ (s.re - 1) * ‚Äñf t‚Äñ) T := by
  have : AEStronglyMeasurable (fun t : ‚Ñù => (t : ‚ÑÇ) ^ (s - 1) ‚Ä¢ f t) (volume.restrict T) := by
    refine' ((ContinuousAt.continuousOn _).aestronglyMeasurable hT').smul (hfc.mono_set hT)
    exact fun t ht => continuousAt_ofReal_cpow_const _ _ (Or.inr <| ne_of_gt (hT ht))
  rw [IntegrableOn, ‚Üê integrable_norm_iff this, ‚Üê IntegrableOn]
  refine' integrableOn_congr_fun (fun t ht => _) hT'
  simp_rw [norm_smul, Complex.norm_eq_abs, abs_cpow_eq_rpow_re_of_pos (hT ht), sub_re, one_re]
#align mellin_convergent_iff_norm mellin_convergent_iff_norm

/-- If `f` is a locally integrable real-valued function which is `O(x ^ (-a))` at `‚àû`, then for any
`s < a`, its Mellin transform converges on some neighbourhood of `+‚àû`. -/
theorem mellin_convergent_top_of_isBigO {f : ‚Ñù ‚Üí ‚Ñù}
    (hfc : AEStronglyMeasurable f <| volume.restrict (Ioi 0)) {a s : ‚Ñù}
    (hf : f =O[atTop] (¬∑ ^ (-a))) (hs : s < a) :
    ‚àÉ c : ‚Ñù, 0 < c ‚àß IntegrableOn (fun t : ‚Ñù => t ^ (s - 1) * f t) (Ioi c) := by
  obtain ‚ü®d, hd'‚ü© := hf.isBigOWith
  simp_rw [IsBigOWith, eventually_atTop] at hd'
  obtain ‚ü®e, he‚ü© := hd'
  have he' : 0 < max e 1 := zero_lt_one.trans_le (le_max_right _ _)
  refine' ‚ü®max e 1, he', _, _‚ü©
  ¬∑ refine' AEStronglyMeasurable.mul _ (hfc.mono_set (Ioi_subset_Ioi he'.le))
    refine' (ContinuousAt.continuousOn fun t ht => _).aestronglyMeasurable measurableSet_Ioi
    exact continuousAt_rpow_const _ _ (Or.inl <| (he'.trans ht).ne')
  ¬∑ have : ‚àÄ·µê t : ‚Ñù ‚àÇvolume.restrict (Ioi <| max e 1),
        ‚Äñt ^ (s - 1) * f t‚Äñ ‚â§ t ^ (s - 1 + -a) * d := by
      refine' (ae_restrict_mem measurableSet_Ioi).mono fun t ht => _
      have ht' : 0 < t := he'.trans ht
      rw [norm_mul, rpow_add ht', ‚Üê norm_of_nonneg (rpow_nonneg ht'.le (-a)), mul_assoc,
        mul_comm _ d, norm_of_nonneg (rpow_nonneg ht'.le _)]
      gcongr
      exact he t ((le_max_left e 1).trans_lt ht).le
    refine' (HasFiniteIntegral.mul_const _ _).mono' this
    exact (integrableOn_Ioi_rpow_of_lt (by linarith) he').hasFiniteIntegral
set_option linter.uppercaseLean3 false in
#align mellin_convergent_top_of_is_O mellin_convergent_top_of_isBigO

/-- If `f` is a locally integrable real-valued function which is `O(x ^ (-b))` at `0`, then for any
`b < s`, its Mellin transform converges on some right neighbourhood of `0`. -/
theorem mellin_convergent_zero_of_isBigO {b : ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù}
    (hfc : AEStronglyMeasurable f <| volume.restrict (Ioi 0))
    (hf :  f =O[ùìù[>] 0] (¬∑ ^ (-b))) {s : ‚Ñù} (hs : b < s) :
    ‚àÉ c : ‚Ñù, 0 < c ‚àß IntegrableOn (fun t : ‚Ñù => t ^ (s - 1) * f t) (Ioc 0 c) := by
  obtain ‚ü®d, _, hd'‚ü© := hf.exists_pos
  simp_rw [IsBigOWith, eventually_nhdsWithin_iff, Metric.eventually_nhds_iff, gt_iff_lt] at hd'
  obtain ‚ü®Œµ, hŒµ, hŒµ'‚ü© := hd'
  refine' ‚ü®Œµ, hŒµ, integrableOn_Ioc_iff_integrableOn_Ioo.mpr ‚ü®_, _‚ü©‚ü©
  ¬∑ refine' AEStronglyMeasurable.mul _ (hfc.mono_set Ioo_subset_Ioi_self)
    refine' (ContinuousAt.continuousOn fun t ht => _).aestronglyMeasurable measurableSet_Ioo
    exact continuousAt_rpow_const _ _ (Or.inl ht.1.ne')
  ¬∑ apply HasFiniteIntegral.mono'
    ¬∑ show HasFiniteIntegral (fun t => d * t ^ (s - b - 1)) _
      refine' (Integrable.hasFiniteIntegral _).const_mul _
      rw [‚Üê IntegrableOn, ‚Üê integrableOn_Ioc_iff_integrableOn_Ioo, ‚Üê
        intervalIntegrable_iff_integrableOn_Ioc_of_le hŒµ.le]
      exact intervalIntegral.intervalIntegrable_rpow' (by linarith)
    ¬∑ refine' (ae_restrict_iff' measurableSet_Ioo).mpr (eventually_of_forall fun t ht => _)
      rw [mul_comm, norm_mul]
      specialize hŒµ' _ ht.1
      ¬∑ rw [dist_eq_norm, sub_zero, norm_of_nonneg (le_of_lt ht.1)]
        exact ht.2
      ¬∑ calc _ ‚â§ d * ‚Äñt ^ (-b)‚Äñ * ‚Äñt ^ (s - 1)‚Äñ := by gcongr
          _ = d * t ^ (s - b - 1) := ?_
        simp_rw [norm_of_nonneg (rpow_nonneg (le_of_lt ht.1) _), mul_assoc]
        rw [‚Üê rpow_add ht.1]
        congr 2
        abel
set_option linter.uppercaseLean3 false in
#align mellin_convergent_zero_of_is_O mellin_convergent_zero_of_isBigO

/-- If `f` is a locally integrable real-valued function on `Ioi 0` which is `O(x ^ (-a))` at `‚àû`
and `O(x ^ (-b))` at `0`, then its Mellin transform integral converges for `b < s < a`. -/
theorem mellin_convergent_of_isBigO_scalar {a b : ‚Ñù} {f : ‚Ñù ‚Üí ‚Ñù} {s : ‚Ñù}
    (hfc : LocallyIntegrableOn f <| Ioi 0) (hf_top : f =O[atTop] (¬∑ ^ (-a)))
    (hs_top : s < a) (hf_bot : f =O[ùìù[>] 0] (¬∑ ^ (-b))) (hs_bot : b < s) :
    IntegrableOn (fun t : ‚Ñù => t ^ (s - 1) * f t) (Ioi 0) := by
  obtain ‚ü®c1, hc1, hc1'‚ü© := mellin_convergent_top_of_isBigO hfc.aestronglyMeasurable hf_top hs_top
  obtain ‚ü®c2, hc2, hc2'‚ü© :=
    mellin_convergent_zero_of_isBigO hfc.aestronglyMeasurable hf_bot hs_bot
  have : Ioi 0 = Ioc 0 c2 ‚à™ Ioc c2 c1 ‚à™ Ioi c1 := by
    rw [union_assoc, Ioc_union_Ioi (le_max_right _ _),
      Ioc_union_Ioi ((min_le_left _ _).trans (le_max_right _ _)), min_eq_left (lt_min hc2 hc1).le]
  rw [this, integrableOn_union, integrableOn_union]
  refine' ‚ü®‚ü®hc2', integrableOn_Icc_iff_integrableOn_Ioc.mp _‚ü©, hc1'‚ü©
  refine'
    (hfc.continuousOn_mul _ isOpen_Ioi).integrableOn_compact_subset
      (fun t ht => (hc2.trans_le ht.1 : 0 < t)) isCompact_Icc
  exact ContinuousAt.continuousOn fun t ht => continuousAt_rpow_const _ _ <| Or.inl <| ne_of_gt ht
set_option linter.uppercaseLean3 false in
#align mellin_convergent_of_is_O_scalar mellin_convergent_of_isBigO_scalar

theorem mellinConvergent_of_isBigO_rpow [NormedSpace ‚ÑÇ E] {a b : ‚Ñù} {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ}
    (hfc : LocallyIntegrableOn f <| Ioi 0) (hf_top : f =O[atTop] (¬∑ ^ (-a)))
    (hs_top : s.re < a) (hf_bot : f =O[ùìù[>] 0] (¬∑ ^ (-b))) (hs_bot : b < s.re) :
    MellinConvergent f s := by
  rw [MellinConvergent,
    mellin_convergent_iff_norm Subset.rfl measurableSet_Ioi hfc.aestronglyMeasurable]
  exact mellin_convergent_of_isBigO_scalar hfc.norm hf_top.norm_left hs_top hf_bot.norm_left hs_bot
set_option linter.uppercaseLean3 false in
#align mellin_convergent_of_is_O_rpow mellinConvergent_of_isBigO_rpow

end MellinConvergent

section MellinDiff

/-- If `f` is `O(x ^ (-a))` as `x ‚Üí +‚àû`, then `log ‚Ä¢ f` is `O(x ^ (-b))` for every `b < a`. -/
theorem isBigO_rpow_top_log_smul [NormedSpace ‚Ñù E] {a b : ‚Ñù} {f : ‚Ñù ‚Üí E} (hab : b < a)
    (hf : f =O[atTop] (¬∑ ^ (-a))) :
    (fun t : ‚Ñù => log t ‚Ä¢ f t) =O[atTop] (¬∑ ^ (-b)) := by
  refine'
    ((isLittleO_log_rpow_atTop (sub_pos.mpr hab)).isBigO.smul hf).congr'
      (eventually_of_forall fun t => by rfl)
      ((eventually_gt_atTop 0).mp (eventually_of_forall fun t ht => _))
  simp only
  rw [smul_eq_mul, ‚Üê rpow_add ht, ‚Üê sub_eq_add_neg, sub_eq_add_neg a, add_sub_cancel']
set_option linter.uppercaseLean3 false in
#align is_O_rpow_top_log_smul isBigO_rpow_top_log_smul

/-- If `f` is `O(x ^ (-a))` as `x ‚Üí 0`, then `log ‚Ä¢ f` is `O(x ^ (-b))` for every `a < b`. -/
theorem isBigO_rpow_zero_log_smul [NormedSpace ‚Ñù E] {a b : ‚Ñù} {f : ‚Ñù ‚Üí E} (hab : a < b)
    (hf : f =O[ùìù[>] 0] (¬∑ ^ (-a))) :
    (fun t : ‚Ñù => log t ‚Ä¢ f t) =O[ùìù[>] 0] (¬∑ ^ (-b)) := by
  have : log =o[ùìù[>] 0] fun t : ‚Ñù => t ^ (a - b) := by
    refine' ((isLittleO_log_rpow_atTop (sub_pos.mpr hab)).neg_left.comp_tendsto
          tendsto_inv_zero_atTop).congr'
      (eventually_nhdsWithin_iff.mpr <| eventually_of_forall fun t ht => _)
      (eventually_nhdsWithin_iff.mpr <| eventually_of_forall fun t ht => _)
    ¬∑ simp_rw [Function.comp_apply, ‚Üê one_div, log_div one_ne_zero (ne_of_gt ht), Real.log_one,
        zero_sub, neg_neg]
    ¬∑ simp_rw [Function.comp_apply, inv_rpow (le_of_lt ht), ‚Üê rpow_neg (le_of_lt ht), neg_sub]
  refine' (this.isBigO.smul hf).congr' (eventually_of_forall fun t => by rfl)
      (eventually_nhdsWithin_iff.mpr (eventually_of_forall fun t ht => _))
  simp_rw [smul_eq_mul, ‚Üê rpow_add ht]
  congr 1
  abel
set_option linter.uppercaseLean3 false in
#align is_O_rpow_zero_log_smul isBigO_rpow_zero_log_smul

/-- Suppose `f` is locally integrable on `(0, ‚àû)`, is `O(x ^ (-a))` as `x ‚Üí ‚àû`, and is
`O(x ^ (-b))` as `x ‚Üí 0`. Then its Mellin transform is differentiable on the domain `b < re s < a`,
with derivative equal to the Mellin transform of `log ‚Ä¢ f`. -/
theorem mellin_hasDerivAt_of_isBigO_rpow [CompleteSpace E] [NormedSpace ‚ÑÇ E] {a b : ‚Ñù}
    {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ} (hfc : LocallyIntegrableOn f (Ioi 0)) (hf_top : f =O[atTop] (¬∑ ^ (-a)))
    (hs_top : s.re < a) (hf_bot : f =O[ùìù[>] 0] (¬∑ ^ (-b))) (hs_bot : b < s.re) :
    MellinConvergent (fun t => log t ‚Ä¢ f t) s ‚àß
      HasDerivAt (mellin f) (mellin (fun t => log t ‚Ä¢ f t) s) s := by
  set F : ‚ÑÇ ‚Üí ‚Ñù ‚Üí E := fun (z : ‚ÑÇ) (t : ‚Ñù) => (t : ‚ÑÇ) ^ (z - 1) ‚Ä¢ f t
  set F' : ‚ÑÇ ‚Üí ‚Ñù ‚Üí E := fun (z : ‚ÑÇ) (t : ‚Ñù) => ((t : ‚ÑÇ) ^ (z - 1) * log t) ‚Ä¢ f t
  -- A convenient radius of ball within which we can uniformly bound the derivative.
  obtain ‚ü®v, hv0, hv1, hv2‚ü© : ‚àÉ v : ‚Ñù, 0 < v ‚àß v < s.re - b ‚àß v < a - s.re := by
    obtain ‚ü®w, hw1, hw2‚ü© := exists_between (sub_pos.mpr hs_top)
    obtain ‚ü®w', hw1', hw2'‚ü© := exists_between (sub_pos.mpr hs_bot)
    exact
      ‚ü®min w w', lt_min hw1 hw1', (min_le_right _ _).trans_lt hw2', (min_le_left _ _).trans_lt hw2‚ü©
  let bound : ‚Ñù ‚Üí ‚Ñù := fun t : ‚Ñù => (t ^ (s.re + v - 1) + t ^ (s.re - v - 1)) * |log t| * ‚Äñf t‚Äñ
  have h1 : ‚àÄ·∂† z : ‚ÑÇ in ùìù s, AEStronglyMeasurable (F z) (volume.restrict <| Ioi 0) := by
    refine' eventually_of_forall fun z => AEStronglyMeasurable.smul _ hfc.aestronglyMeasurable
    refine' ContinuousOn.aestronglyMeasurable _ measurableSet_Ioi
    refine' ContinuousAt.continuousOn fun t ht => _
    exact continuousAt_ofReal_cpow_const _ _ (Or.inr <| ne_of_gt ht)
  have h2 : IntegrableOn (F s) (Ioi (0 : ‚Ñù))
  ¬∑ exact mellinConvergent_of_isBigO_rpow hfc hf_top hs_top hf_bot hs_bot
  have h3 : AEStronglyMeasurable (F' s) (volume.restrict <| Ioi 0) := by
    apply LocallyIntegrableOn.aestronglyMeasurable
    refine' hfc.continuousOn_smul isOpen_Ioi ((ContinuousAt.continuousOn fun t ht => _).mul _)
    ¬∑ exact continuousAt_ofReal_cpow_const _ _ (Or.inr <| ne_of_gt ht)
    ¬∑ refine' continuous_ofReal.comp_continuousOn _
      exact continuousOn_log.mono (subset_compl_singleton_iff.mpr not_mem_Ioi_self)
  have h4 : ‚àÄ·µê t : ‚Ñù ‚àÇvolume.restrict (Ioi 0),
      ‚àÄ z : ‚ÑÇ, z ‚àà Metric.ball s v ‚Üí ‚ÄñF' z t‚Äñ ‚â§ bound t := by
    refine' (ae_restrict_mem measurableSet_Ioi).mono fun t ht z hz => _
    simp_rw [norm_smul, norm_mul, Complex.norm_eq_abs (log _), Complex.abs_ofReal,
      mul_assoc]
    gcongr
    rw [Complex.norm_eq_abs, abs_cpow_eq_rpow_re_of_pos ht]
    rcases le_or_lt 1 t with h | h
    ¬∑ refine' le_add_of_le_of_nonneg (rpow_le_rpow_of_exponent_le h _)
        (rpow_nonneg (zero_le_one.trans h) _)
      rw [sub_re, one_re, sub_le_sub_iff_right]
      rw [mem_ball_iff_norm, Complex.norm_eq_abs] at hz
      have hz' := (re_le_abs _).trans hz.le
      rwa [sub_re, sub_le_iff_le_add'] at hz'
    ¬∑ refine'
        le_add_of_nonneg_of_le (rpow_pos_of_pos ht _).le (rpow_le_rpow_of_exponent_ge ht h.le _)
      rw [sub_re, one_re, sub_le_iff_le_add, sub_add_cancel]
      rw [mem_ball_iff_norm', Complex.norm_eq_abs] at hz
      have hz' := (re_le_abs _).trans hz.le
      rwa [sub_re, sub_le_iff_le_add, ‚Üê sub_le_iff_le_add'] at hz'
  have h5 : IntegrableOn bound (Ioi 0) := by
    simp_rw [add_mul, mul_assoc]
    suffices ‚àÄ {j : ‚Ñù}, b < j ‚Üí j < a ‚Üí
        IntegrableOn (fun t : ‚Ñù => t ^ (j - 1) * (|log t| * ‚Äñf t‚Äñ)) (Ioi 0) volume by
      refine' Integrable.add (this _ _) (this _ _)
      all_goals linarith
    ¬∑ intro j hj hj'
      obtain ‚ü®w, hw1, hw2‚ü© := exists_between hj
      obtain ‚ü®w', hw1', hw2'‚ü© := exists_between hj'
      refine' mellin_convergent_of_isBigO_scalar _ _ hw1' _ hw2
      ¬∑ simp_rw [mul_comm]
        refine' hfc.norm.mul_continuousOn _ isOpen_Ioi
        refine' Continuous.comp_continuousOn _root_.continuous_abs (continuousOn_log.mono _)
        exact subset_compl_singleton_iff.mpr not_mem_Ioi_self
      ¬∑ refine (isBigO_rpow_top_log_smul hw2' hf_top).norm_left.congr_left fun t ‚Ü¶ ?_
        simp only [norm_smul, Real.norm_eq_abs]
      ¬∑ refine (isBigO_rpow_zero_log_smul hw1 hf_bot).norm_left.congr_left fun t ‚Ü¶ ?_
        simp only [norm_smul, Real.norm_eq_abs]
  have h6 : ‚àÄ·µê t : ‚Ñù ‚àÇvolume.restrict (Ioi 0),
      ‚àÄ y : ‚ÑÇ, y ‚àà Metric.ball s v ‚Üí HasDerivAt (fun z : ‚ÑÇ => F z t) (F' y t) y := by
    refine' (ae_restrict_mem measurableSet_Ioi).mono fun t ht y _ => _
    have ht' : (t : ‚ÑÇ) ‚â† 0 := ofReal_ne_zero.mpr (ne_of_gt ht)
    have u1 : HasDerivAt (fun z : ‚ÑÇ => (t : ‚ÑÇ) ^ (z - 1)) (t ^ (y - 1) * log t) y := by
      convert ((hasDerivAt_id' y).sub_const 1).const_cpow (Or.inl ht') using 1
      rw [ofReal_log (le_of_lt ht)]
      ring
    exact u1.smul_const (f t)
  have main := hasDerivAt_integral_of_dominated_loc_of_deriv_le hv0 h1 h2 h3 h4 h5 h6
  simpa only [mul_smul] using main
set_option linter.uppercaseLean3 false in
#align mellin_has_deriv_of_is_O_rpow mellin_hasDerivAt_of_isBigO_rpow

/-- Suppose `f` is locally integrable on `(0, ‚àû)`, is `O(x ^ (-a))` as `x ‚Üí ‚àû`, and is
`O(x ^ (-b))` as `x ‚Üí 0`. Then its Mellin transform is differentiable on the domain `b < re s < a`.
-/
theorem mellin_differentiableAt_of_isBigO_rpow [CompleteSpace E] [NormedSpace ‚ÑÇ E] {a b : ‚Ñù}
    {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ} (hfc : LocallyIntegrableOn f <| Ioi 0)
    (hf_top : f =O[atTop] (¬∑ ^ (-a))) (hs_top : s.re < a)
    (hf_bot : f =O[ùìù[>] 0] (¬∑ ^ (-b))) (hs_bot : b < s.re) :
    DifferentiableAt ‚ÑÇ (mellin f) s :=
  (mellin_hasDerivAt_of_isBigO_rpow hfc hf_top hs_top hf_bot hs_bot).2.differentiableAt
set_option linter.uppercaseLean3 false in
#align mellin_differentiable_at_of_is_O_rpow mellin_differentiableAt_of_isBigO_rpow

end MellinDiff

section ExpDecay

/-- If `f` is locally integrable, decays exponentially at infinity, and is `O(x ^ (-b))` at 0, then
its Mellin transform converges for `b < s.re`. -/
theorem mellinConvergent_of_isBigO_rpow_exp [NormedSpace ‚ÑÇ E] {a b : ‚Ñù} (ha : 0 < a) {f : ‚Ñù ‚Üí E}
    {s : ‚ÑÇ} (hfc : LocallyIntegrableOn f <| Ioi 0) (hf_top : f =O[atTop] fun t => exp (-a * t))
    (hf_bot : f =O[ùìù[>] 0] (¬∑ ^ (-b))) (hs_bot : b < s.re) : MellinConvergent f s :=
  mellinConvergent_of_isBigO_rpow hfc (hf_top.trans (isLittleO_exp_neg_mul_rpow_atTop ha _).isBigO)
    (lt_add_one _) hf_bot hs_bot
set_option linter.uppercaseLean3 false in
#align mellin_convergent_of_is_O_rpow_exp mellinConvergent_of_isBigO_rpow_exp

/-- If `f` is locally integrable, decays exponentially at infinity, and is `O(x ^ (-b))` at 0, then
its Mellin transform is holomorphic on `b < s.re`. -/
theorem mellin_differentiableAt_of_isBigO_rpow_exp [CompleteSpace E] [NormedSpace ‚ÑÇ E] {a b : ‚Ñù}
    (ha : 0 < a) {f : ‚Ñù ‚Üí E} {s : ‚ÑÇ} (hfc : LocallyIntegrableOn f <| Ioi 0)
    (hf_top : f =O[atTop] fun t => exp (-a * t)) (hf_bot : f =O[ùìù[>] 0] (¬∑ ^ (-b)))
    (hs_bot : b < s.re) : DifferentiableAt ‚ÑÇ (mellin f) s :=
  mellin_differentiableAt_of_isBigO_rpow hfc
    (hf_top.trans (isLittleO_exp_neg_mul_rpow_atTop ha _).isBigO) (lt_add_one _) hf_bot hs_bot
set_option linter.uppercaseLean3 false in
#align mellin_differentiable_at_of_is_O_rpow_exp mellin_differentiableAt_of_isBigO_rpow_exp

end ExpDecay

section MellinIoc

/-!
## Mellin transforms of functions on `Ioc 0 1`
-/

/-- The Mellin transform of the indicator function of `Ioc 0 1`. -/
theorem hasMellin_one_Ioc {s : ‚ÑÇ} (hs : 0 < re s) :
    HasMellin (indicator (Ioc 0 1) (fun _ => 1 : ‚Ñù ‚Üí ‚ÑÇ)) s (1 / s) := by
  have aux1 : -1 < (s - 1).re := by
    simpa only [sub_re, one_re, sub_eq_add_neg] using lt_add_of_pos_left _ hs
  have aux2 : s ‚â† 0 := by contrapose! hs; rw [hs, zero_re]
  have aux3 : MeasurableSet (Ioc (0 : ‚Ñù) 1) := measurableSet_Ioc
  simp_rw [HasMellin, mellin, MellinConvergent, ‚Üê indicator_smul, IntegrableOn,
    integrable_indicator_iff aux3, smul_eq_mul, integral_indicator aux3, mul_one, IntegrableOn,
    Measure.restrict_restrict_of_subset Ioc_subset_Ioi_self]
  rw [‚Üê IntegrableOn, ‚Üê intervalIntegrable_iff_integrableOn_Ioc_of_le zero_le_one]
  refine' ‚ü®intervalIntegral.intervalIntegrable_cpow' aux1, _‚ü©
  rw [‚Üê intervalIntegral.integral_of_le zero_le_one, integral_cpow (Or.inl aux1), sub_add_cancel,
    ofReal_zero, ofReal_one, one_cpow, zero_cpow aux2, sub_zero]
#align has_mellin_one_Ioc hasMellin_one_Ioc

/-- The Mellin transform of a power function restricted to `Ioc 0 1`. -/
theorem hasMellin_cpow_Ioc (a : ‚ÑÇ) {s : ‚ÑÇ} (hs : 0 < re s + re a) :
    HasMellin (indicator (Ioc 0 1) (fun t => ‚Üët ^ a : ‚Ñù ‚Üí ‚ÑÇ)) s (1 / (s + a)) := by
  have := hasMellin_one_Ioc (by rwa [add_re] : 0 < (s + a).re)
  simp_rw [HasMellin, ‚Üê MellinConvergent.cpow_smul, ‚Üê mellin_cpow_smul, ‚Üê indicator_smul,
    smul_eq_mul, mul_one] at this
  exact this
#align has_mellin_cpow_Ioc hasMellin_cpow_Ioc

end MellinIoc
