/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# Hofer's lemma

This is an elementary lemma about complete metric spaces. It is motivated by an
application to the bubbling-off analysis for holomorphic curves in symplectic topology.
We are *very* far away from having these applications, but the proof here is a nice
example of a proof needing to construct a sequence by induction in the middle of the proof.

## References:

* H. Hofer and C. Viterbo, *The Weinstein conjecture in the presence of holomorphic spheres*
-/

open Topology Filter Finset

local notation "d" => dist

theorem hofer {X : Type*} [MetricSpace X] [CompleteSpace X] (x : X) (Îµ : â„) (Îµ_pos : 0 < Îµ)
    {Ï• : X â†’ â„} (cont : Continuous Ï•) (nonneg : âˆ€ y, 0 â‰¤ Ï• y) : âˆƒ Îµ' > 0, âˆƒ x' : X,
    Îµ' â‰¤ Îµ âˆ§ d x' x â‰¤ 2 * Îµ âˆ§ Îµ * Ï• x â‰¤ Îµ' * Ï• x' âˆ§ âˆ€ y, d x' y â‰¤ Îµ' â†’ Ï• y â‰¤ 2 * Ï• x' := by
  by_contra H
  have reformulation : âˆ€ (x') (k : â„•), Îµ * Ï• x â‰¤ Îµ / 2 ^ k * Ï• x' â†” 2 ^ k * Ï• x â‰¤ Ï• x' := by
    intro x' k
    rw [div_mul_eq_mul_div, le_div_iffâ‚€, mul_assoc, mul_le_mul_left Îµ_pos, mul_comm]
    positivity
  -- Now let's specialize to `Îµ/2^k`
  replace H : âˆ€ k : â„•, âˆ€ x', d x' x â‰¤ 2 * Îµ âˆ§ 2 ^ k * Ï• x â‰¤ Ï• x' â†’
      âˆƒ y, d x' y â‰¤ Îµ / 2 ^ k âˆ§ 2 * Ï• x' < Ï• y := by
    intro k x'
    push_neg at H
    have := H (Îµ / 2 ^ k) (by positivity) x' (div_le_self Îµ_pos.le <| one_le_powâ‚€ one_le_two)
    simpa [reformulation] using this
  haveI : Nonempty X := âŸ¨xâŸ©
  choose! F hF using H
  -- Use the axiom of choice
  -- Now define u by induction starting at x, with u_{n+1} = F(n, u_n)
  let u : â„• â†’ X := fun n => Nat.recOn n x F
  -- The properties of F translate to properties of u
  have hu :
    âˆ€ n,
      d (u n) x â‰¤ 2 * Îµ âˆ§ 2 ^ n * Ï• x â‰¤ Ï• (u n) â†’
        d (u n) (u <| n + 1) â‰¤ Îµ / 2 ^ n âˆ§ 2 * Ï• (u n) < Ï• (u <| n + 1) := by
    exact fun n â†¦ hF n (u n)
  -- Key properties of u, to be proven by induction
  have key : âˆ€ n, d (u n) (u (n + 1)) â‰¤ Îµ / 2 ^ n âˆ§ 2 * Ï• (u n) < Ï• (u (n + 1)) := by
    intro n
    induction n using Nat.case_strong_induction_on with
    | hz => simpa [u, Îµ_pos.le] using hu 0
    | hi n IH =>
      have A : d (u (n + 1)) x â‰¤ 2 * Îµ := by
        rw [dist_comm]
        let r := range (n + 1) -- range (n+1) = {0, ..., n}
        calc
          d (u 0) (u (n + 1)) â‰¤ âˆ‘ i âˆˆ r, d (u i) (u <| i + 1) := dist_le_range_sum_dist u (n + 1)
          _ â‰¤ âˆ‘ i âˆˆ r, Îµ / 2 ^ i :=
            (sum_le_sum fun i i_in => (IH i <| Nat.lt_succ_iff.mp <| Finset.mem_range.mp i_in).1)
          _ = (âˆ‘ i âˆˆ r, (1 / 2 : â„) ^ i) * Îµ := by
            rw [Finset.sum_mul]
            simp
            field_simp
          _ â‰¤ 2 * Îµ := by gcongr; apply sum_geometric_two_le
      have B : 2 ^ (n + 1) * Ï• x â‰¤ Ï• (u (n + 1)) := by
        refine @geom_le (Ï• âˆ˜ u) _ zero_le_two (n + 1) fun m hm => ?_
        exact (IH _ <| Nat.lt_add_one_iff.1 hm).2.le
      exact hu (n + 1) âŸ¨A, BâŸ©
  obtain âŸ¨keyâ‚, keyâ‚‚âŸ© := forall_and.mp key
  -- Hence u is Cauchy
  have cauchy_u : CauchySeq u := by
    refine cauchySeq_of_le_geometric _ Îµ one_half_lt_one fun n => ?_
    simpa only [one_div, inv_pow] using keyâ‚ n
  -- So u converges to some y
  obtain âŸ¨y, limyâŸ© : âˆƒ y, Tendsto u atTop (ğ“ y) := CompleteSpace.complete cauchy_u
  -- And Ï• âˆ˜ u goes to +âˆ
  have lim_top : Tendsto (Ï• âˆ˜ u) atTop atTop := by
    let v n := (Ï• âˆ˜ u) (n + 1)
    suffices Tendsto v atTop atTop by rwa [tendsto_add_atTop_iff_nat] at this
    have hvâ‚€ : 0 < v 0 := by
      calc
        0 â‰¤ 2 * Ï• (u 0) := by specialize nonneg x; positivity
        _ < Ï• (u (0 + 1)) := keyâ‚‚ 0
    apply tendsto_atTop_of_geom_le hvâ‚€ one_lt_two
    exact fun n => (keyâ‚‚ (n + 1)).le
  -- But Ï• âˆ˜ u also needs to go to Ï•(y)
  have lim : Tendsto (Ï• âˆ˜ u) atTop (ğ“ (Ï• y)) := Tendsto.comp cont.continuousAt limy
  -- So we have our contradiction!
  exact not_tendsto_atTop_of_tendsto_nhds lim lim_top
