/-
Copyright (c) 2020 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# Hofer's lemma

This is an elementary lemma about complete metric spaces. It is motivated by an
application to the bubbling-off analysis for holomorphic curves in symplectic topology.
We are *very* far away from having these applications, but the proof here is a nice
example of a proof needing to construct a sequence by induction in the middle of the proof.

## References:

* H. Hofer and C. Viterbo, *The Weinstein conjecture in the presence of holomorphic spheres*
-/

open Topology Filter Finset

local notation "d" => dist

theorem hofer {X : Type*} [MetricSpace X] [CompleteSpace X] (x : X) (Œµ : ‚Ñù) (Œµ_pos : 0 < Œµ)
    {œï : X ‚Üí ‚Ñù} (cont : Continuous œï) (nonneg : ‚àÄ y, 0 ‚â§ œï y) : ‚àÉ Œµ' > 0, ‚àÉ x' : X,
    Œµ' ‚â§ Œµ ‚àß d x' x ‚â§ 2 * Œµ ‚àß Œµ * œï x ‚â§ Œµ' * œï x' ‚àß ‚àÄ y, d x' y ‚â§ Œµ' ‚Üí œï y ‚â§ 2 * œï x' := by
  by_contra H
  have reformulation : ‚àÄ (x') (k : ‚Ñï), Œµ * œï x ‚â§ Œµ / 2 ^ k * œï x' ‚Üî 2 ^ k * œï x ‚â§ œï x' := by
    intro x' k
    rw [div_mul_eq_mul_div, le_div_iff‚ÇÄ, mul_assoc, mul_le_mul_left Œµ_pos, mul_comm]
    positivity
  -- Now let's specialize to `Œµ/2^k`
  replace H : ‚àÄ k : ‚Ñï, ‚àÄ x', d x' x ‚â§ 2 * Œµ ‚àß 2 ^ k * œï x ‚â§ œï x' ‚Üí
      ‚àÉ y, d x' y ‚â§ Œµ / 2 ^ k ‚àß 2 * œï x' < œï y := by
    intro k x'
    push_neg at H
    have := H (Œµ / 2 ^ k) (by positivity) x' (div_le_self Œµ_pos.le <| one_le_pow‚ÇÄ one_le_two)
    simpa [reformulation] using this
  haveI : Nonempty X := ‚ü®x‚ü©
  choose! F hF using H
  -- Use the axiom of choice
  -- Now define u by induction starting at x, with u_{n+1} = F(n, u_n)
  let u : ‚Ñï ‚Üí X := fun n ‚Ü¶ Nat.recOn n x F
  -- The properties of F translate to properties of u
  have hu :
    ‚àÄ n,
      d (u n) x ‚â§ 2 * Œµ ‚àß 2 ^ n * œï x ‚â§ œï (u n) ‚Üí
        d (u n) (u <| n + 1) ‚â§ Œµ / 2 ^ n ‚àß 2 * œï (u n) < œï (u <| n + 1) := by
    exact fun n ‚Ü¶ hF n (u n)
  -- Key properties of u, to be proven by induction
  have key : ‚àÄ n, d (u n) (u (n + 1)) ‚â§ Œµ / 2 ^ n ‚àß 2 * œï (u n) < œï (u (n + 1)) := by
    intro n
    induction n using Nat.case_strong_induction_on with
    | hz => simpa [u, Œµ_pos.le] using hu 0
    | hi n IH =>
      have A : d (u (n + 1)) x ‚â§ 2 * Œµ := by
        rw [dist_comm]
        let r := range (n + 1) -- range (n+1) = {0, ..., n}
        calc
          d (u 0) (u (n + 1)) ‚â§ ‚àë i ‚àà r, d (u i) (u <| i + 1) := dist_le_range_sum_dist u (n + 1)
          _ ‚â§ ‚àë i ‚àà r, Œµ / 2 ^ i :=
            (sum_le_sum fun i i_in => (IH i <| Nat.lt_succ_iff.mp <| Finset.mem_range.mp i_in).1)
          _ = (‚àë i ‚àà r, (1 / 2 : ‚Ñù) ^ i) * Œµ := by
            rw [Finset.sum_mul]
            field_simp
          _ ‚â§ 2 * Œµ := by gcongr; apply sum_geometric_two_le
      have B : 2 ^ (n + 1) * œï x ‚â§ œï (u (n + 1)) := by
        refine @geom_le (œï ‚àò u) _ zero_le_two (n + 1) fun m hm => ?_
        exact (IH _ <| Nat.lt_add_one_iff.1 hm).2.le
      exact hu (n + 1) ‚ü®A, B‚ü©
  cases' forall_and.mp key with key‚ÇÅ key‚ÇÇ
  -- Hence u is Cauchy
  have cauchy_u : CauchySeq u := by
    refine cauchySeq_of_le_geometric _ Œµ one_half_lt_one fun n ‚Ü¶ ?_
    simpa only [one_div, inv_pow] using key‚ÇÅ n
  -- So u converges to some y
  obtain ‚ü®y, limy‚ü© : ‚àÉ y, Tendsto u atTop (ùìù y) := CompleteSpace.complete cauchy_u
  -- And œï ‚àò u goes to +‚àû
  have lim_top : Tendsto (œï ‚àò u) atTop atTop := by
    let v n := (œï ‚àò u) (n + 1)
    suffices Tendsto v atTop atTop by rwa [tendsto_add_atTop_iff_nat] at this
    have hv‚ÇÄ : 0 < v 0 := by
      calc
        0 ‚â§ 2 * œï (u 0) := by specialize nonneg x; positivity
        _ < œï (u (0 + 1)) := key‚ÇÇ 0
    apply tendsto_atTop_of_geom_le hv‚ÇÄ one_lt_two
    exact fun n ‚Ü¶ (key‚ÇÇ (n + 1)).le
  -- But œï ‚àò u also needs to go to œï(y)
  have lim : Tendsto (œï ‚àò u) atTop (ùìù (œï y)) := Tendsto.comp cont.continuousAt limy
  -- So we have our contradiction!
  exact not_tendsto_atTop_of_tendsto_nhds lim lim_top
