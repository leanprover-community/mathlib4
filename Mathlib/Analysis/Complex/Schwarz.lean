/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Complex.AbsMax
public import Mathlib.Analysis.Complex.RemovableSingularity

/-!
# Schwarz lemma

In this file we prove several versions of the Schwarz lemma.

* `Complex.norm_deriv_le_div_of_mapsTo_ball`. Let `f : ‚ÑÇ ‚Üí E` be a complex analytic function
  on an open disk with center `c` and a positive radius `R‚ÇÅ`.
  If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
  then the norm of the derivative of `f` at `c` is at most the ratio `R‚ÇÇ / R‚ÇÅ`.

* `Complex.dist_le_div_mul_dist_of_mapsTo_ball`. Let `f : E ‚Üí F` be a complex analytic function
  on an open ball with center `c` and radius `R‚ÇÅ`.
  If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
  then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ (R‚ÇÇ / R‚ÇÅ) * dist z c`.

* `Complex.norm_deriv_le_one_of_mapsTo_ball`. If `f : ‚ÑÇ ‚Üí E` is complex analytic
  on an open disk with center `c` and a positive radius `R‚ÇÅ`,
  and it sends this disk to a closed ball with center `f c` and radius the same radius,
  then the norm of the derivative of `f` at the center of this disk is at most `1`.

* `Complex.dist_le_dist_of_mapsTo_ball`. Let `f : E ‚Üí F` be a complex analytic function
  on an open ball with center `c`.
  If `f` sends this ball to a closed ball with center `f c` and the same radius,
  then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ dist z c`.

* `Complex.norm_le_norm_of_mapsTo_ball`:
  Let `f : E ‚Üí F` be a complex analytic on an open ball with center at the origin.
  If `f` sends this ball to the closed ball with center `0` of the same radius and `f 0 = 0`,
  then for any point `z` of this disk we have `‚Äñf z‚Äñ ‚â§ ‚Äñz‚Äñ`.

## Implementation notes

Traditionally, the Schwarz lemma is formulated for maps `f : ‚ÑÇ ‚Üí ‚ÑÇ`.
We generalize all versions of the lemma to the case of maps to any normed space.
For the versions that don't use `deriv` or `dslope`,
we state it for maps between any two normed spaces.

## TODO

* Prove that any diffeomorphism of the unit disk to itself is a M√∂bius map.

## Tags

Schwarz lemma
-/

open Metric Set Function Filter TopologicalSpace

open scoped Topology ComplexConjugate

namespace Complex

/-- An auxiliary lemma for `Complex.dist_le_mul_div_pow_of_mapsTo_ball_of_isLittleO`. -/
theorem schwarz_aux {f : ‚ÑÇ ‚Üí ‚ÑÇ} {c z : ‚ÑÇ} {R‚ÇÅ R‚ÇÇ : ‚Ñù} {n : ‚Ñï}
    (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ)) (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ))
    (hn : (f ¬∑ - f c) =o[ùìù c] (fun w ‚Ü¶ (w - c) ^ n)) (hz : z ‚àà ball c R‚ÇÅ) :
    ‚Äñf z - f c‚Äñ ‚â§ R‚ÇÇ * (‚Äñz - c‚Äñ / R‚ÇÅ) ^ (n + 1) := by
  -- By slightly reducing `R‚ÇÅ`, we can assume that `f` is differentiable on `closedBall c R‚ÇÅ`
  -- and it maps this ball to the closed ball in the codomain.
  have hR‚ÇÅ : 0 < R‚ÇÅ := nonempty_ball.1 ‚ü®z, hz‚ü©
  wlog hd' : DifferentiableOn ‚ÑÇ f (closedBall c R‚ÇÅ) ‚àß
    MapsTo f (closedBall c R‚ÇÅ) (closedBall (f c) R‚ÇÇ) generalizing R‚ÇÅ
  ¬∑ suffices ‚àÄ·∂† r in ùìù[<] R‚ÇÅ, ‚Äñf z - f c‚Äñ ‚â§ R‚ÇÇ * (‚Äñz - c‚Äñ / r) ^ (n + 1) by
      refine ge_of_tendsto ?_ this
      refine ContinuousAt.continuousWithinAt ?_
      fun_prop (disch := positivity)
    rw [mem_ball_iff_norm] at hz
    filter_upwards [Ioo_mem_nhdsLT hz] with r ‚ü®hzr, hrR‚ÇÅ‚ü©
    apply this
    ¬∑ exact hd.mono <| by gcongr
    ¬∑ exact h_maps.mono_left <| by gcongr
    ¬∑ rwa [mem_ball_iff_norm]
    ¬∑ exact (norm_nonneg _).trans_lt hzr
    ¬∑ exact ‚ü®hd.mono <| closedBall_subset_ball hrR‚ÇÅ, h_maps.mono_left <|
        closedBall_subset_ball hrR‚ÇÅ‚ü©
  -- Cleanup, discard the case `z = c`.
  clear hd h_maps
  rcases hd' with ‚ü®hd, h_maps‚ü©
  rcases eq_or_ne z c with rfl | hne
  ¬∑ simp
  -- Consider the function given by `g w := ((w - c) ^ (n + 1))‚Åª¬π * (f w - f c)`.
  -- It is differentiable away from `c` and satisfies `g w = o((w - c)‚Åª¬π)`,
  -- thus it can be extended to a function g'` differentiable on the whole closed ball
  -- with center c` and radius `R‚ÇÅ`.
  set g : ‚ÑÇ ‚Üí ‚ÑÇ := fun w ‚Ü¶ ((w - c) ^ (n + 1))‚Åª¬π * (f w - f c)
  set g' := update g c (limUnder (ùìù[‚â†] c) g)
  have hdg' : DifferentiableOn ‚ÑÇ g' (closedBall c R‚ÇÅ) := by
    refine .mono ?_ (subset_insert_diff_singleton c _)
    apply differentiableOn_update_limUnder_insert_of_isLittleO
    ¬∑ exact diff_mem_nhdsWithin_compl (closedBall_mem_nhds _ hR‚ÇÅ) _
    ¬∑ refine .mul ?_ (hd.mono diff_subset |>.sub_const _)
      fun_prop (disch := simp +contextual [sub_eq_zero])
    ¬∑ refine Asymptotics.isBigO_refl (fun w ‚Ü¶ ((w - c) ^ (n + 1))‚Åª¬π) _ |>.mul_isLittleO hn
        |>.mono (nhdsWithin_le_nhds (s := {c}·∂ú)) |>.congr' ?_ ?_
      ¬∑ simp [g]
      ¬∑ refine eventually_mem_nhdsWithin.mono fun w hw ‚Ü¶ ?_
        rw [mem_compl_singleton_iff, ‚Üê sub_ne_zero] at hw
        simp [pow_succ, field]
  -- Finally, we apply the maximum modulus principle to this function.
  -- On the sphere `dist w c = R‚ÇÅ`, its norm is bounded by `R‚ÇÇ / R‚ÇÅ ^ (n + 1)`,
  -- thus it's bounded by the same constant on the whole closed ball,
  -- in particular, at `w = z`.
  suffices ‚Äñg' z‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ ^ (n + 1) by
    have hz' : ‚Äñz - c‚Äñ ‚â† 0 := by simpa [sub_eq_zero] using hne
    simpa [g', hne, g, div_pow, mul_comm, field] using this
  refine norm_le_of_forall_mem_frontier_norm_le isBounded_ball (hdg'.diffContOnCl_ball subset_rfl)
    ?_ ?_
  ¬∑ grw [frontier_ball_subset_sphere]
    intro w hw
    have hwc := ne_of_mem_sphere hw hR‚ÇÅ.ne'
    have hfw : ‚Äñf w - f c‚Äñ ‚â§ R‚ÇÇ := by simpa using h_maps (sphere_subset_closedBall hw)
    rw [mem_sphere_iff_norm] at hw
    simpa [g', hwc, g, hw, field]
  ¬∑ exact subset_closure hz

public section

section NormedSpace

variable {E F : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [NormedAddCommGroup F] [NormedSpace ‚ÑÇ F]
  {R R‚ÇÅ R‚ÇÇ : ‚Ñù} {f : E ‚Üí F} {c z : E}

open AffineMap in
/-- Let `f : E ‚Üí F` be a complex analytic map
sending an open ball of radius `R‚ÇÅ` to a closed ball of radius `R‚ÇÇ`.
If `f w - f c = o(‚Äñw - c‚Äñ ^ n)`, then for any `z` in the ball in the domain,
we have `dist (f z) (f c) ‚â§ R‚ÇÇ * (dist z c / R‚ÇÅ) ^ (n + 1)`.

For `n = 0`, this theorem gives a usual Schwarz lemma,
see `dist_le_div_mul_dist_of_mapsTo_ball` below.
-/
theorem dist_le_mul_div_pow_of_mapsTo_ball_of_isLittleO {f : E ‚Üí F} {c z : E} {R‚ÇÅ R‚ÇÇ : ‚Ñù} {n : ‚Ñï}
    (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ)) (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ))
    (hn : (f ¬∑ - f c) =o[ùìù c] (fun w ‚Ü¶ ‚Äñw - c‚Äñ ^ n)) (hz : z ‚àà ball c R‚ÇÅ) :
    dist (f z) (f c) ‚â§ R‚ÇÇ * (dist z c / R‚ÇÅ) ^ (n + 1) := by
  -- Note that `0 < R‚ÇÅ`, `0 ‚â§ R‚ÇÇ`, then discard the trivial case `f z = f c`.
  have hR‚ÇÅ : 0 < R‚ÇÅ := nonempty_ball.mp ‚ü®_, hz‚ü©
  have hR‚ÇÇ : 0 ‚â§ R‚ÇÇ := nonempty_closedBall.mp ‚ü®_, h_maps hz‚ü©
  rcases eq_or_ne (f z) (f c) with heq | hfne
  ¬∑ trans 0 <;> [simp [heq]; positivity]
  have hne : z ‚â† c := ne_of_apply_ne _ hfne
  -- Let `g : F ‚Üí ‚ÑÇ` be a continuous linear function such that `‚Äñg‚Äñ = 1`
  -- and `‚Äñg (f z - f c)‚Äñ = ‚Äñf z - f c‚Äñ`.
  rcases exists_dual_vector ‚ÑÇ _ (sub_ne_zero.mpr hfne) with ‚ü®g, hg, hgf‚ü©
  -- Consider `h : ‚ÑÇ ‚Üí ‚ÑÇ` given by `h w = g (f (c + w * (z - c)))`.
  set h : ‚ÑÇ ‚Üí ‚ÑÇ := g ‚àò f ‚àò lineMap c z
  -- This map is differentiable on the ball with center at the origin and radius `R‚ÇÅ / dist z c`
  -- and it sends this ball to the closed ball with center `h 0 = f c` and radius R‚ÇÇ`.
  have hmaps_line : MapsTo (lineMap c z) (ball (0 : ‚ÑÇ) (R‚ÇÅ / dist z c)) (ball c R‚ÇÅ) := by
    intro w hw
    simpa [lt_div_iff‚ÇÄ, hne, dist_comm c] using hw
  have hmaps : MapsTo h (ball 0 (R‚ÇÅ / dist z c)) (closedBall (h 0) R‚ÇÇ) := by
    refine MapsTo.comp ?_ (h_maps.comp hmaps_line)
    simpa [hg, h] using g.lipschitz.mapsTo_closedBall (f c) R‚ÇÇ
  have hdiff : DifferentiableOn ‚ÑÇ h (ball 0 (R‚ÇÅ / dist z c)) :=
    g.differentiable.comp_differentiableOn <| hd.comp (lineMap c z).differentiableOn hmaps_line
  -- This map also satisfies `h(w) - h(0) = o(w ^ n)`, thus we can apply the auxiliary lemma above.
  have hn : (h ¬∑ - h 0) =o[ùìù 0] (fun w ‚Ü¶ (w - 0) ^ n) := by
    simp only [h, ‚Üê map_sub, Function.comp_apply, sub_zero]
    refine (g.isBigO_comp _ _).trans_isLittleO ?_
    rw [‚Üê lineMap_apply_zero (k := ‚ÑÇ) c z] at hn
    refine hn.comp_tendsto ?_ |>.trans_isBigO ?_
    ¬∑ exact Continuous.tendsto (by fun_prop) 0
    ¬∑ simpa [Function.comp_def, ‚Üê dist_eq_norm_sub, mul_pow, mul_comm]
        using (Asymptotics.isBigO_refl (¬∑ ^ n) (ùìù (0 : ‚ÑÇ))).norm_left.const_mul_left _
  have hmem : 1 ‚àà ball (0 : ‚ÑÇ) (R‚ÇÅ / dist z c) := by
    simpa [lt_div_iff‚ÇÄ, hne]
  rw [map_sub] at hgf
  simpa [hgf, h, dist_eq_norm_sub] using schwarz_aux hdiff hmaps hn hmem

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c` and radius `R‚ÇÅ`.
If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ (R‚ÇÇ / R‚ÇÅ) * dist z c`.
-/
theorem dist_le_div_mul_dist_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (hz : z ‚àà ball c R‚ÇÅ) :
    dist (f z) (f c) ‚â§ R‚ÇÇ / R‚ÇÅ * dist z c := by
  refine dist_le_mul_div_pow_of_mapsTo_ball_of_isLittleO (n := 0) hd h_maps ?_ hz |>.trans_eq ?_
  ¬∑ simpa using hd.continuousOn.continuousAt
      (ball_mem_nhds _ <| nonempty_ball.mp ‚ü®_, hz‚ü©) |>.sub_const (f c)
  ¬∑ simp [field]

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c` and positive radius `R‚ÇÅ`.
If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
then the norm of the Fr√©chet derivative of `f` at `c` is at most `R‚ÇÇ / R‚ÇÅ`.
-/
theorem norm_fderiv_le_div_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (h‚ÇÄ : 0 < R‚ÇÅ) :
    ‚Äñfderiv ‚ÑÇ f c‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  have : 0 ‚â§ R‚ÇÇ := nonempty_closedBall.mp <| h_maps.nonempty <| nonempty_ball.mpr h‚ÇÄ
  refine norm_fderiv_le_of_lip' _ (by positivity) ?_
  filter_upwards [ball_mem_nhds _ h‚ÇÄ] with z hz
  simpa [dist_eq_norm_sub] using dist_le_div_mul_dist_of_mapsTo_ball hd h_maps hz

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c`.
If `f` sends this ball to a closed ball with center `f c` and the same radius,
then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ dist z c`.
-/
theorem dist_le_dist_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R))
    (h_maps : MapsTo f (ball c R) (closedBall (f c) R)) (hz : z ‚àà ball c R) :
    dist (f z) (f c) ‚â§ dist z c := by
  simpa [(nonempty_ball.1 ‚ü®z, hz‚ü©).ne'] using dist_le_div_mul_dist_of_mapsTo_ball hd h_maps hz

@[deprecated (since := "2026-01-03")]
alias dist_le_dist_of_mapsTo_ball_self := dist_le_dist_of_mapsTo_ball

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c` and a positive radius.
If `f` sends this ball to a closed ball with center `f c` and the same radius,
then the norm of the Fr√©chet derivative of `f` at `c` is at most one.
-/
theorem norm_fderiv_le_one_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R))
    (h_maps : MapsTo f (ball c R) (closedBall (f c) R)) (hR : 0 < R) :
    ‚Äñfderiv ‚ÑÇ f c‚Äñ ‚â§ 1 := by
  simpa [hR.ne'] using norm_fderiv_le_div_of_mapsTo_ball hd h_maps hR

/-- The **Schwarz Lemma**.
Let `f : E ‚Üí F` be a complex analytic on an open ball with center at the origin.
If `f` sends this ball to the closed ball with center `0` of the same radius and `f 0 = 0`,
then for any point `z` of this disk we have `‚Äñf z‚Äñ ‚â§ ‚Äñz‚Äñ`. -/
theorem norm_le_norm_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball 0 R))
    (h_maps : MapsTo f (ball 0 R) (closedBall 0 R)) (h‚ÇÄ : f 0 = 0) (hz : ‚Äñz‚Äñ < R) :
    ‚Äñf z‚Äñ ‚â§ ‚Äñz‚Äñ := by
  simpa [h‚ÇÄ] using dist_le_dist_of_mapsTo_ball hd (by rwa [h‚ÇÄ]) (mem_ball_zero_iff.mpr hz)

@[deprecated (since := "2026-01-03")]
alias norm_le_norm_of_mapsTo_ball_self := norm_le_norm_of_mapsTo_ball

end NormedSpace

section DimOne

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {R R‚ÇÅ R‚ÇÇ : ‚Ñù} {f : ‚ÑÇ ‚Üí E}
  {c z z‚ÇÄ : ‚ÑÇ}

/-- The **Schwarz Lemma**: if `f : ‚ÑÇ ‚Üí E` is complex analytic
on an open disk with center `c` and a positive radius `R‚ÇÅ`,
and it sends this disk to a closed ball with center `f c` and radius `R‚ÇÇ`,
then the norm of the derivative of `f` at `c` is at most the ratio `R‚ÇÇ / R‚ÇÅ`. -/
theorem norm_deriv_le_div_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (h‚ÇÄ : 0 < R‚ÇÅ) :
    ‚Äñderiv f c‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  rw [norm_deriv_eq_norm_fderiv]
  exact norm_fderiv_le_div_of_mapsTo_ball hd h_maps h‚ÇÄ

/-- The **Schwarz Lemma**: if `f : ‚ÑÇ ‚Üí E` is complex analytic
on an open disk with center `c` and a positive radius `R‚ÇÅ`,
and it sends this disk to a closed ball with center `f c` and radius the same radius,
then the norm of the derivative of `f` at the center of this disk is at most `1`.
-/
theorem norm_deriv_le_one_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R))
    (h_maps : MapsTo f (ball c R) (closedBall (f c) R)) (h‚ÇÄ : 0 < R) : ‚Äñderiv f c‚Äñ ‚â§ 1 :=
  (norm_deriv_le_div_of_mapsTo_ball hd h_maps h‚ÇÄ).trans_eq (div_self h‚ÇÄ.ne')

/-- Two cases of the **Schwarz Lemma** (derivative and distance), merged together.

If `f : ‚ÑÇ ‚Üí E` is a complex analytic function on an open ball `ball c R‚ÇÅ`
hat sends it to a closed ball `closedBall (f c) R‚ÇÇ`, then the norm of `dslope f c z`,
which is defined as `(z - c)‚Åª¬π ‚Ä¢ (f z - f c)` for `z ‚â† c` and as `deriv f c` for `z = c`,
is not greater than the ratio `R‚ÇÇ / R‚ÇÅ`.
-/
theorem norm_dslope_le_div_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (hz : z ‚àà ball c R‚ÇÅ) :
    ‚Äñdslope f c z‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  rcases eq_or_ne z c with rfl | hne
  ¬∑ simpa using norm_deriv_le_div_of_mapsTo_ball hd h_maps (by simpa using hz)
  ¬∑ rw [dslope_of_ne _ hne, slope_def_module, norm_smul, norm_inv, ‚Üê dist_eq_norm_sub,
      ‚Üê dist_eq_norm_sub, ‚Üê div_eq_inv_mul, div_le_iff‚ÇÄ]
    ¬∑ exact dist_le_div_mul_dist_of_mapsTo_ball hd h_maps hz
    ¬∑ simpa

/-- Equality case in the **Schwarz Lemma**: in the setup of `norm_dslope_le_div_of_mapsTo_ball`,
if `‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ` holds at a point in the ball
then the map `f` is affine with slope `dslope f c z‚ÇÄ`.

Note that this lemma requires the codomain to be a strictly convex space.
Indeed, for `E = ‚ÑÇ √ó ‚ÑÇ` there is a counterexample:
the map `f := fun z ‚Ü¶ (z, z ^ 2)` sends `ball 0 1` to `closedBall 0 1`,
`‚Äñdslope f 0 0‚Äñ = ‚Äñderiv f 0‚Äñ = ‚Äñ(1, 0)‚Äñ = 1`, but the map is not an affine map.
-/
theorem affine_of_mapsTo_ball_of_norm_dslope_eq_div [StrictConvexSpace ‚Ñù E]
    (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : Set.MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ))
    (h_z‚ÇÄ : z‚ÇÄ ‚àà ball c R‚ÇÅ) (h_eq : ‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ) :
    Set.EqOn f (fun z => f c + (z - c) ‚Ä¢ dslope f c z‚ÇÄ) (ball c R‚ÇÅ) := by
  set e : E ‚ÜíL[‚ÑÇ] UniformSpace.Completion E := UniformSpace.Completion.toComplL
  set g := dslope (e ‚àò f) c
  rintro z hz
  have h_R‚ÇÅ : 0 < R‚ÇÅ := nonempty_ball.mp ‚ü®_, h_z‚ÇÄ‚ü©
  have hg' : g = e ‚àò dslope f c := by
    ext w
    simp only [g]
    rw [e.dslope_comp, Function.comp_apply]
    rintro rfl
    exact hd.differentiableAt <| ball_mem_nhds _ h_R‚ÇÅ
  have g_le_div : ‚àÄ z ‚àà ball c R‚ÇÅ, ‚Äñg z‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := fun z hz =>
    norm_dslope_le_div_of_mapsTo_ball (e.differentiable.comp_differentiableOn hd)
      (fun w hw ‚Ü¶ by simpa [e] using h_maps hw) hz
  have g_max : IsMaxOn (norm ‚àò g) (ball c R‚ÇÅ) z‚ÇÄ :=
    isMaxOn_iff.mpr fun z hz => by simpa [h_eq, hg', e] using g_le_div z hz
  have g_diff : DifferentiableOn ‚ÑÇ g (ball c R‚ÇÅ) :=
    (differentiableOn_dslope (isOpen_ball.mem_nhds (mem_ball_self h_R‚ÇÅ))).mpr
      (e.differentiable.comp_differentiableOn hd)
  have heq : ‚Äñdslope f c z‚Äñ = ‚Äñdslope f c z‚ÇÄ‚Äñ := by
    simpa [hg', e] using norm_eqOn_of_isPreconnected_of_isMaxOn (convex_ball c R‚ÇÅ).isPreconnected
      isOpen_ball g_diff h_z‚ÇÄ g_max hz
  have heq_add : ‚Äñdslope f c z + dslope f c z‚ÇÄ‚Äñ = ‚Äñdslope f c z‚ÇÄ + dslope f c z‚ÇÄ‚Äñ := by
    simpa [hg', e, ‚Üê UniformSpace.Completion.coe_add]
      using norm_eqOn_of_isPreconnected_of_isMaxOn (convex_ball c R‚ÇÅ).isPreconnected
        isOpen_ball (g_diff.add_const (g z‚ÇÄ)) h_z‚ÇÄ g_max.norm_add_self hz
  have : dslope f c z = dslope f c z‚ÇÄ := eq_of_norm_eq_of_norm_add_eq heq <| by
    simp only [heq, SameRay.rfl.norm_add, heq_add]
  simp [‚Üê this]

@[deprecated (since := "2026-01-03")]
alias affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div :=
  affine_of_mapsTo_ball_of_norm_dslope_eq_div

/-- Equality case in the **Schwarz Lemma**: in the setup of `norm_dslope_le_div_of_mapsTo_ball`,
if there exists a point `z‚ÇÄ` in the ball such that `‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ`,
then the map `f` is affine with the absolute value of the slope equal to `R‚ÇÇ / R‚ÇÅ`.

This is an existence version of `affine_of_mapsTo_ball_of_norm_dslope_eq_div` above.

TODO: once the deprecated alias `affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div` is gone,
rename this theorem to `affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div`.
-/
theorem affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div'
    [StrictConvexSpace ‚Ñù E] (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : Set.MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ))
    (h_z‚ÇÄ : ‚àÉ z‚ÇÄ ‚àà ball c R‚ÇÅ, ‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ) :
    ‚àÉ C : E, ‚ÄñC‚Äñ = R‚ÇÇ / R‚ÇÅ ‚àß Set.EqOn f (fun z => f c + (z - c) ‚Ä¢ C) (ball c R‚ÇÅ) :=
  let ‚ü®z‚ÇÄ, h_z‚ÇÄ, h_eq‚ü© := h_z‚ÇÄ
  ‚ü®dslope f c z‚ÇÄ, h_eq, affine_of_mapsTo_ball_of_norm_dslope_eq_div hd h_maps h_z‚ÇÄ h_eq‚ü©

end DimOne

end -- public section

end Complex
