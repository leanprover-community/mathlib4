/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Complex.AbsMax
public import Mathlib.Analysis.Complex.RemovableSingularity

/-!
# Schwarz lemma

In this file we prove several versions of the Schwarz lemma.

* `Complex.norm_deriv_le_div_of_mapsTo_ball`. Let `f : ‚ÑÇ ‚Üí E` be a complex analytic function
  on an open disk with center `c` and a positive radius `R‚ÇÅ`.
  If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
  then the norm of the derivative of `f` at `c` is at most the ratio `R‚ÇÇ / R‚ÇÅ`.

* `Complex.dist_le_div_mul_dist_of_mapsTo_ball`. Let `f : E ‚Üí F` be a complex analytic function
  on an open ball with center `c` and radius `R‚ÇÅ`.
  If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
  then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ (R‚ÇÇ / R‚ÇÅ) * dist z c`.

* `Complex.norm_deriv_le_one_of_mapsTo_ball`. If `f : ‚ÑÇ ‚Üí E` is complex analytic
  on an open disk with center `c` and a positive radius `R‚ÇÅ`,
  and it sends this disk to a closed ball with center `f c` and radius the same radius,
  then the norm of the derivative of `f` at the center of this disk is at most `1`.

* `Complex.dist_le_dist_of_mapsTo_ball`. Let `f : E ‚Üí F` be a complex analytic function
  on an open ball with center `c`.
  If `f` sends this ball to a closed ball with center `f c` and the same radius,
  then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ dist z c`.

* `Complex.norm_le_norm_of_mapsTo_ball`:
  Let `f : E ‚Üí F` be a complex analytic on an open ball with center at the origin.
  If `f` sends this ball to the closed ball with center `0` of the same radius and `f 0 = 0`,
  then for any point `z` of this disk we have `‚Äñf z‚Äñ ‚â§ ‚Äñz‚Äñ`.

## Implementation notes

Traditionally, the Schwarz lemma is formulated for maps `f : ‚ÑÇ ‚Üí ‚ÑÇ`.
We generalize all versions of the lemma to the case of maps to any normed space.
For the versions that don't use `deriv` or `dslope`,
we state it for maps between any two normed spaces.

## TODO

* Prove that any diffeomorphism of the unit disk to itself is a M√∂bius map.

## Tags

Schwarz lemma
-/

open Metric Set Function Filter TopologicalSpace

open scoped Topology ComplexConjugate

namespace Complex

/-- An auxiliary lemma for `Complex.norm_dslope_le_div_of_mapsTo_ball`. -/
theorem schwarz_aux {f : ‚ÑÇ ‚Üí ‚ÑÇ} {c z : ‚ÑÇ} {R‚ÇÅ R‚ÇÇ : ‚Ñù} (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (hz : z ‚àà ball c R‚ÇÅ) :
    ‚Äñdslope f c z‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  have hR‚ÇÅ : 0 < R‚ÇÅ := nonempty_ball.1 ‚ü®z, hz‚ü©
  suffices ‚àÄ·∂† r in ùìù[<] R‚ÇÅ, ‚Äñdslope f c z‚Äñ ‚â§ R‚ÇÇ / r by
    refine ge_of_tendsto ?_ this
    exact (tendsto_const_nhds.div tendsto_id hR‚ÇÅ.ne').mono_left nhdsWithin_le_nhds
  rw [mem_ball] at hz
  filter_upwards [Ioo_mem_nhdsLT hz] with r hr
  have hr‚ÇÄ : 0 < r := dist_nonneg.trans_lt hr.1
  replace hd : DiffContOnCl ‚ÑÇ (dslope f c) (ball c r) := by
    refine DifferentiableOn.diffContOnCl ?_
    rw [closure_ball c hr‚ÇÄ.ne']
    exact ((differentiableOn_dslope <| ball_mem_nhds _ hR‚ÇÅ).mpr hd).mono
      (closedBall_subset_ball hr.2)
  refine norm_le_of_forall_mem_frontier_norm_le isBounded_ball hd ?_ ?_
  ¬∑ rw [frontier_ball c hr‚ÇÄ.ne']
    intro z hz
    have hz' : z ‚â† c := ne_of_mem_sphere hz hr‚ÇÄ.ne'
    rw [dslope_of_ne _ hz', slope_def_module, norm_smul, norm_inv, mem_sphere_iff_norm.1 hz, ‚Üê
      div_eq_inv_mul, div_le_div_iff_of_pos_right hr‚ÇÄ, ‚Üê mem_closedBall_iff_norm]
    exact h_maps <| mem_ball.2 <| by rw [mem_sphere.1 hz]; exact hr.2
  ¬∑ rw [closure_ball c hr‚ÇÄ.ne', mem_closedBall]
    exact hr.1.le

public section

section DimOne

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {R R‚ÇÅ R‚ÇÇ : ‚Ñù} {f : ‚ÑÇ ‚Üí E}
  {c z z‚ÇÄ : ‚ÑÇ}

/-- Two cases of the **Schwarz Lemma** (derivative and distance), merged together.

If `f : ‚ÑÇ ‚Üí E` is a complex analytic function on an open ball `ball c R‚ÇÅ`
hat sends it to a closed ball `closedBall (f c) R‚ÇÇ`, then the norm of `dslope f c z`,
which is defined as `(z - c)‚Åª¬π ‚Ä¢ (f z - f c)` for `z ‚â† c` and as `deriv f c` for `z = c`,
is not greater than the ratio `R‚ÇÇ / R‚ÇÅ`.
-/
theorem norm_dslope_le_div_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (hz : z ‚àà ball c R‚ÇÅ) :
    ‚Äñdslope f c z‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  have hR‚ÇÅ : 0 < R‚ÇÅ := nonempty_ball.1 ‚ü®z, hz‚ü©
  have hR‚ÇÇ : 0 ‚â§ R‚ÇÇ := nonempty_closedBall.mp ‚ü®f z, h_maps hz‚ü©
  rcases eq_or_ne (dslope f c z) 0 with hc | hc
  ¬∑ rw [hc, norm_zero]; exact div_nonneg hR‚ÇÇ hR‚ÇÅ.le
  rcases exists_dual_vector ‚ÑÇ _ hc with ‚ü®g, hg, hgf‚ü©
  have hg' : ‚Äñg‚Äñ‚Çä = 1 := NNReal.eq hg
  calc
    ‚Äñdslope f c z‚Äñ = ‚Äñdslope (g ‚àò f) c z‚Äñ := by
      rw [g.dslope_comp, hgf, RCLike.norm_ofReal, abs_norm]
      exact fun _ => hd.differentiableAt (ball_mem_nhds _ hR‚ÇÅ)
    _ ‚â§ R‚ÇÇ / R‚ÇÅ := by
      refine schwarz_aux (g.differentiable.comp_differentiableOn hd) (MapsTo.comp ?_ h_maps) hz
      simpa only [hg', NNReal.coe_one, one_mul] using g.lipschitz.mapsTo_closedBall (f c) R‚ÇÇ

/-- Equality case in the **Schwarz Lemma**: in the setup of `norm_dslope_le_div_of_mapsTo_ball`,
if `‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ` holds at a point in the ball
then the map `f` is affine with slope `dslope f c z‚ÇÄ`.

Note that this lemma requires the codomain to be a strictly convex space.
Indeed, for `E = ‚ÑÇ √ó ‚ÑÇ` there is a counterexample:
the map `f := fun z ‚Ü¶ (z, z ^ 2)` sends `ball 0 1` to `closedBall 0 1`,
`‚Äñdslope f 0 0‚Äñ = ‚Äñderiv f 0‚Äñ = ‚Äñ(1, 0)‚Äñ = 1`, but the map is not an affine map.
-/
theorem affine_of_mapsTo_ball_of_norm_dslope_eq_div [StrictConvexSpace ‚Ñù E]
    (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : Set.MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ))
    (h_z‚ÇÄ : z‚ÇÄ ‚àà ball c R‚ÇÅ) (h_eq : ‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ) :
    Set.EqOn f (fun z => f c + (z - c) ‚Ä¢ dslope f c z‚ÇÄ) (ball c R‚ÇÅ) := by
  set e : E ‚ÜíL[‚ÑÇ] UniformSpace.Completion E := UniformSpace.Completion.toComplL
  set g := dslope (e ‚àò f) c
  rintro z hz
  have h_R‚ÇÅ : 0 < R‚ÇÅ := nonempty_ball.mp ‚ü®_, h_z‚ÇÄ‚ü©
  have hg' : g = e ‚àò dslope f c := by
    ext w
    simp only [g]
    rw [e.dslope_comp, Function.comp_apply]
    rintro rfl
    exact hd.differentiableAt <| ball_mem_nhds _ h_R‚ÇÅ
  have g_le_div : ‚àÄ z ‚àà ball c R‚ÇÅ, ‚Äñg z‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := fun z hz =>
    norm_dslope_le_div_of_mapsTo_ball (e.differentiable.comp_differentiableOn hd)
      (fun w hw ‚Ü¶ by simpa [e] using h_maps hw) hz
  have g_max : IsMaxOn (norm ‚àò g) (ball c R‚ÇÅ) z‚ÇÄ :=
    isMaxOn_iff.mpr fun z hz => by simpa [h_eq, hg', e] using g_le_div z hz
  have g_diff : DifferentiableOn ‚ÑÇ g (ball c R‚ÇÅ) :=
    (differentiableOn_dslope (isOpen_ball.mem_nhds (mem_ball_self h_R‚ÇÅ))).mpr
      (e.differentiable.comp_differentiableOn hd)
  have heq : ‚Äñdslope f c z‚Äñ = ‚Äñdslope f c z‚ÇÄ‚Äñ := by
    simpa [hg', e] using norm_eqOn_of_isPreconnected_of_isMaxOn (convex_ball c R‚ÇÅ).isPreconnected
      isOpen_ball g_diff h_z‚ÇÄ g_max hz
  have heq_add : ‚Äñdslope f c z + dslope f c z‚ÇÄ‚Äñ = ‚Äñdslope f c z‚ÇÄ + dslope f c z‚ÇÄ‚Äñ := by
    simpa [hg', e, ‚Üê UniformSpace.Completion.coe_add]
      using norm_eqOn_of_isPreconnected_of_isMaxOn (convex_ball c R‚ÇÅ).isPreconnected
        isOpen_ball (g_diff.add_const (g z‚ÇÄ)) h_z‚ÇÄ g_max.norm_add_self hz
  have : dslope f c z = dslope f c z‚ÇÄ := eq_of_norm_eq_of_norm_add_eq heq <| by
    simp only [heq, SameRay.rfl.norm_add, heq_add]
  simp [‚Üê this]

@[deprecated (since := "2026-01-03")]
alias affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div :=
  affine_of_mapsTo_ball_of_norm_dslope_eq_div

/-- Equality case in the **Schwarz Lemma**: in the setup of `norm_dslope_le_div_of_mapsTo_ball`,
if there exists a point `z‚ÇÄ` in the ball such that `‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ`,
then the map `f` is affine with the absolute value of the slope equal to `R‚ÇÇ / R‚ÇÅ`.

This is an existence version of `affine_of_mapsTo_ball_of_norm_dslope_eq_div` above.

TODO: once the deprecated alias `affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div` is gone,
rename this theorem to `affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div`.
-/
theorem affine_of_mapsTo_ball_of_exists_norm_dslope_eq_div'
    [StrictConvexSpace ‚Ñù E] (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : Set.MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ))
    (h_z‚ÇÄ : ‚àÉ z‚ÇÄ ‚àà ball c R‚ÇÅ, ‚Äñdslope f c z‚ÇÄ‚Äñ = R‚ÇÇ / R‚ÇÅ) :
    ‚àÉ C : E, ‚ÄñC‚Äñ = R‚ÇÇ / R‚ÇÅ ‚àß Set.EqOn f (fun z => f c + (z - c) ‚Ä¢ C) (ball c R‚ÇÅ) :=
  let ‚ü®z‚ÇÄ, h_z‚ÇÄ, h_eq‚ü© := h_z‚ÇÄ
  ‚ü®dslope f c z‚ÇÄ, h_eq, affine_of_mapsTo_ball_of_norm_dslope_eq_div hd h_maps h_z‚ÇÄ h_eq‚ü©

/-- The **Schwarz Lemma**: if `f : ‚ÑÇ ‚Üí E` is complex analytic
on an open disk with center `c` and a positive radius `R‚ÇÅ`,
and it sends this disk to a closed ball with center `f c` and radius `R‚ÇÇ`,
then the norm of the derivative of `f` at `c` is at most the ratio `R‚ÇÇ / R‚ÇÅ`. -/
theorem norm_deriv_le_div_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (h‚ÇÄ : 0 < R‚ÇÅ) :
    ‚Äñderiv f c‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  simpa only [dslope_same] using norm_dslope_le_div_of_mapsTo_ball hd h_maps (mem_ball_self h‚ÇÄ)

/-- The **Schwarz Lemma**: if `f : ‚ÑÇ ‚Üí E` is complex analytic
on an open disk with center `c` and a positive radius `R‚ÇÅ`,
and it sends this disk to a closed ball with center `f c` and radius the same radius,
then the norm of the derivative of `f` at the center of this disk is at most `1`.
-/
theorem norm_deriv_le_one_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R))
    (h_maps : MapsTo f (ball c R) (closedBall (f c) R)) (h‚ÇÄ : 0 < R) : ‚Äñderiv f c‚Äñ ‚â§ 1 :=
  (norm_deriv_le_div_of_mapsTo_ball hd h_maps h‚ÇÄ).trans_eq (div_self h‚ÇÄ.ne')

end DimOne

variable {E F : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [NormedAddCommGroup F] [NormedSpace ‚ÑÇ F]
  {R R‚ÇÅ R‚ÇÇ : ‚Ñù} {f : E ‚Üí F} {c z : E}

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c` and radius `R‚ÇÅ`.
If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ (R‚ÇÇ / R‚ÇÅ) * dist z c`.
-/
theorem dist_le_div_mul_dist_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (hz : z ‚àà ball c R‚ÇÅ) :
    dist (f z) (f c) ‚â§ R‚ÇÇ / R‚ÇÅ * dist z c := by
  rcases eq_or_ne z c with (rfl | hne)
  ¬∑ simp only [dist_self, mul_zero, le_rfl]
  set g : ‚ÑÇ ‚Üí F := f ‚àò AffineMap.lineMap c z
  have hmaps : MapsTo (AffineMap.lineMap c z) (ball (0 : ‚ÑÇ) (R‚ÇÅ / dist z c)) (ball c R‚ÇÅ) := by
    intro w hw
    simpa [lt_div_iff‚ÇÄ, hne, dist_comm c] using hw
  have hdg : DifferentiableOn ‚ÑÇ g (ball 0 (R‚ÇÅ / dist z c)) :=
    hd.comp (by rw [funext (AffineMap.lineMap_apply_module _ _)]; fun_prop) hmaps
  calc
    dist (f z) (f c) = dist (g 1) (g 0) := by simp [g]
    _ ‚â§ R‚ÇÇ / (R‚ÇÅ / dist z c) * dist (1 : ‚ÑÇ) 0 := by
      simpa [dslope_of_ne, slope_def_module, dist_eq_norm_sub]
        using norm_dslope_le_div_of_mapsTo_ball hdg (by simpa [g] using h_maps.comp hmaps)
          (z := 1) (by simpa [lt_div_iff‚ÇÄ, hne])
    _ = _ := by simp [field]

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c` and positive radius `R‚ÇÅ`.
If `f` sends this ball to a closed ball with center `f c` and radius `R‚ÇÇ`,
then the norm of the Fr√©chet derivative of `f` at `c` is at most `R‚ÇÇ / R‚ÇÅ`.
-/
theorem norm_fderiv_le_div_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R‚ÇÅ))
    (h_maps : MapsTo f (ball c R‚ÇÅ) (closedBall (f c) R‚ÇÇ)) (h‚ÇÄ : 0 < R‚ÇÅ) :
    ‚Äñfderiv ‚ÑÇ f c‚Äñ ‚â§ R‚ÇÇ / R‚ÇÅ := by
  have : 0 ‚â§ R‚ÇÇ := nonempty_closedBall.mp <| h_maps.nonempty <| nonempty_ball.mpr h‚ÇÄ
  refine norm_fderiv_le_of_lip' _ (by positivity) ?_
  filter_upwards [ball_mem_nhds _ h‚ÇÄ] with z hz
  simpa [dist_eq_norm_sub] using dist_le_div_mul_dist_of_mapsTo_ball hd h_maps hz

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c`.
If `f` sends this ball to a closed ball with center `f c` and the same radius,
then for any `z` in the former ball we have `dist (f z) (f c) ‚â§ dist z c`.
-/
theorem dist_le_dist_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R))
    (h_maps : MapsTo f (ball c R) (closedBall (f c) R)) (hz : z ‚àà ball c R) :
    dist (f z) (f c) ‚â§ dist z c := by
  simpa [(nonempty_ball.1 ‚ü®z, hz‚ü©).ne'] using dist_le_div_mul_dist_of_mapsTo_ball hd h_maps hz

@[deprecated (since := "2026-01-03")]
alias dist_le_dist_of_mapsTo_ball_self := dist_le_dist_of_mapsTo_ball

/-- The **Schwarz Lemma**. Let `f : E ‚Üí F` be a complex analytic function
on an open ball with center `c` and a positive radius.
If `f` sends this ball to a closed ball with center `f c` and the same radius,
then the norm of the Fr√©chet derivative of `f` at `c` is at most one.
-/
theorem norm_fderiv_le_one_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball c R))
    (h_maps : MapsTo f (ball c R) (closedBall (f c) R)) (hR : 0 < R) :
    ‚Äñfderiv ‚ÑÇ f c‚Äñ ‚â§ 1 := by
  simpa [hR.ne'] using norm_fderiv_le_div_of_mapsTo_ball hd h_maps hR

/-- The **Schwarz Lemma**.
Let `f : E ‚Üí F` be a complex analytic on an open ball with center at the origin.
If `f` sends this ball to the closed ball with center `0` of the same radius and `f 0 = 0`,
then for any point `z` of this disk we have `‚Äñf z‚Äñ ‚â§ ‚Äñz‚Äñ`. -/
theorem norm_le_norm_of_mapsTo_ball (hd : DifferentiableOn ‚ÑÇ f (ball 0 R))
    (h_maps : MapsTo f (ball 0 R) (closedBall 0 R)) (h‚ÇÄ : f 0 = 0) (hz : ‚Äñz‚Äñ < R) :
    ‚Äñf z‚Äñ ‚â§ ‚Äñz‚Äñ := by
  simpa [h‚ÇÄ] using dist_le_dist_of_mapsTo_ball hd (by rwa [h‚ÇÄ]) (mem_ball_zero_iff.mpr hz)

@[deprecated (since := "2026-01-03")]
alias norm_le_norm_of_mapsTo_ball_self := norm_le_norm_of_mapsTo_ball

end -- public section

end Complex
