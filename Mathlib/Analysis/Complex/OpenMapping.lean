/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.Complex.AbsMax
import Mathlib.Topology.MetricSpace.ProperSpace.Lemmas

/-!
# The open mapping theorem for holomorphic functions

This file proves the open mapping theorem for holomorphic functions, namely that an analytic
function on a preconnected set of the complex plane is either constant or open. The main step is to
show a local version of the theorem that states that if `f` is analytic at a point `z‚ÇÄ`, then either
it is constant in a neighborhood of `z‚ÇÄ` or it maps any neighborhood of `z‚ÇÄ` to a neighborhood of
its image `f z‚ÇÄ`. The results extend in higher dimension to `g : E ‚Üí ‚ÑÇ`.

The proof of the local version on `‚ÑÇ` goes through two main steps: first, assuming that the function
is not constant around `z‚ÇÄ`, use the isolated zero principle to show that `‚Äñf z‚Äñ` is bounded below
on a small `sphere z‚ÇÄ r` around `z‚ÇÄ`, and then use the maximum principle applied to the auxiliary
function `(fun z ‚Ü¶ ‚Äñf z - v‚Äñ)` to show that any `v` close enough to `f z‚ÇÄ` is in `f '' ball z‚ÇÄ r`.
That second step is implemented in `DiffContOnCl.ball_subset_image_closedBall`.

## Main results

* `AnalyticAt.eventually_constant_or_nhds_le_map_nhds` is the local version of the open mapping
  theorem around a point;
* `AnalyticOnNhd.is_constant_or_isOpen` is the open mapping theorem on a connected open set.
-/


open Set Filter Metric Complex

open scoped Topology

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {U : Set E} {f : ‚ÑÇ ‚Üí ‚ÑÇ} {g : E ‚Üí ‚ÑÇ}
  {z‚ÇÄ : ‚ÑÇ} {Œµ r : ‚Ñù}

/-- If the modulus of a holomorphic function `f` is bounded below by `Œµ` on a circle, then its range
contains a disk of radius `Œµ / 2`. -/
theorem DiffContOnCl.ball_subset_image_closedBall (h : DiffContOnCl ‚ÑÇ f (ball z‚ÇÄ r)) (hr : 0 < r)
    (hf : ‚àÄ z ‚àà sphere z‚ÇÄ r, Œµ ‚â§ ‚Äñf z - f z‚ÇÄ‚Äñ) (hz‚ÇÄ : ‚àÉ·∂† z in ùìù z‚ÇÄ, f z ‚â† f z‚ÇÄ) :
    ball (f z‚ÇÄ) (Œµ / 2) ‚äÜ f '' closedBall z‚ÇÄ r := by
  /- This is a direct application of the maximum principle. Pick `v` close to `f z‚ÇÄ`, and look at
    the function `fun z ‚Ü¶ ‚Äñf z - v‚Äñ`: it is bounded below on the circle, and takes a small value
    at `z‚ÇÄ` so it is not constant on the disk, which implies that its infimum is equal to `0` and
    hence that `v` is in the range of `f`. -/
  rintro v hv
  have h1 : DiffContOnCl ‚ÑÇ (fun z => f z - v) (ball z‚ÇÄ r) := h.sub_const v
  have h2 : ContinuousOn (fun z => ‚Äñf z - v‚Äñ) (closedBall z‚ÇÄ r) :=
    continuous_norm.comp_continuousOn (closure_ball z‚ÇÄ hr.ne.symm ‚ñ∏ h1.continuousOn)
  have h3 : AnalyticOnNhd ‚ÑÇ f (ball z‚ÇÄ r) := h.differentiableOn.analyticOnNhd isOpen_ball
  have h4 : ‚àÄ z ‚àà sphere z‚ÇÄ r, Œµ / 2 ‚â§ ‚Äñf z - v‚Äñ := fun z hz => by
    linarith [hf z hz, show ‚Äñv - f z‚ÇÄ‚Äñ < Œµ / 2 from mem_ball.mp hv,
      norm_sub_sub_norm_sub_le_norm_sub (f z) v (f z‚ÇÄ)]
  have h5 : ‚Äñf z‚ÇÄ - v‚Äñ < Œµ / 2 := by simpa [‚Üê dist_eq_norm, dist_comm] using mem_ball.mp hv
  obtain ‚ü®z, hz1, hz2‚ü© : ‚àÉ z ‚àà ball z‚ÇÄ r, IsLocalMin (fun z => ‚Äñf z - v‚Äñ) z :=
    exists_isLocalMin_mem_ball h2 (mem_closedBall_self hr.le) fun z hz => h5.trans_le (h4 z hz)
  refine ‚ü®z, ball_subset_closedBall hz1, sub_eq_zero.mp ?_‚ü©
  have h6 := h1.differentiableOn.eventually_differentiableAt (isOpen_ball.mem_nhds hz1)
  refine (eventually_eq_or_eq_zero_of_isLocalMin_norm h6 hz2).resolve_left fun key => ?_
  have h7 : ‚àÄ·∂† w in ùìù z, f w = f z := by filter_upwards [key] with h; field_simp
  replace h7 : ‚àÉ·∂† w in ùìù[‚â†] z, f w = f z := (h7.filter_mono nhdsWithin_le_nhds).frequently
  have h8 : IsPreconnected (ball z‚ÇÄ r) := (convex_ball z‚ÇÄ r).isPreconnected
  have h9 := h3.eqOn_of_preconnected_of_frequently_eq analyticOnNhd_const h8 hz1 h7
  have h10 : f z = f z‚ÇÄ := (h9 (mem_ball_self hr)).symm
  exact not_eventually.mpr hz‚ÇÄ (mem_of_superset (ball_mem_nhds z‚ÇÄ hr) (h10 ‚ñ∏ h9))

/-- A function `f : ‚ÑÇ ‚Üí ‚ÑÇ` which is analytic at a point `z‚ÇÄ` is either constant in a neighborhood
of `z‚ÇÄ`, or behaves locally like an open function (in the sense that the image of every neighborhood
of `z‚ÇÄ` is a neighborhood of `f z‚ÇÄ`, as in `isOpenMap_iff_nhds_le`). For a function `f : E ‚Üí ‚ÑÇ`
the same result holds, see `AnalyticAt.eventually_constant_or_nhds_le_map_nhds`. -/
theorem AnalyticAt.eventually_constant_or_nhds_le_map_nhds_aux (hf : AnalyticAt ‚ÑÇ f z‚ÇÄ) :
    (‚àÄ·∂† z in ùìù z‚ÇÄ, f z = f z‚ÇÄ) ‚à® ùìù (f z‚ÇÄ) ‚â§ map f (ùìù z‚ÇÄ) := by
  /- The function `f` is analytic in a neighborhood of `z‚ÇÄ`; by the isolated zeros principle, if `f`
    is not constant in a neighborhood of `z‚ÇÄ`, then it is nonzero, and therefore bounded below, on
    every small enough circle around `z‚ÇÄ` and then `DiffContOnCl.ball_subset_image_closedBall`
    provides an explicit ball centered at `f z‚ÇÄ` contained in the range of `f`. -/
  refine or_iff_not_imp_left.mpr fun h => ?_
  refine (nhds_basis_ball.le_basis_iff (nhds_basis_closedBall.map f)).mpr fun R hR => ?_
  have h1 := (hf.eventually_eq_or_eventually_ne analyticAt_const).resolve_left h
  have h2 : ‚àÄ·∂† z in ùìù z‚ÇÄ, AnalyticAt ‚ÑÇ f z := (isOpen_analyticAt ‚ÑÇ f).eventually_mem hf
  obtain ‚ü®œÅ, hœÅ, h3, h4‚ü© :
    ‚àÉ œÅ > 0, AnalyticOnNhd ‚ÑÇ f (closedBall z‚ÇÄ œÅ) ‚àß ‚àÄ z ‚àà closedBall z‚ÇÄ œÅ, z ‚â† z‚ÇÄ ‚Üí f z ‚â† f z‚ÇÄ := by
    simpa only [setOf_and, subset_inter_iff] using
      nhds_basis_closedBall.mem_iff.mp (h2.and (eventually_nhdsWithin_iff.mp h1))
  replace h3 : DiffContOnCl ‚ÑÇ f (ball z‚ÇÄ œÅ) :=
    ‚ü®h3.differentiableOn.mono ball_subset_closedBall,
      (closure_ball z‚ÇÄ hœÅ.lt.ne.symm).symm ‚ñ∏ h3.continuousOn‚ü©
  let r := œÅ ‚äì R
  have hr : 0 < r := lt_inf_iff.mpr ‚ü®hœÅ, hR‚ü©
  have h5 : closedBall z‚ÇÄ r ‚äÜ closedBall z‚ÇÄ œÅ := closedBall_subset_closedBall inf_le_left
  have h6 : DiffContOnCl ‚ÑÇ f (ball z‚ÇÄ r) := h3.mono (ball_subset_ball inf_le_left)
  have h7 : ‚àÄ z ‚àà sphere z‚ÇÄ r, f z ‚â† f z‚ÇÄ := fun z hz =>
    h4 z (h5 (sphere_subset_closedBall hz)) (ne_of_mem_sphere hz hr.ne.symm)
  have h8 : (sphere z‚ÇÄ r).Nonempty := NormedSpace.sphere_nonempty.mpr hr.le
  have h9 : ContinuousOn (fun x => ‚Äñf x - f z‚ÇÄ‚Äñ) (sphere z‚ÇÄ r) := continuous_norm.comp_continuousOn
    ((h6.sub_const (f z‚ÇÄ)).continuousOn_ball.mono sphere_subset_closedBall)
  obtain ‚ü®x, hx, hfx‚ü© := (isCompact_sphere z‚ÇÄ r).exists_isMinOn h8 h9
  refine ‚ü®‚Äñf x - f z‚ÇÄ‚Äñ / 2, half_pos (norm_sub_pos_iff.mpr (h7 x hx)), ?_‚ü©
  exact (h6.ball_subset_image_closedBall hr (fun z hz => hfx hz) (not_eventually.mp h)).trans
    (image_subset f (closedBall_subset_closedBall inf_le_right))

/-- The *open mapping theorem* for holomorphic functions, local version: is a function `g : E ‚Üí ‚ÑÇ`
is analytic at a point `z‚ÇÄ`, then either it is constant in a neighborhood of `z‚ÇÄ`, or it maps every
neighborhood of `z‚ÇÄ` to a neighborhood of `z‚ÇÄ`. For the particular case of a holomorphic function on
`‚ÑÇ`, see `AnalyticAt.eventually_constant_or_nhds_le_map_nhds_aux`. -/
theorem AnalyticAt.eventually_constant_or_nhds_le_map_nhds {z‚ÇÄ : E} (hg : AnalyticAt ‚ÑÇ g z‚ÇÄ) :
    (‚àÄ·∂† z in ùìù z‚ÇÄ, g z = g z‚ÇÄ) ‚à® ùìù (g z‚ÇÄ) ‚â§ map g (ùìù z‚ÇÄ) := by
  /- The idea of the proof is to use the one-dimensional version applied to the restriction of `g`
    to lines going through `z‚ÇÄ` (indexed by `sphere (0 : E) 1`). If the restriction is eventually
    constant along each of these lines, then the identity theorem implies that `g` is constant on
    any ball centered at `z‚ÇÄ` on which it is analytic, and in particular `g` is eventually constant.
    If on the other hand there is one line along which `g` is not eventually constant, then the
    one-dimensional version of the open mapping theorem can be used to conclude. -/
  let ray : E ‚Üí ‚ÑÇ ‚Üí E := fun z t => z‚ÇÄ + t ‚Ä¢ z
  let gray : E ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ := fun z => g ‚àò ray z
  obtain ‚ü®r, hr, hgr‚ü© := isOpen_iff.mp (isOpen_analyticAt ‚ÑÇ g) z‚ÇÄ hg
  have h1 : ‚àÄ z ‚àà sphere (0 : E) 1, AnalyticOnNhd ‚ÑÇ (gray z) (ball 0 r) := by
    refine fun z hz t ht => AnalyticAt.comp ?_ ?_
    ¬∑ exact hgr (by simpa [ray, norm_smul, mem_sphere_zero_iff_norm.mp hz] using ht)
    ¬∑ exact analyticAt_const.add
        ((ContinuousLinearMap.smulRight (ContinuousLinearMap.id ‚ÑÇ ‚ÑÇ) z).analyticAt t)
  by_cases h : ‚àÄ z ‚àà sphere (0 : E) 1, ‚àÄ·∂† t in ùìù 0, gray z t = gray z 0
  ¬∑ left
    -- If g is eventually constant along every direction, then it is eventually constant
    refine eventually_of_mem (ball_mem_nhds z‚ÇÄ hr) fun z hz => ?_
    refine (eq_or_ne z z‚ÇÄ).casesOn (congr_arg g) fun h' => ?_
    replace h' : ‚Äñz - z‚ÇÄ‚Äñ ‚â† 0 := by simpa only [Ne, norm_eq_zero, sub_eq_zero]
    let w : E := ‚Äñz - z‚ÇÄ‚Äñ‚Åª¬π ‚Ä¢ (z - z‚ÇÄ)
    have h3 : ‚àÄ t ‚àà ball (0 : ‚ÑÇ) r, gray w t = g z‚ÇÄ := by
      have e1 : IsPreconnected (ball (0 : ‚ÑÇ) r) := (convex_ball 0 r).isPreconnected
      have e2 : w ‚àà sphere (0 : E) 1 := by simp [w, norm_smul, inv_mul_cancel‚ÇÄ h']
      specialize h1 w e2
      apply h1.eqOn_of_preconnected_of_eventuallyEq analyticOnNhd_const e1 (mem_ball_self hr)
      simpa [ray, gray] using h w e2
    have h4 : ‚Äñz - z‚ÇÄ‚Äñ < r := by simpa [dist_eq_norm] using mem_ball.mp hz
    replace h4 : ‚Üë‚Äñz - z‚ÇÄ‚Äñ ‚àà ball (0 : ‚ÑÇ) r := by simpa
    simpa only [ray, gray, w, smul_smul, mul_inv_cancel‚ÇÄ h', one_smul, add_sub_cancel,
      Function.comp_apply, coe_smul] using h3 (‚Üë‚Äñz - z‚ÇÄ‚Äñ) h4
  ¬∑ right
    -- Otherwise, it is open along at least one direction and that implies the result
    push_neg at h
    obtain ‚ü®z, hz, hrz‚ü© := h
    specialize h1 z hz 0 (mem_ball_self hr)
    have h7 := h1.eventually_constant_or_nhds_le_map_nhds_aux.resolve_left hrz
    rw [show gray z 0 = g z‚ÇÄ by simp [gray, ray], ‚Üê map_compose] at h7
    refine h7.trans (map_mono ?_)
    have h10 : Continuous fun t : ‚ÑÇ => z‚ÇÄ + t ‚Ä¢ z :=
      continuous_const.add (continuous_id'.smul continuous_const)
    simpa using h10.tendsto 0

/-- The *open mapping theorem* for holomorphic functions, global version: if a function `g : E ‚Üí ‚ÑÇ`
is analytic on a connected set `U`, then either it is constant on `U`, or it is open on `U` (in the
sense that it maps any open set contained in `U` to an open set in `‚ÑÇ`). -/
theorem AnalyticOnNhd.is_constant_or_isOpen (hg : AnalyticOnNhd ‚ÑÇ g U) (hU : IsPreconnected U) :
    (‚àÉ w, ‚àÄ z ‚àà U, g z = w) ‚à® ‚àÄ s ‚äÜ U, IsOpen s ‚Üí IsOpen (g '' s) := by
  by_cases h : ‚àÉ z‚ÇÄ ‚àà U, ‚àÄ·∂† z in ùìù z‚ÇÄ, g z = g z‚ÇÄ
  ¬∑ obtain ‚ü®z‚ÇÄ, hz‚ÇÄ, h‚ü© := h
    exact Or.inl ‚ü®g z‚ÇÄ, hg.eqOn_of_preconnected_of_eventuallyEq analyticOnNhd_const hU hz‚ÇÄ h‚ü©
  ¬∑ push_neg at h
    refine Or.inr fun s hs1 hs2 => isOpen_iff_mem_nhds.mpr ?_
    rintro z ‚ü®w, hw1, rfl‚ü©
    exact (hg w (hs1 hw1)).eventually_constant_or_nhds_le_map_nhds.resolve_left (h w (hs1 hw1))
        (image_mem_map (hs2.mem_nhds hw1))
