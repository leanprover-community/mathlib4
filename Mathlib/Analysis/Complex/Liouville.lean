/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.NormedSpace.Completion

#align_import analysis.complex.liouville from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Liouville's theorem

In this file we prove Liouville's theorem: if `f : E â†’ F` is complex differentiable on the whole
space and its range is bounded, then the function is a constant. Various versions of this theorem
are formalized in `Differentiable.apply_eq_apply_of_bounded`,
`Differentiable.exists_const_forall_eq_of_bounded`, and
`Differentiable.exists_eq_const_of_bounded`.

The proof is based on the Cauchy integral formula for the derivative of an analytic function, see
`Complex.deriv_eq_smul_circleIntegral`.
-/


local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

open TopologicalSpace Metric Set Filter Asymptotics Function MeasureTheory

open scoped Topology Filter NNReal Real

universe u v

variable {E : Type u} [NormedAddCommGroup E] [NormedSpace â„‚ E] {F : Type v} [NormedAddCommGroup F]
  [NormedSpace â„‚ F]

local postfix:100 "Ì‚" => UniformSpace.Completion

namespace Complex

/-- If `f` is complex differentiable on an open disc with center `c` and radius `R > 0` and is
continuous on its closure, then `f' c` can be represented as an integral over the corresponding
circle.

TODO: add a version for `w âˆˆ Metric.ball c R`.

TODO: add a version for higher derivatives. -/
theorem deriv_eq_smul_circleIntegral [CompleteSpace F] {R : â„} {c : â„‚} {f : â„‚ â†’ F} (hR : 0 < R)
    (hf : DiffContOnCl â„‚ f (ball c R)) :
    deriv f c = (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c) ^ (-2 : â„¤) â€¢ f z := by
  lift R to â„â‰¥0 using hR.le
  -- âŠ¢ deriv f c = (2 * â†‘Ï€ * I)â»Â¹ â€¢ âˆ® (z : â„‚) in C(c, â†‘R), (z - c) ^ (-2) â€¢ f z
  refine' (hf.hasFPowerSeriesOnBall hR).hasFPowerSeriesAt.deriv.trans _
  -- âŠ¢ (â†‘(cauchyPowerSeries f c (â†‘R) 1) fun x => 1) = (2 * â†‘Ï€ * I)â»Â¹ â€¢ âˆ® (z : â„‚) in â€¦
  simp only [cauchyPowerSeries_apply, one_div, zpow_neg, pow_one, smul_smul, zpow_two, mul_inv]
  -- ğŸ‰ no goals
#align complex.deriv_eq_smul_circle_integral Complex.deriv_eq_smul_circleIntegral

theorem norm_deriv_le_aux [CompleteSpace F] {c : â„‚} {R C : â„} {f : â„‚ â†’ F} (hR : 0 < R)
    (hf : DiffContOnCl â„‚ f (ball c R)) (hC : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C) :
    â€–deriv f câ€– â‰¤ C / R := by
  have : âˆ€ z âˆˆ sphere c R, â€–(z - c) ^ (-2 : â„¤) â€¢ f zâ€– â‰¤ C / (R * R) :=
    fun z (hz : abs (z - c) = R) => by
    simpa [-mul_inv_rev, norm_smul, hz, zpow_two, â† div_eq_inv_mul] using
      (div_le_div_right (mul_pos hR hR)).2 (hC z hz)
  calc
    â€–deriv f câ€– = â€–(2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), (z - c) ^ (-2 : â„¤) â€¢ f zâ€– :=
      congr_arg norm (deriv_eq_smul_circleIntegral hR hf)
    _ â‰¤ R * (C / (R * R)) :=
      (circleIntegral.norm_two_pi_i_inv_smul_integral_le_of_norm_le_const hR.le this)
    _ = C / R := by rw [mul_div_left_comm, div_self_mul_self', div_eq_mul_inv]
#align complex.norm_deriv_le_aux Complex.norm_deriv_le_aux

/-- If `f` is complex differentiable on an open disc of radius `R > 0`, is continuous on its
closure, and its values on the boundary circle of this disc are bounded from above by `C`, then the
norm of its derivative at the center is at most `C / R`. -/
theorem norm_deriv_le_of_forall_mem_sphere_norm_le {c : â„‚} {R C : â„} {f : â„‚ â†’ F} (hR : 0 < R)
    (hd : DiffContOnCl â„‚ f (ball c R)) (hC : âˆ€ z âˆˆ sphere c R, â€–f zâ€– â‰¤ C) :
    â€–deriv f câ€– â‰¤ C / R := by
  set e : F â†’L[â„‚] FÌ‚ := UniformSpace.Completion.toComplL
  -- âŠ¢ â€–deriv f câ€– â‰¤ C / R
  have : HasDerivAt (e âˆ˜ f) (e (deriv f c)) c :=
    e.hasFDerivAt.comp_hasDerivAt c
      (hd.differentiableAt isOpen_ball <| mem_ball_self hR).hasDerivAt
  calc
    â€–deriv f câ€– = â€–deriv (e âˆ˜ f) câ€– := by
      rw [this.deriv]
      exact (UniformSpace.Completion.norm_coe _).symm
    _ â‰¤ C / R :=
      norm_deriv_le_aux hR (e.differentiable.comp_diffContOnCl hd) fun z hz =>
        (UniformSpace.Completion.norm_coe _).trans_le (hC z hz)
#align complex.norm_deriv_le_of_forall_mem_sphere_norm_le Complex.norm_deriv_le_of_forall_mem_sphere_norm_le

/-- An auxiliary lemma for Liouville's theorem `Differentiable.apply_eq_apply_of_bounded`. -/
theorem liouville_theorem_aux {f : â„‚ â†’ F} (hf : Differentiable â„‚ f) (hb : Bounded (range f))
    (z w : â„‚) : f z = f w := by
  suffices : âˆ€ c, deriv f c = 0; exact is_const_of_deriv_eq_zero hf this z w
  -- âŠ¢ f z = f w
                                 -- âŠ¢ âˆ€ (c : â„‚), deriv f c = 0
  clear z w; intro c
  -- âŠ¢ âˆ€ (c : â„‚), deriv f c = 0
             -- âŠ¢ deriv f c = 0
  obtain âŸ¨C, Câ‚€, hCâŸ© : âˆƒ C > (0 : â„), âˆ€ z, â€–f zâ€– â‰¤ C := by
    rcases bounded_iff_forall_norm_le.1 hb with âŸ¨C, hCâŸ©
    exact
      âŸ¨max C 1, lt_max_iff.2 (Or.inr zero_lt_one), fun z =>
        (hC (f z) (mem_range_self _)).trans (le_max_left _ _)âŸ©
  refine' norm_le_zero_iff.1 (le_of_forall_le_of_dense fun Îµ Îµâ‚€ => _)
  -- âŠ¢ â€–deriv f câ€– â‰¤ Îµ
  calc
    â€–deriv f câ€– â‰¤ C / (C / Îµ) :=
      norm_deriv_le_of_forall_mem_sphere_norm_le (div_pos Câ‚€ Îµâ‚€) hf.diffContOnCl fun z _ => hC z
    _ = Îµ := div_div_cancel' Câ‚€.lt.ne'
#align complex.liouville_theorem_aux Complex.liouville_theorem_aux

end Complex

namespace Differentiable

open Complex

/-- **Liouville's theorem**: a complex differentiable bounded function `f : E â†’ F` is a constant. -/
theorem apply_eq_apply_of_bounded {f : E â†’ F} (hf : Differentiable â„‚ f) (hb : Bounded (range f))
    (z w : E) : f z = f w := by
  set g : â„‚ â†’ F := f âˆ˜ fun t : â„‚ => t â€¢ (w - z) + z
  -- âŠ¢ f z = f w
  suffices g 0 = g 1 by simpa
  -- âŠ¢ g 0 = g 1
  apply liouville_theorem_aux
  -- âŠ¢ Differentiable â„‚ g
  exacts [hf.comp ((differentiable_id.smul_const (w - z)).add_const z),
    hb.mono (range_comp_subset_range _ _)]
#align differentiable.apply_eq_apply_of_bounded Differentiable.apply_eq_apply_of_bounded

/-- **Liouville's theorem**: a complex differentiable bounded function is a constant. -/
theorem exists_const_forall_eq_of_bounded {f : E â†’ F} (hf : Differentiable â„‚ f)
    (hb : Bounded (range f)) : âˆƒ c, âˆ€ z, f z = c :=
  âŸ¨f 0, fun _ => hf.apply_eq_apply_of_bounded hb _ _âŸ©
#align differentiable.exists_const_forall_eq_of_bounded Differentiable.exists_const_forall_eq_of_bounded

/-- **Liouville's theorem**: a complex differentiable bounded function is a constant. -/
theorem exists_eq_const_of_bounded {f : E â†’ F} (hf : Differentiable â„‚ f) (hb : Bounded (range f)) :
    âˆƒ c, f = const E c :=
  (hf.exists_const_forall_eq_of_bounded hb).imp fun _ => funext
#align differentiable.exists_eq_const_of_bounded Differentiable.exists_eq_const_of_bounded

end Differentiable
