/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Normed.Module.Completion

/-!
# Liouville's theorem

In this file we prove Liouville's theorem: if `f : E ‚Üí F` is complex differentiable on the whole
space and its range is bounded, then the function is a constant. Various versions of this theorem
are formalized in `Differentiable.apply_eq_apply_of_bounded`,
`Differentiable.exists_const_forall_eq_of_bounded`, and
`Differentiable.exists_eq_const_of_bounded`.

The proof is based on the Cauchy integral formula for the derivative of an analytic function, see
`Complex.deriv_eq_smul_circleIntegral`.
-/

open TopologicalSpace Metric Set Filter Asymptotics Function MeasureTheory Bornology

open scoped Topology Filter NNReal Real

universe u v

variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {F : Type v} [NormedAddCommGroup F]
  [NormedSpace ‚ÑÇ F]

local postfix:100 "ÃÇ" => UniformSpace.Completion

namespace Complex

/-- If `f` is complex differentiable on an open disc with center `c` and radius `R > 0` and is
continuous on its closure, then `f' c` can be represented as an integral over the corresponding
circle.

TODO: add a version for `w ‚àà Metric.ball c R`.

TODO: add a version for higher derivatives. -/
theorem deriv_eq_smul_circleIntegral [CompleteSpace F] {R : ‚Ñù} {c : ‚ÑÇ} {f : ‚ÑÇ ‚Üí F} (hR : 0 < R)
    (hf : DiffContOnCl ‚ÑÇ f (ball c R)) :
    deriv f c = (2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - c) ^ (-2 : ‚Ñ§) ‚Ä¢ f z := by
  lift R to ‚Ñù‚â•0 using hR.le
  refine (hf.hasFPowerSeriesOnBall hR).hasFPowerSeriesAt.deriv.trans ?_
  simp only [cauchyPowerSeries_apply, one_div, zpow_neg, pow_one, smul_smul, zpow_two, mul_inv]

theorem norm_deriv_le_aux [CompleteSpace F] {c : ‚ÑÇ} {R C : ‚Ñù} {f : ‚ÑÇ ‚Üí F} (hR : 0 < R)
    (hf : DiffContOnCl ‚ÑÇ f (ball c R)) (hC : ‚àÄ z ‚àà sphere c R, ‚Äñf z‚Äñ ‚â§ C) :
    ‚Äñderiv f c‚Äñ ‚â§ C / R := by
  have : ‚àÄ z ‚àà sphere c R, ‚Äñ(z - c) ^ (-2 : ‚Ñ§) ‚Ä¢ f z‚Äñ ‚â§ C / (R * R) :=
    fun z (hz : ‚Äñz - c‚Äñ = R) => by
    simpa [-mul_inv_rev, norm_smul, hz, zpow_two, ‚Üê div_eq_inv_mul] using
      (div_le_div_iff_of_pos_right (mul_pos hR hR)).2 (hC z hz)
  calc
    ‚Äñderiv f c‚Äñ = ‚Äñ(2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), (z - c) ^ (-2 : ‚Ñ§) ‚Ä¢ f z‚Äñ :=
      congr_arg norm (deriv_eq_smul_circleIntegral hR hf)
    _ ‚â§ R * (C / (R * R)) :=
      (circleIntegral.norm_two_pi_i_inv_smul_integral_le_of_norm_le_const hR.le this)
    _ = C / R := by rw [mul_div_left_comm, div_self_mul_self', div_eq_mul_inv]

/-- If `f` is complex differentiable on an open disc of radius `R > 0`, is continuous on its
closure, and its values on the boundary circle of this disc are bounded from above by `C`, then the
norm of its derivative at the center is at most `C / R`. -/
theorem norm_deriv_le_of_forall_mem_sphere_norm_le {c : ‚ÑÇ} {R C : ‚Ñù} {f : ‚ÑÇ ‚Üí F} (hR : 0 < R)
    (hd : DiffContOnCl ‚ÑÇ f (ball c R)) (hC : ‚àÄ z ‚àà sphere c R, ‚Äñf z‚Äñ ‚â§ C) :
    ‚Äñderiv f c‚Äñ ‚â§ C / R := by
  set e : F ‚ÜíL[‚ÑÇ] FÃÇ := UniformSpace.Completion.toComplL
  have : HasDerivAt (e ‚àò f) (e (deriv f c)) c :=
    e.hasFDerivAt.comp_hasDerivAt c
      (hd.differentiableAt isOpen_ball <| mem_ball_self hR).hasDerivAt
  calc
    ‚Äñderiv f c‚Äñ = ‚Äñderiv (e ‚àò f) c‚Äñ := by
      rw [this.deriv]
      exact (UniformSpace.Completion.norm_coe _).symm
    _ ‚â§ C / R :=
      norm_deriv_le_aux hR (e.differentiable.comp_diffContOnCl hd) fun z hz =>
        (UniformSpace.Completion.norm_coe _).trans_le (hC z hz)

/-- An auxiliary lemma for Liouville's theorem `Differentiable.apply_eq_apply_of_bounded`. -/
theorem liouville_theorem_aux {f : ‚ÑÇ ‚Üí F} (hf : Differentiable ‚ÑÇ f) (hb : IsBounded (range f))
    (z w : ‚ÑÇ) : f z = f w := by
  suffices ‚àÄ c, deriv f c = 0 from is_const_of_deriv_eq_zero hf this z w
  clear z w; intro c
  obtain ‚ü®C, C‚ÇÄ, hC‚ü© : ‚àÉ C > (0 : ‚Ñù), ‚àÄ z, ‚Äñf z‚Äñ ‚â§ C := by
    rcases isBounded_iff_forall_norm_le.1 hb with ‚ü®C, hC‚ü©
    exact
      ‚ü®max C 1, lt_max_iff.2 (Or.inr zero_lt_one), fun z =>
        (hC (f z) (mem_range_self _)).trans (le_max_left _ _)‚ü©
  refine norm_le_zero_iff.1 (le_of_forall_gt_imp_ge_of_dense fun Œµ Œµ‚ÇÄ => ?_)
  calc
    ‚Äñderiv f c‚Äñ ‚â§ C / (C / Œµ) :=
      norm_deriv_le_of_forall_mem_sphere_norm_le (div_pos C‚ÇÄ Œµ‚ÇÄ) hf.diffContOnCl fun z _ => hC z
    _ = Œµ := div_div_cancel‚ÇÄ C‚ÇÄ.lt.ne'

end Complex

namespace Differentiable

open Complex

/-- **Liouville's theorem**: a complex differentiable bounded function `f : E ‚Üí F` is a constant. -/
theorem apply_eq_apply_of_bounded {f : E ‚Üí F} (hf : Differentiable ‚ÑÇ f) (hb : IsBounded (range f))
    (z w : E) : f z = f w := by
  set g : ‚ÑÇ ‚Üí F := f ‚àò fun t : ‚ÑÇ => t ‚Ä¢ (w - z) + z
  suffices g 0 = g 1 by simpa [g]
  apply liouville_theorem_aux
  exacts [hf.comp ((differentiable_id.smul_const (w - z)).add_const z),
    hb.subset (range_comp_subset_range _ _)]

/-- **Liouville's theorem**: a complex differentiable bounded function is a constant. -/
theorem exists_const_forall_eq_of_bounded {f : E ‚Üí F} (hf : Differentiable ‚ÑÇ f)
    (hb : IsBounded (range f)) : ‚àÉ c, ‚àÄ z, f z = c :=
  ‚ü®f 0, fun _ => hf.apply_eq_apply_of_bounded hb _ _‚ü©

/-- **Liouville's theorem**: a complex differentiable bounded function is a constant. -/
theorem exists_eq_const_of_bounded {f : E ‚Üí F} (hf : Differentiable ‚ÑÇ f)
    (hb : IsBounded (range f)) : ‚àÉ c, f = const E c :=
  (hf.exists_const_forall_eq_of_bounded hb).imp fun _ => funext

/-- A corollary of Liouville's theorem where the function tends to a finite value at infinity
(i.e., along `Filter.cocompact`, which in proper spaces coincides with `Bornology.cobounded`). -/
theorem eq_const_of_tendsto_cocompact [Nontrivial E] {f : E ‚Üí F} (hf : Differentiable ‚ÑÇ f) {c : F}
    (hb : Tendsto f (cocompact E) (ùìù c)) : f = Function.const E c := by
  have h_bdd : Bornology.IsBounded (Set.range f) := by
    obtain ‚ü®s, hs, hs_bdd‚ü© := Metric.exists_isBounded_image_of_tendsto hb
    obtain ‚ü®t, ht, hts‚ü© := mem_cocompact.mp hs
    apply ht.image hf.continuous |>.isBounded.union hs_bdd |>.subset
    simpa [Set.image_union, Set.image_univ] using Set.image_subset _ <| calc
      Set.univ = t ‚à™ t·∂ú := t.union_compl_self.symm
      _        ‚äÜ t ‚à™ s  := by gcongr
  obtain ‚ü®c', hc'‚ü© := hf.exists_eq_const_of_bounded h_bdd
  convert hc'
  exact tendsto_nhds_unique hb (by simpa [hc'] using tendsto_const_nhds)

/-- A corollary of Liouville's theorem where the function tends to a finite value at infinity
(i.e., along `Filter.cocompact`, which in proper spaces coincides with `Bornology.cobounded`). -/
theorem apply_eq_of_tendsto_cocompact [Nontrivial E] {f : E ‚Üí F} (hf : Differentiable ‚ÑÇ f) {c : F}
    (x : E) (hb : Tendsto f (cocompact E) (ùìù c)) : f x = c :=
  congr($(hf.eq_const_of_tendsto_cocompact hb) x)

end Differentiable
