/-
Copyright (c) 2025 Maksym Radziwill. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Maksym Radziwill
-/

import Mathlib.Topology.EMetricSpace.Defs
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Data.Complex.Basic
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Analytic.Within
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Analysis.Complex.AbsMax

/-!
# Borel-Caratheodory theorem

This file proves the Borel-Caratheodory theorem, in the version that is needed for
the PrimeNumberTheoremAnd/StrongPNT [kontorovich2025] project of Alex Kontorovich and
Terence Tao. This version is also used in most textbook proofs of the Prime Number Theorem.

## Main results.

`borelCaratheodory_closedBall`

Let $R, M>0$. Let $f$ be analytic on $|z|‚â§ R$ such that $f(0) = 0$ and suppose
$\mathfrak{R}f(z)\leq M$ for all $|z|\leq R$. Then for any $0 < r < R$,
$$
  \sup_{|z|\leq r}|f(z)|\leq\frac{2Mr}{R-r}.
$$

We also provide a variant that does not require vanishing of $f$ at $0$.
This version aligns with the statement of the Theorem available on Wikipedia.

`borelCaratheodory_closedBall'`

Let $R, M>0$. Let $f$ be analytic on $|z|‚â§ R$ and suppose $\mathfrak{R}f(z)\leq M$
for all $|z|\leq R$. Then for any $0 < r < R$,
$$
  \sup_{|z|\leq r}|f(z)|\leq\frac{2Mr}{R-r} + |f(0)|\frac{R+r}{R-r}.
$$

## Implementation notes

The proof relies on the fact that if $f(0) = 0$ and $f$ is analytic in $|z| ‚â§ R$
with $R > 0$ then $f(z) / z$ is also analytic in $|z| ‚â§ R$. To avoid repetition
the file defines in `divRemovableZero` a function equal to $f(z) / z$ for
$z ‚â† 0$ and equal to $f'(0)$ for $z = 0$. No restriction to $f$ with
$f(0) = 0$ is made in this definition.

In `AnalyticOn.divRemovableZero` we prove that if $f$ is analytic in an open
set containing zero, and $f(0) = 0$ then $f(z) / z$ is analytic in the same
open set.

In `AnalyticOn.divRemovableZero_closedBall` we prove the same statement for
functions analytic on closed balls $|z| ‚â§ R$. As is the proof is a little bit
awkward, it would be more natural to prove that if $f$ is analytic in $|z| ‚â§ R$,
then it is analytic on an open set containing $|z| ‚â§ R$ and appeal to the
previous result.

In `schwartzQuotient` we define the Schwartz quotient
$$
  \frac{f(z) / z}{2 * M - f(z)}
$$
on which the proof of Borel-Caratheodory hinges. No restrictions that
$f(0) = 0$ or $2 M - f(z) ‚â† 0$ are placed in this definition.

In `AnalyticOn.schwartzQuotient` we prove that the Schwarz quotient is analytic
in $|z| ‚â§ R$ provided that $f$ is analytic in $|z| ‚â§ R$ and $f(0) = 0$ and
$2 * M - f(z) ‚â† 0$ for all $|z| ‚â§ R$. The latter condition will be implied by
the assumption $\Re{f(z)} ‚â§ M$ in Borel-Caratheodory.

In `Complex.norm_le_norm_two_mul_sub_of_re_le` we prove an elementary inequality
asserting that if $\Re{x} ‚â§ M$ then $|x| ‚â§ |2 M - x|$. This is used in the
proof of Borel-Caratheodory.

The function `AnalyticOn.norm_le_of_norm_le_on_sphere` is a specialization
of the maximum modulus principle to the case of $|z| ‚â§ R$. Since the proof of
Borel-Caratheodory uses the maximum modulus twice on this domain, it is handy
to have a dedicated function for this domain. The statement allows for a
conclusion specialized to $|z| ‚â§ r$ with $r ‚â§ R$. This is of course trivial,
and the only reason to include such a conclusion, is because this form of
the conclusion is used twice in the proof of Borel-Caratheodory.

In `borelCaratheodory_closedBall` we prove the Borel-Caratheodory
theorem under the assumption that $f(0) = 0$. This proof of Borel-Caratheodory
follows the blueprint laid out in the StrongPNT project [kontorovich2025].

Finally in `borelCaratheodory_closedBall'` we deduce from
`borelCaratheodory_closedBall` a version of Borel-Caratheodory that does
not assume vanishing of $f$ at $0$. This version matches the statement of
the theorem as seen in Wikipedia.

## References

* [Alex Kontorovich et al., *PrimeNumberTheoremAnd*][kontorovich2025]
* <https://en.wikipedia.org/wiki/Borel%E2%80%93Carath%C3%A9odory_theorem>

## Tags

Borel-Caratheodory, borel, caratheodory, complex, analytic, prime number theorem

-/

/-- Given `f`, the function `divRemovableZero f` defines a function equal
to `f z / z` for `z ‚â† 0` and equal to `deriv f 0` for `z = 0`. -/
noncomputable abbrev divRemovableZero (f : ‚ÑÇ ‚Üí ‚ÑÇ) : ‚ÑÇ ‚Üí ‚ÑÇ :=
  Function.update (fun z ‚Ü¶ (f z) / z) 0 ((deriv f) 0)

/-- For `z ‚â† 0`, `divRemovableZero f z` is equal to `f z / z`. -/
lemma divRemovableZero_of_ne_zero {z : ‚ÑÇ} (f : ‚ÑÇ ‚Üí ‚ÑÇ)
    (z_ne_0 : z ‚â† 0) : divRemovableZero f z = f z / z := by
  unfold divRemovableZero; apply Function.update_of_ne z_ne_0

/-- If `f` is analytic on an open set `s` and `f 0 = 0` then `f z / z` is also
analytic on `s`. -/
lemma AnalyticOn.divRemovableZero {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ}
    (sInNhds0 : s ‚àà nhds (0 : ‚ÑÇ)) (zero : f 0 = 0) (o : IsOpen s)
    (analytic : AnalyticOn ‚ÑÇ f s) : AnalyticOn ‚ÑÇ (divRemovableZero f) s := by
  rw [Complex.analyticOn_iff_differentiableOn o]
  rw [‚Üê(Complex.differentiableOn_compl_singleton_and_continuousAt_iff sInNhds0)]
  constructor
  ¬∑ rw [differentiableOn_congr
          (by
            intro x hyp_x
            apply Function.update_of_ne
            rw [Set.mem_diff, Set.mem_singleton_iff] at hyp_x
            rw [ne_eq]; exact hyp_x.right) ]

    exact DifferentiableOn.fun_div
      (AnalyticOn.differentiableOn (AnalyticOn.mono analytic Set.diff_subset))
      (DifferentiableOn.mono (differentiableOn_id (s := Set.univ))
      (Set.subset_univ (s \ {0})))
      (by
        intro x hyp_x
        rw [Set.mem_diff, Set.mem_singleton_iff] at hyp_x
        rw [ne_eq]; exact hyp_x.right)

  ¬∑ have U := HasDerivAt.continuousAt_div (c := 0) (a := (deriv f) 0) (f := f)
      (DifferentiableOn.hasDerivAt
         ((Complex.analyticOn_iff_differentiableOn o).mp analytic) sInNhds0)
    have T : (fun (x : ‚ÑÇ) ‚Ü¶ (f x - 0) / (x - 0)) = (fun (x : ‚ÑÇ) ‚Ü¶ (f x) / x) := by
      funext x; rw [sub_zero, sub_zero]
    rw [zero, T] at U; exact U

/-- If `f` is analytic on `|z| ‚â§ R` and `f 0 = 0` then `f z / z` is analytic in
`|z| ‚â§ R`. -/
lemma AnalyticOn.divRemovableZero_closedBall {f : ‚ÑÇ ‚Üí ‚ÑÇ} {R : ‚Ñù} (Rpos : 0 < R)
    (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R)) (zero : f 0 = 0) :
    AnalyticOn ‚ÑÇ (_root_.divRemovableZero f) (Metric.closedBall (0 : ‚ÑÇ) R) := by
  apply analyticOn_of_locally_analyticOn
  intro x x_hyp
  by_cases h : ‚Äñx‚Äñ = R
  ¬∑ use Metric.ball x (R / 2)
    constructor
    ¬∑ exact Metric.isOpen_ball
    ¬∑ constructor
      ¬∑ simp only [Metric.mem_ball, dist_self, Nat.ofNat_pos,
                   div_pos_iff_of_pos_right]; positivity
      ¬∑ have Z : ‚àÄ w ‚àà Metric.closedBall 0 R ‚à© Metric.ball x (R / 2),
                   _root_.divRemovableZero f w = f w / w := by
          intro x‚ÇÇ hyp_x‚ÇÇ
          apply divRemovableZero_of_ne_zero
          rw [ball_eq, Set.mem_inter_iff, Metric.mem_closedBall,
              dist_zero_right, Set.mem_setOf_eq] at hyp_x‚ÇÇ
          rw [‚Üê norm_pos_iff]
          calc 0
            _ < R - ‚Äñx‚ÇÇ - x‚Äñ := by let ‚ü®u,v‚ü© := hyp_x‚ÇÇ; linarith
            _ = ‚Äñx‚Äñ - ‚Äñx - x‚ÇÇ‚Äñ := by rw [h]; simp only [sub_right_inj]; apply norm_sub_rev
            _ ‚â§ ‚Äñx - (x - x‚ÇÇ)‚Äñ := by apply norm_sub_norm_le
            _ ‚â§ ‚Äñx‚ÇÇ‚Äñ := by simp only [sub_sub_cancel, le_refl]

        apply AnalyticOn.congr
        ¬∑ apply AnalyticOn.div (AnalyticOn.mono analytic Set.inter_subset_left) analyticOn_id
          ¬∑ intro x‚ÇÅ hyp_x‚ÇÅ
            rw [ball_eq, Set.mem_inter_iff, Metric.mem_closedBall,
                dist_zero_right, Set.mem_setOf_eq] at hyp_x‚ÇÅ
            rw [‚Üê norm_pos_iff]
            calc 0
              _ < R - ‚Äñx‚ÇÅ - x‚Äñ := by let ‚ü®u,v‚ü© := hyp_x‚ÇÅ; linarith
              _ = ‚Äñx‚Äñ - ‚Äñ-(x‚ÇÅ - x)‚Äñ := by rw [h, neg_sub, sub_right_inj]; apply norm_sub_rev
              _ ‚â§ ‚Äñx - (-(x‚ÇÅ - x))‚Äñ := by apply norm_sub_norm_le
              _ = ‚Äñx‚ÇÅ‚Äñ := by rw [neg_sub, sub_sub_cancel]

        ¬∑ simp only [Set.EqOn.eq_1, Set.mem_inter_iff,
                     Metric.mem_closedBall, dist_zero_right,
                     Metric.mem_ball, and_imp]
          intro x‚ÇÉ hyp_x‚ÇÉ dist_hyp
          have : x‚ÇÉ ‚àà Metric.closedBall 0 R ‚à© Metric.ball x (R / 2) := by
            apply Set.mem_inter
            ¬∑ rw [Metric.mem_closedBall, dist_zero_right]; exact hyp_x‚ÇÉ
            ¬∑ rw [Metric.mem_ball]; exact dist_hyp
          exact Z x‚ÇÉ this

  ¬∑ use Metric.ball 0 R
    constructor
    ¬∑ exact Metric.isOpen_ball
    ¬∑ constructor
      ¬∑ simp only [ball_eq, sub_zero, Set.mem_setOf_eq]
        simp only [Metric.mem_closedBall, dist_zero_right] at x_hyp
        apply lt_of_le_of_ne x_hyp
        ¬∑ rw [ne_eq]; exact h
      ¬∑ have si : Metric.closedBall (0 : ‚ÑÇ) R ‚à© Metric.ball (0 : ‚ÑÇ) R = Metric.ball (0 : ‚ÑÇ) R := by
          apply Set.inter_eq_self_of_subset_right
          rw [Metric.mem_closedBall, dist_zero_right] at x_hyp
          exact Metric.ball_subset_closedBall
        rw [si]
        apply AnalyticOn.divRemovableZero
        ¬∑ apply Metric.ball_mem_nhds; positivity
        ¬∑ exact zero
        ¬∑ apply Metric.isOpen_ball
        ¬∑ apply AnalyticOn.mono analytic Metric.ball_subset_closedBall

/-- Given `f` we define the `schwarzQuotient` as `(divRemovableZero f z) / (2 * M - f z)`. -/
noncomputable abbrev schwartzQuotient (f : ‚ÑÇ ‚Üí ‚ÑÇ) (M : ‚Ñù) : ‚ÑÇ ‚Üí ‚ÑÇ :=
  fun z ‚Ü¶ (divRemovableZero f z) / (2 * M - f z)

/-- Given `f` analytic in `|z| ‚â§ R`, the Schwartz quotient
`(f(z) / z) / (2 * M - f(z))` is analytic in `|z| ‚â§ R` provided
that `f 0 = 0` and `2 * M - f z ‚â† 0` on the entire domain `|z| ‚â§ R` -/
lemma AnalyticOn.schwartzQuotient {f : ‚ÑÇ ‚Üí ‚ÑÇ} {R : ‚Ñù} (M : ‚Ñù)
    (Rpos : 0 < R) (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
    (nonzero : ‚àÄ z ‚àà Metric.closedBall 0 R, 2 * M - f z ‚â† 0)
    (zero : f 0 = 0) : AnalyticOn ‚ÑÇ (schwartzQuotient f M) (Metric.closedBall 0 R) := by

  have sInNhds0 : Metric.closedBall 0 R ‚àà nhds 0 := by
    apply Metric.closedBall_mem_nhds; exact Rpos

  exact AnalyticOn.div
    (AnalyticOn.divRemovableZero_closedBall Rpos analytic zero)
    (AnalyticOn.sub (analyticOn_const) analytic) nonzero

/-- If `x.re ‚â§ M` then `|x| ‚â§ |2 * M - x|` -/
lemma Complex.norm_le_norm_two_mul_sub_of_re_le {M : ‚Ñù} {x : ‚ÑÇ}
    (Mpos : 0 < M) (hyp_re_x : x.re ‚â§ M) : ‚Äñx‚Äñ ‚â§ ‚Äñ2 * M - x‚Äñ := by
  rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (by positivity)]
  repeat rw [Complex.sq_norm, Complex.normSq_apply]
  simp only [sub_re, mul_re, re_ofNat, ofReal_re,
             im_ofNat, ofReal_im, mul_zero, sub_zero,
             sub_im, mul_im, zero_mul, add_zero, zero_sub,
             mul_neg, neg_mul, neg_neg, add_le_add_iff_right]
  ring_nf
  simp only [add_comm (-(x.re * M * 4)) (x.re ^ 2), sq M,
             add_assoc, le_add_iff_nonneg_right (x.re ^ 2),
             le_neg_add_iff_add_le, add_zero, Nat.ofNat_pos,
             mul_le_mul_iff_left‚ÇÄ, mul_le_mul_iff_left‚ÇÄ Mpos]
  exact hyp_re_x

/-- Specialization of the maximum modulus to `|z| ‚â§ R`: if `f` is analytic
on `|z| ‚â§ R` and `|f z| ‚â§ C` for all `|z| = R` then for any `r ‚â§ R` and
for all `|z| ‚â§ r`, `|f z| ‚â§ C` -/
lemma AnalyticOn.norm_le_of_norm_le_on_sphere {f : ‚ÑÇ ‚Üí ‚ÑÇ} {C R r : ‚Ñù}
    (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
    (hyp_r : r ‚â§ R) (cond : ‚àÄ z ‚àà Metric.sphere 0 r, ‚Äñf z‚Äñ ‚â§ C)
    (w : ‚ÑÇ) (wInS : w ‚àà Metric.closedBall 0 r) : ‚Äñf w‚Äñ ‚â§ C := by
  apply Complex.norm_le_of_forall_mem_frontier_norm_le
    (U := Metric.closedBall 0 r) (Metric.isBounded_closedBall)
  ¬∑ apply DifferentiableOn.diffContOnCl; rw [Metric.closure_closedBall]
    apply AnalyticOn.differentiableOn
    apply AnalyticOn.mono (ùïú := ‚ÑÇ) analytic
    ¬∑ apply Metric.closedBall_subset_closedBall; linarith
  ¬∑ rw [frontier_closedBall']; exact cond
  ¬∑ rw [Metric.closure_closedBall]; exact wInS

/-- The Borel-Caratheodory theorem (first version): If `f` is analytic in `|z| ‚â§ R`
and `f 0 = 0` and `(f z).re ‚â§ M` for all `|z| ‚â§ R` then for any
`0 < r < R` and all `|z| ‚â§ r` we have `|f(z)| ‚â§ 2 * M * r / (R - r)` -/
theorem borelCaratheodory_closedBall {M R r : ‚Ñù} {z : ‚ÑÇ} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (Rpos : 0 < R) (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
    (zeroAtZero : f 0 = 0) (Mpos : 0 < M)
    (realPartBounded : ‚àÄ z ‚àà Metric.closedBall 0 R, (f z).re ‚â§ M)
    (hyp_r : r < R) (hyp_z : z ‚àà Metric.closedBall 0 r)
    : ‚Äñf z‚Äñ ‚â§ (2 * M * r) / (R - r) := by

  have zInSFunc : ‚àÄ r ‚â§ R, ‚àÄ z ‚àà Metric.sphere (0 : ‚ÑÇ) r, z ‚àà Metric.closedBall (0 : ‚ÑÇ) R := by
    intro r hyp_r z hyp_z
    apply Set.mem_of_mem_of_subset (s := Metric.sphere 0 r) hyp_z
    ¬∑ calc Metric.sphere (0 : ‚ÑÇ) r
        _ ‚äÜ Metric.closedBall (0 : ‚ÑÇ) r := Metric.sphere_subset_closedBall
        _ ‚äÜ Metric.closedBall (0 : ‚ÑÇ) R := Metric.closedBall_subset_closedBall hyp_r

  have fPosAll : ‚àÄ z ‚àà Metric.closedBall 0 R, 2 * M - f z ‚â† 0 := by
    intro z zInS
    exact Complex.ne_zero_of_re_pos (by
      rw [Complex.sub_re, Complex.mul_re, Complex.re_ofNat,
          Complex.ofReal_re, Complex.im_ofNat, Complex.ofReal_im,
          mul_zero, sub_zero, sub_pos]
      linarith [realPartBounded z zInS])

  have schwartzQuotientBounded : ‚àÄ z ‚àà Metric.sphere 0 R, ‚ÄñschwartzQuotient f M z‚Äñ ‚â§ 1 / R := by
    intro z hyp_z
    have zNe0 : z ‚â† 0 := by
      rw [mem_sphere_zero_iff_norm] at hyp_z
      exact ne_zero_of_norm_ne_zero (by linarith)
    have zInS : z ‚àà Metric.closedBall 0 R := zInSFunc R (by rfl) z hyp_z
    rw [mem_sphere_iff_norm, sub_zero] at hyp_z

    calc ‚ÄñschwartzQuotient f M z‚Äñ
      _ = (‚Äñf z‚Äñ / ‚Äñz‚Äñ) / ‚Äñ2 * M - f z‚Äñ := by
        simp only [Complex.norm_div, divRemovableZero_of_ne_zero f zNe0]
      _ ‚â§ (‚Äñf z‚Äñ / ‚Äñz‚Äñ) / ‚Äñf z‚Äñ := by
        by_cases h : ‚Äñf z‚Äñ = 0;
        ¬∑ simp only [h, zero_div, div_zero, le_refl]
        ¬∑ exact div_le_div_of_nonneg_left (by positivity) (by positivity)
            (Complex.norm_le_norm_two_mul_sub_of_re_le Mpos (realPartBounded z zInS))
      _ ‚â§ (1 / ‚Äñz‚Äñ) := by
        by_cases h : ‚Äñf z‚Äñ = 0
        ¬∑ rw [h, zero_div, div_zero, one_div, inv_nonneg]; apply norm_nonneg
        ¬∑ rw [div_div, mul_comm, ‚Üê div_div, div_self]; exact h
      _ = 1 / R := by rw [hyp_z]

  have maxMod : ‚àÄ z ‚àà Metric.closedBall 0 R, ‚ÄñschwartzQuotient f M z‚Äñ ‚â§ 1 / R := by
    exact AnalyticOn.norm_le_of_norm_le_on_sphere
      (AnalyticOn.schwartzQuotient M Rpos analytic fPosAll zeroAtZero)
      (by rfl) schwartzQuotientBounded

  have boundForF : ‚àÄ r < R, 0 < r ‚Üí ‚àÄ z ‚àà Metric.sphere 0 r, ‚Äñf z‚Äñ ‚â§ 2 * M * r / (R - r) := by
    intro r hyp_r r_pos z zOnR
    have zInS : z ‚àà Metric.closedBall 0 R := zInSFunc r (by linarith) z (zOnR)
    rw [mem_sphere_zero_iff_norm] at zOnR
    have := maxMod z zInS
    unfold schwartzQuotient at this
    have U : z ‚â† 0 := by rw [‚Üê norm_pos_iff]; linarith
    rw [divRemovableZero_of_ne_zero f U] at this
    simp only [Complex.norm_div, one_div] at this
    have U : 0 < r * ‚Äñ2 * M - f z‚Äñ := by
      simp only [r_pos, mul_pos_iff_of_pos_left, norm_pos_iff,
                 ne_eq, fPosAll z zInS, not_false_eq_true]
    rw [zOnR, div_div, div_le_iff‚ÇÄ' U] at this
    have U0 : ‚Äñf z‚Äñ ‚â§ 2 * M * r / R + ( r / R ) * ‚Äñf z‚Äñ := by
      calc ‚Äñf z‚Äñ
        _ ‚â§ r * ‚Äñ2 * M - f z‚Äñ * R‚Åª¬π := this
        _ ‚â§ r * (‚Äñ(2 : ‚ÑÇ) * M‚Äñ + ‚Äñf z‚Äñ) * R‚Åª¬π := by
          gcongr; apply norm_sub_le (E := ‚ÑÇ) ((2 : ‚ÑÇ) * ‚ÜëM) (f z)
        _ = r * (2 * M + ‚Äñf z‚Äñ) * R‚Åª¬π := by
          have U : ‚Äñ(2 : ‚ÑÇ) * M‚Äñ = 2 * M := by
            simp only [Complex.norm_mul, Complex.norm_ofNat,
                       Complex.norm_real, Real.norm_eq_abs,
                       mul_eq_mul_left_iff, abs_eq_self,
                       OfNat.ofNat_ne_zero, or_false]
            linarith
          rw [U]
        _ = 2 * M * r / R + (r / R) * ‚Äñf z‚Äñ := by ring_nf
    have U1 : ‚Äñf z‚Äñ - ‚Äñf z‚Äñ * (r * R‚Åª¬π) = ‚Äñf z‚Äñ * (1 - r * R‚Åª¬π) := by ring
    have U2 : (0 : ‚Ñù) < 1 - r * R‚Åª¬π := by
      have U1 : 0 < R := by linarith
      have U : r * R‚Åª¬π < 1 := by simp only [‚Üê div_lt_one‚ÇÄ U1] at hyp_r; exact hyp_r
      linarith
    have U3 : r * R‚Åª¬π * M * 2 / (1 - r * R‚Åª¬π) = 2 * M * r / (R - r) := by
      have : R ‚â† 0 := by linarith
      rw [‚Üê mul_div_mul_left (r * R‚Åª¬π * M * (2 : ‚Ñù)) ((1 : ‚Ñù) - r * R‚Åª¬π) this ];
      ring_nf
      have U : R * r * R‚Åª¬π = r := by
        rw [mul_comm, ‚Üê mul_assoc, ‚Üê mul_comm R R‚Åª¬π,
            CommGroupWithZero.mul_inv_cancel R this, one_mul]
      rw [U]

    rw [‚Üê sub_le_sub_iff_right ((r / R) * ‚Äñf z‚Äñ)] at U0; ring_nf at U0
    rw [mul_assoc, U1, ‚Üê le_div_iff‚ÇÄ U2, U3] at U0
    exact U0

  have maxBoundForF : ‚àÄ r < R, 0 < r ‚Üí ‚àÄ z ‚àà Metric.closedBall 0 r,
  ‚Äñf z‚Äñ ‚â§ 2 * M * r / (R - r) := by
    intro r hyp_r pos_r
    exact AnalyticOn.norm_le_of_norm_le_on_sphere analytic
      (by linarith) (boundForF r hyp_r pos_r)

  by_cases pos_r : r = 0
  ¬∑ have U : z = 0 := by
      rw [pos_r, Metric.closedBall_zero, Set.mem_singleton_iff] at hyp_z
      exact hyp_z
    rw [U, pos_r]; rw [mul_zero, sub_zero, zero_div, norm_le_zero_iff]; exact zeroAtZero
  ¬∑ have U : 0 ‚â§ r := by
      rw [mem_closedBall_iff_norm, sub_zero] at hyp_z; linarith [norm_nonneg z]
    exact maxBoundForF r (by linarith)
      (by
        apply lt_of_le_of_ne U
        ¬∑ rw [ne_eq, eq_comm]; exact pos_r) z hyp_z

/-- The Borel-Caratheodory theorem (second version): If `f` is analytic in `|z| ‚â§ R`
and `(f z).re ‚â§ M` for all `|z| ‚â§ R` then for any `0 < r < R` and all
`|z| ‚â§ r` we have `|f(z)| ‚â§ 2 * M * r / (R - r) + |f 0| * (R + r) / (R - r) -/
theorem borelCaratheodory_closedBall' {M R r : ‚Ñù} {z : ‚ÑÇ} {f : ‚ÑÇ ‚Üí ‚ÑÇ}
    (Rpos : 0 < R) (analytic : AnalyticOn ‚ÑÇ f (Metric.closedBall 0 R))
    (Mpos : 0 < M) (realPartBounded : ‚àÄ z ‚àà Metric.closedBall 0 R, (f z).re ‚â§ M)
    (hyp_r : r < R) (hyp_z : z ‚àà Metric.closedBall 0 r)
    : ‚Äñf z‚Äñ ‚â§ (2 * M * r) / (R - r) + ‚Äñf 0‚Äñ * (R + r) / (R - r) := by
  have Z : ‚Äñf z - f 0‚Äñ ‚â§ (2 * (M + ‚Äñf 0‚Äñ) * r) / (R - r) := by
    apply borelCaratheodory_closedBall (f := fun z ‚Ü¶ f z - f 0)
    ¬∑ exact Rpos
    ¬∑ apply AnalyticOn.sub analytic analyticOn_const
    ¬∑ rw [sub_self]
    ¬∑ positivity
    ¬∑ intro z hyp_z
      calc (f z - f 0).re
        _ ‚â§ (f z).re + (- (f 0)).re := by
          rw [Complex.sub_re, Complex.neg_re, le_add_neg_iff_add_le, sub_add_cancel]
        _ ‚â§ M + (- (f 0)).re := by gcongr; apply realPartBounded; exact hyp_z
        _ ‚â§ M + ‚Äñ- (f 0)‚Äñ := by gcongr; apply Complex.re_le_norm
        _ = M + ‚Äñf 0‚Äñ := by rw [norm_neg]
    ¬∑ exact hyp_r
    ¬∑ exact hyp_z

  have T : R - r ‚â† 0 := by linarith

  calc ‚Äñf z‚Äñ
    _ ‚â§ ‚Äñf z - f 0‚Äñ + ‚Äñf 0‚Äñ := by apply norm_le_norm_sub_add
    _ ‚â§ 2 * (M + ‚Äñf 0‚Äñ) * r / (R - r) + ‚Äñf 0‚Äñ := by gcongr
    _ = 2 * (M + ‚Äñf 0‚Äñ) * r / (R - r) + ‚Äñf 0‚Äñ * 1 := by rw [mul_one]
    _ = 2 * (M + ‚Äñf 0‚Äñ) * r / (R - r) + ‚Äñf 0‚Äñ * ((R - r) / (R - r)) := by congr; rw [‚Üê div_self T]
    _ = 2 * M * r / (R - r) + ‚Äñf 0‚Äñ * (R + r) / (R - r) := by ring_nf
