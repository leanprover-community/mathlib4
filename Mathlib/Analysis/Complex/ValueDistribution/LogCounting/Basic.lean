/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
module

public import Mathlib.Analysis.Complex.JensenFormula

/-!
# The Logarithmic Counting Function of Value Distribution Theory

For nontrivially normed fields `ùïú`, this file defines the logarithmic counting function of a
meromorphic function defined on `ùïú`.  Also known as the `Nevanlinna counting function`, this is one
of the three main functions used in Value Distribution Theory.

The logarithmic counting function of a meromorphic function `f` is a logarithmically weighted
measure of the number of times the function `f` takes a given value `a` within the disk `‚à£z‚à£ ‚â§ r`,
taking multiplicities into account.

See Section VI.1 of [Lang, *Introduction to Complex Hyperbolic Spaces*][MR886677] or Section 1.1 of
[Noguchi-Winkelmann, *Nevanlinna Theory in Several Complex Variables and Diophantine
Approximation*][MR3156076] for a detailed discussion.

## Implementation Notes

- This file defines the logarithmic counting function first for functions with locally finite
  support on `ùïú` and then specializes to the setting where the function with locally finite support
  is the pole or zero-divisor of a meromorphic function.

- Even though value distribution theory is best developed for meromorphic functions on the complex
  plane (and therefore placed in the complex analysis section of Mathlib), we introduce the
  logarithmic counting function for arbitrary normed fields.

## TODO

- Discuss the logarithmic counting function for rational functions, add a forward reference to the
  upcoming converse, formulated in terms of the Nevanlinna height.
-/

@[expose] public section

open Filter Function MeromorphicOn Metric Real Set

/-!
## Supporting Notation
-/

namespace Function.locallyFinsuppWithin

variable {E : Type*} [NormedAddCommGroup E]

/--
Shorthand notation for the restriction of a function with locally finite support to the closed unit
ball of radius `r`.
-/
noncomputable def toClosedBall (r : ‚Ñù) :
    locallyFinsupp E ‚Ñ§ ‚Üí+ locallyFinsuppWithin (closedBall (0 : E) |r|) ‚Ñ§ := by
  apply restrictMonoidHom
  tauto

@[simp]
lemma toClosedBall_eval_within {r : ‚Ñù} {z : E} (f : locallyFinsupp E ‚Ñ§)
    (ha : z ‚àà closedBall 0 |r|) :
    toClosedBall r f z = f z := by
  unfold toClosedBall
  simp_all [restrict_apply]

@[simp]
lemma toClosedBall_divisor {r : ‚Ñù} {f : ‚ÑÇ ‚Üí ‚ÑÇ} (h : Meromorphic f) :
    (divisor f (closedBall 0 |r|)) = (locallyFinsuppWithin.toClosedBall r) (divisor f univ) := by
  simp_all [locallyFinsuppWithin.toClosedBall]

lemma toClosedBall_support_subset_closedBall {E : Type*} [NormedAddCommGroup E] {r : ‚Ñù}
    (f : locallyFinsupp E ‚Ñ§) :
    (toClosedBall r f).support ‚äÜ closedBall 0 |r| := by
  simp_all [toClosedBall, restrict_apply]

/-!
## The Logarithmic Counting Function of a Function with Locally Finite Support
-/

/--
Definition of the logarithmic counting function, as a group morphism mapping functions `D` with
locally finite support to maps `‚Ñù ‚Üí ‚Ñù`.  Given `D`, the result map `logCounting D` takes `r : ‚Ñù` to
a logarithmically weighted measure of values that `D` takes within the disk `‚à£z‚à£ ‚â§ r`.

Implementation Note: In case where `z = 0`, the term `log (r * ‚Äñz‚Äñ‚Åª¬π)` evaluates to zero, which is
typically different from `log r - log ‚Äñz‚Äñ = log r`. The summand `(D 0) * log r` compensates this,
producing cleaner formulas when the logarithmic counting function is used in the main theorems of
Value Distribution Theory.  We refer the reader to page 164 of [Lang: Introduction to Complex
Hyperbolic Spaces](https://link.springer.com/book/10.1007/978-1-4757-1945-1) for more details, and
to the lemma `countingFunction_finsum_eq_finsum_add` in
`Mathlib/Analysis/Complex/JensenFormula.lean` for a formal statement.
-/
noncomputable def logCounting {E : Type*} [NormedAddCommGroup E] [ProperSpace E] :
    locallyFinsupp E ‚Ñ§ ‚Üí+ (‚Ñù ‚Üí ‚Ñù) where
  toFun D := fun r ‚Ü¶ ‚àë·∂† z, D.toClosedBall r z * log (r * ‚Äñz‚Äñ‚Åª¬π) + (D 0) * log r
  map_zero' := by aesop
  map_add' D‚ÇÅ D‚ÇÇ := by
    simp only [map_add, coe_add, Pi.add_apply, Int.cast_add]
    ext r
    have {A B C D : ‚Ñù} : A + B + (C + D) = A + C + (B + D) := by ring
    rw [Pi.add_apply, this]
    congr 1
    ¬∑ have h‚ÇÅs : ((D‚ÇÅ.toClosedBall r).support ‚à™ (D‚ÇÇ.toClosedBall r).support).Finite := by
        apply Set.finite_union.2
        constructor
        <;> apply finiteSupport _ (isCompact_closedBall 0 |r|)
      repeat
        rw [finsum_eq_sum_of_support_subset (s := h‚ÇÅs.toFinset)]
        try simp_rw [‚Üê Finset.sum_add_distrib, ‚Üê add_mul]
      repeat
        intro x hx
        by_contra
        simp_all
    ¬∑ ring

/--
Evaluation of the logarithmic counting function at zero yields zero.
-/
@[simp] lemma logCounting_eval_zero {E : Type*} [NormedAddCommGroup E] [ProperSpace E]
    (D : locallyFinsupp E ‚Ñ§) :
    logCounting D 0 = 0 := by
  simp [logCounting]

/--
The logarithmic counting function is even.
-/
lemma logCounting_even [ProperSpace E] (D : locallyFinsupp E ‚Ñ§) :
    (logCounting D).Even := fun r ‚Ü¶ by simp [logCounting, toClosedBall, restrict_apply]

/--
The logarithmic counting function is monotonous.
-/
lemma logCounting_mono [ProperSpace E] {D : locallyFinsupp E ‚Ñ§} (hD : 0 ‚â§ D) :
    MonotoneOn (logCounting D) (Ioi 0) := by
  intro a ha b hb _
  simp_all only [mem_Ioi, logCounting, AddMonoidHom.coe_mk, ZeroHom.coe_mk]
  gcongr
  ¬∑ let s := (toClosedBall b D).support
    have hs : s.Finite := (toClosedBall b D).finiteSupport (isCompact_closedBall 0 |b|)
    repeat rw [finsum_eq_sum_of_support_subset (s := hs.toFinset)]
    ¬∑ gcongr 1 with z hz
      by_cases h‚ÇÇz : z = 0
      ¬∑ simp [h‚ÇÇz]
      ¬∑ have := (toClosedBall_support_subset_closedBall D (hs.mem_toFinset.1 hz))
        rw [toClosedBall_eval_within _ this]
        by_cases h‚ÇÉz : z ‚àà closedBall 0 |a|
        ¬∑ rw [toClosedBall_eval_within _ h‚ÇÉz]
          gcongr
          exact Int.cast_nonneg (hD z)
        ¬∑ simp only [h‚ÇÉz, not_false_eq_true, apply_eq_zero_of_notMem, Int.cast_zero, zero_mul,
            ge_iff_le]
          apply mul_nonneg (Int.cast_nonneg (hD z)) (log_nonneg _)
          apply (le_mul_inv_iff‚ÇÄ (norm_pos_iff.mpr h‚ÇÇz)).2
          simp_all [abs_of_pos hb]
    ¬∑ intro z
      aesop
    ¬∑ intro z
      simp only [support_mul, mem_inter_iff, mem_support, ne_eq, Int.cast_eq_zero, log_eq_zero,
        mul_eq_zero, inv_eq_zero, norm_eq_zero, not_or, Finite.coe_toFinset, and_imp, s]
      intro h‚ÇÅ _ _ _ _
      have : z ‚àà closedBall 0 |a| := mem_of_indicator_ne_zero h‚ÇÅ
      rw [toClosedBall_eval_within _ this] at h‚ÇÅ
      rwa [toClosedBall_eval_within]
      ¬∑ simp_all only [abs_of_pos ha, mem_closedBall, dist_zero_right, abs_of_pos hb]
        linarith
  ¬∑ exact Int.cast_nonneg (hD 0)

/--
For `1 ‚â§ r`, the logarithmic counting function is non-negative.
-/
theorem logCounting_nonneg {E : Type*} [NormedAddCommGroup E] [ProperSpace E]
    {f : locallyFinsupp E ‚Ñ§} {r : ‚Ñù} (h : 0 ‚â§ f) (hr : 1 ‚â§ r) :
    0 ‚â§ logCounting f r := by
  have h‚ÇÉr : 0 < r := by linarith
  suffices ‚àÄ z, 0 ‚â§ toClosedBall r f z * log (r * ‚Äñz‚Äñ‚Åª¬π) from
    add_nonneg (finsum_nonneg this) <| mul_nonneg (by simpa using h 0) (log_nonneg hr)
  intro a
  by_cases h‚ÇÅa : a = 0
  ¬∑ simp_all
  by_cases h‚ÇÇa : a ‚àà closedBall 0 |r|
  ¬∑ refine mul_nonneg ?_ <| log_nonneg ?_
    ¬∑ simpa [h‚ÇÇa] using h a
    ¬∑ simpa [mul_comm r, one_le_inv_mul‚ÇÄ (norm_pos_iff.mpr h‚ÇÅa), abs_of_pos h‚ÇÉr] using h‚ÇÇa
  ¬∑ simp [apply_eq_zero_of_notMem ((toClosedBall r) _) h‚ÇÇa]

/--
For `1 ‚â§ r`, the logarithmic counting function respects the `‚â§` relation.
-/
theorem logCounting_le {E : Type*} [NormedAddCommGroup E] [ProperSpace E]
    {f‚ÇÅ f‚ÇÇ : locallyFinsupp E ‚Ñ§} {r : ‚Ñù} (h : f‚ÇÅ ‚â§ f‚ÇÇ) (hr : 1 ‚â§ r) :
    logCounting f‚ÇÅ r ‚â§ logCounting f‚ÇÇ r := by
  rw [‚Üê sub_nonneg] at h ‚ä¢
  simpa using logCounting_nonneg h hr

/--
The logarithmic counting function respects the `‚â§` relation asymptotically.
-/
theorem logCounting_eventuallyLE {E : Type*} [NormedAddCommGroup E] [ProperSpace E]
    {f‚ÇÅ f‚ÇÇ : locallyFinsupp E ‚Ñ§} (h : f‚ÇÅ ‚â§ f‚ÇÇ) :
    logCounting f‚ÇÅ ‚â§·∂†[atTop] logCounting f‚ÇÇ := by
  filter_upwards [eventually_ge_atTop 1] using fun _ hr ‚Ü¶ logCounting_le h hr

@[deprecated (since := "2025-12-11")] alias logCounting_eventually_le := logCounting_eventuallyLE

end Function.locallyFinsuppWithin

/-!
## The Logarithmic Counting Function of a Meromorphic Function
-/

namespace ValueDistribution

variable
  {ùïú : Type*} [NontriviallyNormedField ùïú] [ProperSpace ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {U : Set ùïú} {f g : ùïú ‚Üí E} {a : WithTop E} {a‚ÇÄ : E}

variable (f a) in
/--
The logarithmic counting function of a meromorphic function.

If `f : ùïú ‚Üí E` is meromorphic and `a : WithTop E` is any value, this is a logarithmically weighted
measure of the number of times the function `f` takes a given value `a` within the disk `‚à£z‚à£ ‚â§ r`,
taking multiplicities into account.  In the special case where `a = ‚ä§`, it counts the poles of `f`.
-/
noncomputable def logCounting : ‚Ñù ‚Üí ‚Ñù := by
  by_cases h : a = ‚ä§
  ¬∑ exact (divisor f univ)‚Åª.logCounting
  ¬∑ exact (divisor (f ¬∑ - a.untop‚ÇÄ) univ)‚Å∫.logCounting

/--
Relation between `ValueDistribution.logCounting` and `locallyFinsuppWithin.logCounting`.
-/
lemma _root_.locallyFinsuppWithin.logCounting_divisor {f : ‚ÑÇ ‚Üí ‚ÑÇ} :
    locallyFinsuppWithin.logCounting (divisor f ‚ä§) = logCounting f 0 - logCounting f ‚ä§ := by
  simp [logCounting, ‚Üê locallyFinsuppWithin.logCounting.map_sub]

/--
For finite values `a‚ÇÄ`, the logarithmic counting function `logCounting f a‚ÇÄ` is the logarithmic
counting function for the zeros of `f - a‚ÇÄ`.
-/
lemma logCounting_coe :
    logCounting f a‚ÇÄ = (divisor (f ¬∑ - a‚ÇÄ) univ)‚Å∫.logCounting := by
  simp [logCounting]

/--
For finite values `a‚ÇÄ`, the logarithmic counting function `logCounting f a‚ÇÄ` equals the logarithmic
counting function for the zeros of `f - a‚ÇÄ`.
-/
lemma logCounting_coe_eq_logCounting_sub_const_zero :
    logCounting f a‚ÇÄ = logCounting (f - fun _ ‚Ü¶ a‚ÇÄ) 0 := by
  simp [logCounting]

/--
The logarithmic counting function `logCounting f 0` is the logarithmic counting function associated
with the zero-divisor of `f`.
-/
lemma logCounting_zero :
    logCounting f 0 = (divisor f univ)‚Å∫.logCounting := by
  simp [logCounting]

/--
The logarithmic counting function `logCounting f ‚ä§` is the logarithmic counting function associated
with the pole-divisor of `f`.
-/
lemma logCounting_top :
    logCounting f ‚ä§ = (divisor f univ)‚Åª.logCounting := by
  simp [logCounting]

/--
Evaluation of the logarithmic counting function at zero yields zero.
-/
@[simp] lemma logCounting_eval_zero :
    logCounting f a 0 = 0 := by
  by_cases h : a = ‚ä§ <;> simp [logCounting, h]

/--
The logarithmic counting function associated with the divisor of `f` is the difference between
`logCounting f 0` and `logCounting f ‚ä§`.
-/
theorem log_counting_zero_sub_logCounting_top {f : ùïú ‚Üí E} :
    (divisor f univ).logCounting = logCounting f 0 - logCounting f ‚ä§ := by
  rw [‚Üê posPart_sub_negPart (divisor f univ), logCounting_zero, logCounting_top, map_sub]

/--
The logarithmic counting function of a constant function is zero.
-/
@[simp] theorem logCounting_const {c : E} {e : WithTop E} :
    logCounting (fun _ ‚Ü¶ c : ùïú ‚Üí E) e = 0 := by
  simp [logCounting]

/--
The logarithmic counting function of the constant function zero is zero.
-/
@[simp] theorem logCounting_const_zero {e : WithTop E} :
    logCounting (0 : ùïú ‚Üí E) e = 0 := logCounting_const

/--
The logarithmic counting function is even.
-/
theorem logCounting_even {f : ùïú ‚Üí E} {e : WithTop E} :
    (logCounting f e).Even := by
  intro r
  by_cases h : e = ‚ä§ <;> simp [logCounting, h, locallyFinsuppWithin.logCounting_even _ r]

/--
The logarithmic counting function is monotonous.
-/
theorem logCounting_monotoneOn {f : ùïú ‚Üí E} {e : WithTop E} :
    MonotoneOn (logCounting f e) (Ioi 0) := by
  by_cases h : e = ‚ä§ <;>
    simpa [logCounting, h] using locallyFinsuppWithin.logCounting_mono (by positivity)

/--
For `1 ‚â§ r`, the logarithmic counting function is non-negative.
-/
theorem logCounting_nonneg {r : ‚Ñù} {f : ùïú ‚Üí E} {e : WithTop E} (hr : 1 ‚â§ r) :
    0 ‚â§ logCounting f e r := by
  by_cases h : e = ‚ä§
  ¬∑ simp [logCounting, h, locallyFinsuppWithin.logCounting_nonneg
      (negPart_nonneg (divisor f univ)) hr]
  ¬∑ simp [logCounting, h, locallyFinsuppWithin.logCounting_nonneg
      (posPart_nonneg (divisor (f ¬∑ - e.untop‚ÇÄ) univ)) hr]

/--
The logarithmic counting function is asymptotically non-negative.
-/
theorem logCounting_eventually_nonneg {f : ùïú ‚Üí E} {e : WithTop E} :
    0 ‚â§·∂†[atTop] logCounting f e := by
  filter_upwards [eventually_ge_atTop 1] using fun _ hr ‚Ü¶ by simp [logCounting_nonneg hr]

/-!
## Elementary Properties of the Logarithmic Counting Function
-/

/--
If two functions differ only on a discrete set, then their logarithmic counting
functions agree.
-/
theorem logCounting_congr_codiscrete [NormedSpace ‚ÑÇ E] {f g : ‚ÑÇ ‚Üí E} (hfg : f =·∂†[codiscrete ‚ÑÇ] g) :
    logCounting f = logCounting g := by
  ext a : 1
  by_cases h : a = ‚ä§
  ¬∑ simp only [logCounting, h, ‚ÜìreduceDIte]
    congr 2
    exact divisor_congr_codiscreteWithin hfg isOpen_univ
  ¬∑ simp only [logCounting, h, ‚ÜìreduceDIte]
    congr 2
    apply divisor_congr_codiscreteWithin _ isOpen_univ
    filter_upwards [hfg] using by simp

/--
Relation between the logarithmic counting functions of `f` and of `f‚Åª¬π`.
-/
@[simp] theorem logCounting_inv {f : ùïú ‚Üí ùïú} :
     logCounting f‚Åª¬π ‚ä§ = logCounting f 0 := by
  simp [logCounting_zero, logCounting_top]

/--
Adding an analytic function does not change the logarithmic counting function for the poles.
-/
theorem logCounting_add_analyticOn (hf : Meromorphic f) (hg : AnalyticOn ùïú g univ) :
    logCounting (f + g) ‚ä§ = logCounting f ‚ä§ := by
  simp only [logCounting, ‚ÜìreduceDIte]
  rw [hf.meromorphicOn.negPart_divisor_add_of_analyticNhdOn_right
    (isOpen_univ.analyticOn_iff_analyticOnNhd.1 hg)]

/--
Special case of `logCounting_add_analyticOn`: Adding a constant does not change the logarithmic
counting function for the poles.
-/
@[simp] theorem logCounting_add_const (hf : Meromorphic f) :
    logCounting (f + fun _ ‚Ü¶ a‚ÇÄ) ‚ä§ = logCounting f ‚ä§ := by
  apply logCounting_add_analyticOn hf analyticOn_const

/--
Special case of `logCounting_add_analyticOn`: Subtracting a constant does not change the logarithmic
counting function for the poles.
-/
@[simp] theorem logCounting_sub_const (hf : Meromorphic f) :
    logCounting (f - fun _ ‚Ü¶ a‚ÇÄ) ‚ä§ = logCounting f ‚ä§ := by
  simpa [sub_eq_add_neg] using logCounting_add_const hf

/-!
## Behaviour under Arithmetic Operations
-/

/--
For `1 ‚â§ r`, the logarithmic counting function for the poles of `f + g` is less than or equal to the
sum of the logarithmic counting functions for the poles of `f` and `g`, respectively.
-/
theorem logCounting_add_top_le {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí E} {r : ‚Ñù} (h‚ÇÅf‚ÇÅ : Meromorphic f‚ÇÅ)
    (h‚ÇÅf‚ÇÇ : Meromorphic f‚ÇÇ) (hr : 1 ‚â§ r) :
    logCounting (f‚ÇÅ + f‚ÇÇ) ‚ä§ r ‚â§ (logCounting f‚ÇÅ ‚ä§ + logCounting f‚ÇÇ ‚ä§) r := by
  simp only [logCounting, ‚ÜìreduceDIte]
  rw [‚Üê locallyFinsuppWithin.logCounting.map_add]
  exact locallyFinsuppWithin.logCounting_le
    (negPart_divisor_add_le_add h‚ÇÅf‚ÇÅ.meromorphicOn h‚ÇÅf‚ÇÇ.meromorphicOn) hr

/--
Asymptotically, the logarithmic counting function for the poles of `f + g` is less than or equal to
the sum of the logarithmic counting functions for the poles of `f` and `g`, respectively.
-/
theorem logCounting_add_top_eventuallyLE {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí E} (h‚ÇÅf‚ÇÅ : Meromorphic f‚ÇÅ)
    (h‚ÇÅf‚ÇÇ : Meromorphic f‚ÇÇ) :
    logCounting (f‚ÇÅ + f‚ÇÇ) ‚ä§ ‚â§·∂†[atTop] logCounting f‚ÇÅ ‚ä§ + logCounting f‚ÇÇ ‚ä§ := by
  filter_upwards [eventually_ge_atTop 1] using fun _ hr ‚Ü¶ logCounting_add_top_le h‚ÇÅf‚ÇÅ h‚ÇÅf‚ÇÇ hr

/--
For `1 ‚â§ r`, the logarithmic counting function for the poles of a sum `‚àë a ‚àà s, f a` is less than or
equal to the sum of the logarithmic counting functions for the poles of the `f ¬∑`.
-/
theorem logCounting_sum_top_le {Œ± : Type*} (s : Finset Œ±) (f : Œ± ‚Üí ùïú ‚Üí E) {r : ‚Ñù}
    (h‚ÇÅf : ‚àÄ a, Meromorphic (f a)) (hr : 1 ‚â§ r) :
    logCounting (‚àë a ‚àà s, f a) ‚ä§ r ‚â§ (‚àë a ‚àà s, (logCounting (f a) ‚ä§)) r := by
  classical
  induction s using Finset.induction with
  | empty =>
    simp
  | insert a s ha hs =>
    rw [Finset.sum_insert ha, Finset.sum_insert ha]
    calc logCounting (f a + ‚àë x ‚àà s, f x) ‚ä§ r
      _ ‚â§ (logCounting (f a) ‚ä§ + logCounting (‚àë x ‚àà s, f x) ‚ä§) r :=
        logCounting_add_top_le (h‚ÇÅf a) (Meromorphic.sum h‚ÇÅf) hr
      _ ‚â§ (logCounting (f a) ‚ä§ + ‚àë x ‚àà s, logCounting (f x) ‚ä§) r :=
        add_le_add (by trivial) hs

/--
Asymptotically, the logarithmic counting function for the poles of a sum `‚àë a ‚àà s, f a` is less than
or equal to the sum of the logarithmic counting functions for the poles of the `f ¬∑`.
-/
theorem logCounting_sum_top_eventuallyLE {Œ± : Type*} (s : Finset Œ±) (f : Œ± ‚Üí ùïú ‚Üí E)
    (h‚ÇÅf : ‚àÄ a, Meromorphic (f a)) :
    logCounting (‚àë a ‚àà s, f a) ‚ä§ ‚â§·∂†[atTop] ‚àë a ‚àà s, (logCounting (f a) ‚ä§) := by
  filter_upwards [eventually_ge_atTop 1] using fun _ hr ‚Ü¶ logCounting_sum_top_le s f h‚ÇÅf hr

/--
For `1 ‚â§ r`, the logarithmis counting function for the zeros of `f * g` is less than or equal to the
sum of the logarithmic counting functions for the zeros of `f` and `g`, respectively.

Note: The statement proven here is found at the top of page 169 of [Lang: Introduction to Complex
Hyperbolic Spaces](https://link.springer.com/book/10.1007/978-1-4757-1945-1) where it is written as
an inequality between functions. This could be interpreted as claiming that the inequality holds for
ALL values of `r`, which is not true. For a counterexample, take `f‚ÇÅ : z ‚Üí z` and `f‚ÇÇ : z ‚Üí z‚Åª¬π`.
Then,

- `logCounting f‚ÇÅ 0 = log`
- `logCounting f‚ÇÇ 0 = 0`
- `logCounting (f‚ÇÅ * f‚ÇÇ) 0 = 0`

But `log r` is negative for small `r`.
-/
theorem logCounting_mul_zero_le {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí ùïú} {r : ‚Ñù} (hr : 1 ‚â§ r)
    (h‚ÇÅf‚ÇÅ : Meromorphic f‚ÇÅ) (h‚ÇÇf‚ÇÅ : ‚àÄ z, meromorphicOrderAt f‚ÇÅ z ‚â† ‚ä§)
    (h‚ÇÅf‚ÇÇ : Meromorphic f‚ÇÇ) (h‚ÇÇf‚ÇÇ : ‚àÄ z, meromorphicOrderAt f‚ÇÇ z ‚â† ‚ä§) :
    logCounting (f‚ÇÅ * f‚ÇÇ) 0 r ‚â§ (logCounting f‚ÇÅ 0 + logCounting f‚ÇÇ 0) r := by
  simp only [logCounting, WithTop.zero_ne_top, reduceDIte, WithTop.untop‚ÇÄ_zero, sub_zero]
  rw [divisor_mul h‚ÇÅf‚ÇÅ.meromorphicOn h‚ÇÅf‚ÇÇ.meromorphicOn (fun z _ ‚Ü¶ h‚ÇÇf‚ÇÅ z) (fun z _ ‚Ü¶ h‚ÇÇf‚ÇÇ z),
    ‚Üê locallyFinsuppWithin.logCounting.map_add]
  apply locallyFinsuppWithin.logCounting_le _ hr
  apply locallyFinsuppWithin.posPart_add

@[deprecated (since := "2025-12-11")] alias logCounting_zero_mul_le := logCounting_mul_zero_le

/--
Asymptotically, the logarithmic counting function for the zeros of `f * g` is less than or equal to
the sum of the logarithmic counting functions for the zeros of `f` and `g`, respectively.
-/
theorem logCounting_mul_zero_eventuallyLE {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí ùïú}
    (h‚ÇÅf‚ÇÅ : Meromorphic f‚ÇÅ) (h‚ÇÇf‚ÇÅ : ‚àÄ z, meromorphicOrderAt f‚ÇÅ z ‚â† ‚ä§)
    (h‚ÇÅf‚ÇÇ : Meromorphic f‚ÇÇ) (h‚ÇÇf‚ÇÇ : ‚àÄ z, meromorphicOrderAt f‚ÇÇ z ‚â† ‚ä§) :
    logCounting (f‚ÇÅ * f‚ÇÇ) 0 ‚â§·∂†[atTop] logCounting f‚ÇÅ 0 + logCounting f‚ÇÇ 0 := by
  filter_upwards [eventually_ge_atTop 1] using
    fun _ hr ‚Ü¶ logCounting_mul_zero_le hr h‚ÇÅf‚ÇÅ h‚ÇÇf‚ÇÅ h‚ÇÅf‚ÇÇ h‚ÇÇf‚ÇÇ

@[deprecated (since := "2025-12-11")]
alias logCounting_zero_mul_eventually_le := logCounting_mul_zero_eventuallyLE

/--
For `1 ‚â§ r`, the logarithmic counting function for the poles of `f * g` is less than or equal to the
sum of the logarithmic counting functions for the poles of `f` and `g`, respectively.
-/
theorem logCounting_mul_top_le {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí ùïú} {r : ‚Ñù} (hr : 1 ‚â§ r)
    (h‚ÇÅf‚ÇÅ : Meromorphic f‚ÇÅ) (h‚ÇÇf‚ÇÅ : ‚àÄ z, meromorphicOrderAt f‚ÇÅ z ‚â† ‚ä§)
    (h‚ÇÅf‚ÇÇ : Meromorphic f‚ÇÇ) (h‚ÇÇf‚ÇÇ : ‚àÄ z, meromorphicOrderAt f‚ÇÇ z ‚â† ‚ä§) :
    logCounting (f‚ÇÅ * f‚ÇÇ) ‚ä§ r ‚â§ (logCounting f‚ÇÅ ‚ä§ + logCounting f‚ÇÇ ‚ä§) r := by
  simp only [logCounting, reduceDIte]
  rw [divisor_mul h‚ÇÅf‚ÇÅ.meromorphicOn h‚ÇÅf‚ÇÇ.meromorphicOn (fun z _ ‚Ü¶ h‚ÇÇf‚ÇÅ z) (fun z _ ‚Ü¶ h‚ÇÇf‚ÇÇ z),
    ‚Üê locallyFinsuppWithin.logCounting.map_add]
  apply locallyFinsuppWithin.logCounting_le _ hr
  apply locallyFinsuppWithin.negPart_add

@[deprecated (since := "2025-12-11")] alias logCounting_top_mul_le := logCounting_mul_top_le

/--
Asymptotically, the logarithmic counting function for the zeros of `f * g` is less than or equal to
the sum of the logarithmic counting functions for the zeros of `f` and `g`, respectively.
-/
theorem logCounting_mul_top_eventuallyLE {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí ùïú}
    (h‚ÇÅf‚ÇÅ : Meromorphic f‚ÇÅ) (h‚ÇÇf‚ÇÅ : ‚àÄ z, meromorphicOrderAt f‚ÇÅ z ‚â† ‚ä§)
    (h‚ÇÅf‚ÇÇ : Meromorphic f‚ÇÇ) (h‚ÇÇf‚ÇÇ : ‚àÄ z, meromorphicOrderAt f‚ÇÇ z ‚â† ‚ä§) :
    logCounting (f‚ÇÅ * f‚ÇÇ) ‚ä§ ‚â§·∂†[atTop] logCounting f‚ÇÅ ‚ä§ + logCounting f‚ÇÇ ‚ä§ := by
  filter_upwards [eventually_ge_atTop 1] using
    fun _ hr ‚Ü¶ logCounting_mul_top_le hr h‚ÇÅf‚ÇÅ h‚ÇÇf‚ÇÅ h‚ÇÅf‚ÇÇ h‚ÇÇf‚ÇÇ

@[deprecated (since := "2025-12-11")]
alias logCounting_top_mul_eventually_le := logCounting_mul_top_eventuallyLE

/--
For natural numbers `n`, the logarithmic counting function for the zeros of `f ^ n` equals `n`
times the logarithmic counting function for the zeros of `f`.
-/
@[simp] theorem logCounting_pow_zero {f : ùïú ‚Üí ùïú} {n : ‚Ñï} (hf : Meromorphic f) :
    logCounting (f ^ n) 0 = n ‚Ä¢ logCounting f 0 := by
  simp [logCounting, divisor_fun_pow hf.meromorphicOn n]

/--
For natural numbers `n`, the logarithmic counting function for the poles of `f ^ n` equals `n` times
the logarithmic counting function for the poles of `f`.
-/
@[simp] theorem logCounting_pow_top {f : ùïú ‚Üí ùïú} {n : ‚Ñï} (hf : Meromorphic f) :
    logCounting (f ^ n) ‚ä§ = n ‚Ä¢ logCounting f ‚ä§ := by
  simp [logCounting, divisor_pow hf.meromorphicOn n]

end ValueDistribution

/-!
## Representation by Integrals

For `ùïú = ‚ÑÇ`, the theorems below describe the logarithmic counting function in terms of circle
averages.
-/

/--
Over the complex numbers, present the logarithmic counting function attached to the divisor of a
meromorphic function `f` as a circle average over `log ‚Äñf ¬∑‚Äñ`.

This is a reformulation of Jensen's formula of complex analysis. See
`MeromorphicOn.circleAverage_log_norm` for Jensen's formula in the original context.
-/
theorem Function.locallyFinsuppWithin.logCounting_divisor_eq_circleAverage_sub_const {R : ‚Ñù}
    {f : ‚ÑÇ ‚Üí ‚ÑÇ} (h : Meromorphic f) (hR : R ‚â† 0) :
    logCounting (divisor f ‚ä§) R =
      circleAverage (log ‚Äñf ¬∑‚Äñ) 0 R - log ‚ÄñmeromorphicTrailingCoeffAt f 0‚Äñ := by
  have h‚ÇÅf : MeromorphicOn f (closedBall 0 |R|) := by tauto
  simp only [MeromorphicOn.circleAverage_log_norm hR h‚ÇÅf, logCounting, top_eq_univ,
    AddMonoidHom.coe_mk, ZeroHom.coe_mk, zero_sub, norm_neg, add_sub_cancel_right]
  congr 1
  ¬∑ simp_all
  ¬∑ rw [divisor_apply, divisor_apply]
    all_goals aesop

/--
Variant of `locallyFinsuppWithin.logCounting_divisor_eq_circleAverage_sub_const`, using
`ValueDistribution.logCounting` instead of `locallyFinsuppWithin.logCounting`.
-/
theorem ValueDistribution.logCounting_zero_sub_logCounting_top_eq_circleAverage_sub_const {R : ‚Ñù}
    {f : ‚ÑÇ ‚Üí ‚ÑÇ} (h : Meromorphic f) (hR : R ‚â† 0) :
    (logCounting f 0 - logCounting f ‚ä§) R =
      circleAverage (log ‚Äñf ¬∑‚Äñ) 0 R - log ‚ÄñmeromorphicTrailingCoeffAt f 0‚Äñ := by
  rw [‚Üê locallyFinsuppWithin.logCounting_divisor]
  exact locallyFinsuppWithin.logCounting_divisor_eq_circleAverage_sub_const h hR
