/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Asymptotics.SpecificAsymptotics
import Mathlib.Analysis.Complex.CauchyIntegral

/-!
# Removable singularity theorem

In this file we prove Riemann's removable singularity theorem: if `f : ‚ÑÇ ‚Üí E` is complex
differentiable in a punctured neighborhood of a point `c` and is bounded in a punctured neighborhood
of `c` (or, more generally, $f(z) - f(c)=o((z-c)^{-1})$), then it has a limit at `c` and the
function `update f c (limUnder (ùìù[‚â†] c) f)` is complex differentiable in a neighborhood of `c`.
-/


open TopologicalSpace Metric Set Filter Asymptotics Function

open scoped Topology Filter NNReal Real

universe u

variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] [CompleteSpace E]

namespace Complex

/-- **Removable singularity** theorem, weak version. If `f : ‚ÑÇ ‚Üí E` is differentiable in a punctured
neighborhood of a point and is continuous at this point, then it is analytic at this point. -/
theorem analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ}
    (hd : ‚àÄ·∂† z in ùìù[‚â†] c, DifferentiableAt ‚ÑÇ f z) (hc : ContinuousAt f c) : AnalyticAt ‚ÑÇ f c := by
  rcases (nhdsWithin_hasBasis nhds_basis_closedBall _).mem_iff.1 hd with ‚ü®R, hR0, hRs‚ü©
  lift R to ‚Ñù‚â•0 using hR0.le
  replace hc : ContinuousOn f (closedBall c R) := by
    refine fun z hz ‚Ü¶ ContinuousAt.continuousWithinAt ?_
    rcases eq_or_ne z c with (rfl | hne)
    exacts [hc, (hRs ‚ü®hz, hne‚ü©).continuousAt]
  exact (hasFPowerSeriesOnBall_of_differentiable_off_countable (countable_singleton c) hc
    (fun z hz ‚Ü¶ hRs (diff_subset_diff_left ball_subset_closedBall hz)) hR0).analyticAt

theorem differentiableOn_compl_singleton_and_continuousAt_iff {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} {c : ‚ÑÇ}
    (hs : s ‚àà ùìù c) :
    DifferentiableOn ‚ÑÇ f (s \ {c}) ‚àß ContinuousAt f c ‚Üî DifferentiableOn ‚ÑÇ f s := by
  refine ‚ü®?_, fun hd ‚Ü¶ ‚ü®hd.mono diff_subset, (hd.differentiableAt hs).continuousAt‚ü©‚ü©
  rintro ‚ü®hd, hc‚ü© x hx
  rcases eq_or_ne x c with (rfl | hne)
  ¬∑ refine (analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt
      ?_ hc).differentiableAt.differentiableWithinAt
    refine eventually_nhdsWithin_iff.2 ((eventually_mem_nhds_iff.2 hs).mono fun z hz hzx ‚Ü¶ ?_)
    exact hd.differentiableAt (inter_mem hz (isOpen_ne.mem_nhds hzx))
  ¬∑ simpa only [DifferentiableWithinAt, HasFDerivWithinAt, hne.nhdsWithin_diff_singleton] using
      hd x ‚ü®hx, hne‚ü©

theorem differentiableOn_dslope {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} {c : ‚ÑÇ} (hc : s ‚àà ùìù c) :
    DifferentiableOn ‚ÑÇ (dslope f c) s ‚Üî DifferentiableOn ‚ÑÇ f s :=
  ‚ü®fun h ‚Ü¶ h.of_dslope, fun h ‚Ü¶
    (differentiableOn_compl_singleton_and_continuousAt_iff hc).mp <|
      ‚ü®Iff.mpr (differentiableOn_dslope_of_notMem fun h ‚Ü¶ h.2 rfl) (h.mono diff_subset),
        continuousAt_dslope_same.2 <| h.differentiableAt hc‚ü©‚ü©

/-- **Removable singularity** theorem: if `s` is a neighborhood of `c : ‚ÑÇ`, a function `f : ‚ÑÇ ‚Üí E`
is complex differentiable on `s \ {c}`, and $f(z) - f(c)=o((z-c)^{-1})$, then `f` redefined to be
equal to `limUnder (ùìù[‚â†] c) f` at `c` is complex differentiable on `s`. -/
theorem differentiableOn_update_limUnder_of_isLittleO {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} {c : ‚ÑÇ} (hc : s ‚àà ùìù c)
    (hd : DifferentiableOn ‚ÑÇ f (s \ {c}))
    (ho : (fun z ‚Ü¶ f z - f c) =o[ùìù[‚â†] c] fun z ‚Ü¶ (z - c)‚Åª¬π) :
    DifferentiableOn ‚ÑÇ (update f c (limUnder (ùìù[‚â†] c) f)) s := by
  set F : ‚ÑÇ ‚Üí E := fun z ‚Ü¶ (z - c) ‚Ä¢ f z
  suffices DifferentiableOn ‚ÑÇ F (s \ {c}) ‚àß ContinuousAt F c by
    rw [differentiableOn_compl_singleton_and_continuousAt_iff hc, ‚Üê differentiableOn_dslope hc,
      dslope_sub_smul] at this
    have hc : Tendsto f (ùìù[‚â†] c) (ùìù (deriv F c)) :=
      continuousAt_update_same.mp (this.continuousOn.continuousAt hc)
    rwa [hc.limUnder_eq]
  refine ‚ü®(differentiableOn_id.sub_const _).smul hd, ?_‚ü©
  rw [‚Üê continuousWithinAt_compl_self]
  have H := ho.tendsto_inv_smul_nhds_zero
  have H' : Tendsto (fun z ‚Ü¶ (z - c) ‚Ä¢ f c) (ùìù[‚â†] c) (ùìù (F c)) :=
    (continuousWithinAt_id.tendsto.sub tendsto_const_nhds).smul tendsto_const_nhds
  simpa [‚Üê smul_add, ContinuousWithinAt] using H.add H'

/-- **Removable singularity** theorem: if `s` is a punctured neighborhood of `c : ‚ÑÇ`, a function
`f : ‚ÑÇ ‚Üí E` is complex differentiable on `s`, and $f(z) - f(c)=o((z-c)^{-1})$, then `f` redefined to
be equal to `limUnder (ùìù[‚â†] c) f` at `c` is complex differentiable on `{c} ‚à™ s`. -/
theorem differentiableOn_update_limUnder_insert_of_isLittleO {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} {c : ‚ÑÇ}
    (hc : s ‚àà ùìù[‚â†] c) (hd : DifferentiableOn ‚ÑÇ f s)
    (ho : (fun z ‚Ü¶ f z - f c) =o[ùìù[‚â†] c] fun z ‚Ü¶ (z - c)‚Åª¬π) :
    DifferentiableOn ‚ÑÇ (update f c (limUnder (ùìù[‚â†] c) f)) (insert c s) :=
  differentiableOn_update_limUnder_of_isLittleO (insert_mem_nhds_iff.2 hc)
    (hd.mono fun _ hz ‚Ü¶ hz.1.resolve_left hz.2) ho

/-- **Removable singularity** theorem: if `s` is a neighborhood of `c : ‚ÑÇ`, a function `f : ‚ÑÇ ‚Üí E`
is complex differentiable and is bounded on `s \ {c}`, then `f` redefined to be equal to
`limUnder (ùìù[‚â†] c) f` at `c` is complex differentiable on `s`. -/
theorem differentiableOn_update_limUnder_of_bddAbove {f : ‚ÑÇ ‚Üí E} {s : Set ‚ÑÇ} {c : ‚ÑÇ} (hc : s ‚àà ùìù c)
    (hd : DifferentiableOn ‚ÑÇ f (s \ {c})) (hb : BddAbove (norm ‚àò f '' (s \ {c}))) :
    DifferentiableOn ‚ÑÇ (update f c (limUnder (ùìù[‚â†] c) f)) s :=
  differentiableOn_update_limUnder_of_isLittleO hc hd <| IsBoundedUnder.isLittleO_sub_self_inv <|
    let ‚ü®C, hC‚ü© := hb
    ‚ü®C + ‚Äñf c‚Äñ, eventually_map.2 <| mem_nhdsWithin_iff_exists_mem_nhds_inter.2
      ‚ü®s, hc, fun _ hz ‚Ü¶ norm_sub_le_of_le (hC <| mem_image_of_mem _ hz) le_rfl‚ü©‚ü©

/-- **Removable singularity** theorem: if a function `f : ‚ÑÇ ‚Üí E` is complex differentiable on a
punctured neighborhood of `c` and $f(z) - f(c)=o((z-c)^{-1})$, then `f` has a limit at `c`. -/
theorem tendsto_limUnder_of_differentiable_on_punctured_nhds_of_isLittleO {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ}
    (hd : ‚àÄ·∂† z in ùìù[‚â†] c, DifferentiableAt ‚ÑÇ f z)
    (ho : (fun z ‚Ü¶ f z - f c) =o[ùìù[‚â†] c] fun z ‚Ü¶ (z - c)‚Åª¬π) :
    Tendsto f (ùìù[‚â†] c) (ùìù <| limUnder (ùìù[‚â†] c) f) := by
  rw [eventually_nhdsWithin_iff] at hd
  have : DifferentiableOn ‚ÑÇ f ({z | z ‚â† c ‚Üí DifferentiableAt ‚ÑÇ f z} \ {c}) := fun z hz ‚Ü¶
    (hz.1 hz.2).differentiableWithinAt
  have H := differentiableOn_update_limUnder_of_isLittleO hd this ho
  exact continuousAt_update_same.1 (H.differentiableAt hd).continuousAt

/-- **Removable singularity** theorem: if a function `f : ‚ÑÇ ‚Üí E` is complex differentiable and
bounded on a punctured neighborhood of `c`, then `f` has a limit at `c`. -/
theorem tendsto_limUnder_of_differentiable_on_punctured_nhds_of_bounded_under {f : ‚ÑÇ ‚Üí E} {c : ‚ÑÇ}
    (hd : ‚àÄ·∂† z in ùìù[‚â†] c, DifferentiableAt ‚ÑÇ f z)
    (hb : IsBoundedUnder (¬∑ ‚â§ ¬∑) (ùìù[‚â†] c) fun z ‚Ü¶ ‚Äñf z - f c‚Äñ) :
    Tendsto f (ùìù[‚â†] c) (ùìù <| limUnder (ùìù[‚â†] c) f) :=
  tendsto_limUnder_of_differentiable_on_punctured_nhds_of_isLittleO hd hb.isLittleO_sub_self_inv

/-- The Cauchy formula for the derivative of a holomorphic function. -/
theorem two_pi_I_inv_smul_circleIntegral_sub_sq_inv_smul_of_differentiable {U : Set ‚ÑÇ}
    (hU : IsOpen U) {c w‚ÇÄ : ‚ÑÇ} {R : ‚Ñù} {f : ‚ÑÇ ‚Üí E} (hc : closedBall c R ‚äÜ U)
    (hf : DifferentiableOn ‚ÑÇ f U) (hw‚ÇÄ : w‚ÇÄ ‚àà ball c R) :
    ((2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ z in C(c, R), ((z - w‚ÇÄ) ^ 2)‚Åª¬π ‚Ä¢ f z) = deriv f w‚ÇÄ := by
  -- We apply the removable singularity theorem and the Cauchy formula to `dslope f w‚ÇÄ`
  have hf' : DifferentiableOn ‚ÑÇ (dslope f w‚ÇÄ) U :=
    (differentiableOn_dslope (hU.mem_nhds ((ball_subset_closedBall.trans hc) hw‚ÇÄ))).mpr hf
  have h0 := (hf'.diffContOnCl_ball hc).two_pi_i_inv_smul_circleIntegral_sub_inv_smul hw‚ÇÄ
  rw [‚Üê dslope_same, ‚Üê h0]
  congr 1
  trans ‚àÆ z in C(c, R), ((z - w‚ÇÄ) ^ 2)‚Åª¬π ‚Ä¢ (f z - f w‚ÇÄ)
  ¬∑ have h1 : ContinuousOn (fun z : ‚ÑÇ ‚Ü¶ ((z - w‚ÇÄ) ^ 2)‚Åª¬π) (sphere c R) := by
      refine ((continuous_id'.sub continuous_const).pow 2).continuousOn.inv‚ÇÄ fun w hw h ‚Ü¶ ?_
      exact sphere_disjoint_ball.ne_of_mem hw hw‚ÇÄ (sub_eq_zero.mp (sq_eq_zero_iff.mp h))
    have h2 : CircleIntegrable (fun z : ‚ÑÇ ‚Ü¶ ((z - w‚ÇÄ) ^ 2)‚Åª¬π ‚Ä¢ f z) c R := by
      refine ContinuousOn.circleIntegrable (pos_of_mem_ball hw‚ÇÄ).le ?_
      exact h1.smul (hf.continuousOn.mono (sphere_subset_closedBall.trans hc))
    have h3 : CircleIntegrable (fun z : ‚ÑÇ ‚Ü¶ ((z - w‚ÇÄ) ^ 2)‚Åª¬π ‚Ä¢ f w‚ÇÄ) c R :=
      ContinuousOn.circleIntegrable (pos_of_mem_ball hw‚ÇÄ).le (h1.smul continuousOn_const)
    have h4 : (‚àÆ z : ‚ÑÇ in C(c, R), ((z - w‚ÇÄ) ^ 2)‚Åª¬π) = 0 := by
      simpa using circleIntegral.integral_sub_zpow_of_ne (by decide : (-2 : ‚Ñ§) ‚â† -1) c w‚ÇÄ R
    simp only [smul_sub, circleIntegral.integral_sub h2 h3, h4, circleIntegral.integral_smul_const,
      zero_smul, sub_zero]
  ¬∑ refine circleIntegral.integral_congr (pos_of_mem_ball hw‚ÇÄ).le fun z hz ‚Ü¶ ?_
    simp only [dslope_of_ne, Metric.sphere_disjoint_ball.ne_of_mem hz hw‚ÇÄ, slope, ‚Üê smul_assoc, sq,
      mul_inv, Ne, not_false_iff, vsub_eq_sub, Algebra.id.smul_eq_mul]

end Complex
