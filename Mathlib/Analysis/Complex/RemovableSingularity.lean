/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Asymptotics.SpecificAsymptotics
import Mathlib.Analysis.Complex.CauchyIntegral

/-!
# Removable singularity theorem

In this file we prove Riemann's removable singularity theorem: if `f : â„‚ â†’ E` is complex
differentiable in a punctured neighborhood of a point `c` and is bounded in a punctured neighborhood
of `c` (or, more generally, $f(z) - f(c)=o((z-c)^{-1})$), then it has a limit at `c` and the
function `update f c (limUnder (ğ“[â‰ ] c) f)` is complex differentiable in a neighborhood of `c`.
-/


open TopologicalSpace Metric Set Filter Asymptotics Function

open scoped Topology Filter NNReal Real

universe u

variable {E : Type u} [NormedAddCommGroup E] [NormedSpace â„‚ E] [CompleteSpace E]

namespace Complex

/-- **Removable singularity** theorem, weak version. If `f : â„‚ â†’ E` is differentiable in a punctured
neighborhood of a point and is continuous at this point, then it is analytic at this point. -/
theorem analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt {f : â„‚ â†’ E} {c : â„‚}
    (hd : âˆ€á¶  z in ğ“[â‰ ] c, DifferentiableAt â„‚ f z) (hc : ContinuousAt f c) : AnalyticAt â„‚ f c := by
  rcases (nhdsWithin_hasBasis nhds_basis_closedBall _).mem_iff.1 hd with âŸ¨R, hR0, hRsâŸ©
  lift R to â„â‰¥0 using hR0.le
  replace hc : ContinuousOn f (closedBall c R) := by
    refine fun z hz => ContinuousAt.continuousWithinAt ?_
    rcases eq_or_ne z c with (rfl | hne)
    exacts [hc, (hRs âŸ¨hz, hneâŸ©).continuousAt]
  exact (hasFPowerSeriesOnBall_of_differentiable_off_countable (countable_singleton c) hc
    (fun z hz => hRs (diff_subset_diff_left ball_subset_closedBall hz)) hR0).analyticAt

theorem differentiableOn_compl_singleton_and_continuousAt_iff {f : â„‚ â†’ E} {s : Set â„‚} {c : â„‚}
    (hs : s âˆˆ ğ“ c) :
    DifferentiableOn â„‚ f (s \ {c}) âˆ§ ContinuousAt f c â†” DifferentiableOn â„‚ f s := by
  refine âŸ¨?_, fun hd => âŸ¨hd.mono diff_subset, (hd.differentiableAt hs).continuousAtâŸ©âŸ©
  rintro âŸ¨hd, hcâŸ© x hx
  rcases eq_or_ne x c with (rfl | hne)
  Â· refine (analyticAt_of_differentiable_on_punctured_nhds_of_continuousAt
      ?_ hc).differentiableAt.differentiableWithinAt
    refine eventually_nhdsWithin_iff.2 ((eventually_mem_nhds_iff.2 hs).mono fun z hz hzx => ?_)
    exact hd.differentiableAt (inter_mem hz (isOpen_ne.mem_nhds hzx))
  Â· simpa only [DifferentiableWithinAt, HasFDerivWithinAt, hne.nhdsWithin_diff_singleton] using
      hd x âŸ¨hx, hneâŸ©

theorem differentiableOn_dslope {f : â„‚ â†’ E} {s : Set â„‚} {c : â„‚} (hc : s âˆˆ ğ“ c) :
    DifferentiableOn â„‚ (dslope f c) s â†” DifferentiableOn â„‚ f s :=
  âŸ¨fun h => h.of_dslope, fun h =>
    (differentiableOn_compl_singleton_and_continuousAt_iff hc).mp <|
      âŸ¨Iff.mpr (differentiableOn_dslope_of_notMem fun h => h.2 rfl) (h.mono diff_subset),
        continuousAt_dslope_same.2 <| h.differentiableAt hcâŸ©âŸ©

/-- **Removable singularity** theorem: if `s` is a neighborhood of `c : â„‚`, a function `f : â„‚ â†’ E`
is complex differentiable on `s \ {c}`, and $f(z) - f(c)=o((z-c)^{-1})$, then `f` redefined to be
equal to `limUnder (ğ“[â‰ ] c) f` at `c` is complex differentiable on `s`. -/
theorem differentiableOn_update_limUnder_of_isLittleO {f : â„‚ â†’ E} {s : Set â„‚} {c : â„‚} (hc : s âˆˆ ğ“ c)
    (hd : DifferentiableOn â„‚ f (s \ {c}))
    (ho : (fun z => f z - f c) =o[ğ“[â‰ ] c] fun z => (z - c)â»Â¹) :
    DifferentiableOn â„‚ (update f c (limUnder (ğ“[â‰ ] c) f)) s := by
  set F : â„‚ â†’ E := fun z => (z - c) â€¢ f z
  suffices DifferentiableOn â„‚ F (s \ {c}) âˆ§ ContinuousAt F c by
    rw [differentiableOn_compl_singleton_and_continuousAt_iff hc, â† differentiableOn_dslope hc,
      dslope_sub_smul] at this
    have hc : Tendsto f (ğ“[â‰ ] c) (ğ“ (deriv F c)) :=
      continuousAt_update_same.mp (this.continuousOn.continuousAt hc)
    rwa [hc.limUnder_eq]
  refine âŸ¨(differentiableOn_id.sub_const _).smul hd, ?_âŸ©
  rw [â† continuousWithinAt_compl_self]
  have H := ho.tendsto_inv_smul_nhds_zero
  have H' : Tendsto (fun z => (z - c) â€¢ f c) (ğ“[â‰ ] c) (ğ“ (F c)) :=
    (continuousWithinAt_id.tendsto.sub tendsto_const_nhds).smul tendsto_const_nhds
  simpa [â† smul_add, ContinuousWithinAt] using H.add H'

/-- **Removable singularity** theorem: if `s` is a punctured neighborhood of `c : â„‚`, a function
`f : â„‚ â†’ E` is complex differentiable on `s`, and $f(z) - f(c)=o((z-c)^{-1})$, then `f` redefined to
be equal to `limUnder (ğ“[â‰ ] c) f` at `c` is complex differentiable on `{c} âˆª s`. -/
theorem differentiableOn_update_limUnder_insert_of_isLittleO {f : â„‚ â†’ E} {s : Set â„‚} {c : â„‚}
    (hc : s âˆˆ ğ“[â‰ ] c) (hd : DifferentiableOn â„‚ f s)
    (ho : (fun z => f z - f c) =o[ğ“[â‰ ] c] fun z => (z - c)â»Â¹) :
    DifferentiableOn â„‚ (update f c (limUnder (ğ“[â‰ ] c) f)) (insert c s) :=
  differentiableOn_update_limUnder_of_isLittleO (insert_mem_nhds_iff.2 hc)
    (hd.mono fun _ hz => hz.1.resolve_left hz.2) ho

/-- **Removable singularity** theorem: if `s` is a neighborhood of `c : â„‚`, a function `f : â„‚ â†’ E`
is complex differentiable and is bounded on `s \ {c}`, then `f` redefined to be equal to
`limUnder (ğ“[â‰ ] c) f` at `c` is complex differentiable on `s`. -/
theorem differentiableOn_update_limUnder_of_bddAbove {f : â„‚ â†’ E} {s : Set â„‚} {c : â„‚} (hc : s âˆˆ ğ“ c)
    (hd : DifferentiableOn â„‚ f (s \ {c})) (hb : BddAbove (norm âˆ˜ f '' (s \ {c}))) :
    DifferentiableOn â„‚ (update f c (limUnder (ğ“[â‰ ] c) f)) s :=
  differentiableOn_update_limUnder_of_isLittleO hc hd <| IsBoundedUnder.isLittleO_sub_self_inv <|
    let âŸ¨C, hCâŸ© := hb
    âŸ¨C + â€–f câ€–, eventually_map.2 <| mem_nhdsWithin_iff_exists_mem_nhds_inter.2
      âŸ¨s, hc, fun _ hz => norm_sub_le_of_le (hC <| mem_image_of_mem _ hz) le_rflâŸ©âŸ©

/-- **Removable singularity** theorem: if a function `f : â„‚ â†’ E` is complex differentiable on a
punctured neighborhood of `c` and $f(z) - f(c)=o((z-c)^{-1})$, then `f` has a limit at `c`. -/
theorem tendsto_limUnder_of_differentiable_on_punctured_nhds_of_isLittleO {f : â„‚ â†’ E} {c : â„‚}
    (hd : âˆ€á¶  z in ğ“[â‰ ] c, DifferentiableAt â„‚ f z)
    (ho : (fun z => f z - f c) =o[ğ“[â‰ ] c] fun z => (z - c)â»Â¹) :
    Tendsto f (ğ“[â‰ ] c) (ğ“ <| limUnder (ğ“[â‰ ] c) f) := by
  rw [eventually_nhdsWithin_iff] at hd
  have : DifferentiableOn â„‚ f ({z | z â‰  c â†’ DifferentiableAt â„‚ f z} \ {c}) := fun z hz =>
    (hz.1 hz.2).differentiableWithinAt
  have H := differentiableOn_update_limUnder_of_isLittleO hd this ho
  exact continuousAt_update_same.1 (H.differentiableAt hd).continuousAt

/-- **Removable singularity** theorem: if a function `f : â„‚ â†’ E` is complex differentiable and
bounded on a punctured neighborhood of `c`, then `f` has a limit at `c`. -/
theorem tendsto_limUnder_of_differentiable_on_punctured_nhds_of_bounded_under {f : â„‚ â†’ E} {c : â„‚}
    (hd : âˆ€á¶  z in ğ“[â‰ ] c, DifferentiableAt â„‚ f z)
    (hb : IsBoundedUnder (Â· â‰¤ Â·) (ğ“[â‰ ] c) fun z => â€–f z - f câ€–) :
    Tendsto f (ğ“[â‰ ] c) (ğ“ <| limUnder (ğ“[â‰ ] c) f) :=
  tendsto_limUnder_of_differentiable_on_punctured_nhds_of_isLittleO hd hb.isLittleO_sub_self_inv

/-- The Cauchy formula for the derivative of a holomorphic function. -/
theorem two_pi_I_inv_smul_circleIntegral_sub_sq_inv_smul_of_differentiable {U : Set â„‚}
    (hU : IsOpen U) {c wâ‚€ : â„‚} {R : â„} {f : â„‚ â†’ E} (hc : closedBall c R âŠ† U)
    (hf : DifferentiableOn â„‚ f U) (hwâ‚€ : wâ‚€ âˆˆ ball c R) :
    ((2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® z in C(c, R), ((z - wâ‚€) ^ 2)â»Â¹ â€¢ f z) = deriv f wâ‚€ := by
  -- We apply the removable singularity theorem and the Cauchy formula to `dslope f wâ‚€`
  have hf' : DifferentiableOn â„‚ (dslope f wâ‚€) U :=
    (differentiableOn_dslope (hU.mem_nhds ((ball_subset_closedBall.trans hc) hwâ‚€))).mpr hf
  have h0 := (hf'.diffContOnCl_ball hc).two_pi_i_inv_smul_circleIntegral_sub_inv_smul hwâ‚€
  rw [â† dslope_same, â† h0]
  congr 1
  trans âˆ® z in C(c, R), ((z - wâ‚€) ^ 2)â»Â¹ â€¢ (f z - f wâ‚€)
  Â· have h1 : ContinuousOn (fun z : â„‚ => ((z - wâ‚€) ^ 2)â»Â¹) (sphere c R) := by
      refine ((continuous_id'.sub continuous_const).pow 2).continuousOn.invâ‚€ fun w hw h => ?_
      exact sphere_disjoint_ball.ne_of_mem hw hwâ‚€ (sub_eq_zero.mp (sq_eq_zero_iff.mp h))
    have h2 : CircleIntegrable (fun z : â„‚ => ((z - wâ‚€) ^ 2)â»Â¹ â€¢ f z) c R := by
      refine ContinuousOn.circleIntegrable (pos_of_mem_ball hwâ‚€).le ?_
      exact h1.smul (hf.continuousOn.mono (sphere_subset_closedBall.trans hc))
    have h3 : CircleIntegrable (fun z : â„‚ => ((z - wâ‚€) ^ 2)â»Â¹ â€¢ f wâ‚€) c R :=
      ContinuousOn.circleIntegrable (pos_of_mem_ball hwâ‚€).le (h1.smul continuousOn_const)
    have h4 : (âˆ® z : â„‚ in C(c, R), ((z - wâ‚€) ^ 2)â»Â¹) = 0 := by
      simpa using circleIntegral.integral_sub_zpow_of_ne (by decide : (-2 : â„¤) â‰  -1) c wâ‚€ R
    simp only [smul_sub, circleIntegral.integral_sub h2 h3, h4, circleIntegral.integral_smul_const,
      zero_smul, sub_zero]
  Â· refine circleIntegral.integral_congr (pos_of_mem_ball hwâ‚€).le fun z hz => ?_
    simp only [dslope_of_ne, Metric.sphere_disjoint_ball.ne_of_mem hz hwâ‚€, slope, â† smul_assoc, sq,
      mul_inv, Ne, not_false_iff, vsub_eq_sub, Algebra.id.smul_eq_mul]

end Complex
