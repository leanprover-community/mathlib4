/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yourong Zang
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.Linear
import Mathlib.Analysis.Complex.Conformal
import Mathlib.Analysis.Calculus.Conformal.NormedSpace

/-! # Real differentiability of complex-differentiable functions

`HasDerivAt.real_of_complex` expresses that, if a function on `â„‚` is differentiable (over `â„‚`),
then its restriction to `â„` is differentiable over `â„`, with derivative the real part of the
complex derivative.

`DifferentiableAt.conformalAt` states that a real-differentiable function with a nonvanishing
differential from the complex plane into an arbitrary complex-normed space is conformal at a point
if it's holomorphic at that point. This is a version of Cauchy-Riemann equations.

`conformalAt_iff_differentiableAt_or_differentiableAt_comp_conj` proves that a real-differential
function with a nonvanishing differential between the complex plane is conformal at a point if and
only if it's holomorphic or antiholomorphic at that point.

## TODO

* The classical form of Cauchy-Riemann equations
* On a connected open set `u`, a function which is `ConformalAt` each point is either holomorphic
throughout or antiholomorphic throughout.

## Warning

We do NOT require conformal functions to be orientation-preserving in this file.
-/


section RealDerivOfComplex

/-! ### Differentiability of the restriction to `â„` of complex functions -/

open Complex

variable {e : â„‚ â†’ â„‚} {e' : â„‚} {z : â„}

/-- If a complex function is differentiable at a real point, then the induced real function is also
differentiable at this point, with a derivative equal to the real part of the complex derivative. -/
theorem HasStrictDerivAt.real_of_complex (h : HasStrictDerivAt e e' z) :
    HasStrictDerivAt (fun x : â„ => (e x).re) e'.re z := by
  have A : HasStrictFDerivAt ((â†‘) : â„ â†’ â„‚) ofRealCLM z := ofRealCLM.hasStrictFDerivAt
  have B :
    HasStrictFDerivAt e ((ContinuousLinearMap.smulRight 1 e' : â„‚ â†’L[â„‚] â„‚).restrictScalars â„)
      (ofRealCLM z) :=
    h.hasStrictFDerivAt.restrictScalars â„
  have C : HasStrictFDerivAt re reCLM (e (ofRealCLM z)) := reCLM.hasStrictFDerivAt
  -- Porting note: this should be by:
  -- simpa using (C.comp z (B.comp z A)).hasStrictDerivAt
  -- but for some reason simp can not use `ContinuousLinearMap.comp_apply`
  convert (C.comp z (B.comp z A)).hasStrictDerivAt
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.comp_apply]
  simp

/-- If a complex function `e` is differentiable at a real point, then the function `â„ â†’ â„` given by
the real part of `e` is also differentiable at this point, with a derivative equal to the real part
of the complex derivative. -/
theorem HasDerivAt.real_of_complex (h : HasDerivAt e e' z) :
    HasDerivAt (fun x : â„ => (e x).re) e'.re z := by
  have A : HasFDerivAt ((â†‘) : â„ â†’ â„‚) ofRealCLM z := ofRealCLM.hasFDerivAt
  have B :
    HasFDerivAt e ((ContinuousLinearMap.smulRight 1 e' : â„‚ â†’L[â„‚] â„‚).restrictScalars â„)
      (ofRealCLM z) :=
    h.hasFDerivAt.restrictScalars â„
  have C : HasFDerivAt re reCLM (e (ofRealCLM z)) := reCLM.hasFDerivAt
  -- Porting note: this should be by:
  -- simpa using (C.comp z (B.comp z A)).hasStrictDerivAt
  -- but for some reason simp can not use `ContinuousLinearMap.comp_apply`
  convert (C.comp z (B.comp z A)).hasDerivAt
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.comp_apply]
  simp

theorem ContDiffAt.real_of_complex {n : â„•âˆ} (h : ContDiffAt â„‚ n e z) :
    ContDiffAt â„ n (fun x : â„ => (e x).re) z := by
  have A : ContDiffAt â„ n ((â†‘) : â„ â†’ â„‚) z := ofRealCLM.contDiff.contDiffAt
  have B : ContDiffAt â„ n e z := h.restrict_scalars â„
  have C : ContDiffAt â„ n re (e z) := reCLM.contDiff.contDiffAt
  exact C.comp z (B.comp z A)

theorem ContDiff.real_of_complex {n : â„•âˆ} (h : ContDiff â„‚ n e) :
    ContDiff â„ n fun x : â„ => (e x).re :=
  contDiff_iff_contDiffAt.2 fun _ => h.contDiffAt.real_of_complex

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

theorem HasStrictDerivAt.complexToReal_fderiv' {f : â„‚ â†’ E} {x : â„‚} {f' : E}
    (h : HasStrictDerivAt f f' x) :
    HasStrictFDerivAt f (reCLM.smulRight f' + I â€¢ imCLM.smulRight f') x := by
  simpa only [Complex.restrictScalars_one_smulRight'] using
    h.hasStrictFDerivAt.restrictScalars â„

theorem HasDerivAt.complexToReal_fderiv' {f : â„‚ â†’ E} {x : â„‚} {f' : E} (h : HasDerivAt f f' x) :
    HasFDerivAt f (reCLM.smulRight f' + I â€¢ imCLM.smulRight f') x := by
  simpa only [Complex.restrictScalars_one_smulRight'] using h.hasFDerivAt.restrictScalars â„

theorem HasDerivWithinAt.complexToReal_fderiv' {f : â„‚ â†’ E} {s : Set â„‚} {x : â„‚} {f' : E}
    (h : HasDerivWithinAt f f' s x) :
    HasFDerivWithinAt f (reCLM.smulRight f' + I â€¢ imCLM.smulRight f') s x := by
  simpa only [Complex.restrictScalars_one_smulRight'] using
    h.hasFDerivWithinAt.restrictScalars â„

theorem HasStrictDerivAt.complexToReal_fderiv {f : â„‚ â†’ â„‚} {f' x : â„‚} (h : HasStrictDerivAt f f' x) :
    HasStrictFDerivAt f (f' â€¢ (1 : â„‚ â†’L[â„] â„‚)) x := by
  simpa only [Complex.restrictScalars_one_smulRight] using h.hasStrictFDerivAt.restrictScalars â„

theorem HasDerivAt.complexToReal_fderiv {f : â„‚ â†’ â„‚} {f' x : â„‚} (h : HasDerivAt f f' x) :
    HasFDerivAt f (f' â€¢ (1 : â„‚ â†’L[â„] â„‚)) x := by
  simpa only [Complex.restrictScalars_one_smulRight] using h.hasFDerivAt.restrictScalars â„

theorem HasDerivWithinAt.complexToReal_fderiv {f : â„‚ â†’ â„‚} {s : Set â„‚} {f' x : â„‚}
    (h : HasDerivWithinAt f f' s x) : HasFDerivWithinAt f (f' â€¢ (1 : â„‚ â†’L[â„] â„‚)) s x := by
  simpa only [Complex.restrictScalars_one_smulRight] using h.hasFDerivWithinAt.restrictScalars â„

/-- If a complex function `e` is differentiable at a real point, then its restriction to `â„` is
differentiable there as a function `â„ â†’ â„‚`, with the same derivative. -/
theorem HasDerivAt.comp_ofReal (hf : HasDerivAt e e' â†‘z) : HasDerivAt (fun y : â„ => e â†‘y) e' z := by
  simpa only [ofRealCLM_apply, ofReal_one, mul_one] using hf.comp z ofRealCLM.hasDerivAt

/-- If a function `f : â„ â†’ â„` is differentiable at a (real) point `x`, then it is also
differentiable as a function `â„ â†’ â„‚`. -/
theorem HasDerivAt.ofReal_comp {f : â„ â†’ â„} {u : â„} (hf : HasDerivAt f u z) :
    HasDerivAt (fun y : â„ => â†‘(f y) : â„ â†’ â„‚) u z := by
  simpa only [ofRealCLM_apply, ofReal_one, real_smul, mul_one] using
    ofRealCLM.hasDerivAt.scomp z hf

end RealDerivOfComplex

section Conformality

/-! ### Conformality of real-differentiable complex maps -/

open Complex ContinuousLinearMap

open scoped ComplexConjugate

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] {z : â„‚} {f : â„‚ â†’ E}

/-- A real differentiable function of the complex plane into some complex normed space `E` is
    conformal at a point `z` if it is holomorphic at that point with a nonvanishing differential.
    This is a version of the Cauchy-Riemann equations. -/
theorem DifferentiableAt.conformalAt (h : DifferentiableAt â„‚ f z) (hf' : deriv f z â‰  0) :
    ConformalAt f z := by
  rw [conformalAt_iff_isConformalMap_fderiv, (h.hasFDerivAt.restrictScalars â„).fderiv]
  apply isConformalMap_complex_linear
  simpa only [Ne, ContinuousLinearMap.ext_ring_iff]

/-- A complex function is conformal if and only if the function is holomorphic or antiholomorphic
    with a nonvanishing differential. -/
theorem conformalAt_iff_differentiableAt_or_differentiableAt_comp_conj {f : â„‚ â†’ â„‚} {z : â„‚} :
    ConformalAt f z â†”
      (DifferentiableAt â„‚ f z âˆ¨ DifferentiableAt â„‚ (f âˆ˜ conj) (conj z)) âˆ§ fderiv â„ f z â‰  0 := by
  rw [conformalAt_iff_isConformalMap_fderiv]
  rw [isConformalMap_iff_is_complex_or_conj_linear]
  apply and_congr_left
  intro h
  have h_diff := h.imp_symm fderiv_zero_of_not_differentiableAt
  apply or_congr
  Â· rw [differentiableAt_iff_restrictScalars â„ h_diff]
  rw [â† conj_conj z] at h_diff
  rw [differentiableAt_iff_restrictScalars â„ (h_diff.comp _ conjCLE.differentiableAt)]
  refine exists_congr fun g => rfl.congr ?_
  have : fderiv â„ conj (conj z) = _ := conjCLE.fderiv
  simp [fderiv_comp _ h_diff conjCLE.differentiableAt, this, conj_conj]

end Conformality

section BigO

namespace Complex

open Topology

lemma isBigO_comp_ofReal {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“ (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“ x] (fun y : â„ â†¦ g y) :=
  h.comp_tendsto <| continuous_ofReal.tendsto x

lemma isBigO_comp_ofReal_nhds_ne {f g : â„‚ â†’ â„‚} {x : â„} (h : f =O[ğ“[â‰ ] (x : â„‚)] g) :
    (fun y : â„ â†¦ f y) =O[ğ“[â‰ ] x] (fun y : â„ â†¦ g y) :=
  h.comp_tendsto <| ((hasDerivAt_id (x : â„‚)).comp_ofReal).tendsto_punctured_nhds one_ne_zero

end Complex

end BigO
