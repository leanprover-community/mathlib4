/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Data.Complex.BigOperators
import Mathlib.Data.Complex.Module
import Mathlib.Data.Complex.Order
import Mathlib.Topology.Algebra.InfiniteSum.Field
import Mathlib.Topology.Algebra.InfiniteSum.Module
import Mathlib.Topology.Instances.RealVectorSpace
import Mathlib.Topology.MetricSpace.ProperSpace.Real

/-!

# Normed space structure on `â„‚`.

This file gathers basic facts of analytic nature on the complex numbers.

## Main results

This file registers `â„‚` as a normed field, expresses basic properties of the norm, and gives tools
on the real vector space structure of `â„‚`. Notably, it defines the following functions in the
namespace `Complex`.

|Name              |Type         |Description                                             |
|------------------|-------------|--------------------------------------------------------|
|`equivRealProdCLM`|â„‚ â‰ƒL[â„] â„ Ã— â„|The natural `ContinuousLinearEquiv` from `â„‚` to `â„ Ã— â„` |
|`reCLM`           |â„‚ â†’L[â„] â„    |Real part function as a `ContinuousLinearMap`           |
|`imCLM`           |â„‚ â†’L[â„] â„    |Imaginary part function as a `ContinuousLinearMap`      |
|`ofRealCLM`       |â„ â†’L[â„] â„‚    |Embedding of the reals as a `ContinuousLinearMap`       |
|`ofRealLI`        |â„ â†’â‚—áµ¢[â„] â„‚   |Embedding of the reals as a `LinearIsometry`            |
|`conjCLE`         |â„‚ â‰ƒL[â„] â„‚    |Complex conjugation as a `ContinuousLinearEquiv`        |
|`conjLIE`         |â„‚ â‰ƒâ‚—áµ¢[â„] â„‚   |Complex conjugation as a `LinearIsometryEquiv`          |

We also register the fact that `â„‚` is an `RCLike` field.

-/


assert_not_exists Absorbs

noncomputable section

namespace Complex
variable {z : â„‚}

open ComplexConjugate Topology Filter

instance : NormedField â„‚ where
  dist_eq _ _ := rfl
  norm_mul := Complex.norm_mul

instance : DenselyNormedField â„‚ where
  lt_norm_lt râ‚ râ‚‚ hâ‚€ hr :=
    let âŸ¨x, hâŸ© := exists_between hr
    âŸ¨x, by rwa [norm_real, Real.norm_of_nonneg (hâ‚€.trans_lt h.1).le]âŸ©

instance {R : Type*} [NormedField R] [NormedAlgebra R â„] : NormedAlgebra R â„‚ where
  norm_smul_le r x := by
    rw [â† algebraMap_smul â„ r x, real_smul, norm_mul, norm_real, norm_algebraMap']

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„‚ E]

-- see Note [lower instance priority]
/-- The module structure from `Module.complexToReal` is a normed space. -/
instance (priority := 900) _root_.NormedSpace.complexToReal : NormedSpace â„ E :=
  NormedSpace.restrictScalars â„ â„‚ E

-- see Note [lower instance priority]
/-- The algebra structure from `Algebra.complexToReal` is a normed algebra. -/
instance (priority := 900) _root_.NormedAlgebra.complexToReal {A : Type*} [SeminormedRing A]
    [NormedAlgebra â„‚ A] : NormedAlgebra â„ A :=
  NormedAlgebra.restrictScalars â„ â„‚ A

-- This result cannot be moved to `Data/Complex/Norm` since `â„¤` gets its norm from its
-- normed ring structure and that file does not know about rings
@[simp 1100, norm_cast] lemma nnnorm_intCast (n : â„¤) : â€–(n : â„‚)â€–â‚Š = â€–nâ€–â‚Š := by
  ext; exact norm_intCast n

@[deprecated (since := "2025-02-16")] alias comap_abs_nhds_zero := comap_norm_nhds_zero
@[deprecated (since := "2025-02-16")] alias continuous_abs := continuous_norm

@[continuity, fun_prop]
theorem continuous_normSq : Continuous normSq := by
  simpa [â† Complex.normSq_eq_norm_sq] using continuous_norm (E := â„‚).pow 2

theorem nnnorm_eq_one_of_pow_eq_one {Î¶ : â„‚} {n : â„•} (h : Î¶ ^ n = 1) (hn : n â‰  0) : â€–Î¶â€–â‚Š = 1 :=
  (pow_left_injâ‚€ zero_le' zero_le' hn).1 <| by rw [â† nnnorm_pow, h, nnnorm_one, one_pow]

theorem norm_eq_one_of_pow_eq_one {Î¶ : â„‚} {n : â„•} (h : Î¶ ^ n = 1) (hn : n â‰  0) : â€–Î¶â€– = 1 :=
  congr_arg Subtype.val (nnnorm_eq_one_of_pow_eq_one h hn)

lemma le_of_eq_sum_of_eq_sum_norm {Î¹ : Type*} {a b : â„} (f : Î¹ â†’ â„‚) (s : Finset Î¹) (haâ‚€ : 0 â‰¤ a)
    (ha : a = âˆ‘ i âˆˆ s, f i) (hb : b = âˆ‘ i âˆˆ s, (â€–f iâ€– : â„‚)) : a â‰¤ b := by
  norm_cast at hb; rw [â† Complex.norm_of_nonneg haâ‚€, ha, hb]; exact norm_sum_le s f

theorem equivRealProd_apply_le (z : â„‚) : â€–equivRealProd zâ€– â‰¤ â€–zâ€– := by
  simp [Prod.norm_def, abs_re_le_norm, abs_im_le_norm]

theorem equivRealProd_apply_le' (z : â„‚) : â€–equivRealProd zâ€– â‰¤ 1 * â€–zâ€– := by
  simpa using equivRealProd_apply_le z

theorem lipschitz_equivRealProd : LipschitzWith 1 equivRealProd := by
  simpa using AddMonoidHomClass.lipschitz_of_bound equivRealProdLm 1 equivRealProd_apply_le'

theorem antilipschitz_equivRealProd : AntilipschitzWith (NNReal.sqrt 2) equivRealProd :=
  AddMonoidHomClass.antilipschitz_of_bound equivRealProdLm fun z â†¦ by
    simpa only [Real.coe_sqrt, NNReal.coe_ofNat] using norm_le_sqrt_two_mul_max z

theorem isUniformEmbedding_equivRealProd : IsUniformEmbedding equivRealProd :=
  antilipschitz_equivRealProd.isUniformEmbedding lipschitz_equivRealProd.uniformContinuous

instance : CompleteSpace â„‚ :=
  (completeSpace_congr isUniformEmbedding_equivRealProd).mpr inferInstance

instance instT2Space : T2Space â„‚ := TopologicalSpace.t2Space_of_metrizableSpace

/-- The natural `ContinuousLinearEquiv` from `â„‚` to `â„ Ã— â„`. -/
@[simps! +simpRhs apply symm_apply_re symm_apply_im]
def equivRealProdCLM : â„‚ â‰ƒL[â„] â„ Ã— â„ :=
  equivRealProdLm.toContinuousLinearEquivOfBounds 1 (âˆš2) equivRealProd_apply_le' fun p â†¦
    norm_le_sqrt_two_mul_max (equivRealProd.symm p)

theorem equivRealProdCLM_symm_apply (p : â„ Ã— â„) :
    Complex.equivRealProdCLM.symm p = p.1 + p.2 * Complex.I := Complex.equivRealProd_symm_apply p

instance : ProperSpace â„‚ := lipschitz_equivRealProd.properSpace
  equivRealProdCLM.toHomeomorph.isProperMap

@[deprecated (since := "2025-02-16")] alias tendsto_abs_cocompact_atTop :=
  tendsto_norm_cocompact_atTop

/-- The `normSq` function on `â„‚` is proper. -/
theorem tendsto_normSq_cocompact_atTop : Tendsto normSq (cocompact â„‚) atTop := by
  simpa [norm_mul_self_eq_normSq]
    using tendsto_norm_cocompact_atTop.atTop_mul_atTopâ‚€ (tendsto_norm_cocompact_atTop (E := â„‚))

open ContinuousLinearMap

/-- Continuous linear map version of the real part function, from `â„‚` to `â„`. -/
def reCLM : â„‚ â†’L[â„] â„ :=
  reLm.mkContinuous 1 fun x â†¦ by simp [abs_re_le_norm]

@[continuity, fun_prop]
theorem continuous_re : Continuous re :=
  reCLM.continuous

lemma uniformlyContinuous_re : UniformContinuous re :=
  reCLM.uniformContinuous

@[simp]
theorem reCLM_coe : (reCLM : â„‚ â†’â‚—[â„] â„) = reLm :=
  rfl

@[simp]
theorem reCLM_apply (z : â„‚) : (reCLM : â„‚ â†’ â„) z = z.re :=
  rfl

/-- Continuous linear map version of the imaginary part function, from `â„‚` to `â„`. -/
def imCLM : â„‚ â†’L[â„] â„ :=
  imLm.mkContinuous 1 fun x â†¦ by simp [abs_im_le_norm]

@[continuity, fun_prop]
theorem continuous_im : Continuous im :=
  imCLM.continuous

lemma uniformlyContinuous_im : UniformContinuous im :=
  imCLM.uniformContinuous

@[simp]
theorem imCLM_coe : (imCLM : â„‚ â†’â‚—[â„] â„) = imLm :=
  rfl

@[simp]
theorem imCLM_apply (z : â„‚) : (imCLM : â„‚ â†’ â„) z = z.im :=
  rfl

theorem restrictScalars_one_smulRight' (x : E) :
    ContinuousLinearMap.restrictScalars â„ ((1 : â„‚ â†’L[â„‚] â„‚).smulRight x : â„‚ â†’L[â„‚] E) =
      reCLM.smulRight x + I â€¢ imCLM.smulRight x := by
  ext âŸ¨a, bâŸ©
  simp [map_add, mk_eq_add_mul_I, mul_smul, smul_comm I b x]

theorem restrictScalars_one_smulRight (x : â„‚) :
    ContinuousLinearMap.restrictScalars â„ ((1 : â„‚ â†’L[â„‚] â„‚).smulRight x : â„‚ â†’L[â„‚] â„‚) =
    x â€¢ (1 : â„‚ â†’L[â„] â„‚) := by
  ext1 z
  dsimp
  apply mul_comm

/-- The complex-conjugation function from `â„‚` to itself is an isometric linear equivalence. -/
def conjLIE : â„‚ â‰ƒâ‚—áµ¢[â„] â„‚ :=
  âŸ¨conjAe.toLinearEquiv, norm_conjâŸ©

@[simp]
theorem conjLIE_apply (z : â„‚) : conjLIE z = conj z :=
  rfl

@[simp]
theorem conjLIE_symm : conjLIE.symm = conjLIE :=
  rfl

theorem isometry_conj : Isometry (conj : â„‚ â†’ â„‚) :=
  conjLIE.isometry

@[simp]
theorem dist_conj_conj (z w : â„‚) : dist (conj z) (conj w) = dist z w :=
  isometry_conj.dist_eq z w

@[simp]
theorem nndist_conj_conj (z w : â„‚) : nndist (conj z) (conj w) = nndist z w :=
  isometry_conj.nndist_eq z w

theorem dist_conj_comm (z w : â„‚) : dist (conj z) w = dist z (conj w) := by
  rw [â† dist_conj_conj, conj_conj]

theorem nndist_conj_comm (z w : â„‚) : nndist (conj z) w = nndist z (conj w) :=
  Subtype.ext <| dist_conj_comm _ _

instance : ContinuousStar â„‚ :=
  âŸ¨conjLIE.continuousâŸ©

@[continuity]
theorem continuous_conj : Continuous (conj : â„‚ â†’ â„‚) :=
  continuous_star

/-- The only continuous ring homomorphisms from `â„‚` to `â„‚` are the identity and the complex
conjugation. -/
theorem ringHom_eq_id_or_conj_of_continuous {f : â„‚ â†’+* â„‚} (hf : Continuous f) :
    f = RingHom.id â„‚ âˆ¨ f = conj := by
  simpa only [DFunLike.ext_iff] using real_algHom_eq_id_or_conj (AlgHom.mk' f (map_real_smul f hf))

/-- Continuous linear equiv version of the conj function, from `â„‚` to `â„‚`. -/
def conjCLE : â„‚ â‰ƒL[â„] â„‚ :=
  conjLIE

@[simp]
theorem conjCLE_coe : conjCLE.toLinearEquiv = conjAe.toLinearEquiv :=
  rfl

@[simp]
theorem conjCLE_apply (z : â„‚) : conjCLE z = conj z :=
  rfl

/-- Linear isometry version of the canonical embedding of `â„` in `â„‚`. -/
def ofRealLI : â„ â†’â‚—áµ¢[â„] â„‚ :=
  âŸ¨ofRealAm.toLinearMap, norm_realâŸ©

theorem isometry_ofReal : Isometry ((â†‘) : â„ â†’ â„‚) :=
  ofRealLI.isometry

@[continuity, fun_prop]
theorem continuous_ofReal : Continuous ((â†‘) : â„ â†’ â„‚) :=
  ofRealLI.continuous

theorem isUniformEmbedding_ofReal : IsUniformEmbedding ((â†‘) : â„ â†’ â„‚) :=
  ofRealLI.isometry.isUniformEmbedding

lemma _root_.RCLike.isUniformEmbedding_ofReal {ğ•œ : Type*} [RCLike ğ•œ] :
    IsUniformEmbedding ((â†‘) : â„ â†’ ğ•œ) :=
  RCLike.ofRealLI.isometry.isUniformEmbedding

theorem _root_.Filter.tendsto_ofReal_iff {Î± : Type*} {l : Filter Î±} {f : Î± â†’ â„} {x : â„} :
    Tendsto (fun x â†¦ (f x : â„‚)) l (ğ“ (x : â„‚)) â†” Tendsto f l (ğ“ x) :=
  isUniformEmbedding_ofReal.isClosedEmbedding.tendsto_nhds_iff.symm

lemma _root_.Filter.tendsto_ofReal_iff' {Î± ğ•œ : Type*} [RCLike ğ•œ]
    {l : Filter Î±} {f : Î± â†’ â„} {x : â„} :
    Tendsto (fun x â†¦ (f x : ğ•œ)) l (ğ“ (x : ğ•œ)) â†” Tendsto f l (ğ“ x) :=
  RCLike.isUniformEmbedding_ofReal.isClosedEmbedding.tendsto_nhds_iff.symm

lemma _root_.Filter.Tendsto.ofReal {Î± : Type*} {l : Filter Î±} {f : Î± â†’ â„} {x : â„}
    (hf : Tendsto f l (ğ“ x)) : Tendsto (fun x â†¦ (f x : â„‚)) l (ğ“ (x : â„‚)) :=
  tendsto_ofReal_iff.mpr hf

/-- The only continuous ring homomorphism from `â„` to `â„‚` is the identity. -/
theorem ringHom_eq_ofReal_of_continuous {f : â„ â†’+* â„‚} (h : Continuous f) : f = ofRealHom := by
  convert congr_arg AlgHom.toRingHom <| Subsingleton.elim (AlgHom.mk' f <| map_real_smul f h)
    (Algebra.ofId â„ â„‚)

/-- Continuous linear map version of the canonical embedding of `â„` in `â„‚`. -/
def ofRealCLM : â„ â†’L[â„] â„‚ :=
  ofRealLI.toContinuousLinearMap

@[simp]
theorem ofRealCLM_coe : (ofRealCLM : â„ â†’â‚—[â„] â„‚) = ofRealAm.toLinearMap :=
  rfl

@[simp]
theorem ofRealCLM_apply (x : â„) : ofRealCLM x = x :=
  rfl

noncomputable instance : RCLike â„‚ where
  re := âŸ¨âŸ¨Complex.re, Complex.zero_reâŸ©, Complex.add_reâŸ©
  im := âŸ¨âŸ¨Complex.im, Complex.zero_imâŸ©, Complex.add_imâŸ©
  I := Complex.I
  I_re_ax := I_re
  I_mul_I_ax := .inr Complex.I_mul_I
  re_add_im_ax := re_add_im
  ofReal_re_ax := ofReal_re
  ofReal_im_ax := ofReal_im
  mul_re_ax := mul_re
  mul_im_ax := mul_im
  conj_re_ax _ := rfl
  conj_im_ax _ := rfl
  conj_I_ax := conj_I
  norm_sq_eq_def_ax z := (normSq_eq_norm_sq z).symm
  mul_im_I_ax _ := mul_one _
  toPartialOrder := Complex.partialOrder
  le_iff_re_im := Iff.rfl

theorem _root_.RCLike.re_eq_complex_re : â‡‘(RCLike.re : â„‚ â†’+ â„) = Complex.re :=
  rfl

theorem _root_.RCLike.im_eq_complex_im : â‡‘(RCLike.im : â„‚ â†’+ â„) = Complex.im :=
  rfl

theorem _root_.RCLike.ofReal_eq_complex_ofReal : (RCLike.ofReal : â„ â†’ â„‚) = Complex.ofReal := rfl

-- TODO: Replace `mul_conj` and `conj_mul` once `norm` has replaced `abs`
lemma mul_conj' (z : â„‚) : z * conj z = â€–zâ€– ^ 2 := RCLike.mul_conj z
lemma conj_mul' (z : â„‚) : conj z * z = â€–zâ€– ^ 2 := RCLike.conj_mul z

lemma inv_eq_conj (hz : â€–zâ€– = 1) : zâ»Â¹ = conj z := RCLike.inv_eq_conj hz

lemma exists_norm_eq_mul_self (z : â„‚) : âˆƒ c, â€–câ€– = 1 âˆ§ â€–zâ€– = c * z :=
  RCLike.exists_norm_eq_mul_self _

lemma exists_norm_mul_eq_self (z : â„‚) : âˆƒ c, â€–câ€– = 1 âˆ§ c * â€–zâ€– = z :=
  RCLike.exists_norm_mul_eq_self _

lemma im_eq_zero_iff_isSelfAdjoint (x : â„‚) : Complex.im x = 0 â†” IsSelfAdjoint x := by
  rw [â† RCLike.im_eq_complex_im]
  exact RCLike.im_eq_zero_iff_isSelfAdjoint

lemma re_eq_ofReal_of_isSelfAdjoint {x : â„‚} {y : â„} (hx : IsSelfAdjoint x) :
    Complex.re x = y â†” x = y := by
  rw [â† RCLike.re_eq_complex_re]
  exact RCLike.re_eq_ofReal_of_isSelfAdjoint hx

lemma ofReal_eq_re_of_isSelfAdjoint {x : â„‚} {y : â„} (hx : IsSelfAdjoint x) :
    y = Complex.re x â†” y = x := by
  rw [â† RCLike.re_eq_complex_re]
  exact RCLike.ofReal_eq_re_of_isSelfAdjoint hx

/-- The natural isomorphism between `ğ•œ` satisfying `RCLike ğ•œ` and `â„‚` when
`RCLike.im RCLike.I = 1`. -/
@[simps]
def _root_.RCLike.complexRingEquiv {ğ•œ : Type*} [RCLike ğ•œ]
    (h : RCLike.im (RCLike.I : ğ•œ) = 1) : ğ•œ â‰ƒ+* â„‚ where
  toFun x := RCLike.re x + RCLike.im x * I
  invFun x := re x + im x * RCLike.I
  left_inv x := by simp
  right_inv x := by simp [h]
  map_add' x y := by simp only [map_add, ofReal_add]; ring
  map_mul' x y := by
    simp only [RCLike.mul_re, ofReal_sub, ofReal_mul, RCLike.mul_im, ofReal_add]
    ring_nf
    rw [I_sq]
    ring

/-- The natural `â„`-linear isometry equivalence between `ğ•œ` satisfying `RCLike ğ•œ` and `â„‚` when
`RCLike.im RCLike.I = 1`. -/
@[simps]
def _root_.RCLike.complexLinearIsometryEquiv {ğ•œ : Type*} [RCLike ğ•œ]
    (h : RCLike.im (RCLike.I : ğ•œ) = 1) : ğ•œ â‰ƒâ‚—áµ¢[â„] â„‚ where
  map_smul' _ _ := by simp [RCLike.smul_re, RCLike.smul_im, ofReal_mul]; ring
  norm_map' _ := by
    rw [â† sq_eq_sqâ‚€ (by positivity) (by positivity), â† normSq_eq_norm_sq, â† RCLike.normSq_eq_def',
      RCLike.normSq_apply]
    simp [normSq_add]
  __ := RCLike.complexRingEquiv h

theorem isometry_intCast : Isometry ((â†‘) : â„¤ â†’ â„‚) :=
  Isometry.of_dist_eq <| by simp_rw [â† Complex.ofReal_intCast,
    Complex.isometry_ofReal.dist_eq, Int.dist_cast_real, implies_true]

theorem closedEmbedding_intCast : IsClosedEmbedding ((â†‘) : â„¤ â†’ â„‚) :=
  isometry_intCast.isClosedEmbedding

lemma isClosed_range_intCast : IsClosed (Set.range ((â†‘) : â„¤ â†’ â„‚)) :=
  Complex.closedEmbedding_intCast.isClosed_range

lemma isOpen_compl_range_intCast : IsOpen (Set.range ((â†‘) : â„¤ â†’ â„‚))á¶œ :=
  Complex.isClosed_range_intCast.isOpen_compl

section ComplexOrder

open ComplexOrder

theorem eq_coe_norm_of_nonneg {z : â„‚} (hz : 0 â‰¤ z) : z = â†‘â€–zâ€– := by
  lift z to â„ using hz.2.symm
  rw [norm_real, Real.norm_of_nonneg (id hz.1 : 0 â‰¤ z)]

/-- We show that the partial order and the topology on `â„‚` are compatible.
We turn this into an instance scoped to `ComplexOrder`. -/
lemma orderClosedTopology : OrderClosedTopology â„‚ where
  isClosed_le' := by
    simp_rw [le_def, Set.setOf_and]
    refine IsClosed.inter (isClosed_le ?_ ?_) (isClosed_eq ?_ ?_) <;> continuity

scoped[ComplexOrder] attribute [instance] Complex.orderClosedTopology

theorem norm_of_nonneg' {x : â„‚} (hx : 0 â‰¤ x) : â€–xâ€– = x := by
  rw [â† RCLike.ofReal_eq_complex_ofReal]
  exact RCLike.norm_of_nonneg' hx

lemma re_nonneg_iff_nonneg {x : â„‚} (hx : IsSelfAdjoint x) : 0 â‰¤ re x â†” 0 â‰¤ x := by
  rw [â† RCLike.re_eq_complex_re]
  exact RCLike.re_nonneg_of_nonneg hx

@[gcongr]
lemma re_le_re {x y : â„‚} (h : x â‰¤ y) : re x â‰¤ re y := by
  rw [RCLike.le_iff_re_im] at h
  exact h.1

end ComplexOrder

end Complex

namespace RCLike

open ComplexConjugate

local notation "reC" => @RCLike.re â„‚ _
local notation "imC" => @RCLike.im â„‚ _
local notation "IC" => @RCLike.I â„‚ _
local notation "norm_sqC" => @RCLike.normSq â„‚ _

@[simp]
theorem re_to_complex {x : â„‚} : reC x = x.re :=
  rfl

@[simp]
theorem im_to_complex {x : â„‚} : imC x = x.im :=
  rfl

@[simp]
theorem I_to_complex : IC = Complex.I :=
  rfl

@[simp]
theorem normSq_to_complex {x : â„‚} : norm_sqC x = Complex.normSq x :=
  rfl

section tsum

variable {Î± : Type*} (ğ•œ : Type*) [RCLike ğ•œ]

@[simp]
theorem hasSum_conj {f : Î± â†’ ğ•œ} {x : ğ•œ} : HasSum (fun x â†¦ conj (f x)) x â†” HasSum f (conj x) :=
  conjCLE.hasSum

theorem hasSum_conj' {f : Î± â†’ ğ•œ} {x : ğ•œ} : HasSum (fun x â†¦ conj (f x)) (conj x) â†” HasSum f x :=
  conjCLE.hasSum'

@[simp]
theorem summable_conj {f : Î± â†’ ğ•œ} : (Summable fun x â†¦ conj (f x)) â†” Summable f :=
  summable_star_iff

variable {ğ•œ} in
theorem conj_tsum (f : Î± â†’ ğ•œ) : conj (âˆ‘' a, f a) = âˆ‘' a, conj (f a) :=
  tsum_star

@[simp, norm_cast]
theorem hasSum_ofReal {f : Î± â†’ â„} {x : â„} : HasSum (fun x â†¦ (f x : ğ•œ)) x â†” HasSum f x :=
  âŸ¨fun h â†¦ by simpa only [RCLike.reCLM_apply, RCLike.ofReal_re] using reCLM.hasSum h,
    ofRealCLM.hasSumâŸ©

@[simp, norm_cast]
theorem summable_ofReal {f : Î± â†’ â„} : (Summable fun x â†¦ (f x : ğ•œ)) â†” Summable f :=
  âŸ¨fun h â†¦ by simpa only [RCLike.reCLM_apply, RCLike.ofReal_re] using reCLM.summable h,
    ofRealCLM.summableâŸ©

@[norm_cast]
theorem ofReal_tsum (f : Î± â†’ â„) : (â†‘(âˆ‘' a, f a) : ğ•œ) = âˆ‘' a, (f a : ğ•œ) := by
  by_cases h : Summable f
  Â· exact ContinuousLinearMap.map_tsum ofRealCLM h
  Â· rw [tsum_eq_zero_of_not_summable h,
      tsum_eq_zero_of_not_summable ((summable_ofReal _).not.mpr h), ofReal_zero]

theorem hasSum_re {f : Î± â†’ ğ•œ} {x : ğ•œ} (h : HasSum f x) : HasSum (fun x â†¦ re (f x)) (re x) :=
  reCLM.hasSum h

theorem hasSum_im {f : Î± â†’ ğ•œ} {x : ğ•œ} (h : HasSum f x) : HasSum (fun x â†¦ im (f x)) (im x) :=
  imCLM.hasSum h

theorem re_tsum {f : Î± â†’ ğ•œ} (h : Summable f) : re (âˆ‘' a, f a) = âˆ‘' a, re (f a) :=
  reCLM.map_tsum h

theorem im_tsum {f : Î± â†’ ğ•œ} (h : Summable f) : im (âˆ‘' a, f a) = âˆ‘' a, im (f a) :=
  imCLM.map_tsum h

variable {ğ•œ}

theorem hasSum_iff (f : Î± â†’ ğ•œ) (c : ğ•œ) :
    HasSum f c â†” HasSum (fun x â†¦ re (f x)) (re c) âˆ§ HasSum (fun x â†¦ im (f x)) (im c) := by
  refine âŸ¨fun h â†¦ âŸ¨hasSum_re _ h, hasSum_im _ hâŸ©, ?_âŸ©
  rintro âŸ¨hâ‚, hâ‚‚âŸ©
  simpa only [re_add_im] using
    ((hasSum_ofReal ğ•œ).mpr hâ‚).add (((hasSum_ofReal ğ•œ).mpr hâ‚‚).mul_right I)

end tsum

end RCLike

namespace Complex

@[deprecated (since := "2025-02-16")] alias hasProd_abs := HasProd.norm
@[deprecated (since := "2025-02-16")] alias multipliable_abs := Multipliable.norm
@[deprecated (since := "2025-02-16")] alias abs_tprod := norm_tprod

/-!
We have to repeat the lemmas about `RCLike.re` and `RCLike.im` as they are not syntactic
matches for `Complex.re` and `Complex.im`.

We do not have this problem with `ofReal` and `conj`, although we repeat them anyway for
discoverability and to avoid the need to unify `ğ•œ`.
-/


section tsum

variable {Î± : Type*}

open ComplexConjugate

theorem hasSum_conj {f : Î± â†’ â„‚} {x : â„‚} : HasSum (fun x â†¦ conj (f x)) x â†” HasSum f (conj x) :=
  RCLike.hasSum_conj _

theorem hasSum_conj' {f : Î± â†’ â„‚} {x : â„‚} : HasSum (fun x â†¦ conj (f x)) (conj x) â†” HasSum f x :=
  RCLike.hasSum_conj' _

theorem summable_conj {f : Î± â†’ â„‚} : (Summable fun x â†¦ conj (f x)) â†” Summable f :=
  RCLike.summable_conj _

theorem conj_tsum (f : Î± â†’ â„‚) : conj (âˆ‘' a, f a) = âˆ‘' a, conj (f a) :=
  RCLike.conj_tsum _

@[simp, norm_cast]
theorem hasSum_ofReal {f : Î± â†’ â„} {x : â„} : HasSum (fun x â†¦ (f x : â„‚)) x â†” HasSum f x :=
  RCLike.hasSum_ofReal _

@[simp, norm_cast]
theorem summable_ofReal {f : Î± â†’ â„} : (Summable fun x â†¦ (f x : â„‚)) â†” Summable f :=
  RCLike.summable_ofReal _

@[norm_cast]
theorem ofReal_tsum (f : Î± â†’ â„) : (â†‘(âˆ‘' a, f a) : â„‚) = âˆ‘' a, â†‘(f a) :=
  RCLike.ofReal_tsum _ _

theorem hasSum_re {f : Î± â†’ â„‚} {x : â„‚} (h : HasSum f x) : HasSum (fun x â†¦ (f x).re) x.re :=
  RCLike.hasSum_re â„‚ h

theorem hasSum_im {f : Î± â†’ â„‚} {x : â„‚} (h : HasSum f x) : HasSum (fun x â†¦ (f x).im) x.im :=
  RCLike.hasSum_im â„‚ h

theorem re_tsum {f : Î± â†’ â„‚} (h : Summable f) : (âˆ‘' a, f a).re = âˆ‘' a, (f a).re :=
  RCLike.re_tsum _ h

theorem im_tsum {f : Î± â†’ â„‚} (h : Summable f) : (âˆ‘' a, f a).im = âˆ‘' a, (f a).im :=
  RCLike.im_tsum _ h

theorem hasSum_iff (f : Î± â†’ â„‚) (c : â„‚) :
    HasSum f c â†” HasSum (fun x â†¦ (f x).re) c.re âˆ§ HasSum (fun x â†¦ (f x).im) c.im :=
  RCLike.hasSum_iff _ _

end tsum

section slitPlane

/-!
### Define the "slit plane" `â„‚ âˆ– â„â‰¤0` and provide some API
-/

open scoped ComplexOrder

/-- The *slit plane* is the complex plane with the closed negative real axis removed. -/
def slitPlane : Set â„‚ := {z | 0 < z.re âˆ¨ z.im â‰  0}

lemma mem_slitPlane_iff {z : â„‚} : z âˆˆ slitPlane â†” 0 < z.re âˆ¨ z.im â‰  0 := Set.mem_setOf

/- If `z` is non-zero, then either `z` or `-z` is in `slitPlane`. -/
lemma mem_slitPlane_or_neg_mem_slitPlane {z : â„‚} (hz : z â‰  0) :
    z âˆˆ slitPlane âˆ¨ -z âˆˆ slitPlane := by
  rw [mem_slitPlane_iff, mem_slitPlane_iff]
  rw [ne_eq, Complex.ext_iff] at hz
  push_neg at hz
  simp_all only [ne_eq, zero_re, zero_im, neg_re, Left.neg_pos_iff, neg_im, neg_eq_zero]
  by_contra! contra
  exact hz (le_antisymm contra.1.1 contra.2.1) contra.1.2

lemma slitPlane_eq_union : slitPlane = {z | 0 < z.re} âˆª {z | z.im â‰  0} := Set.setOf_or.symm

lemma isOpen_slitPlane : IsOpen slitPlane :=
  (isOpen_lt continuous_const continuous_re).union (isOpen_ne_fun continuous_im continuous_const)

@[simp]
lemma ofReal_mem_slitPlane {x : â„} : â†‘x âˆˆ slitPlane â†” 0 < x := by simp [mem_slitPlane_iff]

@[simp]
lemma neg_ofReal_mem_slitPlane {x : â„} : -â†‘x âˆˆ slitPlane â†” x < 0 := by
  simpa using ofReal_mem_slitPlane (x := -x)

@[simp] lemma one_mem_slitPlane : 1 âˆˆ slitPlane := ofReal_mem_slitPlane.2 one_pos

@[simp]
lemma zero_notMem_slitPlane : 0 âˆ‰ slitPlane := mt ofReal_mem_slitPlane.1 (lt_irrefl _)

@[deprecated (since := "2025-05-23")] alias zero_not_mem_slitPlane := zero_notMem_slitPlane

@[simp]
lemma natCast_mem_slitPlane {n : â„•} : â†‘n âˆˆ slitPlane â†” n â‰  0 := by
  simpa [pos_iff_ne_zero] using @ofReal_mem_slitPlane n

@[simp]
lemma ofNat_mem_slitPlane (n : â„•) [n.AtLeastTwo] : ofNat(n) âˆˆ slitPlane :=
  natCast_mem_slitPlane.2 (NeZero.ne n)

lemma mem_slitPlane_iff_not_le_zero {z : â„‚} : z âˆˆ slitPlane â†” Â¬z â‰¤ 0 :=
  mem_slitPlane_iff.trans not_le_zero_iff.symm

protected lemma compl_Iic_zero : (Set.Iic 0)á¶œ = slitPlane := Set.ext fun _ â†¦
  mem_slitPlane_iff_not_le_zero.symm

lemma slitPlane_ne_zero {z : â„‚} (hz : z âˆˆ slitPlane) : z â‰  0 :=
  ne_of_mem_of_not_mem hz zero_notMem_slitPlane

/-- The slit plane includes the open unit ball of radius `1` around `1`. -/
lemma ball_one_subset_slitPlane : Metric.ball 1 1 âŠ† slitPlane := fun z hz â†¦ .inl <|
  have : -1 < z.re - 1 := neg_lt_of_abs_lt <| (abs_re_le_norm _).trans_lt hz
  by linarith

/-- The slit plane includes the open unit ball of radius `1` around `1`. -/
lemma mem_slitPlane_of_norm_lt_one {z : â„‚} (hz : â€–zâ€– < 1) : 1 + z âˆˆ slitPlane :=
  ball_one_subset_slitPlane <| by simpa

end slitPlane

lemma _root_.IsCompact.reProdIm {s t : Set â„} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s Ã—â„‚ t) :=
  equivRealProdCLM.toHomeomorph.isCompact_preimage.2 (hs.prod ht)

end Complex

section realPart_imaginaryPart

variable {A : Type*} [SeminormedAddCommGroup A] [StarAddMonoid A] [NormedSpace â„‚ A] [StarModule â„‚ A]
  [NormedStarGroup A]

lemma realPart.norm_le (x : A) : â€–realPart xâ€– â‰¤ â€–xâ€– := by
  rw [â† inv_mul_cancel_leftâ‚€ two_ne_zero â€–xâ€–, â† AddSubgroup.norm_coe, realPart_apply_coe,
    norm_smul, norm_inv, Real.norm_ofNat]
  gcongr
  exact norm_add_le _ _ |>.trans <| by simp [two_mul]

lemma imaginaryPart.norm_le (x : A) : â€–imaginaryPart xâ€– â‰¤ â€–xâ€– := by
  calc â€–imaginaryPart xâ€– = â€–realPart (Complex.I â€¢ (-x))â€– := by simp
    _ â‰¤ â€–xâ€– := by simpa only [smul_neg, map_neg, realPart_I_smul, neg_neg,
        AddSubgroupClass.coe_norm, norm_neg, norm_smul, Complex.norm_I, one_mul] using
        realPart.norm_le (Complex.I â€¢ (-x))

end realPart_imaginaryPart
