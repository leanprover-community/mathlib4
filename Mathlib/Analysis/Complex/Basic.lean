/-
Copyright (c) S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Data.Complex.Module
import Mathlib.Data.Complex.Order
import Mathlib.Data.Complex.Exponential
import Mathlib.Data.IsROrC.Basic
import Mathlib.Topology.Algebra.InfiniteSum.Module
import Mathlib.Topology.Instances.RealVectorSpace

#align_import analysis.complex.basic from "leanprover-community/mathlib"@"3f655f5297b030a87d641ad4e825af8d9679eb0b"

/-!
# Normed space structure on `‚ÑÇ`.

This file gathers basic facts on complex numbers of an analytic nature.

## Main results

This file registers `‚ÑÇ` as a normed field, expresses basic properties of the norm, and gives
tools on the real vector space structure of `‚ÑÇ`. Notably, in the namespace `Complex`,
it defines functions:

* `reClm`
* `imClm`
* `ofRealClm`
* `conjCle`

They are bundled versions of the real part, the imaginary part, the embedding of `‚Ñù` in `‚ÑÇ`, and
the complex conjugate as continuous `‚Ñù`-linear maps. The last two are also bundled as linear
isometries in `ofRealLi` and `conjLie`.

We also register the fact that `‚ÑÇ` is an `IsROrC` field.
-/


assert_not_exists Absorbs

noncomputable section

namespace Complex

open ComplexConjugate Topology Filter

instance : Norm ‚ÑÇ :=
  ‚ü®abs‚ü©

@[simp]
theorem norm_eq_abs (z : ‚ÑÇ) : ‚Äñz‚Äñ = abs z :=
  rfl
#align complex.norm_eq_abs Complex.norm_eq_abs

theorem norm_exp_ofReal_mul_I (t : ‚Ñù) : ‚Äñexp (t * I)‚Äñ = 1 := by
  simp only [norm_eq_abs, abs_exp_ofReal_mul_I]
set_option linter.uppercaseLean3 false in
#align complex.norm_exp_of_real_mul_I Complex.norm_exp_ofReal_mul_I

instance : NormedAddCommGroup ‚ÑÇ :=
  AddGroupNorm.toNormedAddCommGroup
    { abs with
      map_zero' := map_zero abs
      neg' := abs.map_neg
      eq_zero_of_map_eq_zero' := fun _ => abs.eq_zero.1 }

instance : NormedField ‚ÑÇ where
  dist_eq _ _ := rfl
  norm_mul' := map_mul abs

instance : DenselyNormedField ‚ÑÇ where
  lt_norm_lt r‚ÇÅ r‚ÇÇ h‚ÇÄ hr :=
    let ‚ü®x, h‚ü© := exists_between hr
    ‚ü®x, by rwa [norm_eq_abs, abs_ofReal, abs_of_pos (h‚ÇÄ.trans_lt h.1)]‚ü©

instance {R : Type*} [NormedField R] [NormedAlgebra R ‚Ñù] : NormedAlgebra R ‚ÑÇ where
  norm_smul_le r x := by
    rw [‚Üê algebraMap_smul ‚Ñù r x, real_smul, norm_mul, norm_eq_abs, abs_ofReal, ‚Üê Real.norm_eq_abs,
      norm_algebraMap']

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]

-- see Note [lower instance priority]
/-- The module structure from `Module.complexToReal` is a normed space. -/
instance (priority := 900) _root_.NormedSpace.complexToReal : NormedSpace ‚Ñù E :=
  NormedSpace.restrictScalars ‚Ñù ‚ÑÇ E
#align normed_space.complex_to_real NormedSpace.complexToReal

theorem dist_eq (z w : ‚ÑÇ) : dist z w = abs (z - w) :=
  rfl
#align complex.dist_eq Complex.dist_eq

theorem dist_eq_re_im (z w : ‚ÑÇ) : dist z w = Real.sqrt ((z.re - w.re) ^ 2 + (z.im - w.im) ^ 2) := by
  rw [sq, sq]
  rfl
#align complex.dist_eq_re_im Complex.dist_eq_re_im

@[simp]
theorem dist_mk (x‚ÇÅ y‚ÇÅ x‚ÇÇ y‚ÇÇ : ‚Ñù) :
    dist (mk x‚ÇÅ y‚ÇÅ) (mk x‚ÇÇ y‚ÇÇ) = Real.sqrt ((x‚ÇÅ - x‚ÇÇ) ^ 2 + (y‚ÇÅ - y‚ÇÇ) ^ 2) :=
  dist_eq_re_im _ _
#align complex.dist_mk Complex.dist_mk

theorem dist_of_re_eq {z w : ‚ÑÇ} (h : z.re = w.re) : dist z w = dist z.im w.im := by
  rw [dist_eq_re_im, h, sub_self, zero_pow two_pos, zero_add, Real.sqrt_sq_eq_abs, Real.dist_eq]
#align complex.dist_of_re_eq Complex.dist_of_re_eq

theorem nndist_of_re_eq {z w : ‚ÑÇ} (h : z.re = w.re) : nndist z w = nndist z.im w.im :=
  NNReal.eq <| dist_of_re_eq h
#align complex.nndist_of_re_eq Complex.nndist_of_re_eq

theorem edist_of_re_eq {z w : ‚ÑÇ} (h : z.re = w.re) : edist z w = edist z.im w.im := by
  rw [edist_nndist, edist_nndist, nndist_of_re_eq h]
#align complex.edist_of_re_eq Complex.edist_of_re_eq

theorem dist_of_im_eq {z w : ‚ÑÇ} (h : z.im = w.im) : dist z w = dist z.re w.re := by
  rw [dist_eq_re_im, h, sub_self, zero_pow two_pos, add_zero, Real.sqrt_sq_eq_abs, Real.dist_eq]
#align complex.dist_of_im_eq Complex.dist_of_im_eq

theorem nndist_of_im_eq {z w : ‚ÑÇ} (h : z.im = w.im) : nndist z w = nndist z.re w.re :=
  NNReal.eq <| dist_of_im_eq h
#align complex.nndist_of_im_eq Complex.nndist_of_im_eq

theorem edist_of_im_eq {z w : ‚ÑÇ} (h : z.im = w.im) : edist z w = edist z.re w.re := by
  rw [edist_nndist, edist_nndist, nndist_of_im_eq h]
#align complex.edist_of_im_eq Complex.edist_of_im_eq

theorem dist_conj_self (z : ‚ÑÇ) : dist (conj z) z = 2 * |z.im| := by
  rw [dist_of_re_eq (conj_re z), conj_im, dist_comm, Real.dist_eq, sub_neg_eq_add, ‚Üê two_mul,
    _root_.abs_mul, abs_of_pos (zero_lt_two' ‚Ñù)]
#align complex.dist_conj_self Complex.dist_conj_self

theorem nndist_conj_self (z : ‚ÑÇ) : nndist (conj z) z = 2 * Real.nnabs z.im :=
  NNReal.eq <| by rw [‚Üê dist_nndist, NNReal.coe_mul, NNReal.coe_two, Real.coe_nnabs, dist_conj_self]
#align complex.nndist_conj_self Complex.nndist_conj_self

theorem dist_self_conj (z : ‚ÑÇ) : dist z (conj z) = 2 * |z.im| := by rw [dist_comm, dist_conj_self]
#align complex.dist_self_conj Complex.dist_self_conj

theorem nndist_self_conj (z : ‚ÑÇ) : nndist z (conj z) = 2 * Real.nnabs z.im := by
  rw [nndist_comm, nndist_conj_self]
#align complex.nndist_self_conj Complex.nndist_self_conj

@[simp 1100]
theorem comap_abs_nhds_zero : comap abs (ùìù 0) = ùìù 0 :=
  comap_norm_nhds_zero
#align complex.comap_abs_nhds_zero Complex.comap_abs_nhds_zero

theorem norm_real (r : ‚Ñù) : ‚Äñ(r : ‚ÑÇ)‚Äñ = ‚Äñr‚Äñ :=
  abs_ofReal _
#align complex.norm_real Complex.norm_real

@[simp 1100]
theorem norm_rat (r : ‚Ñö) : ‚Äñ(r : ‚ÑÇ)‚Äñ = |(r : ‚Ñù)| := by
  rw [‚Üê ofReal_rat_cast]
  exact norm_real _
#align complex.norm_rat Complex.norm_rat

@[simp 1100]
theorem norm_nat (n : ‚Ñï) : ‚Äñ(n : ‚ÑÇ)‚Äñ = n :=
  abs_natCast _
#align complex.norm_nat Complex.norm_nat

@[simp 1100]
theorem norm_int {n : ‚Ñ§} : ‚Äñ(n : ‚ÑÇ)‚Äñ = |(n : ‚Ñù)| := (int_cast_abs n).symm
#align complex.norm_int Complex.norm_int

theorem norm_int_of_nonneg {n : ‚Ñ§} (hn : 0 ‚â§ n) : ‚Äñ(n : ‚ÑÇ)‚Äñ = n := by
  rw [norm_int, ‚Üê Int.cast_abs, _root_.abs_of_nonneg hn]
#align complex.norm_int_of_nonneg Complex.norm_int_of_nonneg

@[continuity]
theorem continuous_abs : Continuous abs :=
  continuous_norm
#align complex.continuous_abs Complex.continuous_abs

@[continuity]
theorem continuous_normSq : Continuous normSq := by
  simpa [‚Üê normSq_eq_abs] using continuous_abs.pow 2
#align complex.continuous_norm_sq Complex.continuous_normSq

@[simp, norm_cast]
theorem nnnorm_real (r : ‚Ñù) : ‚Äñ(r : ‚ÑÇ)‚Äñ‚Çä = ‚Äñr‚Äñ‚Çä :=
  Subtype.ext <| norm_real r
#align complex.nnnorm_real Complex.nnnorm_real

@[simp, norm_cast]
theorem nnnorm_nat (n : ‚Ñï) : ‚Äñ(n : ‚ÑÇ)‚Äñ‚Çä = n :=
  Subtype.ext <| by simp
#align complex.nnnorm_nat Complex.nnnorm_nat

@[simp, norm_cast]
theorem nnnorm_int (n : ‚Ñ§) : ‚Äñ(n : ‚ÑÇ)‚Äñ‚Çä = ‚Äñn‚Äñ‚Çä :=
  Subtype.ext norm_int
#align complex.nnnorm_int Complex.nnnorm_int

theorem nnnorm_eq_one_of_pow_eq_one {Œ∂ : ‚ÑÇ} {n : ‚Ñï} (h : Œ∂ ^ n = 1) (hn : n ‚â† 0) : ‚ÄñŒ∂‚Äñ‚Çä = 1 := by
  refine' (@pow_left_inj NNReal _ _ _ _ zero_le' zero_le' hn.bot_lt).mp _
  rw [‚Üê nnnorm_pow, h, nnnorm_one, one_pow]
#align complex.nnnorm_eq_one_of_pow_eq_one Complex.nnnorm_eq_one_of_pow_eq_one

theorem norm_eq_one_of_pow_eq_one {Œ∂ : ‚ÑÇ} {n : ‚Ñï} (h : Œ∂ ^ n = 1) (hn : n ‚â† 0) : ‚ÄñŒ∂‚Äñ = 1 :=
  congr_arg Subtype.val (nnnorm_eq_one_of_pow_eq_one h hn)
#align complex.norm_eq_one_of_pow_eq_one Complex.norm_eq_one_of_pow_eq_one

theorem equivRealProd_apply_le (z : ‚ÑÇ) : ‚ÄñequivRealProd z‚Äñ ‚â§ abs z := by
  simp [Prod.norm_def, abs_re_le_abs, abs_im_le_abs]
#align complex.equiv_real_prod_apply_le Complex.equivRealProd_apply_le

theorem equivRealProd_apply_le' (z : ‚ÑÇ) : ‚ÄñequivRealProd z‚Äñ ‚â§ 1 * abs z := by
  simpa using equivRealProd_apply_le z
#align complex.equiv_real_prod_apply_le' Complex.equivRealProd_apply_le'

theorem lipschitz_equivRealProd : LipschitzWith 1 equivRealProd := by
  simpa using AddMonoidHomClass.lipschitz_of_bound equivRealProdLm 1 equivRealProd_apply_le'
#align complex.lipschitz_equiv_real_prod Complex.lipschitz_equivRealProd

theorem antilipschitz_equivRealProd : AntilipschitzWith (NNReal.sqrt 2) equivRealProd :=
  AddMonoidHomClass.antilipschitz_of_bound equivRealProdLm fun z ‚Ü¶ by
    simpa only [Real.coe_sqrt, NNReal.coe_ofNat] using abs_le_sqrt_two_mul_max z
#align complex.antilipschitz_equiv_real_prod Complex.antilipschitz_equivRealProd

theorem uniformEmbedding_equivRealProd : UniformEmbedding equivRealProd :=
  antilipschitz_equivRealProd.uniformEmbedding lipschitz_equivRealProd.uniformContinuous
#align complex.uniform_embedding_equiv_real_prod Complex.uniformEmbedding_equivRealProd

instance : CompleteSpace ‚ÑÇ :=
  (completeSpace_congr uniformEmbedding_equivRealProd).mpr inferInstance

/-- The natural `ContinuousLinearEquiv` from `‚ÑÇ` to `‚Ñù √ó ‚Ñù`. -/
@[simps! (config := { simpRhs := true }) apply symm_apply_re symm_apply_im]
def equivRealProdClm : ‚ÑÇ ‚âÉL[‚Ñù] ‚Ñù √ó ‚Ñù :=
  equivRealProdLm.toContinuousLinearEquivOfBounds 1 (Real.sqrt 2) equivRealProd_apply_le' fun p =>
    abs_le_sqrt_two_mul_max (equivRealProd.symm p)
#align complex.equiv_real_prod_clm Complex.equivRealProdClm

theorem equivRealProdClm_symm_apply (p : ‚Ñù √ó ‚Ñù) :
    Complex.equivRealProdClm.symm p = p.1 + p.2 * Complex.I := Complex.equivRealProd_symm_apply p

instance : ProperSpace ‚ÑÇ :=
  (id lipschitz_equivRealProd : LipschitzWith 1 equivRealProdClm.toHomeomorph).properSpace

/-- The `abs` function on `‚ÑÇ` is proper. -/
theorem tendsto_abs_cocompact_atTop : Tendsto abs (cocompact ‚ÑÇ) atTop :=
  tendsto_norm_cocompact_atTop
#align complex.tendsto_abs_cocompact_at_top Complex.tendsto_abs_cocompact_atTop

/-- The `normSq` function on `‚ÑÇ` is proper. -/
theorem tendsto_normSq_cocompact_atTop : Tendsto normSq (cocompact ‚ÑÇ) atTop :=
  by simpa [mul_self_abs]
    using tendsto_abs_cocompact_atTop.atTop_mul_atTop tendsto_abs_cocompact_atTop
#align complex.tendsto_norm_sq_cocompact_at_top Complex.tendsto_normSq_cocompact_atTop

open ContinuousLinearMap

/-- Continuous linear map version of the real part function, from `‚ÑÇ` to `‚Ñù`. -/
def reClm : ‚ÑÇ ‚ÜíL[‚Ñù] ‚Ñù :=
  reLm.mkContinuous 1 fun x => by simp [abs_re_le_abs]
#align complex.re_clm Complex.reClm

@[continuity]
theorem continuous_re : Continuous re :=
  reClm.continuous
#align complex.continuous_re Complex.continuous_re

@[simp]
theorem reClm_coe : (reClm : ‚ÑÇ ‚Üí‚Çó[‚Ñù] ‚Ñù) = reLm :=
  rfl
#align complex.re_clm_coe Complex.reClm_coe

@[simp]
theorem reClm_apply (z : ‚ÑÇ) : (reClm : ‚ÑÇ ‚Üí ‚Ñù) z = z.re :=
  rfl
#align complex.re_clm_apply Complex.reClm_apply

/-- Continuous linear map version of the imaginary part function, from `‚ÑÇ` to `‚Ñù`. -/
def imClm : ‚ÑÇ ‚ÜíL[‚Ñù] ‚Ñù :=
  imLm.mkContinuous 1 fun x => by simp [abs_im_le_abs]
#align complex.im_clm Complex.imClm

@[continuity]
theorem continuous_im : Continuous im :=
  imClm.continuous
#align complex.continuous_im Complex.continuous_im

@[simp]
theorem imClm_coe : (imClm : ‚ÑÇ ‚Üí‚Çó[‚Ñù] ‚Ñù) = imLm :=
  rfl
#align complex.im_clm_coe Complex.imClm_coe

@[simp]
theorem imClm_apply (z : ‚ÑÇ) : (imClm : ‚ÑÇ ‚Üí ‚Ñù) z = z.im :=
  rfl
#align complex.im_clm_apply Complex.imClm_apply

theorem restrictScalars_one_smulRight' (x : E) :
    ContinuousLinearMap.restrictScalars ‚Ñù ((1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smulRight x : ‚ÑÇ ‚ÜíL[‚ÑÇ] E) =
      reClm.smulRight x + I ‚Ä¢ imClm.smulRight x := by
  ext ‚ü®a, b‚ü©
  simp [mk_eq_add_mul_I, mul_smul, smul_comm I b x]
#align complex.restrict_scalars_one_smul_right' Complex.restrictScalars_one_smulRight'

theorem restrictScalars_one_smulRight (x : ‚ÑÇ) :
    ContinuousLinearMap.restrictScalars ‚Ñù ((1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smulRight x : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ) =
    x ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚Ñù] ‚ÑÇ) := by
  ext1 z
  dsimp
  apply mul_comm
#align complex.restrict_scalars_one_smul_right Complex.restrictScalars_one_smulRight

/-- The complex-conjugation function from `‚ÑÇ` to itself is an isometric linear equivalence. -/
def conjLie : ‚ÑÇ ‚âÉ‚Çó·µ¢[‚Ñù] ‚ÑÇ :=
  ‚ü®conjAe.toLinearEquiv, abs_conj‚ü©
#align complex.conj_lie Complex.conjLie

@[simp]
theorem conjLie_apply (z : ‚ÑÇ) : conjLie z = conj z :=
  rfl
#align complex.conj_lie_apply Complex.conjLie_apply

@[simp]
theorem conjLie_symm : conjLie.symm = conjLie :=
  rfl
#align complex.conj_lie_symm Complex.conjLie_symm

theorem isometry_conj : Isometry (conj : ‚ÑÇ ‚Üí ‚ÑÇ) :=
  conjLie.isometry
#align complex.isometry_conj Complex.isometry_conj

@[simp]
theorem dist_conj_conj (z w : ‚ÑÇ) : dist (conj z) (conj w) = dist z w :=
  isometry_conj.dist_eq z w
#align complex.dist_conj_conj Complex.dist_conj_conj

@[simp]
theorem nndist_conj_conj (z w : ‚ÑÇ) : nndist (conj z) (conj w) = nndist z w :=
  isometry_conj.nndist_eq z w
#align complex.nndist_conj_conj Complex.nndist_conj_conj

theorem dist_conj_comm (z w : ‚ÑÇ) : dist (conj z) w = dist z (conj w) := by
  rw [‚Üê dist_conj_conj, conj_conj]
#align complex.dist_conj_comm Complex.dist_conj_comm

theorem nndist_conj_comm (z w : ‚ÑÇ) : nndist (conj z) w = nndist z (conj w) :=
  Subtype.ext <| dist_conj_comm _ _
#align complex.nndist_conj_comm Complex.nndist_conj_comm

instance : ContinuousStar ‚ÑÇ :=
  ‚ü®conjLie.continuous‚ü©

@[continuity]
theorem continuous_conj : Continuous (conj : ‚ÑÇ ‚Üí ‚ÑÇ) :=
  continuous_star
#align complex.continuous_conj Complex.continuous_conj

/-- The only continuous ring homomorphisms from `‚ÑÇ` to `‚ÑÇ` are the identity and the complex
conjugation. -/
theorem ringHom_eq_id_or_conj_of_continuous {f : ‚ÑÇ ‚Üí+* ‚ÑÇ} (hf : Continuous f) :
    f = RingHom.id ‚ÑÇ ‚à® f = conj := by
  simpa only [FunLike.ext_iff] using real_algHom_eq_id_or_conj (AlgHom.mk' f (map_real_smul f hf))
#align complex.ring_hom_eq_id_or_conj_of_continuous Complex.ringHom_eq_id_or_conj_of_continuous

/-- Continuous linear equiv version of the conj function, from `‚ÑÇ` to `‚ÑÇ`. -/
def conjCle : ‚ÑÇ ‚âÉL[‚Ñù] ‚ÑÇ :=
  conjLie
#align complex.conj_cle Complex.conjCle

@[simp]
theorem conjCle_coe : conjCle.toLinearEquiv = conjAe.toLinearEquiv :=
  rfl
#align complex.conj_cle_coe Complex.conjCle_coe

@[simp]
theorem conjCle_apply (z : ‚ÑÇ) : conjCle z = conj z :=
  rfl
#align complex.conj_cle_apply Complex.conjCle_apply

/-- Linear isometry version of the canonical embedding of `‚Ñù` in `‚ÑÇ`. -/
def ofRealLi : ‚Ñù ‚Üí‚Çó·µ¢[‚Ñù] ‚ÑÇ :=
  ‚ü®ofRealAm.toLinearMap, norm_real‚ü©
#align complex.of_real_li Complex.ofRealLi

theorem isometry_ofReal : Isometry ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) :=
  ofRealLi.isometry
#align complex.isometry_of_real Complex.isometry_ofReal

@[continuity]
theorem continuous_ofReal : Continuous ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) :=
  ofRealLi.continuous
#align complex.continuous_of_real Complex.continuous_ofReal

/-- The only continuous ring homomorphism from `‚Ñù` to `‚ÑÇ` is the identity. -/
theorem ringHom_eq_ofReal_of_continuous {f : ‚Ñù ‚Üí+* ‚ÑÇ} (h : Continuous f) : f = Complex.ofReal := by
  convert congr_arg AlgHom.toRingHom <| Subsingleton.elim (AlgHom.mk' f <| map_real_smul f h)
    (Algebra.ofId ‚Ñù ‚ÑÇ)
#align complex.ring_hom_eq_of_real_of_continuous Complex.ringHom_eq_ofReal_of_continuous

/-- Continuous linear map version of the canonical embedding of `‚Ñù` in `‚ÑÇ`. -/
def ofRealClm : ‚Ñù ‚ÜíL[‚Ñù] ‚ÑÇ :=
  ofRealLi.toContinuousLinearMap
#align complex.of_real_clm Complex.ofRealClm

@[simp]
theorem ofRealClm_coe : (ofRealClm : ‚Ñù ‚Üí‚Çó[‚Ñù] ‚ÑÇ) = ofRealAm.toLinearMap :=
  rfl
#align complex.of_real_clm_coe Complex.ofRealClm_coe

@[simp]
theorem ofRealClm_apply (x : ‚Ñù) : ofRealClm x = x :=
  rfl
#align complex.of_real_clm_apply Complex.ofRealClm_apply

noncomputable instance : IsROrC ‚ÑÇ where
  re := ‚ü®‚ü®Complex.re, Complex.zero_re‚ü©, Complex.add_re‚ü©
  im := ‚ü®‚ü®Complex.im, Complex.zero_im‚ü©, Complex.add_im‚ü©
  I := Complex.I
  I_re_ax := I_re
  I_mul_I_ax := .inr Complex.I_mul_I
  re_add_im_ax := re_add_im
  ofReal_re_ax := ofReal_re
  ofReal_im_ax := ofReal_im
  mul_re_ax := mul_re
  mul_im_ax := mul_im
  conj_re_ax _ := rfl
  conj_im_ax _ := rfl
  conj_I_ax := conj_I
  norm_sq_eq_def_ax z := (normSq_eq_abs z).symm
  mul_im_I_ax _ := mul_one _
  toPartialOrder := Complex.partialOrder
  le_iff_re_im := Iff.rfl

theorem _root_.IsROrC.re_eq_complex_re : ‚áë(IsROrC.re : ‚ÑÇ ‚Üí+ ‚Ñù) = Complex.re :=
  rfl
#align is_R_or_C.re_eq_complex_re IsROrC.re_eq_complex_re

theorem _root_.IsROrC.im_eq_complex_im : ‚áë(IsROrC.im : ‚ÑÇ ‚Üí+ ‚Ñù) = Complex.im :=
  rfl
#align is_R_or_C.im_eq_complex_im IsROrC.im_eq_complex_im

section ComplexOrder

open ComplexOrder

theorem eq_coe_norm_of_nonneg {z : ‚ÑÇ} (hz : 0 ‚â§ z) : z = ‚Üë‚Äñz‚Äñ := by
  lift z to ‚Ñù using hz.2.symm
  rw [norm_eq_abs, abs_ofReal, _root_.abs_of_nonneg (id hz.1 : 0 ‚â§ z)]
#align complex.eq_coe_norm_of_nonneg Complex.eq_coe_norm_of_nonneg

end ComplexOrder

end Complex

namespace IsROrC

open ComplexConjugate

local notation "reC" => @IsROrC.re ‚ÑÇ _
local notation "imC" => @IsROrC.im ‚ÑÇ _
local notation "IC" => @IsROrC.I ‚ÑÇ _
local notation "norm_sqC" => @IsROrC.normSq ‚ÑÇ _

@[simp]
theorem re_to_complex {x : ‚ÑÇ} : reC x = x.re :=
  rfl
#align is_R_or_C.re_to_complex IsROrC.re_to_complex

@[simp]
theorem im_to_complex {x : ‚ÑÇ} : imC x = x.im :=
  rfl
#align is_R_or_C.im_to_complex IsROrC.im_to_complex

@[simp]
theorem I_to_complex : IC = Complex.I :=
  rfl
set_option linter.uppercaseLean3 false in
#align is_R_or_C.I_to_complex IsROrC.I_to_complex

@[simp]
theorem normSq_to_complex {x : ‚ÑÇ} : norm_sqC x = Complex.normSq x :=
  rfl
#align is_R_or_C.norm_sq_to_complex IsROrC.normSq_to_complex

section tsum

variable {Œ± : Type*} (ùïú : Type*) [IsROrC ùïú]

@[simp]
theorem hasSum_conj {f : Œ± ‚Üí ùïú} {x : ùïú} : HasSum (fun x => conj (f x)) x ‚Üî HasSum f (conj x) :=
  conjCle.hasSum
#align is_R_or_C.has_sum_conj IsROrC.hasSum_conj

theorem hasSum_conj' {f : Œ± ‚Üí ùïú} {x : ùïú} : HasSum (fun x => conj (f x)) (conj x) ‚Üî HasSum f x :=
  conjCle.hasSum'
#align is_R_or_C.has_sum_conj' IsROrC.hasSum_conj'

@[simp]
theorem summable_conj {f : Œ± ‚Üí ùïú} : (Summable fun x => conj (f x)) ‚Üî Summable f :=
  summable_star_iff
#align is_R_or_C.summable_conj IsROrC.summable_conj

variable {ùïú}

theorem conj_tsum (f : Œ± ‚Üí ùïú) : conj (‚àë' a, f a) = ‚àë' a, conj (f a) :=
  tsum_star
#align is_R_or_C.conj_tsum IsROrC.conj_tsum

variable (ùïú)

@[simp, norm_cast]
theorem hasSum_ofReal {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} : HasSum (fun x => (f x : ùïú)) x ‚Üî HasSum f x :=
  ‚ü®fun h => by simpa only [IsROrC.reClm_apply, IsROrC.ofReal_re] using reClm.hasSum h,
    ofRealClm.hasSum‚ü©
#align is_R_or_C.has_sum_of_real IsROrC.hasSum_ofReal

@[simp, norm_cast]
theorem summable_ofReal {f : Œ± ‚Üí ‚Ñù} : (Summable fun x => (f x : ùïú)) ‚Üî Summable f :=
  ‚ü®fun h => by simpa only [IsROrC.reClm_apply, IsROrC.ofReal_re] using reClm.summable h,
    ofRealClm.summable‚ü©
#align is_R_or_C.summable_of_real IsROrC.summable_ofReal

@[norm_cast]
theorem ofReal_tsum (f : Œ± ‚Üí ‚Ñù) : (‚Üë(‚àë' a, f a) : ùïú) = ‚àë' a, (f a : ùïú) := by
  by_cases h : Summable f
  ¬∑ exact ContinuousLinearMap.map_tsum ofRealClm h
  ¬∑ rw [tsum_eq_zero_of_not_summable h,
      tsum_eq_zero_of_not_summable ((summable_ofReal _).not.mpr h), ofReal_zero]
#align is_R_or_C.of_real_tsum IsROrC.ofReal_tsum

theorem hasSum_re {f : Œ± ‚Üí ùïú} {x : ùïú} (h : HasSum f x) : HasSum (fun x => re (f x)) (re x) :=
  reClm.hasSum h
#align is_R_or_C.has_sum_re IsROrC.hasSum_re

theorem hasSum_im {f : Œ± ‚Üí ùïú} {x : ùïú} (h : HasSum f x) : HasSum (fun x => im (f x)) (im x) :=
  imClm.hasSum h
#align is_R_or_C.has_sum_im IsROrC.hasSum_im

theorem re_tsum {f : Œ± ‚Üí ùïú} (h : Summable f) : re (‚àë' a, f a) = ‚àë' a, re (f a) :=
  reClm.map_tsum h
#align is_R_or_C.re_tsum IsROrC.re_tsum

theorem im_tsum {f : Œ± ‚Üí ùïú} (h : Summable f) : im (‚àë' a, f a) = ‚àë' a, im (f a) :=
  imClm.map_tsum h
#align is_R_or_C.im_tsum IsROrC.im_tsum

variable {ùïú}

theorem hasSum_iff (f : Œ± ‚Üí ùïú) (c : ùïú) :
    HasSum f c ‚Üî HasSum (fun x => re (f x)) (re c) ‚àß HasSum (fun x => im (f x)) (im c) := by
  refine' ‚ü®fun h => ‚ü®hasSum_re _ h, hasSum_im _ h‚ü©, _‚ü©
  rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  simpa only [re_add_im] using
    ((hasSum_ofReal ùïú).mpr h‚ÇÅ).add (((hasSum_ofReal ùïú).mpr h‚ÇÇ).mul_right I)
#align is_R_or_C.has_sum_iff IsROrC.hasSum_iff

end tsum

end IsROrC

namespace Complex

/-!
We have to repeat the lemmas about `IsROrC.re` and `IsROrC.im` as they are not syntactic
matches for `Complex.re` and `Complex.im`.

We do not have this problem with `ofReal` and `conj`, although we repeat them anyway for
discoverability and to avoid the need to unify `ùïú`.
-/


section tsum

variable {Œ± : Type*}

open ComplexConjugate

-- Porting note: @[simp] unneeded due to `IsROrC.hasSum_conj`
theorem hasSum_conj {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} : HasSum (fun x => conj (f x)) x ‚Üî HasSum f (conj x) :=
  IsROrC.hasSum_conj _
#align complex.has_sum_conj Complex.hasSum_conj

theorem hasSum_conj' {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} : HasSum (fun x => conj (f x)) (conj x) ‚Üî HasSum f x :=
  IsROrC.hasSum_conj' _
#align complex.has_sum_conj' Complex.hasSum_conj'

-- Porting note: @[simp] unneeded due to `IsROrC.summable_conj`
theorem summable_conj {f : Œ± ‚Üí ‚ÑÇ} : (Summable fun x => conj (f x)) ‚Üî Summable f :=
  IsROrC.summable_conj _
#align complex.summable_conj Complex.summable_conj

theorem conj_tsum (f : Œ± ‚Üí ‚ÑÇ) : conj (‚àë' a, f a) = ‚àë' a, conj (f a) :=
  IsROrC.conj_tsum _
#align complex.conj_tsum Complex.conj_tsum

@[simp, norm_cast]
theorem hasSum_ofReal {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} : HasSum (fun x => (f x : ‚ÑÇ)) x ‚Üî HasSum f x :=
  IsROrC.hasSum_ofReal _
#align complex.has_sum_of_real Complex.hasSum_ofReal

@[simp, norm_cast]
theorem summable_ofReal {f : Œ± ‚Üí ‚Ñù} : (Summable fun x => (f x : ‚ÑÇ)) ‚Üî Summable f :=
  IsROrC.summable_ofReal _
#align complex.summable_of_real Complex.summable_ofReal

@[norm_cast]
theorem ofReal_tsum (f : Œ± ‚Üí ‚Ñù) : (‚Üë(‚àë' a, f a) : ‚ÑÇ) = ‚àë' a, ‚Üë(f a) :=
  IsROrC.ofReal_tsum _ _
#align complex.of_real_tsum Complex.ofReal_tsum

theorem hasSum_re {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} (h : HasSum f x) : HasSum (fun x => (f x).re) x.re :=
  IsROrC.hasSum_re _ h
#align complex.has_sum_re Complex.hasSum_re

theorem hasSum_im {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} (h : HasSum f x) : HasSum (fun x => (f x).im) x.im :=
  IsROrC.hasSum_im _ h
#align complex.has_sum_im Complex.hasSum_im

theorem re_tsum {f : Œ± ‚Üí ‚ÑÇ} (h : Summable f) : (‚àë' a, f a).re = ‚àë' a, (f a).re :=
  IsROrC.re_tsum _ h
#align complex.re_tsum Complex.re_tsum

theorem im_tsum {f : Œ± ‚Üí ‚ÑÇ} (h : Summable f) : (‚àë' a, f a).im = ‚àë' a, (f a).im :=
  IsROrC.im_tsum _ h
#align complex.im_tsum Complex.im_tsum

theorem hasSum_iff (f : Œ± ‚Üí ‚ÑÇ) (c : ‚ÑÇ) :
    HasSum f c ‚Üî HasSum (fun x => (f x).re) c.re ‚àß HasSum (fun x => (f x).im) c.im :=
  IsROrC.hasSum_iff _ _
#align complex.has_sum_iff Complex.hasSum_iff

end tsum

end Complex
