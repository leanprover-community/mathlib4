/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Data.Complex.BigOperators
import Mathlib.Data.Complex.Module
import Mathlib.Data.Complex.Order
import Mathlib.Topology.Algebra.InfiniteSum.Field
import Mathlib.Topology.Algebra.InfiniteSum.Module
import Mathlib.Topology.Instances.RealVectorSpace
import Mathlib.Topology.MetricSpace.ProperSpace.Real

/-!

# Normed space structure on `‚ÑÇ`.

This file gathers basic facts of analytic nature on the complex numbers.

## Main results

This file registers `‚ÑÇ` as a normed field, expresses basic properties of the norm, and gives tools
on the real vector space structure of `‚ÑÇ`. Notably, it defines the following functions in the
namespace `Complex`.

|Name              |Type         |Description                                             |
|------------------|-------------|--------------------------------------------------------|
|`equivRealProdCLM`|‚ÑÇ ‚âÉL[‚Ñù] ‚Ñù √ó ‚Ñù|The natural `ContinuousLinearEquiv` from `‚ÑÇ` to `‚Ñù √ó ‚Ñù` |
|`reCLM`           |‚ÑÇ ‚ÜíL[‚Ñù] ‚Ñù    |Real part function as a `ContinuousLinearMap`           |
|`imCLM`           |‚ÑÇ ‚ÜíL[‚Ñù] ‚Ñù    |Imaginary part function as a `ContinuousLinearMap`      |
|`ofRealCLM`       |‚Ñù ‚ÜíL[‚Ñù] ‚ÑÇ    |Embedding of the reals as a `ContinuousLinearMap`       |
|`ofRealLI`        |‚Ñù ‚Üí‚Çó·µ¢[‚Ñù] ‚ÑÇ   |Embedding of the reals as a `LinearIsometry`            |
|`conjCLE`         |‚ÑÇ ‚âÉL[‚Ñù] ‚ÑÇ    |Complex conjugation as a `ContinuousLinearEquiv`        |
|`conjLIE`         |‚ÑÇ ‚âÉ‚Çó·µ¢[‚Ñù] ‚ÑÇ   |Complex conjugation as a `LinearIsometryEquiv`          |

We also register the fact that `‚ÑÇ` is an `RCLike` field.

-/


assert_not_exists Absorbs

noncomputable section

namespace Complex
variable {z : ‚ÑÇ}

open ComplexConjugate Topology Filter

instance : NormedField ‚ÑÇ where
  dist_eq _ _ := rfl
  norm_mul := Complex.norm_mul

instance : DenselyNormedField ‚ÑÇ where
  lt_norm_lt r‚ÇÅ r‚ÇÇ h‚ÇÄ hr :=
    let ‚ü®x, h‚ü© := exists_between hr
    ‚ü®x, by rwa [norm_real, Real.norm_of_nonneg (h‚ÇÄ.trans_lt h.1).le]‚ü©

instance {R : Type*} [NormedField R] [NormedAlgebra R ‚Ñù] : NormedAlgebra R ‚ÑÇ where
  norm_smul_le r x := by
    rw [‚Üê algebraMap_smul ‚Ñù r x, real_smul, norm_mul, norm_real, norm_algebraMap']

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ‚ÑÇ E]

-- see Note [lower instance priority]
/-- The module structure from `Module.complexToReal` is a normed space. -/
instance (priority := 900) _root_.NormedSpace.complexToReal : NormedSpace ‚Ñù E :=
  NormedSpace.restrictScalars ‚Ñù ‚ÑÇ E

-- see Note [lower instance priority]
/-- The algebra structure from `Algebra.complexToReal` is a normed algebra. -/
instance (priority := 900) _root_.NormedAlgebra.complexToReal {A : Type*} [SeminormedRing A]
    [NormedAlgebra ‚ÑÇ A] : NormedAlgebra ‚Ñù A :=
  NormedAlgebra.restrictScalars ‚Ñù ‚ÑÇ A

-- This result cannot be moved to `Data/Complex/Norm` since `‚Ñ§` gets its norm from its
-- normed ring structure and that file does not know about rings
@[simp 1100, norm_cast] lemma nnnorm_intCast (n : ‚Ñ§) : ‚Äñ(n : ‚ÑÇ)‚Äñ‚Çä = ‚Äñn‚Äñ‚Çä := by
  ext; exact norm_intCast n

@[deprecated (since := "2025-02-16")] alias comap_abs_nhds_zero := comap_norm_nhds_zero
@[deprecated (since := "2025-02-16")] alias continuous_abs := continuous_norm

@[continuity, fun_prop]
theorem continuous_normSq : Continuous normSq := by
  simpa [‚Üê Complex.normSq_eq_norm_sq] using continuous_norm (E := ‚ÑÇ).pow 2

theorem nnnorm_eq_one_of_pow_eq_one {Œ∂ : ‚ÑÇ} {n : ‚Ñï} (h : Œ∂ ^ n = 1) (hn : n ‚â† 0) : ‚ÄñŒ∂‚Äñ‚Çä = 1 :=
  (pow_left_inj‚ÇÄ zero_le' zero_le' hn).1 <| by rw [‚Üê nnnorm_pow, h, nnnorm_one, one_pow]

theorem norm_eq_one_of_pow_eq_one {Œ∂ : ‚ÑÇ} {n : ‚Ñï} (h : Œ∂ ^ n = 1) (hn : n ‚â† 0) : ‚ÄñŒ∂‚Äñ = 1 :=
  congr_arg Subtype.val (nnnorm_eq_one_of_pow_eq_one h hn)

lemma le_of_eq_sum_of_eq_sum_norm {Œπ : Type*} {a b : ‚Ñù} (f : Œπ ‚Üí ‚ÑÇ) (s : Finset Œπ) (ha‚ÇÄ : 0 ‚â§ a)
    (ha : a = ‚àë i ‚àà s, f i) (hb : b = ‚àë i ‚àà s, (‚Äñf i‚Äñ : ‚ÑÇ)) : a ‚â§ b := by
  norm_cast at hb; rw [‚Üê Complex.norm_of_nonneg ha‚ÇÄ, ha, hb]; exact norm_sum_le s f

theorem equivRealProd_apply_le (z : ‚ÑÇ) : ‚ÄñequivRealProd z‚Äñ ‚â§ ‚Äñz‚Äñ := by
  simp [Prod.norm_def, abs_re_le_norm, abs_im_le_norm]

theorem equivRealProd_apply_le' (z : ‚ÑÇ) : ‚ÄñequivRealProd z‚Äñ ‚â§ 1 * ‚Äñz‚Äñ := by
  simpa using equivRealProd_apply_le z

theorem lipschitz_equivRealProd : LipschitzWith 1 equivRealProd := by
  simpa using AddMonoidHomClass.lipschitz_of_bound equivRealProdLm 1 equivRealProd_apply_le'

theorem antilipschitz_equivRealProd : AntilipschitzWith (NNReal.sqrt 2) equivRealProd :=
  AddMonoidHomClass.antilipschitz_of_bound equivRealProdLm fun z ‚Ü¶ by
    simpa only [Real.coe_sqrt, NNReal.coe_ofNat] using norm_le_sqrt_two_mul_max z

theorem isUniformEmbedding_equivRealProd : IsUniformEmbedding equivRealProd :=
  antilipschitz_equivRealProd.isUniformEmbedding lipschitz_equivRealProd.uniformContinuous

instance : CompleteSpace ‚ÑÇ :=
  (completeSpace_congr isUniformEmbedding_equivRealProd).mpr inferInstance

instance instT2Space : T2Space ‚ÑÇ := TopologicalSpace.t2Space_of_metrizableSpace

/-- The natural `ContinuousLinearEquiv` from `‚ÑÇ` to `‚Ñù √ó ‚Ñù`. -/
@[simps! +simpRhs apply symm_apply_re symm_apply_im]
def equivRealProdCLM : ‚ÑÇ ‚âÉL[‚Ñù] ‚Ñù √ó ‚Ñù :=
  equivRealProdLm.toContinuousLinearEquivOfBounds 1 (‚àö2) equivRealProd_apply_le' fun p =>
    norm_le_sqrt_two_mul_max (equivRealProd.symm p)

theorem equivRealProdCLM_symm_apply (p : ‚Ñù √ó ‚Ñù) :
    Complex.equivRealProdCLM.symm p = p.1 + p.2 * Complex.I := Complex.equivRealProd_symm_apply p

instance : ProperSpace ‚ÑÇ := lipschitz_equivRealProd.properSpace
  equivRealProdCLM.toHomeomorph.isProperMap

@[deprecated (since := "2025-02-16")] alias tendsto_abs_cocompact_atTop :=
  tendsto_norm_cocompact_atTop

/-- The `normSq` function on `‚ÑÇ` is proper. -/
theorem tendsto_normSq_cocompact_atTop : Tendsto normSq (cocompact ‚ÑÇ) atTop := by
  simpa [norm_mul_self_eq_normSq]
    using tendsto_norm_cocompact_atTop.atTop_mul_atTop‚ÇÄ (tendsto_norm_cocompact_atTop (E := ‚ÑÇ))

open ContinuousLinearMap

/-- Continuous linear map version of the real part function, from `‚ÑÇ` to `‚Ñù`. -/
def reCLM : ‚ÑÇ ‚ÜíL[‚Ñù] ‚Ñù :=
  reLm.mkContinuous 1 fun x => by simp [abs_re_le_norm]

@[continuity, fun_prop]
theorem continuous_re : Continuous re :=
  reCLM.continuous

lemma uniformlyContinuous_re : UniformContinuous re :=
  reCLM.uniformContinuous

@[simp]
theorem reCLM_coe : (reCLM : ‚ÑÇ ‚Üí‚Çó[‚Ñù] ‚Ñù) = reLm :=
  rfl

@[simp]
theorem reCLM_apply (z : ‚ÑÇ) : (reCLM : ‚ÑÇ ‚Üí ‚Ñù) z = z.re :=
  rfl

/-- Continuous linear map version of the imaginary part function, from `‚ÑÇ` to `‚Ñù`. -/
def imCLM : ‚ÑÇ ‚ÜíL[‚Ñù] ‚Ñù :=
  imLm.mkContinuous 1 fun x => by simp [abs_im_le_norm]

@[continuity, fun_prop]
theorem continuous_im : Continuous im :=
  imCLM.continuous

lemma uniformlyContinuous_im : UniformContinuous im :=
  imCLM.uniformContinuous

@[simp]
theorem imCLM_coe : (imCLM : ‚ÑÇ ‚Üí‚Çó[‚Ñù] ‚Ñù) = imLm :=
  rfl

@[simp]
theorem imCLM_apply (z : ‚ÑÇ) : (imCLM : ‚ÑÇ ‚Üí ‚Ñù) z = z.im :=
  rfl

theorem restrictScalars_one_smulRight' (x : E) :
    ContinuousLinearMap.restrictScalars ‚Ñù ((1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smulRight x : ‚ÑÇ ‚ÜíL[‚ÑÇ] E) =
      reCLM.smulRight x + I ‚Ä¢ imCLM.smulRight x := by
  ext ‚ü®a, b‚ü©
  simp [map_add, mk_eq_add_mul_I, mul_smul, smul_comm I b x]

theorem restrictScalars_one_smulRight (x : ‚ÑÇ) :
    ContinuousLinearMap.restrictScalars ‚Ñù ((1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smulRight x : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ) =
    x ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚Ñù] ‚ÑÇ) := by
  ext1 z
  dsimp
  apply mul_comm

/-- The complex-conjugation function from `‚ÑÇ` to itself is an isometric linear equivalence. -/
def conjLIE : ‚ÑÇ ‚âÉ‚Çó·µ¢[‚Ñù] ‚ÑÇ :=
  ‚ü®conjAe.toLinearEquiv, norm_conj‚ü©

@[simp]
theorem conjLIE_apply (z : ‚ÑÇ) : conjLIE z = conj z :=
  rfl

@[simp]
theorem conjLIE_symm : conjLIE.symm = conjLIE :=
  rfl

theorem isometry_conj : Isometry (conj : ‚ÑÇ ‚Üí ‚ÑÇ) :=
  conjLIE.isometry

@[simp]
theorem dist_conj_conj (z w : ‚ÑÇ) : dist (conj z) (conj w) = dist z w :=
  isometry_conj.dist_eq z w

@[simp]
theorem nndist_conj_conj (z w : ‚ÑÇ) : nndist (conj z) (conj w) = nndist z w :=
  isometry_conj.nndist_eq z w

theorem dist_conj_comm (z w : ‚ÑÇ) : dist (conj z) w = dist z (conj w) := by
  rw [‚Üê dist_conj_conj, conj_conj]

theorem nndist_conj_comm (z w : ‚ÑÇ) : nndist (conj z) w = nndist z (conj w) :=
  Subtype.ext <| dist_conj_comm _ _

instance : ContinuousStar ‚ÑÇ :=
  ‚ü®conjLIE.continuous‚ü©

@[continuity]
theorem continuous_conj : Continuous (conj : ‚ÑÇ ‚Üí ‚ÑÇ) :=
  continuous_star

/-- The only continuous ring homomorphisms from `‚ÑÇ` to `‚ÑÇ` are the identity and the complex
conjugation. -/
theorem ringHom_eq_id_or_conj_of_continuous {f : ‚ÑÇ ‚Üí+* ‚ÑÇ} (hf : Continuous f) :
    f = RingHom.id ‚ÑÇ ‚à® f = conj := by
  simpa only [DFunLike.ext_iff] using real_algHom_eq_id_or_conj (AlgHom.mk' f (map_real_smul f hf))

/-- Continuous linear equiv version of the conj function, from `‚ÑÇ` to `‚ÑÇ`. -/
def conjCLE : ‚ÑÇ ‚âÉL[‚Ñù] ‚ÑÇ :=
  conjLIE

@[simp]
theorem conjCLE_coe : conjCLE.toLinearEquiv = conjAe.toLinearEquiv :=
  rfl

@[simp]
theorem conjCLE_apply (z : ‚ÑÇ) : conjCLE z = conj z :=
  rfl

/-- Linear isometry version of the canonical embedding of `‚Ñù` in `‚ÑÇ`. -/
def ofRealLI : ‚Ñù ‚Üí‚Çó·µ¢[‚Ñù] ‚ÑÇ :=
  ‚ü®ofRealAm.toLinearMap, norm_real‚ü©

theorem isometry_ofReal : Isometry ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) :=
  ofRealLI.isometry

@[continuity, fun_prop]
theorem continuous_ofReal : Continuous ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) :=
  ofRealLI.continuous

theorem isUniformEmbedding_ofReal : IsUniformEmbedding ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) :=
  ofRealLI.isometry.isUniformEmbedding

lemma _root_.RCLike.isUniformEmbedding_ofReal {ùïú : Type*} [RCLike ùïú] :
    IsUniformEmbedding ((‚Üë) : ‚Ñù ‚Üí ùïú) :=
  RCLike.ofRealLI.isometry.isUniformEmbedding

theorem _root_.Filter.tendsto_ofReal_iff {Œ± : Type*} {l : Filter Œ±} {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} :
    Tendsto (fun x ‚Ü¶ (f x : ‚ÑÇ)) l (ùìù (x : ‚ÑÇ)) ‚Üî Tendsto f l (ùìù x) :=
  isUniformEmbedding_ofReal.isClosedEmbedding.tendsto_nhds_iff.symm

lemma _root_.Filter.tendsto_ofReal_iff' {Œ± ùïú : Type*} [RCLike ùïú]
    {l : Filter Œ±} {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} :
    Tendsto (fun x ‚Ü¶ (f x : ùïú)) l (ùìù (x : ùïú)) ‚Üî Tendsto f l (ùìù x) :=
  RCLike.isUniformEmbedding_ofReal.isClosedEmbedding.tendsto_nhds_iff.symm

lemma _root_.Filter.Tendsto.ofReal {Œ± : Type*} {l : Filter Œ±} {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù}
    (hf : Tendsto f l (ùìù x)) : Tendsto (fun x ‚Ü¶ (f x : ‚ÑÇ)) l (ùìù (x : ‚ÑÇ)) :=
  tendsto_ofReal_iff.mpr hf

/-- The only continuous ring homomorphism from `‚Ñù` to `‚ÑÇ` is the identity. -/
theorem ringHom_eq_ofReal_of_continuous {f : ‚Ñù ‚Üí+* ‚ÑÇ} (h : Continuous f) : f = ofRealHom := by
  convert congr_arg AlgHom.toRingHom <| Subsingleton.elim (AlgHom.mk' f <| map_real_smul f h)
    (Algebra.ofId ‚Ñù ‚ÑÇ)

/-- Continuous linear map version of the canonical embedding of `‚Ñù` in `‚ÑÇ`. -/
def ofRealCLM : ‚Ñù ‚ÜíL[‚Ñù] ‚ÑÇ :=
  ofRealLI.toContinuousLinearMap

@[simp]
theorem ofRealCLM_coe : (ofRealCLM : ‚Ñù ‚Üí‚Çó[‚Ñù] ‚ÑÇ) = ofRealAm.toLinearMap :=
  rfl

@[simp]
theorem ofRealCLM_apply (x : ‚Ñù) : ofRealCLM x = x :=
  rfl

noncomputable instance : RCLike ‚ÑÇ where
  re := ‚ü®‚ü®Complex.re, Complex.zero_re‚ü©, Complex.add_re‚ü©
  im := ‚ü®‚ü®Complex.im, Complex.zero_im‚ü©, Complex.add_im‚ü©
  I := Complex.I
  I_re_ax := I_re
  I_mul_I_ax := .inr Complex.I_mul_I
  re_add_im_ax := re_add_im
  ofReal_re_ax := ofReal_re
  ofReal_im_ax := ofReal_im
  mul_re_ax := mul_re
  mul_im_ax := mul_im
  conj_re_ax _ := rfl
  conj_im_ax _ := rfl
  conj_I_ax := conj_I
  norm_sq_eq_def_ax z := (normSq_eq_norm_sq z).symm
  mul_im_I_ax _ := mul_one _
  toPartialOrder := Complex.partialOrder
  le_iff_re_im := Iff.rfl

theorem _root_.RCLike.re_eq_complex_re : ‚áë(RCLike.re : ‚ÑÇ ‚Üí+ ‚Ñù) = Complex.re :=
  rfl

theorem _root_.RCLike.im_eq_complex_im : ‚áë(RCLike.im : ‚ÑÇ ‚Üí+ ‚Ñù) = Complex.im :=
  rfl

theorem _root_.RCLike.ofReal_eq_complex_ofReal : (RCLike.ofReal : ‚Ñù ‚Üí ‚ÑÇ) = Complex.ofReal := rfl

-- TODO: Replace `mul_conj` and `conj_mul` once `norm` has replaced `abs`
lemma mul_conj' (z : ‚ÑÇ) : z * conj z = ‚Äñz‚Äñ ^ 2 := RCLike.mul_conj z
lemma conj_mul' (z : ‚ÑÇ) : conj z * z = ‚Äñz‚Äñ ^ 2 := RCLike.conj_mul z

lemma inv_eq_conj (hz : ‚Äñz‚Äñ = 1) : z‚Åª¬π = conj z := RCLike.inv_eq_conj hz

lemma exists_norm_eq_mul_self (z : ‚ÑÇ) : ‚àÉ c, ‚Äñc‚Äñ = 1 ‚àß ‚Äñz‚Äñ = c * z :=
  RCLike.exists_norm_eq_mul_self _

lemma exists_norm_mul_eq_self (z : ‚ÑÇ) : ‚àÉ c, ‚Äñc‚Äñ = 1 ‚àß c * ‚Äñz‚Äñ = z :=
  RCLike.exists_norm_mul_eq_self _

lemma im_eq_zero_iff_isSelfAdjoint (x : ‚ÑÇ) : Complex.im x = 0 ‚Üî IsSelfAdjoint x := by
  rw [‚Üê RCLike.im_eq_complex_im]
  exact RCLike.im_eq_zero_iff_isSelfAdjoint

lemma re_eq_ofReal_of_isSelfAdjoint {x : ‚ÑÇ} {y : ‚Ñù} (hx : IsSelfAdjoint x) :
    Complex.re x = y ‚Üî x = y := by
  rw [‚Üê RCLike.re_eq_complex_re]
  exact RCLike.re_eq_ofReal_of_isSelfAdjoint hx

lemma ofReal_eq_re_of_isSelfAdjoint {x : ‚ÑÇ} {y : ‚Ñù} (hx : IsSelfAdjoint x) :
    y = Complex.re x ‚Üî y = x := by
  rw [‚Üê RCLike.re_eq_complex_re]
  exact RCLike.ofReal_eq_re_of_isSelfAdjoint hx

/-- The natural isomorphism between `ùïú` satisfying `RCLike ùïú` and `‚ÑÇ` when
`RCLike.im RCLike.I = 1`. -/
@[simps]
def _root_.RCLike.complexRingEquiv {ùïú : Type*} [RCLike ùïú]
    (h : RCLike.im (RCLike.I : ùïú) = 1) : ùïú ‚âÉ+* ‚ÑÇ where
  toFun x := RCLike.re x + RCLike.im x * I
  invFun x := re x + im x * RCLike.I
  left_inv x := by simp
  right_inv x := by simp [h]
  map_add' x y := by simp only [map_add, ofReal_add]; ring
  map_mul' x y := by
    simp only [RCLike.mul_re, ofReal_sub, ofReal_mul, RCLike.mul_im, ofReal_add]
    ring_nf
    rw [I_sq]
    ring

/-- The natural `‚Ñù`-linear isometry equivalence between `ùïú` satisfying `RCLike ùïú` and `‚ÑÇ` when
`RCLike.im RCLike.I = 1`. -/
@[simps]
def _root_.RCLike.complexLinearIsometryEquiv {ùïú : Type*} [RCLike ùïú]
    (h : RCLike.im (RCLike.I : ùïú) = 1) : ùïú ‚âÉ‚Çó·µ¢[‚Ñù] ‚ÑÇ where
  map_smul' _ _ := by simp [RCLike.smul_re, RCLike.smul_im, ofReal_mul]; ring
  norm_map' _ := by
    rw [‚Üê sq_eq_sq‚ÇÄ (by positivity) (by positivity), ‚Üê normSq_eq_norm_sq, ‚Üê RCLike.normSq_eq_def',
      RCLike.normSq_apply]
    simp [normSq_add]
  __ := RCLike.complexRingEquiv h

theorem isometry_intCast : Isometry ((‚Üë) : ‚Ñ§ ‚Üí ‚ÑÇ) :=
  Isometry.of_dist_eq <| by simp_rw [‚Üê Complex.ofReal_intCast,
    Complex.isometry_ofReal.dist_eq, Int.dist_cast_real, implies_true]

theorem closedEmbedding_intCast : IsClosedEmbedding ((‚Üë) : ‚Ñ§ ‚Üí ‚ÑÇ) :=
  isometry_intCast.isClosedEmbedding

lemma isClosed_range_intCast : IsClosed (Set.range ((‚Üë) : ‚Ñ§ ‚Üí ‚ÑÇ)) :=
  Complex.closedEmbedding_intCast.isClosed_range

lemma isOpen_compl_range_intCast : IsOpen (Set.range ((‚Üë) : ‚Ñ§ ‚Üí ‚ÑÇ))·∂ú :=
  Complex.isClosed_range_intCast.isOpen_compl

section ComplexOrder

open ComplexOrder

theorem eq_coe_norm_of_nonneg {z : ‚ÑÇ} (hz : 0 ‚â§ z) : z = ‚Üë‚Äñz‚Äñ := by
  lift z to ‚Ñù using hz.2.symm
  rw [norm_real, Real.norm_of_nonneg (id hz.1 : 0 ‚â§ z)]

/-- We show that the partial order and the topology on `‚ÑÇ` are compatible.
We turn this into an instance scoped to `ComplexOrder`. -/
lemma orderClosedTopology : OrderClosedTopology ‚ÑÇ where
  isClosed_le' := by
    simp_rw [le_def, Set.setOf_and]
    refine IsClosed.inter (isClosed_le ?_ ?_) (isClosed_eq ?_ ?_) <;> continuity

scoped[ComplexOrder] attribute [instance] Complex.orderClosedTopology

theorem norm_of_nonneg' {x : ‚ÑÇ} (hx : 0 ‚â§ x) : ‚Äñx‚Äñ = x := by
  rw [‚Üê RCLike.ofReal_eq_complex_ofReal]
  exact RCLike.norm_of_nonneg' hx

lemma re_nonneg_iff_nonneg {x : ‚ÑÇ} (hx : IsSelfAdjoint x) : 0 ‚â§ re x ‚Üî 0 ‚â§ x := by
  rw [‚Üê RCLike.re_eq_complex_re]
  exact RCLike.re_nonneg_of_nonneg hx

@[gcongr]
lemma re_le_re {x y : ‚ÑÇ} (h : x ‚â§ y) : re x ‚â§ re y := by
  rw [RCLike.le_iff_re_im] at h
  exact h.1

end ComplexOrder

end Complex

namespace RCLike

open ComplexConjugate

local notation "reC" => @RCLike.re ‚ÑÇ _
local notation "imC" => @RCLike.im ‚ÑÇ _
local notation "IC" => @RCLike.I ‚ÑÇ _
local notation "norm_sqC" => @RCLike.normSq ‚ÑÇ _

@[simp]
theorem re_to_complex {x : ‚ÑÇ} : reC x = x.re :=
  rfl

@[simp]
theorem im_to_complex {x : ‚ÑÇ} : imC x = x.im :=
  rfl

@[simp]
theorem I_to_complex : IC = Complex.I :=
  rfl

@[simp]
theorem normSq_to_complex {x : ‚ÑÇ} : norm_sqC x = Complex.normSq x :=
  rfl

section tsum

variable {Œ± : Type*} (ùïú : Type*) [RCLike ùïú]

@[simp]
theorem hasSum_conj {f : Œ± ‚Üí ùïú} {x : ùïú} : HasSum (fun x => conj (f x)) x ‚Üî HasSum f (conj x) :=
  conjCLE.hasSum

theorem hasSum_conj' {f : Œ± ‚Üí ùïú} {x : ùïú} : HasSum (fun x => conj (f x)) (conj x) ‚Üî HasSum f x :=
  conjCLE.hasSum'

@[simp]
theorem summable_conj {f : Œ± ‚Üí ùïú} : (Summable fun x => conj (f x)) ‚Üî Summable f :=
  summable_star_iff

variable {ùïú} in
theorem conj_tsum (f : Œ± ‚Üí ùïú) : conj (‚àë' a, f a) = ‚àë' a, conj (f a) :=
  tsum_star

@[simp, norm_cast]
theorem hasSum_ofReal {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} : HasSum (fun x => (f x : ùïú)) x ‚Üî HasSum f x :=
  ‚ü®fun h => by simpa only [RCLike.reCLM_apply, RCLike.ofReal_re] using reCLM.hasSum h,
    ofRealCLM.hasSum‚ü©

@[simp, norm_cast]
theorem summable_ofReal {f : Œ± ‚Üí ‚Ñù} : (Summable fun x => (f x : ùïú)) ‚Üî Summable f :=
  ‚ü®fun h => by simpa only [RCLike.reCLM_apply, RCLike.ofReal_re] using reCLM.summable h,
    ofRealCLM.summable‚ü©

@[norm_cast]
theorem ofReal_tsum (f : Œ± ‚Üí ‚Ñù) : (‚Üë(‚àë' a, f a) : ùïú) = ‚àë' a, (f a : ùïú) := by
  by_cases h : Summable f
  ¬∑ exact ContinuousLinearMap.map_tsum ofRealCLM h
  ¬∑ rw [tsum_eq_zero_of_not_summable h,
      tsum_eq_zero_of_not_summable ((summable_ofReal _).not.mpr h), ofReal_zero]

theorem hasSum_re {f : Œ± ‚Üí ùïú} {x : ùïú} (h : HasSum f x) : HasSum (fun x => re (f x)) (re x) :=
  reCLM.hasSum h

theorem hasSum_im {f : Œ± ‚Üí ùïú} {x : ùïú} (h : HasSum f x) : HasSum (fun x => im (f x)) (im x) :=
  imCLM.hasSum h

theorem re_tsum {f : Œ± ‚Üí ùïú} (h : Summable f) : re (‚àë' a, f a) = ‚àë' a, re (f a) :=
  reCLM.map_tsum h

theorem im_tsum {f : Œ± ‚Üí ùïú} (h : Summable f) : im (‚àë' a, f a) = ‚àë' a, im (f a) :=
  imCLM.map_tsum h

variable {ùïú}

theorem hasSum_iff (f : Œ± ‚Üí ùïú) (c : ùïú) :
    HasSum f c ‚Üî HasSum (fun x => re (f x)) (re c) ‚àß HasSum (fun x => im (f x)) (im c) := by
  refine ‚ü®fun h => ‚ü®hasSum_re _ h, hasSum_im _ h‚ü©, ?_‚ü©
  rintro ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
  simpa only [re_add_im] using
    ((hasSum_ofReal ùïú).mpr h‚ÇÅ).add (((hasSum_ofReal ùïú).mpr h‚ÇÇ).mul_right I)

end tsum

end RCLike

namespace Complex

@[deprecated (since := "2025-02-16")] alias hasProd_abs := HasProd.norm
@[deprecated (since := "2025-02-16")] alias multipliable_abs := Multipliable.norm
@[deprecated (since := "2025-02-16")] alias abs_tprod := norm_tprod

/-!
We have to repeat the lemmas about `RCLike.re` and `RCLike.im` as they are not syntactic
matches for `Complex.re` and `Complex.im`.

We do not have this problem with `ofReal` and `conj`, although we repeat them anyway for
discoverability and to avoid the need to unify `ùïú`.
-/


section tsum

variable {Œ± : Type*}

open ComplexConjugate

theorem hasSum_conj {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} : HasSum (fun x => conj (f x)) x ‚Üî HasSum f (conj x) :=
  RCLike.hasSum_conj _

theorem hasSum_conj' {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} : HasSum (fun x => conj (f x)) (conj x) ‚Üî HasSum f x :=
  RCLike.hasSum_conj' _

theorem summable_conj {f : Œ± ‚Üí ‚ÑÇ} : (Summable fun x => conj (f x)) ‚Üî Summable f :=
  RCLike.summable_conj _

theorem conj_tsum (f : Œ± ‚Üí ‚ÑÇ) : conj (‚àë' a, f a) = ‚àë' a, conj (f a) :=
  RCLike.conj_tsum _

@[simp, norm_cast]
theorem hasSum_ofReal {f : Œ± ‚Üí ‚Ñù} {x : ‚Ñù} : HasSum (fun x => (f x : ‚ÑÇ)) x ‚Üî HasSum f x :=
  RCLike.hasSum_ofReal _

@[simp, norm_cast]
theorem summable_ofReal {f : Œ± ‚Üí ‚Ñù} : (Summable fun x => (f x : ‚ÑÇ)) ‚Üî Summable f :=
  RCLike.summable_ofReal _

@[norm_cast]
theorem ofReal_tsum (f : Œ± ‚Üí ‚Ñù) : (‚Üë(‚àë' a, f a) : ‚ÑÇ) = ‚àë' a, ‚Üë(f a) :=
  RCLike.ofReal_tsum _ _

theorem hasSum_re {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} (h : HasSum f x) : HasSum (fun x => (f x).re) x.re :=
  RCLike.hasSum_re ‚ÑÇ h

theorem hasSum_im {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} (h : HasSum f x) : HasSum (fun x => (f x).im) x.im :=
  RCLike.hasSum_im ‚ÑÇ h

theorem re_tsum {f : Œ± ‚Üí ‚ÑÇ} (h : Summable f) : (‚àë' a, f a).re = ‚àë' a, (f a).re :=
  RCLike.re_tsum _ h

theorem im_tsum {f : Œ± ‚Üí ‚ÑÇ} (h : Summable f) : (‚àë' a, f a).im = ‚àë' a, (f a).im :=
  RCLike.im_tsum _ h

theorem hasSum_iff (f : Œ± ‚Üí ‚ÑÇ) (c : ‚ÑÇ) :
    HasSum f c ‚Üî HasSum (fun x => (f x).re) c.re ‚àß HasSum (fun x => (f x).im) c.im :=
  RCLike.hasSum_iff _ _

end tsum

section slitPlane

/-!
### Define the "slit plane" `‚ÑÇ ‚àñ ‚Ñù‚â§0` and provide some API
-/

open scoped ComplexOrder

/-- The *slit plane* is the complex plane with the closed negative real axis removed. -/
def slitPlane : Set ‚ÑÇ := {z | 0 < z.re ‚à® z.im ‚â† 0}

lemma mem_slitPlane_iff {z : ‚ÑÇ} : z ‚àà slitPlane ‚Üî 0 < z.re ‚à® z.im ‚â† 0 := Set.mem_setOf

/- If `z` is non-zero, then either `z` or `-z` is in `slitPlane`. -/
lemma mem_slitPlane_or_neg_mem_slitPlane {z : ‚ÑÇ} (hz : z ‚â† 0) :
    z ‚àà slitPlane ‚à® -z ‚àà slitPlane := by
  rw [mem_slitPlane_iff, mem_slitPlane_iff]
  rw [ne_eq, Complex.ext_iff] at hz
  push_neg at hz
  simp_all only [ne_eq, zero_re, zero_im, neg_re, Left.neg_pos_iff, neg_im, neg_eq_zero]
  by_contra! contra
  exact hz (le_antisymm contra.1.1 contra.2.1) contra.1.2

lemma slitPlane_eq_union : slitPlane = {z | 0 < z.re} ‚à™ {z | z.im ‚â† 0} := Set.setOf_or.symm

lemma isOpen_slitPlane : IsOpen slitPlane :=
  (isOpen_lt continuous_const continuous_re).union (isOpen_ne_fun continuous_im continuous_const)

@[simp]
lemma ofReal_mem_slitPlane {x : ‚Ñù} : ‚Üëx ‚àà slitPlane ‚Üî 0 < x := by simp [mem_slitPlane_iff]

@[simp]
lemma neg_ofReal_mem_slitPlane {x : ‚Ñù} : -‚Üëx ‚àà slitPlane ‚Üî x < 0 := by
  simpa using ofReal_mem_slitPlane (x := -x)

@[simp] lemma one_mem_slitPlane : 1 ‚àà slitPlane := ofReal_mem_slitPlane.2 one_pos

@[simp]
lemma zero_notMem_slitPlane : 0 ‚àâ slitPlane := mt ofReal_mem_slitPlane.1 (lt_irrefl _)

@[deprecated (since := "2025-05-23")] alias zero_not_mem_slitPlane := zero_notMem_slitPlane

@[simp]
lemma natCast_mem_slitPlane {n : ‚Ñï} : ‚Üën ‚àà slitPlane ‚Üî n ‚â† 0 := by
  simpa [pos_iff_ne_zero] using @ofReal_mem_slitPlane n

@[simp]
lemma ofNat_mem_slitPlane (n : ‚Ñï) [n.AtLeastTwo] : ofNat(n) ‚àà slitPlane :=
  natCast_mem_slitPlane.2 (NeZero.ne n)

lemma mem_slitPlane_iff_not_le_zero {z : ‚ÑÇ} : z ‚àà slitPlane ‚Üî ¬¨z ‚â§ 0 :=
  mem_slitPlane_iff.trans not_le_zero_iff.symm

protected lemma compl_Iic_zero : (Set.Iic 0)·∂ú = slitPlane := Set.ext fun _ ‚Ü¶
  mem_slitPlane_iff_not_le_zero.symm

lemma slitPlane_ne_zero {z : ‚ÑÇ} (hz : z ‚àà slitPlane) : z ‚â† 0 :=
  ne_of_mem_of_not_mem hz zero_notMem_slitPlane

/-- The slit plane includes the open unit ball of radius `1` around `1`. -/
lemma ball_one_subset_slitPlane : Metric.ball 1 1 ‚äÜ slitPlane := fun z hz ‚Ü¶ .inl <|
  have : -1 < z.re - 1 := neg_lt_of_abs_lt <| (abs_re_le_norm _).trans_lt hz
  by linarith

/-- The slit plane includes the open unit ball of radius `1` around `1`. -/
lemma mem_slitPlane_of_norm_lt_one {z : ‚ÑÇ} (hz : ‚Äñz‚Äñ < 1) : 1 + z ‚àà slitPlane :=
  ball_one_subset_slitPlane <| by simpa

end slitPlane

lemma _root_.IsCompact.reProdIm {s t : Set ‚Ñù} (hs : IsCompact s) (ht : IsCompact t) :
    IsCompact (s √ó‚ÑÇ t) :=
  equivRealProdCLM.toHomeomorph.isCompact_preimage.2 (hs.prod ht)

end Complex

section realPart_imaginaryPart

variable {A : Type*} [SeminormedAddCommGroup A] [StarAddMonoid A] [NormedSpace ‚ÑÇ A] [StarModule ‚ÑÇ A]
  [NormedStarGroup A]

lemma realPart.norm_le (x : A) : ‚ÄñrealPart x‚Äñ ‚â§ ‚Äñx‚Äñ := by
  rw [‚Üê inv_mul_cancel_left‚ÇÄ two_ne_zero ‚Äñx‚Äñ, ‚Üê AddSubgroup.norm_coe, realPart_apply_coe,
    norm_smul, norm_inv, Real.norm_ofNat]
  gcongr
  exact norm_add_le _ _ |>.trans <| by simp [two_mul]

lemma imaginaryPart.norm_le (x : A) : ‚ÄñimaginaryPart x‚Äñ ‚â§ ‚Äñx‚Äñ := by
  calc ‚ÄñimaginaryPart x‚Äñ = ‚ÄñrealPart (Complex.I ‚Ä¢ (-x))‚Äñ := by simp
    _ ‚â§ ‚Äñx‚Äñ := by simpa only [smul_neg, map_neg, realPart_I_smul, neg_neg,
        AddSubgroupClass.coe_norm, norm_neg, norm_smul, Complex.norm_I, one_mul] using
        realPart.norm_le (Complex.I ‚Ä¢ (-x))

end realPart_imaginaryPart
