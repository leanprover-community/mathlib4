/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/
module

public import Mathlib.Analysis.Complex.CauchyIntegral
public import Mathlib.Analysis.Complex.UpperHalfPlane.Topology
public import Mathlib.Geometry.Manifold.Algebra.Structures
public import Mathlib.Geometry.Manifold.ContMDiff.Atlas
public import Mathlib.Geometry.Manifold.MFDeriv.FDeriv

/-!
# Manifold structure on the upper half plane.

In this file we define the complex manifold structure on the upper half-plane.
-/

@[expose] public section

open Filter

open scoped Manifold ContDiff MatrixGroups Topology

variable {n : WithTop â„•âˆ}

namespace UpperHalfPlane

noncomputable instance : ChartedSpace â„‚ â„ :=
  UpperHalfPlane.isOpenEmbedding_coe.singletonChartedSpace

instance : IsManifold ğ“˜(â„‚) Ï‰ â„ :=
  UpperHalfPlane.isOpenEmbedding_coe.isManifold_singleton

/-- The inclusion map `â„ â†’ â„‚` is a map of `C^n` manifolds. -/
theorem contMDiff_coe : ContMDiff ğ“˜(â„‚) ğ“˜(â„‚) n ((â†‘) : â„ â†’ â„‚) :=
  fun _ => contMDiffAt_extChartAt

/-- The inclusion map `â„ â†’ â„‚` is a differentiable map of manifolds. -/
theorem mdifferentiable_coe : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) ((â†‘) : â„ â†’ â„‚) :=
  contMDiff_coe.mdifferentiable le_top

lemma contMDiffAt_ofComplex {z : â„‚} (hz : 0 < z.im) :
    ContMDiffAt ğ“˜(â„‚) ğ“˜(â„‚) n ofComplex z := by
  rw [contMDiffAt_iff]
  constructor
  Â· -- continuity at z
    rw [ContinuousAt, nhds_induced, tendsto_comap_iff]
    refine Tendsto.congr' (eventuallyEq_coe_comp_ofComplex hz).symm ?_
    simpa [ofComplex_apply_of_im_pos hz] using tendsto_id
  Â· -- smoothness in local chart
    simpa using contDiffAt_id.congr_of_eventuallyEq (eventuallyEq_coe_comp_ofComplex hz)

lemma mdifferentiableAt_ofComplex {z : â„‚} (hz : 0 < z.im) :
    MDifferentiableAt ğ“˜(â„‚) ğ“˜(â„‚) ofComplex z :=
  (contMDiffAt_ofComplex hz).mdifferentiableAt le_top

lemma contMDiffAt_iff {f : â„ â†’ â„‚} {Ï„ : â„} :
    ContMDiffAt ğ“˜(â„‚) ğ“˜(â„‚) n f Ï„ â†” ContDiffAt â„‚ n (f âˆ˜ ofComplex) Ï„ := by
  rw [â† contMDiffAt_iff_contDiffAt]
  refine âŸ¨fun hf â†¦ ?_, fun hf â†¦ ?_âŸ©
  Â· exact (ofComplex_apply Ï„ â–¸ hf).comp _ (contMDiffAt_ofComplex Ï„.im_pos)
  Â· simpa only [Function.comp_def, ofComplex_apply] using hf.comp Ï„ (contMDiff_coe Ï„)

lemma mdifferentiableAt_iff {f : â„ â†’ â„‚} {Ï„ : â„} :
    MDifferentiableAt ğ“˜(â„‚) ğ“˜(â„‚) f Ï„ â†” DifferentiableAt â„‚ (f âˆ˜ ofComplex) â†‘Ï„ := by
  rw [â† mdifferentiableAt_iff_differentiableAt]
  refine âŸ¨fun hf â†¦ ?_, fun hf â†¦ ?_âŸ©
  Â· exact (ofComplex_apply Ï„ â–¸ hf).comp _ (mdifferentiableAt_ofComplex Ï„.im_pos)
  Â· simpa only [Function.comp_def, ofComplex_apply] using hf.comp Ï„ (mdifferentiable_coe Ï„)

lemma mdifferentiable_iff {f : â„ â†’ â„‚} :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) f â†” DifferentiableOn â„‚ (f âˆ˜ ofComplex) {z | 0 < z.im} :=
  âŸ¨fun h z hz â†¦ (mdifferentiableAt_iff.mp (h âŸ¨z, hzâŸ©)).differentiableWithinAt,
    fun h âŸ¨z, hzâŸ© â†¦ mdifferentiableAt_iff.mpr <| (h z hz).differentiableAt
     <| isOpen_upperHalfPlaneSet.mem_nhds hzâŸ©

lemma contMDiff_num (g : GL (Fin 2) â„) : ContMDiff ğ“˜(â„‚) ğ“˜(â„‚) n (fun Ï„ : â„ â†¦ num g Ï„) :=
  (contMDiff_const.smul contMDiff_coe).add contMDiff_const

lemma contMDiff_denom (g : GL (Fin 2) â„) : ContMDiff ğ“˜(â„‚) ğ“˜(â„‚) n (fun Ï„ : â„ â†¦ denom g Ï„) :=
  (contMDiff_const.smul contMDiff_coe).add contMDiff_const

lemma contMDiff_denom_zpow (g : GL (Fin 2) â„) (k : â„¤) :
    ContMDiff ğ“˜(â„‚) ğ“˜(â„‚) n (denom g Â· ^ k : â„ â†’ â„‚) := fun Ï„ â†¦ by
  have : AnalyticAt â„‚ (Â· ^ k) (denom g Ï„) := (differentiableOn_zpow k _ (by tauto)).analyticOnNhd
    isOpen_compl_singleton _ (denom_ne_zero g Ï„)
  exact this.contDiffAt.contMDiffAt.comp Ï„ (contMDiff_denom g Ï„)

lemma contMDiff_inv_denom (g : GL (Fin 2) â„) :
    ContMDiff ğ“˜(â„‚) ğ“˜(â„‚) n (fun Ï„ : â„ â†¦ (denom g Ï„)â»Â¹) := by
  simpa using contMDiff_denom_zpow g (-1)

/-- Each element of `GL(2, â„)âº` defines a map of `C ^ n` manifolds `â„ â†’ â„`. -/
lemma contMDiff_smul {g : GL (Fin 2) â„} (hg : 0 < g.det.val) :
    ContMDiff ğ“˜(â„‚) ğ“˜(â„‚) n (fun Ï„ : â„ â†¦ g â€¢ Ï„) := fun Ï„ â†¦ by
  refine contMDiffAt_iff_target.mpr âŸ¨(continuous_const_smul g).continuousAt, ?_âŸ©
  simpa [glPos_smul_def hg] using (contMDiff_num g Ï„).mul (contMDiff_inv_denom g Ï„)

lemma mdifferentiable_num (g : GL (Fin 2) â„) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun Ï„ : â„ â†¦ num g Ï„) :=
  (contMDiff_num g).mdifferentiable le_top

lemma mdifferentiable_denom (g : GL (Fin 2) â„) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun Ï„ : â„ â†¦ denom g Ï„) :=
  (contMDiff_denom g).mdifferentiable le_top

lemma mdifferentiable_denom_zpow (g : GL (Fin 2) â„) (k : â„¤) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (denom g Â· ^ k : â„ â†’ â„‚) :=
  (contMDiff_denom_zpow g k).mdifferentiable le_top

lemma mdifferentiable_inv_denom (g : GL (Fin 2) â„) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun Ï„ : â„ â†¦ (denom g Ï„)â»Â¹) :=
  (contMDiff_inv_denom g).mdifferentiable le_top

/-- Each element of `GL(2, â„)âº` defines a complex-differentiable map `â„ â†’ â„`. -/
lemma mdifferentiable_smul {g : GL (Fin 2) â„} (hg : 0 < g.det.val) :
    MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (fun Ï„ : â„ â†¦ g â€¢ Ï„) :=
  (contMDiff_smul hg).mdifferentiable le_top

lemma eq_zero_of_frequently {f : â„ â†’ â„‚} (hf : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) f)
    {Ï„ : â„} (hÏ„ : âˆƒá¶  z in ğ“[â‰ ] Ï„, f z = 0) : f = 0 := by
  rw [UpperHalfPlane.mdifferentiable_iff] at hf
  have := hf.analyticOnNhd isOpen_upperHalfPlaneSet
  ext w
  convert this.eqOn_zero_of_preconnected_of_frequently_eq_zero (zâ‚€ := â†‘Ï„) ?_ Ï„.2 ?_ w.property
  Â· rw [Function.comp_apply, ofComplex_apply_of_im_pos w.property]
    rfl
  Â· exact (Complex.isConnected_of_upperHalfPlane subset_rfl (by grind)).isPreconnected
  Â· contrapose! hÏ„
    rw [eventually_nhdsWithin_iff, â† isOpenEmbedding_coe.map_nhds_eq, eventually_map] at hÏ„
    rw [eventually_nhdsWithin_iff]
    filter_upwards [hÏ„] with a ha
    simpa using ha

lemma mul_eq_zero_iff {f g : â„ â†’ â„‚} (hf : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) f)
    (hg : MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) g) : f * g = 0 â†” f = 0 âˆ¨ g = 0 :=
  âŸ¨fun hfg â†¦ (frequently_or_distrib.mp <| .of_forall <| by simpa using congrFun hfg).imp
    (eq_zero_of_frequently (Ï„ := I) hf) (eq_zero_of_frequently hg), by grindâŸ©

lemma prod_eq_zero_iff {Î¹ : Type*} {f : Î¹ â†’ â„ â†’ â„‚} {s : Finset Î¹}
    (hf : âˆ€ i âˆˆ s, MDifferentiable ğ“˜(â„‚) ğ“˜(â„‚) (f i)) :
    âˆ i âˆˆ s, f i = 0 â†” âˆƒ i âˆˆ s, f i = 0 := by
  refine âŸ¨fun h0 â†¦ ?_, fun âŸ¨i, hi, hi'âŸ© â†¦ Finset.prod_eq_zero hi hi'âŸ©
  have : âˆƒá¶  Ï„ in ğ“[â‰ ] I, âˆ i âˆˆ s, f i Ï„ = 0 := .of_forall <| by simpa using congrFun h0
  simp only [Finset.prod_eq_zero_iff, Finset.frequently_exists] at this
  exact this.imp fun i hi â†¦ âŸ¨hi.1, eq_zero_of_frequently (hf i hi.1) hi.2âŸ©

end UpperHalfPlane
