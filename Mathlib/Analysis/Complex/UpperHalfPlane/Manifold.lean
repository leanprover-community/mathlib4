/-
Copyright (c) 2022 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, David Loeffler
-/
module

public import Mathlib.Analysis.Complex.CauchyIntegral
public import Mathlib.Analysis.Complex.UpperHalfPlane.Topology
public import Mathlib.Geometry.Manifold.Algebra.Structures
public import Mathlib.Geometry.Manifold.ContMDiff.Atlas
import Mathlib.Geometry.Manifold.Notation
public import Mathlib.Geometry.Manifold.MFDeriv.FDeriv

/-!
# Manifold structure on the upper half plane.

In this file we define the complex manifold structure on the upper half-plane.
-/

@[expose] public section

open Filter

open scoped Manifold ContDiff MatrixGroups Topology

variable {n : WithTop ‚Ñï‚àû}

namespace UpperHalfPlane

noncomputable instance : ChartedSpace ‚ÑÇ ‚Ñç :=
  UpperHalfPlane.isOpenEmbedding_coe.singletonChartedSpace

instance : IsManifold ùìò(‚ÑÇ) œâ ‚Ñç :=
  UpperHalfPlane.isOpenEmbedding_coe.isManifold_singleton

/-- The inclusion map `‚Ñç ‚Üí ‚ÑÇ` is a map of `C^n` manifolds. -/
theorem contMDiff_coe : CMDiff n ((‚Üë) : ‚Ñç ‚Üí ‚ÑÇ) :=
  fun _ => contMDiffAt_extChartAt

/-- The inclusion map `‚Ñç ‚Üí ‚ÑÇ` is a differentiable map of manifolds. -/
theorem mdifferentiable_coe : MDiff ((‚Üë) : ‚Ñç ‚Üí ‚ÑÇ) :=
  contMDiff_coe.mdifferentiable one_ne_zero

lemma contMDiffAt_ofComplex {z : ‚ÑÇ} (hz : 0 < z.im) : CMDiffAt n ofComplex z := by
  rw [contMDiffAt_iff]
  constructor
  ¬∑ -- continuity at z
    rw [ContinuousAt, nhds_induced, tendsto_comap_iff]
    refine Tendsto.congr' (eventuallyEq_coe_comp_ofComplex hz).symm ?_
    simpa [ofComplex_apply_of_im_pos hz] using tendsto_id
  ¬∑ -- smoothness in local chart
    simpa using contDiffAt_id.congr_of_eventuallyEq (eventuallyEq_coe_comp_ofComplex hz)

lemma mdifferentiableAt_ofComplex {z : ‚ÑÇ} (hz : 0 < z.im) : MDiffAt ofComplex z :=
  (contMDiffAt_ofComplex hz).mdifferentiableAt one_ne_zero

lemma contMDiffAt_iff {f : ‚Ñç ‚Üí ‚ÑÇ} {œÑ : ‚Ñç} :
    CMDiffAt n f œÑ ‚Üî ContDiffAt ‚ÑÇ n (f ‚àò ofComplex) œÑ := by
  rw [‚Üê contMDiffAt_iff_contDiffAt]
  refine ‚ü®fun hf ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ exact (ofComplex_apply œÑ ‚ñ∏ hf).comp _ (contMDiffAt_ofComplex œÑ.im_pos)
  ¬∑ simpa only [Function.comp_def, ofComplex_apply] using hf.comp œÑ (contMDiff_coe œÑ)

lemma mdifferentiableAt_iff {f : ‚Ñç ‚Üí ‚ÑÇ} {œÑ : ‚Ñç} :
    MDiffAt f œÑ ‚Üî DifferentiableAt ‚ÑÇ (f ‚àò ofComplex) ‚ÜëœÑ := by
  rw [‚Üê mdifferentiableAt_iff_differentiableAt]
  refine ‚ü®fun hf ‚Ü¶ ?_, fun hf ‚Ü¶ ?_‚ü©
  ¬∑ exact (ofComplex_apply œÑ ‚ñ∏ hf).comp _ (mdifferentiableAt_ofComplex œÑ.im_pos)
  ¬∑ simpa only [Function.comp_def, ofComplex_apply] using hf.comp œÑ (mdifferentiable_coe œÑ)

lemma mdifferentiable_iff {f : ‚Ñç ‚Üí ‚ÑÇ} :
    MDiff f ‚Üî DifferentiableOn ‚ÑÇ (f ‚àò ofComplex) {z | 0 < z.im} :=
  ‚ü®fun h z hz ‚Ü¶ (mdifferentiableAt_iff.mp (h ‚ü®z, hz‚ü©)).differentiableWithinAt,
    fun h ‚ü®z, hz‚ü© ‚Ü¶ mdifferentiableAt_iff.mpr <| (h z hz).differentiableAt
     <| isOpen_upperHalfPlaneSet.mem_nhds hz‚ü©

lemma contMDiff_num (g : GL (Fin 2) ‚Ñù) : CMDiff n (fun œÑ : ‚Ñç ‚Ü¶ num g œÑ) :=
  (contMDiff_const.smul contMDiff_coe).add contMDiff_const

lemma contMDiff_denom (g : GL (Fin 2) ‚Ñù) : CMDiff n (fun œÑ : ‚Ñç ‚Ü¶ denom g œÑ) :=
  (contMDiff_const.smul contMDiff_coe).add contMDiff_const

lemma contMDiff_denom_zpow (g : GL (Fin 2) ‚Ñù) (k : ‚Ñ§) : CMDiff n (denom g ¬∑ ^ k : ‚Ñç ‚Üí ‚ÑÇ) := by
  intro œÑ
  have : AnalyticAt ‚ÑÇ (¬∑ ^ k) (denom g œÑ) := (differentiableOn_zpow k _ (by tauto)).analyticOnNhd
    isOpen_compl_singleton _ (denom_ne_zero g œÑ)
  exact this.contDiffAt.contMDiffAt.comp œÑ (contMDiff_denom g œÑ)

lemma contMDiff_inv_denom (g : GL (Fin 2) ‚Ñù) : CMDiff n (fun œÑ : ‚Ñç ‚Ü¶ (denom g œÑ)‚Åª¬π) := by
  simpa using contMDiff_denom_zpow g (-1)

/-- Each element of `GL(2, ‚Ñù)‚Å∫` defines a map of `C ^ n` manifolds `‚Ñç ‚Üí ‚Ñç`. -/
lemma contMDiff_smul {g : GL (Fin 2) ‚Ñù} (hg : 0 < g.det.val) : CMDiff n (fun œÑ : ‚Ñç ‚Ü¶ g ‚Ä¢ œÑ) := by
  intro œÑ
  refine contMDiffAt_iff_target.mpr ‚ü®(continuous_const_smul g).continuousAt, ?_‚ü©
  simpa [glPos_smul_def hg] using (contMDiff_num g œÑ).mul (contMDiff_inv_denom g œÑ)

lemma mdifferentiable_num (g : GL (Fin 2) ‚Ñù) : MDiff (fun œÑ : ‚Ñç ‚Ü¶ num g œÑ) :=
  (contMDiff_num g).mdifferentiable one_ne_zero

lemma mdifferentiable_denom (g : GL (Fin 2) ‚Ñù) : MDiff (fun œÑ : ‚Ñç ‚Ü¶ denom g œÑ) :=
  (contMDiff_denom g).mdifferentiable one_ne_zero

lemma mdifferentiable_denom_zpow (g : GL (Fin 2) ‚Ñù) (k : ‚Ñ§) : MDiff (denom g ¬∑ ^ k : ‚Ñç ‚Üí ‚ÑÇ) :=
  (contMDiff_denom_zpow g k).mdifferentiable one_ne_zero

lemma mdifferentiable_inv_denom (g : GL (Fin 2) ‚Ñù) : MDiff (fun œÑ : ‚Ñç ‚Ü¶ (denom g œÑ)‚Åª¬π) :=
  (contMDiff_inv_denom g).mdifferentiable one_ne_zero

/-- Each element of `GL(2, ‚Ñù)‚Å∫` defines a complex-differentiable map `‚Ñç ‚Üí ‚Ñç`. -/
lemma mdifferentiable_smul {g : GL (Fin 2) ‚Ñù} (hg : 0 < g.det.val) : MDiff (fun œÑ : ‚Ñç ‚Ü¶ g ‚Ä¢ œÑ) :=
  (contMDiff_smul hg).mdifferentiable one_ne_zero

lemma eq_zero_of_frequently {f : ‚Ñç ‚Üí ‚ÑÇ} (hf : MDiff f) {œÑ : ‚Ñç} (hœÑ : ‚àÉ·∂† z in ùìù[‚â†] œÑ, f z = 0) :
    f = 0 := by
  rw [UpperHalfPlane.mdifferentiable_iff] at hf
  have := hf.analyticOnNhd isOpen_upperHalfPlaneSet
  ext w
  convert this.eqOn_zero_of_preconnected_of_frequently_eq_zero (z‚ÇÄ := ‚ÜëœÑ) ?_ œÑ.2 ?_ w.property
  ¬∑ rw [Function.comp_apply, ofComplex_apply_of_im_pos w.property]
    rfl
  ¬∑ exact (Complex.isConnected_of_upperHalfPlane subset_rfl (by grind)).isPreconnected
  ¬∑ contrapose! hœÑ
    rw [eventually_nhdsWithin_iff, ‚Üê isOpenEmbedding_coe.map_nhds_eq, eventually_map] at hœÑ
    rw [eventually_nhdsWithin_iff]
    filter_upwards [hœÑ] with a ha
    simpa using ha

lemma mul_eq_zero_iff {f g : ‚Ñç ‚Üí ‚ÑÇ} (hf : MDiff f) (hg : MDiff g) : f * g = 0 ‚Üî f = 0 ‚à® g = 0 :=
  ‚ü®fun hfg ‚Ü¶ (frequently_or_distrib.mp <| .of_forall <| by simpa using congrFun hfg).imp
    (eq_zero_of_frequently (œÑ := I) hf) (eq_zero_of_frequently hg), by grind‚ü©

lemma prod_eq_zero_iff {Œπ : Type*} {f : Œπ ‚Üí ‚Ñç ‚Üí ‚ÑÇ} {s : Finset Œπ}
    (hf : ‚àÄ i ‚àà s, MDiff (f i)) :
    ‚àè i ‚àà s, f i = 0 ‚Üî ‚àÉ i ‚àà s, f i = 0 := by
  refine ‚ü®fun h0 ‚Ü¶ ?_, fun ‚ü®i, hi, hi'‚ü© ‚Ü¶ Finset.prod_eq_zero hi hi'‚ü©
  have : ‚àÉ·∂† œÑ in ùìù[‚â†] I, ‚àè i ‚àà s, f i œÑ = 0 := .of_forall <| by simpa using congrFun h0
  simp only [Finset.prod_eq_zero_iff, Finset.frequently_exists] at this
  exact this.imp fun i hi ‚Ü¶ ‚ü®hi.1, eq_zero_of_frequently (hf i hi.1) hi.2‚ü©

end UpperHalfPlane
