/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Complex.UpperHalfPlane.Basic
import Mathlib.Analysis.Convex.Contractible
import Mathlib.Analysis.Convex.Normed
import Mathlib.Analysis.Convex.Complex
import Mathlib.Analysis.Complex.ReImTopology
import Mathlib.Topology.Homotopy.Contractible

#align_import analysis.complex.upper_half_plane.topology from "leanprover-community/mathlib"@"57f9349f2fe19d2de7207e99b0341808d977cdcf"

/-!
# Topology on the upper half plane

In this file we introduce a `TopologicalSpace` structure on the upper half plane and provide
various instances.
-/


noncomputable section

open Set Filter Function TopologicalSpace Complex

open scoped Filter Topology UpperHalfPlane

namespace UpperHalfPlane

instance : TopologicalSpace ‚Ñç :=
  instTopologicalSpaceSubtype

theorem openEmbedding_coe : OpenEmbedding ((‚Üë) : ‚Ñç ‚Üí ‚ÑÇ) :=
  IsOpen.openEmbedding_subtype_val <| isOpen_lt continuous_const Complex.continuous_im
#align upper_half_plane.open_embedding_coe UpperHalfPlane.openEmbedding_coe

theorem embedding_coe : Embedding ((‚Üë) : ‚Ñç ‚Üí ‚ÑÇ) :=
  embedding_subtype_val
#align upper_half_plane.embedding_coe UpperHalfPlane.embedding_coe

theorem continuous_coe : Continuous ((‚Üë) : ‚Ñç ‚Üí ‚ÑÇ) :=
  embedding_coe.continuous
#align upper_half_plane.continuous_coe UpperHalfPlane.continuous_coe

theorem continuous_re : Continuous re :=
  Complex.continuous_re.comp continuous_coe
#align upper_half_plane.continuous_re UpperHalfPlane.continuous_re

theorem continuous_im : Continuous im :=
  Complex.continuous_im.comp continuous_coe
#align upper_half_plane.continuous_im UpperHalfPlane.continuous_im

instance : TopologicalSpace.SecondCountableTopology ‚Ñç :=
  TopologicalSpace.Subtype.secondCountableTopology _

instance : T3Space ‚Ñç :=
  Subtype.t3Space

instance : NormalSpace ‚Ñç :=
  normalSpaceOfT3SecondCountable ‚Ñç

instance : ContractibleSpace ‚Ñç :=
  (convex_halfspace_im_gt 0).contractibleSpace ‚ü®I, one_pos.trans_eq I_im.symm‚ü©

instance : LocPathConnectedSpace ‚Ñç :=
  locPathConnected_of_isOpen <| isOpen_lt continuous_const Complex.continuous_im

instance : NoncompactSpace ‚Ñç := by
  refine' ‚ü®fun h => _‚ü©
  -- ‚ä¢ False
  have : IsCompact (Complex.im ‚Åª¬π' Ioi 0) := isCompact_iff_isCompact_univ.2 h
  -- ‚ä¢ False
  replace := this.isClosed.closure_eq
  -- ‚ä¢ False
  rw [closure_preimage_im, closure_Ioi, Set.ext_iff] at this
  -- ‚ä¢ False
  exact absurd ((this 0).1 (@left_mem_Ici ‚Ñù _ 0)) (@lt_irrefl ‚Ñù _ 0)
  -- üéâ no goals

instance : LocallyCompactSpace ‚Ñç :=
  openEmbedding_coe.locallyCompactSpace

end UpperHalfPlane
