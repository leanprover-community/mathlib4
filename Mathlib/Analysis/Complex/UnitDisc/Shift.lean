/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/

module
public import Mathlib.Analysis.Complex.UnitDisc.Basic

/-!
# Shift on the unit disc

In this file we define `Complex.UnitDisc.shift z` to be the automorphism of the unit disc
that sends `0` to `z` and preserves the diameter joining `0` to `z`.

We also prove basic properties of this function.
-/

open Set Function Metric
open scoped ComplexConjugate

noncomputable section

namespace Complex.UnitDisc

public section

lemma shift_den_ne_zero (z w : ùîª) : 1 + conj (z : ‚ÑÇ) * w ‚â† 0 :=
  (star z * w).one_add_coe_ne_zero

lemma shift_den_ne_zero' (z w : ùîª) : 1 + z * conj (w : ‚ÑÇ) ‚â† 0 :=
  (z * star w).one_add_coe_ne_zero

end

theorem norm_shiftFun_le (z w : ùîª) :
    ‚Äñ(z + w : ‚ÑÇ) / (1 + conj ‚Üëz * w)‚Äñ ‚â§ (‚Äñ(z : ‚ÑÇ)‚Äñ + ‚Äñ(w : ‚ÑÇ)‚Äñ) / (1 + ‚Äñ(z : ‚ÑÇ)‚Äñ * ‚Äñ(w : ‚ÑÇ)‚Äñ) := by
  have hz := z.sq_norm_lt_one
  have hw := w.sq_norm_lt_one
  have hzw : z.re * w.re + z.im * w.im ‚â§ ‚Äñ(z : ‚ÑÇ)‚Äñ * ‚Äñ(w : ‚ÑÇ)‚Äñ := by
    rw [norm_def, norm_def, ‚Üê Real.sqrt_mul, normSq_apply, normSq_apply]
    ¬∑ apply Real.le_sqrt_of_sq_le
      linear_combination (norm := {apply le_of_eq; simp; ring})
        sq_nonneg (z.re * w.im - z.im * w.re)
    ¬∑ apply normSq_nonneg
  rw [norm_div, div_le_div_iff‚ÇÄ, ‚Üê sq_le_sq‚ÇÄ]
  ¬∑ rw [‚Üê sub_nonneg] at hzw
    simp [mul_pow, RCLike.norm_sq_eq_def, add_sq] at hz hw ‚ä¢
    linear_combination 2 * mul_nonneg hzw (mul_nonneg (sub_nonneg.2 hz.le) (sub_nonneg.2 hw.le))
  any_goals positivity
  simpa using shift_den_ne_zero z w

/-- Auxiliary definition for `shift` below. This function is not a part of the public API. -/
def shiftFun (z w : ùîª) : ùîª :=
  mk ((z + w : ‚ÑÇ) / (1 + conj ‚Üëz * w)) <| by
    refine (norm_shiftFun_le _ _).trans_lt ?_
    rw [div_lt_one (by positivity)]
    nlinarith only [z.norm_lt_one, w.norm_lt_one]

theorem coe_shiftFun (z w : ùîª) : (shiftFun z w : ‚ÑÇ) = (z + w) / (1 + conj ‚Üëz * w) := rfl

theorem shiftFun_eq_iff {z w u : ùîª} : shiftFun z w = u ‚Üî (z + w : ‚ÑÇ) = u + u * conj ‚Üëz * w := by
  rw [‚Üê coe_inj, coe_shiftFun, div_eq_iff (shift_den_ne_zero _ _)]
  ring_nf

theorem shiftFun_neg_apply_shiftFun (z w : ùîª) : shiftFun (-z) (shiftFun z w) = w := by
  rw [shiftFun_eq_iff, coe_shiftFun, add_div_eq_mul_add_div, ‚Üê mul_div_assoc,
    add_div_eq_mul_add_div]
  ¬∑ simp; ring
  all_goals exact shift_den_ne_zero z w

public section

/-- The automorphism of the unit disc that sends zero to `z`. -/
def shift (z : ùîª) : ùîª ‚âÉ ùîª where
  toFun := shiftFun z
  invFun := shiftFun (-z)
  left_inv := shiftFun_neg_apply_shiftFun _
  right_inv := by simpa using shiftFun_neg_apply_shiftFun (-z)

/-- An explicit formula for `shift z w`.

Note that the definition of `shift` is not exposed, so this is not a `dsimp` lemma. -/
theorem coe_shift (z w : ùîª) : (shift z w : ‚ÑÇ) = (z + w) / (1 + conj ‚Üëz * w) := by rfl

theorem shift_eq_iff {z w u : ùîª} : shift z w = u ‚Üî (z + w : ‚ÑÇ) = u + u * conj ‚Üëz * w :=
  shiftFun_eq_iff

theorem symm_shift (z : ùîª) : (shift z).symm = shift (-z) := by
  ext1
  rfl

@[simp]
theorem star_shift (z w : ùîª) : star (shift z w) = shift (star z) (star w) :=
  coe_injective <| by simp [coe_shift]

theorem norm_shift_swap (z w : ùîª) : ‚Äñ(shift z w : ‚ÑÇ)‚Äñ = ‚Äñ(shift w z : ‚ÑÇ)‚Äñ := by
  rw [coe_shift, norm_div, ‚Üê norm_conj (1 + _)]
  simp [coe_shift, add_comm, mul_comm]

theorem neg_shift (z w : ùîª) : -shift z w = shift (-z) (-w) := by
  ext1
  simp [coe_shift, ‚Üê neg_add_rev, add_comm, neg_div]

@[simp]
theorem shift_zero : shift 0 = .refl ùîª := by
  ext z
  simp [coe_shift]

@[simp]
theorem shift_apply_zero (z : ùîª) : shift z 0 = z := by simp [shift_eq_iff]

@[simp]
theorem shift_eq_zero_iff {z w : ùîª} : shift z w = 0 ‚Üî w = -z := by
  rw [‚Üê Equiv.eq_symm_apply, symm_shift, shift_apply_zero]

@[simp]
theorem shift_apply_neg_self (z : ùîª) : shift z (-z) = 0 := by simp

@[simp]
theorem shift_neg_apply_self (z : ùîª) : shift (-z) z = 0 := by simp

@[simp]
theorem shift_neg_apply_shift (z w : ùîª) : shift (-z) (shift z w) = w := by
  rw [‚Üê symm_shift, Equiv.symm_apply_apply]

@[simp]
theorem shift_apply_shift_neg (z w : ùîª) : shift z (shift (-z) w) = w := by
  simpa using shift_neg_apply_shift (-z) w

theorem neg_shift_neg (z w : ùîª) : -z.shift (-w) = (-z).shift w := by
  rw [eq_comm, shift_eq_iff, coe_neg, coe_neg, coe_shift]
  field_simp (disch := apply shift_den_ne_zero')
  simp; ring

theorem involutive_shift_comp_neg (z : ùîª) : (shift z <| -¬∑).Involutive := by
  intro x
  simp [neg_shift_neg]

theorem norm_shift_le (z w : ùîª) :
    ‚Äñ(z.shift w : ‚ÑÇ)‚Äñ ‚â§ (‚Äñ(z : ‚ÑÇ)‚Äñ + ‚Äñ(w : ‚ÑÇ)‚Äñ) / (1 + ‚Äñ(z : ‚ÑÇ)‚Äñ * ‚Äñ(w : ‚ÑÇ)‚Äñ) :=
  norm_shiftFun_le z w

/-- A shift by `0` is the identity map, and all other shifts have no fixed points. -/
@[simp]
theorem shift_eq_self_iff {z w : ùîª} : shift z w = w ‚Üî z = 0 := by
  rcases eq_or_ne z 0 with rfl | hz
  ¬∑ simp
  suffices (z : ‚ÑÇ) ‚â† w * conj ‚Üëz * w by
    simpa [shift_eq_iff, hz, add_comm (z : ‚ÑÇ)] using this
  refine ne_of_apply_ne norm (ne_of_gt ?_)
  calc
    _ = ‚Äñ(z : ‚ÑÇ)‚Äñ * ‚Äñ(w : ‚ÑÇ)‚Äñ ^ 2 := by simp; ring
    _ < ‚Äñ(z : ‚ÑÇ)‚Äñ := mul_lt_of_lt_one_right (by simpa) <| by
      rw [‚Üê Complex.normSq_eq_norm_sq]; exact w.normSq_lt_one

lemma shift_apply_circle_smul (c : Circle) (z w : ùîª) :
    shift z (c ‚Ä¢ w) = c ‚Ä¢ shift (c‚Åª¬π ‚Ä¢ z) w := by
  ext1
  simp only [coe_shift, coe_circle_smul, ‚Üê mul_div_assoc, mul_add,
    map_mul, Circle.coe_inv_eq_conj, Complex.conj_conj, ‚Üê mul_assoc]
  rw [‚Üê Circle.coe_inv_eq_conj, ‚Üê Circle.coe_mul]
  simp [mul_comm (c : ‚ÑÇ)]

/-- The composition of two shifts is a rotation by `shiftCompCoeff z w` composed with a shift. -/
@[simps! coe]
def shiftCompCoeff (z w : ùîª) : Circle :=
  .ofConjDivSelf (1 + conj ‚Üëz * w) (shift_den_ne_zero _ _)

theorem shift_apply_shift (z w u : ùîª) :
    shift z (shift w u) = shiftCompCoeff z w ‚Ä¢ shift (shift w z) u := by
  ext1
  have H‚ÇÅ (z w u : ùîª) : 1 + u * conj (w : ‚ÑÇ) + (w + u) * conj (z : ‚ÑÇ) ‚â† 0 := by
    have := shift_den_ne_zero' (shift w u) z
    rw [coe_shift, div_mul_eq_mul_div‚ÇÄ, add_div_eq_mul_add_div, div_ne_zero_iff] at this
    ¬∑ simpa [mul_comm] using this.left
    ¬∑ apply shift_den_ne_zero
  have H‚ÇÇ : 1 + w * conj (z : ‚ÑÇ) + u * (conj ‚Üëw + conj ‚Üëz) ‚â† 0 := by
    simpa [mul_comm] using H‚ÇÅ (star u) (star w) (star z)
  simp [coe_shift]
  field_simp
    (disch := first | apply shift_den_ne_zero | apply shift_den_ne_zero' | apply H‚ÇÅ | exact H‚ÇÇ)
  ring

theorem shift_trans_shift (z w : ùîª) :
    (shift z).trans (shift w) =
      (shift (shift z w)).trans (MulAction.toPerm (shiftCompCoeff w z)) := by
  ext1 u
  simp [shift_apply_shift]

end

end UnitDisc

end Complex
