/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Complex.Circle
public import Mathlib.Analysis.Normed.Module.Ball.Action
public import Mathlib.Algebra.Group.PNatPowAssoc

/-!
# PoincarÃ© disc

In this file we define `Complex.UnitDisc` to be the unit disc in the complex plane. We also
introduce some basic operations on this disc.
-/

@[expose] public section

open Set Function Metric Filter
open scoped ComplexConjugate Topology

noncomputable section

namespace Complex

/-- The complex unit disc, denoted as `ğ”»` withinin the Complex namespace -/
def UnitDisc : Type :=
  ball (0 : â„‚) 1 deriving TopologicalSpace

@[inherit_doc] scoped[Complex.UnitDisc] notation "ğ”»" => Complex.UnitDisc
open UnitDisc

namespace UnitDisc

/-- Coercion to `â„‚`. -/
@[coe] protected def coe : ğ”» â†’ â„‚ := Subtype.val

instance instCommSemigroup : CommSemigroup UnitDisc := by unfold UnitDisc; infer_instance
instance instSemigroupWithZero : SemigroupWithZero UnitDisc := by unfold UnitDisc; infer_instance
instance instIsCancelMulZero : IsCancelMulZero UnitDisc := by unfold UnitDisc; infer_instance
instance instHasDistribNeg : HasDistribNeg UnitDisc := by unfold UnitDisc; infer_instance
instance instCoe : Coe UnitDisc â„‚ := âŸ¨UnitDisc.coeâŸ©

@[ext]
theorem coe_injective : Injective ((â†‘) : ğ”» â†’ â„‚) :=
  Subtype.coe_injective

@[simp, norm_cast]
theorem coe_inj {z w : ğ”»} : (z : â„‚) = w â†” z = w := Subtype.val_inj

@[fun_prop]
theorem isEmbedding_coe : Topology.IsEmbedding ((â†‘) : ğ”» â†’ â„‚) := .subtypeVal

@[fun_prop]
theorem continuous_coe : Continuous ((â†‘) : ğ”» â†’ â„‚) := isEmbedding_coe.continuous

theorem norm_lt_one (z : ğ”») : â€–(z : â„‚)â€– < 1 :=
  mem_ball_zero_iff.1 z.2

theorem norm_ne_one (z : ğ”») : â€–(z : â„‚)â€– â‰  1 :=
  z.norm_lt_one.ne

theorem sq_norm_lt_one (z : ğ”») : â€–(z : â„‚)â€– ^ 2 < 1 := by
  rw [sq_lt_one_iff_abs_lt_one, abs_norm]
  exact z.norm_lt_one

theorem normSq_lt_one (z : ğ”») : normSq z < 1 := by
  rw [â† Complex.norm_mul_self_eq_normSq, â† sq]
  exact z.sq_norm_lt_one

theorem coe_ne_one (z : ğ”») : (z : â„‚) â‰  1 :=
  ne_of_apply_ne (â€–Â·â€–) <| by simp [z.norm_ne_one]

theorem coe_ne_neg_one (z : ğ”») : (z : â„‚) â‰  -1 :=
  ne_of_apply_ne (â€–Â·â€–) <| by simpa [norm_neg] using z.norm_ne_one

theorem one_add_coe_ne_zero (z : ğ”») : (1 + z : â„‚) â‰  0 :=
  mt neg_eq_iff_add_eq_zero.2 z.coe_ne_neg_one.symm

@[simp, norm_cast]
theorem coe_mul (z w : ğ”») : â†‘(z * w) = (z * w : â„‚) :=
  rfl

@[simp, norm_cast]
theorem coe_neg (z : ğ”») : â†‘(-z) = (-z : â„‚) := rfl

/-- A constructor that assumes `â€–zâ€– < 1` instead of `dist z 0 < 1` and returns an element
of `ğ”»` instead of `â†¥Metric.ball (0 : â„‚) 1`. -/
def mk (z : â„‚) (hz : â€–zâ€– < 1) : ğ”» :=
  âŸ¨z, mem_ball_zero_iff.2 hzâŸ©

instance : CanLift â„‚ ğ”» (â†‘) (â€–Â·â€– < 1) where
  prf z hz := âŸ¨mk z hz, rflâŸ©

/-- A cases eliminator that makes `cases z` use `UnitDisc.mk` instead of `Subtype.mk`. -/
@[elab_as_elim, cases_eliminator]
protected def casesOn {motive : ğ”» â†’ Sort*} (mk : âˆ€ z hz, motive (.mk z hz)) (z : ğ”») :
    motive z :=
  mk z z.norm_lt_one

@[simp]
theorem casesOn_mk {motive : ğ”» â†’ Sort*} (mk' : âˆ€ z hz, motive (.mk z hz)) {z : â„‚} (hz : â€–zâ€– < 1) :
    (mk z hz).casesOn mk' = mk' z hz :=
  rfl

@[simp]
theorem coe_mk (z : â„‚) (hz : â€–zâ€– < 1) : (mk z hz : â„‚) = z :=
  rfl

@[simp]
theorem mk_coe (z : ğ”») (hz : â€–(z : â„‚)â€– < 1 := z.norm_lt_one) : mk z hz = z :=
  Subtype.eta _ _

@[simp]
theorem mk_inj {z w : â„‚} (hz : â€–zâ€– < 1) (hw : â€–wâ€– < 1) : mk z hz = mk w hw â†” z = w :=
  Subtype.mk_eq_mk

protected theorem Â«forallÂ» {p : ğ”» â†’ Prop} : (âˆ€ z, p z) â†” âˆ€ z hz, p (mk z hz) :=
  âŸ¨fun h z hz â†¦ h (mk z hz), fun h z â†¦ h z z.norm_lt_oneâŸ©

protected theorem Â«existsÂ» {p : ğ”» â†’ Prop} : (âˆƒ z, p z) â†” âˆƒ z hz, p (mk z hz) :=
  âŸ¨fun âŸ¨z, hzâŸ© â†¦ âŸ¨z, z.norm_lt_one, hzâŸ©, fun âŸ¨z, hz, hâŸ© â†¦ âŸ¨mk z hz, hâŸ©âŸ©

@[simp]
theorem mk_neg (z : â„‚) (hz : â€–-zâ€– < 1) : mk (-z) hz = -mk z (norm_neg z â–¸ hz) :=
  rfl

@[simp]
theorem coe_zero : ((0 : ğ”») : â„‚) = 0 :=
  rfl

@[simp]
theorem coe_eq_zero {z : ğ”»} : (z : â„‚) = 0 â†” z = 0 :=
  coe_injective.eq_iff' coe_zero

@[simp] theorem mk_zero : mk 0 (by simp) = 0 := rfl
@[simp] theorem mk_eq_zero {z : â„‚} (hz : â€–zâ€– < 1) : mk z hz = 0 â†” z = 0 := by simp [â† coe_inj]

instance : Inhabited ğ”» :=
  âŸ¨0âŸ©

instance instMulActionCircle : MulAction Circle ğ”» :=
  mulActionSphereBall

instance instIsScalarTower_circle_circle : IsScalarTower Circle Circle ğ”» :=
  isScalarTower_sphere_sphere_ball

instance instIsScalarTower_circle : IsScalarTower Circle ğ”» ğ”» :=
  isScalarTower_sphere_ball_ball

instance instSMulCommClass_circle_left : SMulCommClass Circle ğ”» ğ”» :=
  instSMulCommClass_sphere_ball_ball

instance instSMulCommClass_circle_right : SMulCommClass ğ”» Circle ğ”» :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_circle_smul (z : Circle) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl

@[deprecated (since := "2026-01-06")]
alias coe_smul_circle := coe_circle_smul

instance instMulActionClosedBall : MulAction (closedBall (0 : â„‚) 1) ğ”» :=
  mulActionClosedBallBall

instance instIsScalarTower_closedBall_closedBall :
    IsScalarTower (closedBall (0 : â„‚) 1) (closedBall (0 : â„‚) 1) ğ”» :=
  isScalarTower_closedBall_closedBall_ball

instance instIsScalarTower_closedBall : IsScalarTower (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  isScalarTower_closedBall_ball_ball

instance instSMulCommClass_closedBall_left : SMulCommClass (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  âŸ¨fun _ _ _ => Subtype.ext <| mul_left_comm _ _ _âŸ©

instance instSMulCommClass_closedBall_right : SMulCommClass ğ”» (closedBall (0 : â„‚) 1) ğ”» :=
  SMulCommClass.symm _ _ _

instance instSMulCommClass_circle_closedBall : SMulCommClass Circle (closedBall (0 : â„‚) 1) ğ”» :=
  instSMulCommClass_sphere_closedBall_ball

instance instSMulCommClass_closedBall_circle : SMulCommClass (closedBall (0 : â„‚) 1) Circle ğ”» :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_closedBall_smul (z : closedBall (0 : â„‚) 1) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl

@[deprecated (since := "2026-01-06")]
alias coe_smul_closedBall := coe_closedBall_smul

instance : Pow UnitDisc â„•+ where
  pow z n := âŸ¨z ^ (n : â„•), by simp [pow_lt_one_iff_of_nonneg, z.norm_lt_one]âŸ©

@[simp, norm_cast]
theorem coe_pow (z : ğ”») (n : â„•+) : ((z ^ n : ğ”») : â„‚) = z ^ (n : â„•) := rfl

@[fun_prop]
theorem continuous_pow (n : â„•+) : Continuous (Â· ^ n : ğ”» â†’ ğ”») := by
  simp only [isEmbedding_coe.continuous_iff, Function.comp_def, coe_pow]
  fun_prop

@[simp]
theorem pow_eq_zero {z : ğ”»} {n : â„•+} : z ^ n = 0 â†” z = 0 := by
  rw [â† coe_inj, coe_pow]
  simp

instance : PNatPowAssoc ğ”» where
  ppow_add m n z := mod_cast pow_add (z : â„‚) m n
  ppow_one z := by simp [â† coe_inj]

theorem tendsto_pow_atTop_nhds_zero (z : ğ”») :
    Tendsto (fun n : â„•+ â†¦ z ^ n) atTop (ğ“ 0) := by
  simp only [isEmbedding_coe.tendsto_nhds_iff, comp_def, coe_pow]
  exact tendsto_pow_atTop_nhds_zero_iff_norm_lt_one.mpr z.norm_lt_one
    |>.comp tendsto_PNat_val_atTop_atTop

/-- Real part of a point of the unit disc. -/
def re (z : ğ”») : â„ :=
  Complex.re z

/-- Imaginary part of a point of the unit disc. -/
def im (z : ğ”») : â„ :=
  Complex.im z

@[simp, norm_cast]
theorem re_coe (z : ğ”») : (z : â„‚).re = z.re :=
  rfl

@[simp, norm_cast]
theorem im_coe (z : ğ”») : (z : â„‚).im = z.im :=
  rfl

@[simp]
theorem re_neg (z : ğ”») : (-z).re = -z.re :=
  rfl

@[simp]
theorem im_neg (z : ğ”») : (-z).im = -z.im :=
  rfl

/-- Conjugate point of the unit disc. -/
instance : Star ğ”» where
  star z := mk (conj z) <| (norm_conj z).symm â–¸ z.norm_lt_one

/-- Conjugate point of the unit disc. Deprecated, use `star` instead. -/
@[deprecated star (since := "2026-01-06")]
protected def Â«conjÂ» (z : ğ”») := star z

@[simp] theorem coe_star (z : ğ”») : (â†‘(star z) : â„‚) = conj â†‘z := rfl

@[deprecated (since := "2026-01-06")]
alias coe_conj := coe_star

@[simp]
protected theorem star_eq_zero {z : ğ”»} : star z = 0 â†” z = 0 := by
  simp [â† coe_eq_zero]

@[simp]
protected theorem star_zero : star (0 : ğ”») = 0 := by simp

instance : InvolutiveStar ğ”» where
  star_involutive z := by ext; simp

@[deprecated star_star (since := "2026-01-06")]
theorem conj_conj (z : ğ”») : star (star z) = z := star_star z

@[simp] protected theorem star_neg (z : ğ”») : star (-z) = -(star z) := rfl

@[deprecated (since := "2026-01-06")]
alias conj_neg := UnitDisc.star_neg

@[simp] protected theorem re_star (z : ğ”») : (star z).re = z.re := rfl

@[deprecated (since := "2026-01-06")]
alias re_conj := UnitDisc.re_star

@[simp] protected theorem im_star (z : ğ”») : (star z).im = -z.im := rfl

@[deprecated (since := "2026-01-06")] alias im_conj := UnitDisc.im_star

instance : StarMul ğ”» where
  star_mul z w := coe_injective <| by simp [mul_comm]

@[deprecated star_mul' (since := "2026-01-06")]
theorem conj_mul (z w : ğ”») : star (z * w) = star z * star w :=
  star_mul' z w

end UnitDisc

end Complex
