/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.NormedSpace.BallAction

/-!
# PoincarÃ© disc

In this file we define `Complex.UnitDisc` to be the unit disc in the complex plane. We also
introduce some basic operations on this disc.
-/


open Set Function Metric

noncomputable section

local notation "conj'" => starRingEnd â„‚

namespace Complex

/-- Complex unit disc. -/
def UnitDisc : Type :=
  ball (0 : â„‚) 1 deriving TopologicalSpace

@[inherit_doc] scoped[UnitDisc] notation "ğ”»" => Complex.UnitDisc
open UnitDisc

namespace UnitDisc

instance instCommSemigroup : CommSemigroup UnitDisc := by unfold UnitDisc; infer_instance
instance instHasDistribNeg : HasDistribNeg UnitDisc := by unfold UnitDisc; infer_instance
instance instCoe : Coe UnitDisc â„‚ := âŸ¨Subtype.valâŸ©

theorem coe_injective : Injective ((â†‘) : ğ”» â†’ â„‚) :=
  Subtype.coe_injective

theorem abs_lt_one (z : ğ”») : abs (z : â„‚) < 1 :=
  mem_ball_zero_iff.1 z.2

theorem abs_ne_one (z : ğ”») : abs (z : â„‚) â‰  1 :=
  z.abs_lt_one.ne

theorem normSq_lt_one (z : ğ”») : normSq z < 1 := by
  convert (Real.sqrt_lt' one_pos).1 z.abs_lt_one
  exact (one_pow 2).symm

theorem coe_ne_one (z : ğ”») : (z : â„‚) â‰  1 :=
  ne_of_apply_ne abs <| (map_one abs).symm â–¸ z.abs_ne_one

theorem coe_ne_neg_one (z : ğ”») : (z : â„‚) â‰  -1 :=
  ne_of_apply_ne abs <| by
    rw [abs.map_neg, map_one]
    exact z.abs_ne_one

theorem one_add_coe_ne_zero (z : ğ”») : (1 + z : â„‚) â‰  0 :=
  mt neg_eq_iff_add_eq_zero.2 z.coe_ne_neg_one.symm

@[simp, norm_cast]
theorem coe_mul (z w : ğ”») : â†‘(z * w) = (z * w : â„‚) :=
  rfl

/-- A constructor that assumes `abs z < 1` instead of `dist z 0 < 1` and returns an element
of `ğ”»` instead of `â†¥Metric.ball (0 : â„‚) 1`. -/
def mk (z : â„‚) (hz : abs z < 1) : ğ”» :=
  âŸ¨z, mem_ball_zero_iff.2 hzâŸ©

@[simp]
theorem coe_mk (z : â„‚) (hz : abs z < 1) : (mk z hz : â„‚) = z :=
  rfl

@[simp]
theorem mk_coe (z : ğ”») (hz : abs (z : â„‚) < 1 := z.abs_lt_one) : mk z hz = z :=
  Subtype.eta _ _

@[simp]
theorem mk_neg (z : â„‚) (hz : abs (-z) < 1) : mk (-z) hz = -mk z (abs.map_neg z â–¸ hz) :=
  rfl

instance : SemigroupWithZero ğ”» :=
  { instCommSemigroup with
    zero := mk 0 <| (map_zero _).trans_lt one_pos
    zero_mul := fun _ => coe_injective <| zero_mul _
    mul_zero := fun _ => coe_injective <| mul_zero _ }

@[simp]
theorem coe_zero : ((0 : ğ”») : â„‚) = 0 :=
  rfl

@[simp]
theorem coe_eq_zero {z : ğ”»} : (z : â„‚) = 0 â†” z = 0 :=
  coe_injective.eq_iff' coe_zero

instance : Inhabited ğ”» :=
  âŸ¨0âŸ©

instance circleAction : MulAction circle ğ”» :=
  mulActionSphereBall

instance isScalarTower_circle_circle : IsScalarTower circle circle ğ”» :=
  isScalarTower_sphere_sphere_ball

instance isScalarTower_circle : IsScalarTower circle ğ”» ğ”» :=
  isScalarTower_sphere_ball_ball

instance instSMulCommClass_circle : SMulCommClass circle ğ”» ğ”» :=
  instSMulCommClass_sphere_ball_ball

instance instSMulCommClass_circle' : SMulCommClass ğ”» circle ğ”» :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_smul_circle (z : circle) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl

instance closedBallAction : MulAction (closedBall (0 : â„‚) 1) ğ”» :=
  mulActionClosedBallBall

instance isScalarTower_closedBall_closedBall :
    IsScalarTower (closedBall (0 : â„‚) 1) (closedBall (0 : â„‚) 1) ğ”» :=
  isScalarTower_closedBall_closedBall_ball

instance isScalarTower_closedBall : IsScalarTower (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  isScalarTower_closedBall_ball_ball

instance instSMulCommClass_closedBall : SMulCommClass (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  âŸ¨fun _ _ _ => Subtype.ext <| mul_left_comm _ _ _âŸ©

instance instSMulCommClass_closedBall' : SMulCommClass ğ”» (closedBall (0 : â„‚) 1) ğ”» :=
  SMulCommClass.symm _ _ _

instance instSMulCommClass_circle_closedBall : SMulCommClass circle (closedBall (0 : â„‚) 1) ğ”» :=
  instSMulCommClass_sphere_closedBall_ball

instance instSMulCommClass_closedBall_circle : SMulCommClass (closedBall (0 : â„‚) 1) circle ğ”» :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_smul_closedBall (z : closedBall (0 : â„‚) 1) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl

/-- Real part of a point of the unit disc. -/
def re (z : ğ”») : â„ :=
  Complex.re z

/-- Imaginary part of a point of the unit disc. -/
def im (z : ğ”») : â„ :=
  Complex.im z

@[simp, norm_cast]
theorem re_coe (z : ğ”») : (z : â„‚).re = z.re :=
  rfl

@[simp, norm_cast]
theorem im_coe (z : ğ”») : (z : â„‚).im = z.im :=
  rfl

@[simp]
theorem re_neg (z : ğ”») : (-z).re = -z.re :=
  rfl

@[simp]
theorem im_neg (z : ğ”») : (-z).im = -z.im :=
  rfl

/-- Conjugate point of the unit disc. -/
def conj (z : ğ”») : ğ”» :=
  mk (conj' â†‘z) <| (abs_conj z).symm â–¸ z.abs_lt_one

-- Porting note: removed `norm_cast` because this is a bad `norm_cast` lemma
-- because both sides have a head coe
@[simp]
theorem coe_conj (z : ğ”») : (z.conj : â„‚) = conj' â†‘z :=
  rfl

@[simp]
theorem conj_zero : conj 0 = 0 :=
  coe_injective (map_zero conj')

@[simp]
theorem conj_conj (z : ğ”») : conj (conj z) = z :=
  coe_injective <| Complex.conj_conj (z : â„‚)

@[simp]
theorem conj_neg (z : ğ”») : (-z).conj = -z.conj :=
  rfl

@[simp]
theorem re_conj (z : ğ”») : z.conj.re = z.re :=
  rfl

@[simp]
theorem im_conj (z : ğ”») : z.conj.im = -z.im :=
  rfl

@[simp]
theorem conj_mul (z w : ğ”») : (z * w).conj = z.conj * w.conj :=
  Subtype.ext <| map_mul _ _ _

end UnitDisc

end Complex
