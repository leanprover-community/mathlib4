/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Complex.Circle
public import Mathlib.Analysis.Normed.Module.Ball.Action
public import Mathlib.Algebra.Group.PNatPowAssoc

/-!
# PoincarÃ© disc

In this file we define `Complex.UnitDisc` to be the unit disc in the complex plane. We also
introduce some basic operations on this disc.
-/

@[expose] public section

open Set Function Metric Filter
open scoped Topology

noncomputable section

local notation "conj'" => starRingEnd â„‚

namespace Complex

/-- The complex unit disc, denoted as `ğ”»` withinin the Complex namespace -/
def UnitDisc : Type :=
  ball (0 : â„‚) 1 deriving TopologicalSpace

@[inherit_doc] scoped[UnitDisc] notation "ğ”»" => Complex.UnitDisc
open UnitDisc

namespace UnitDisc

/-- Coercion to `â„‚`. -/
@[coe] protected def coe : ğ”» â†’ â„‚ := Subtype.val

instance instCommSemigroup : CommSemigroup UnitDisc := by unfold UnitDisc; infer_instance
instance instSemigroupWithZero : SemigroupWithZero UnitDisc := by unfold UnitDisc; infer_instance
instance instIsCancelMulZero : IsCancelMulZero UnitDisc := by unfold UnitDisc; infer_instance
instance instHasDistribNeg : HasDistribNeg UnitDisc := by unfold UnitDisc; infer_instance
instance instCoe : Coe UnitDisc â„‚ := âŸ¨UnitDisc.coeâŸ©

theorem coe_injective : Injective ((â†‘) : ğ”» â†’ â„‚) :=
  Subtype.coe_injective

@[simp, norm_cast]
theorem coe_inj {z w : ğ”»} : (z : â„‚) = w â†” z = w := Subtype.val_inj

@[fun_prop]
theorem isEmbedding_coe : Topology.IsEmbedding ((â†‘) : ğ”» â†’ â„‚) := .subtypeVal

@[fun_prop]
theorem continuous_coe : Continuous ((â†‘) : ğ”» â†’ â„‚) := isEmbedding_coe.continuous

theorem norm_lt_one (z : ğ”») : â€–(z : â„‚)â€– < 1 :=
  mem_ball_zero_iff.1 z.2

theorem norm_ne_one (z : ğ”») : â€–(z : â„‚)â€– â‰  1 :=
  z.norm_lt_one.ne

theorem normSq_lt_one (z : ğ”») : normSq z < 1 := by
  convert (Real.sqrt_lt' one_pos).1 z.norm_lt_one
  exact (one_pow 2).symm

theorem coe_ne_one (z : ğ”») : (z : â„‚) â‰  1 :=
  ne_of_apply_ne (â€–Â·â€–) <| by simp [z.norm_ne_one]

theorem coe_ne_neg_one (z : ğ”») : (z : â„‚) â‰  -1 :=
  ne_of_apply_ne (â€–Â·â€–) <| by simpa [norm_neg] using z.norm_ne_one

theorem one_add_coe_ne_zero (z : ğ”») : (1 + z : â„‚) â‰  0 :=
  mt neg_eq_iff_add_eq_zero.2 z.coe_ne_neg_one.symm

@[simp, norm_cast]
theorem coe_mul (z w : ğ”») : â†‘(z * w) = (z * w : â„‚) :=
  rfl

/-- A constructor that assumes `â€–zâ€– < 1` instead of `dist z 0 < 1` and returns an element
of `ğ”»` instead of `â†¥Metric.ball (0 : â„‚) 1`. -/
def mk (z : â„‚) (hz : â€–zâ€– < 1) : ğ”» :=
  âŸ¨z, mem_ball_zero_iff.2 hzâŸ©

@[simp]
theorem coe_mk (z : â„‚) (hz : â€–zâ€– < 1) : (mk z hz : â„‚) = z :=
  rfl

@[simp]
theorem mk_coe (z : ğ”») (hz : â€–(z : â„‚)â€– < 1 := z.norm_lt_one) : mk z hz = z :=
  Subtype.eta _ _

@[simp]
theorem mk_neg (z : â„‚) (hz : â€–-zâ€– < 1) : mk (-z) hz = -mk z (norm_neg z â–¸ hz) :=
  rfl

@[simp]
theorem coe_zero : ((0 : ğ”») : â„‚) = 0 :=
  rfl

@[simp]
theorem coe_eq_zero {z : ğ”»} : (z : â„‚) = 0 â†” z = 0 :=
  coe_injective.eq_iff' coe_zero

@[simp] theorem mk_zero : mk 0 (by simp) = 0 := rfl
@[simp] theorem mk_eq_zero {z : â„‚} (hz : â€–zâ€– < 1) : mk z hz = 0 â†” z = 0 := by simp [â† coe_inj]

instance : Inhabited ğ”» :=
  âŸ¨0âŸ©

instance circleAction : MulAction Circle ğ”» :=
  mulActionSphereBall

instance isScalarTower_circle_circle : IsScalarTower Circle Circle ğ”» :=
  isScalarTower_sphere_sphere_ball

instance isScalarTower_circle : IsScalarTower Circle ğ”» ğ”» :=
  isScalarTower_sphere_ball_ball

instance instSMulCommClass_circle : SMulCommClass Circle ğ”» ğ”» :=
  instSMulCommClass_sphere_ball_ball

instance instSMulCommClass_circle' : SMulCommClass ğ”» Circle ğ”» :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_smul_circle (z : Circle) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl

instance closedBallAction : MulAction (closedBall (0 : â„‚) 1) ğ”» :=
  mulActionClosedBallBall

instance isScalarTower_closedBall_closedBall :
    IsScalarTower (closedBall (0 : â„‚) 1) (closedBall (0 : â„‚) 1) ğ”» :=
  isScalarTower_closedBall_closedBall_ball

instance isScalarTower_closedBall : IsScalarTower (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  isScalarTower_closedBall_ball_ball

instance instSMulCommClass_closedBall : SMulCommClass (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  âŸ¨fun _ _ _ => Subtype.ext <| mul_left_comm _ _ _âŸ©

instance instSMulCommClass_closedBall' : SMulCommClass ğ”» (closedBall (0 : â„‚) 1) ğ”» :=
  SMulCommClass.symm _ _ _

instance instSMulCommClass_circle_closedBall : SMulCommClass Circle (closedBall (0 : â„‚) 1) ğ”» :=
  instSMulCommClass_sphere_closedBall_ball

instance instSMulCommClass_closedBall_circle : SMulCommClass (closedBall (0 : â„‚) 1) Circle ğ”» :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_smul_closedBall (z : closedBall (0 : â„‚) 1) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl

instance : Pow UnitDisc â„•+ where
  pow z n := âŸ¨z ^ (n : â„•), by simp [pow_lt_one_iff_of_nonneg, z.norm_lt_one]âŸ©

@[simp, norm_cast]
theorem coe_pow (z : ğ”») (n : â„•+) : ((z ^ n : ğ”») : â„‚) = z ^ (n : â„•) := rfl

@[fun_prop]
theorem continuous_pow (n : â„•+) : Continuous (Â· ^ n : ğ”» â†’ ğ”») := by
  simp only [isEmbedding_coe.continuous_iff, Function.comp_def, coe_pow]
  fun_prop

@[simp]
theorem pow_eq_zero {z : ğ”»} {n : â„•+} : z ^ n = 0 â†” z = 0 := by
  rw [â† coe_inj, coe_pow]
  simp

instance : PNatPowAssoc ğ”» where
  ppow_add m n z := mod_cast pow_add (z : â„‚) m n
  ppow_one z := by simp [â† coe_inj]

theorem tendsto_pow_atTop_nhds_zero (z : ğ”») :
    Tendsto (fun n : â„•+ â†¦ z ^ n) atTop (ğ“ 0) := by
  simp only [isEmbedding_coe.tendsto_nhds_iff, comp_def, coe_pow]
  exact tendsto_pow_atTop_nhds_zero_iff_norm_lt_one.mpr z.norm_lt_one
    |>.comp tendsto_PNat_val_atTop_atTop

/-- Real part of a point of the unit disc. -/
def re (z : ğ”») : â„ :=
  Complex.re z

/-- Imaginary part of a point of the unit disc. -/
def im (z : ğ”») : â„ :=
  Complex.im z

@[simp, norm_cast]
theorem re_coe (z : ğ”») : (z : â„‚).re = z.re :=
  rfl

@[simp, norm_cast]
theorem im_coe (z : ğ”») : (z : â„‚).im = z.im :=
  rfl

@[simp]
theorem re_neg (z : ğ”») : (-z).re = -z.re :=
  rfl

@[simp]
theorem im_neg (z : ğ”») : (-z).im = -z.im :=
  rfl

/-- Conjugate point of the unit disc. -/
def conj (z : ğ”») : ğ”» :=
  mk (conj' â†‘z) <| (norm_conj z).symm â–¸ z.norm_lt_one

@[simp]
theorem coe_conj (z : ğ”») : (z.conj : â„‚) = conj' â†‘z :=
  rfl

@[simp]
theorem conj_zero : conj 0 = 0 :=
  coe_injective (map_zero conj')

@[simp]
theorem conj_conj (z : ğ”») : conj (conj z) = z :=
  coe_injective <| Complex.conj_conj (z : â„‚)

@[simp]
theorem conj_neg (z : ğ”») : (-z).conj = -z.conj :=
  rfl

@[simp]
theorem re_conj (z : ğ”») : z.conj.re = z.re :=
  rfl

@[simp]
theorem im_conj (z : ğ”») : z.conj.im = -z.im :=
  rfl

@[simp]
theorem conj_mul (z w : ğ”») : (z * w).conj = z.conj * w.conj :=
  Subtype.ext <| map_mul _ _ _

end UnitDisc

end Complex
