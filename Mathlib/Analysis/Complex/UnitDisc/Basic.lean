/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.NormedSpace.BallAction

#align_import analysis.complex.unit_disc.basic from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# PoincarÃ© disc

In this file we define `Complex.UnitDisc` to be the unit disc in the complex plane. We also
introduce some basic operations on this disc.
-/


open Set Function Metric

noncomputable section

local notation "conj'" => starRingEnd â„‚

namespace Complex

/-- Complex unit disc. -/
def UnitDisc : Type :=
  ball (0 : â„‚) 1 deriving TopologicalSpace
#align complex.unit_disc Complex.UnitDisc

@[inherit_doc] scoped[UnitDisc] notation "ğ”»" => Complex.UnitDisc
open UnitDisc

namespace UnitDisc

instance instCommSemigroup : CommSemigroup UnitDisc := by unfold UnitDisc; infer_instance
instance instHasDistribNeg : HasDistribNeg UnitDisc := by unfold UnitDisc; infer_instance
instance instCoe : Coe UnitDisc â„‚ := âŸ¨Subtype.valâŸ©

theorem coe_injective : Injective ((â†‘) : ğ”» â†’ â„‚) :=
  Subtype.coe_injective
#align complex.unit_disc.coe_injective Complex.UnitDisc.coe_injective

theorem abs_lt_one (z : ğ”») : abs (z : â„‚) < 1 :=
  mem_ball_zero_iff.1 z.2
#align complex.unit_disc.abs_lt_one Complex.UnitDisc.abs_lt_one

theorem abs_ne_one (z : ğ”») : abs (z : â„‚) â‰  1 :=
  z.abs_lt_one.ne
#align complex.unit_disc.abs_ne_one Complex.UnitDisc.abs_ne_one

theorem normSq_lt_one (z : ğ”») : normSq z < 1 := by
  convert (Real.sqrt_lt' one_pos).1 z.abs_lt_one
  exact (one_pow 2).symm
#align complex.unit_disc.norm_sq_lt_one Complex.UnitDisc.normSq_lt_one

theorem coe_ne_one (z : ğ”») : (z : â„‚) â‰  1 :=
  ne_of_apply_ne abs <| (map_one abs).symm â–¸ z.abs_ne_one
#align complex.unit_disc.coe_ne_one Complex.UnitDisc.coe_ne_one

theorem coe_ne_neg_one (z : ğ”») : (z : â„‚) â‰  -1 :=
  ne_of_apply_ne abs <| by
    rw [abs.map_neg, map_one]
    exact z.abs_ne_one
#align complex.unit_disc.coe_ne_neg_one Complex.UnitDisc.coe_ne_neg_one

theorem one_add_coe_ne_zero (z : ğ”») : (1 + z : â„‚) â‰  0 :=
  mt neg_eq_iff_add_eq_zero.2 z.coe_ne_neg_one.symm
#align complex.unit_disc.one_add_coe_ne_zero Complex.UnitDisc.one_add_coe_ne_zero

@[simp, norm_cast]
theorem coe_mul (z w : ğ”») : â†‘(z * w) = (z * w : â„‚) :=
  rfl
#align complex.unit_disc.coe_mul Complex.UnitDisc.coe_mul

/-- A constructor that assumes `abs z < 1` instead of `dist z 0 < 1` and returns an element
of `ğ”»` instead of `â†¥Metric.ball (0 : â„‚) 1`. -/
def mk (z : â„‚) (hz : abs z < 1) : ğ”» :=
  âŸ¨z, mem_ball_zero_iff.2 hzâŸ©
#align complex.unit_disc.mk Complex.UnitDisc.mk

@[simp]
theorem coe_mk (z : â„‚) (hz : abs z < 1) : (mk z hz : â„‚) = z :=
  rfl
#align complex.unit_disc.coe_mk Complex.UnitDisc.coe_mk

@[simp]
theorem mk_coe (z : ğ”») (hz : abs (z : â„‚) < 1 := z.abs_lt_one) : mk z hz = z :=
  Subtype.eta _ _
#align complex.unit_disc.mk_coe Complex.UnitDisc.mk_coe

@[simp]
theorem mk_neg (z : â„‚) (hz : abs (-z) < 1) : mk (-z) hz = -mk z (abs.map_neg z â–¸ hz) :=
  rfl
#align complex.unit_disc.mk_neg Complex.UnitDisc.mk_neg

instance : SemigroupWithZero ğ”» :=
  { instCommSemigroup with
    zero := mk 0 <| (map_zero _).trans_lt one_pos
    zero_mul := fun _ => coe_injective <| zero_mul _
    mul_zero := fun _ => coe_injective <| mul_zero _ }

@[simp]
theorem coe_zero : ((0 : ğ”») : â„‚) = 0 :=
  rfl
#align complex.unit_disc.coe_zero Complex.UnitDisc.coe_zero

@[simp]
theorem coe_eq_zero {z : ğ”»} : (z : â„‚) = 0 â†” z = 0 :=
  coe_injective.eq_iff' coe_zero
#align complex.unit_disc.coe_eq_zero Complex.UnitDisc.coe_eq_zero

instance : Inhabited ğ”» :=
  âŸ¨0âŸ©

instance circleAction : MulAction circle ğ”» :=
  mulActionSphereBall
#align complex.unit_disc.circle_action Complex.UnitDisc.circleAction

instance isScalarTower_circle_circle : IsScalarTower circle circle ğ”» :=
  isScalarTower_sphere_sphere_ball
#align complex.unit_disc.is_scalar_tower_circle_circle Complex.UnitDisc.isScalarTower_circle_circle

instance isScalarTower_circle : IsScalarTower circle ğ”» ğ”» :=
  isScalarTower_sphere_ball_ball
#align complex.unit_disc.is_scalar_tower_circle Complex.UnitDisc.isScalarTower_circle

instance instSMulCommClass_circle : SMulCommClass circle ğ”» ğ”» :=
  instSMulCommClass_sphere_ball_ball
#align complex.unit_disc.smul_comm_class_circle Complex.UnitDisc.instSMulCommClass_circle

instance instSMulCommClass_circle' : SMulCommClass ğ”» circle ğ”» :=
  SMulCommClass.symm _ _ _
#align complex.unit_disc.smul_comm_class_circle' Complex.UnitDisc.instSMulCommClass_circle'

@[simp, norm_cast]
theorem coe_smul_circle (z : circle) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl
#align complex.unit_disc.coe_smul_circle Complex.UnitDisc.coe_smul_circle

instance closedBallAction : MulAction (closedBall (0 : â„‚) 1) ğ”» :=
  mulActionClosedBallBall
#align complex.unit_disc.closed_ball_action Complex.UnitDisc.closedBallAction

instance isScalarTower_closedBall_closedBall :
    IsScalarTower (closedBall (0 : â„‚) 1) (closedBall (0 : â„‚) 1) ğ”» :=
  isScalarTower_closedBall_closedBall_ball
#align complex.unit_disc.is_scalar_tower_closed_ball_closed_ball Complex.UnitDisc.isScalarTower_closedBall_closedBall

instance isScalarTower_closedBall : IsScalarTower (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  isScalarTower_closedBall_ball_ball
#align complex.unit_disc.is_scalar_tower_closed_ball Complex.UnitDisc.isScalarTower_closedBall

instance instSMulCommClass_closedBall : SMulCommClass (closedBall (0 : â„‚) 1) ğ”» ğ”» :=
  âŸ¨fun _ _ _ => Subtype.ext <| mul_left_comm _ _ _âŸ©
#align complex.unit_disc.smul_comm_class_closed_ball Complex.UnitDisc.instSMulCommClass_closedBall

instance instSMulCommClass_closedBall' : SMulCommClass ğ”» (closedBall (0 : â„‚) 1) ğ”» :=
  SMulCommClass.symm _ _ _
#align complex.unit_disc.smul_comm_class_closed_ball' Complex.UnitDisc.instSMulCommClass_closedBall'

instance instSMulCommClass_circle_closedBall : SMulCommClass circle (closedBall (0 : â„‚) 1) ğ”» :=
  instSMulCommClass_sphere_closedBall_ball
#align complex.unit_disc.smul_comm_class_circle_closed_ball Complex.UnitDisc.instSMulCommClass_circle_closedBall

instance instSMulCommClass_closedBall_circle : SMulCommClass (closedBall (0 : â„‚) 1) circle ğ”» :=
  SMulCommClass.symm _ _ _
#align complex.unit_disc.smul_comm_class_closed_ball_circle Complex.UnitDisc.instSMulCommClass_closedBall_circle

@[simp, norm_cast]
theorem coe_smul_closedBall (z : closedBall (0 : â„‚) 1) (w : ğ”») : â†‘(z â€¢ w) = (z * w : â„‚) :=
  rfl
#align complex.unit_disc.coe_smul_closed_ball Complex.UnitDisc.coe_smul_closedBall

/-- Real part of a point of the unit disc. -/
def re (z : ğ”») : â„ :=
  Complex.re z
#align complex.unit_disc.re Complex.UnitDisc.re

/-- Imaginary part of a point of the unit disc. -/
def im (z : ğ”») : â„ :=
  Complex.im z
#align complex.unit_disc.im Complex.UnitDisc.im

@[simp, norm_cast]
theorem re_coe (z : ğ”») : (z : â„‚).re = z.re :=
  rfl
#align complex.unit_disc.re_coe Complex.UnitDisc.re_coe

@[simp, norm_cast]
theorem im_coe (z : ğ”») : (z : â„‚).im = z.im :=
  rfl
#align complex.unit_disc.im_coe Complex.UnitDisc.im_coe

@[simp]
theorem re_neg (z : ğ”») : (-z).re = -z.re :=
  rfl
#align complex.unit_disc.re_neg Complex.UnitDisc.re_neg

@[simp]
theorem im_neg (z : ğ”») : (-z).im = -z.im :=
  rfl
#align complex.unit_disc.im_neg Complex.UnitDisc.im_neg

/-- Conjugate point of the unit disc. -/
def conj (z : ğ”») : ğ”» :=
  mk (conj' â†‘z) <| (abs_conj z).symm â–¸ z.abs_lt_one
#align complex.unit_disc.conj Complex.UnitDisc.conj

-- Porting note: removed `norm_cast` because this is a bad `norm_cast` lemma
-- because both sides have a head coe
@[simp]
theorem coe_conj (z : ğ”») : (z.conj : â„‚) = conj' â†‘z :=
  rfl
#align complex.unit_disc.coe_conj Complex.UnitDisc.coe_conj

@[simp]
theorem conj_zero : conj 0 = 0 :=
  coe_injective (map_zero conj')
#align complex.unit_disc.conj_zero Complex.UnitDisc.conj_zero

@[simp]
theorem conj_conj (z : ğ”») : conj (conj z) = z :=
  coe_injective <| Complex.conj_conj (z : â„‚)
#align complex.unit_disc.conj_conj Complex.UnitDisc.conj_conj

@[simp]
theorem conj_neg (z : ğ”») : (-z).conj = -z.conj :=
  rfl
#align complex.unit_disc.conj_neg Complex.UnitDisc.conj_neg

@[simp]
theorem re_conj (z : ğ”») : z.conj.re = z.re :=
  rfl
#align complex.unit_disc.re_conj Complex.UnitDisc.re_conj

@[simp]
theorem im_conj (z : ğ”») : z.conj.im = -z.im :=
  rfl
#align complex.unit_disc.im_conj Complex.UnitDisc.im_conj

@[simp]
theorem conj_mul (z w : ğ”») : (z * w).conj = z.conj * w.conj :=
  Subtype.ext <| map_mul _ _ _
#align complex.unit_disc.conj_mul Complex.UnitDisc.conj_mul

end UnitDisc

end Complex
