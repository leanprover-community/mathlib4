/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.NormedSpace.BallAction

/-!
# Poincar√© disc

In this file we define `Complex.UnitDisc` to be the unit disc in the complex plane. We also
introduce some basic operations on this disc.
-/


open Set Function Metric

noncomputable section

local notation "conj'" => starRingEnd ‚ÑÇ

namespace Complex

/-- The complex unit disc, denoted as `ùîª` withinin the Complex namespace -/
def UnitDisc : Type := ball (0 : ‚ÑÇ) 1

@[inherit_doc] scoped[UnitDisc] notation "ùîª" => Complex.UnitDisc
open UnitDisc

namespace UnitDisc

instance : TopologicalSpace UnitDisc := inferInstanceAs (TopologicalSpace (ball (0 : ‚ÑÇ) 1))
instance instCommSemigroup : CommSemigroup UnitDisc := by unfold UnitDisc; infer_instance
instance instHasDistribNeg : HasDistribNeg UnitDisc := by unfold UnitDisc; infer_instance
instance instCoe : Coe UnitDisc ‚ÑÇ := ‚ü®Subtype.val‚ü©

theorem coe_injective : Injective ((‚Üë) : ùîª ‚Üí ‚ÑÇ) :=
  Subtype.coe_injective

theorem norm_lt_one (z : ùîª) : ‚Äñ(z : ‚ÑÇ)‚Äñ < 1 :=
  mem_ball_zero_iff.1 z.2

theorem norm_ne_one (z : ùîª) : ‚Äñ(z : ‚ÑÇ)‚Äñ ‚â† 1 :=
  z.norm_lt_one.ne

@[deprecated (since := "2025-02-16")] alias abs_lt_one := norm_lt_one
@[deprecated (since := "2025-02-16")] alias abs_ne_one := norm_ne_one

theorem normSq_lt_one (z : ùîª) : normSq z < 1 := by
  convert (Real.sqrt_lt' one_pos).1 z.norm_lt_one
  exact (one_pow 2).symm

theorem coe_ne_one (z : ùîª) : (z : ‚ÑÇ) ‚â† 1 :=
  ne_of_apply_ne (‚Äñ¬∑‚Äñ) <| by simp [z.norm_ne_one]

theorem coe_ne_neg_one (z : ùîª) : (z : ‚ÑÇ) ‚â† -1 :=
  ne_of_apply_ne (‚Äñ¬∑‚Äñ) <| by simpa [norm_neg] using z.norm_ne_one

theorem one_add_coe_ne_zero (z : ùîª) : (1 + z : ‚ÑÇ) ‚â† 0 :=
  mt neg_eq_iff_add_eq_zero.2 z.coe_ne_neg_one.symm

@[simp, norm_cast]
theorem coe_mul (z w : ùîª) : ‚Üë(z * w) = (z * w : ‚ÑÇ) :=
  rfl

/-- A constructor that assumes `‚Äñz‚Äñ < 1` instead of `dist z 0 < 1` and returns an element
of `ùîª` instead of `‚Ü•Metric.ball (0 : ‚ÑÇ) 1`. -/
def mk (z : ‚ÑÇ) (hz : ‚Äñz‚Äñ < 1) : ùîª :=
  ‚ü®z, mem_ball_zero_iff.2 hz‚ü©

@[simp]
theorem coe_mk (z : ‚ÑÇ) (hz : ‚Äñz‚Äñ < 1) : (mk z hz : ‚ÑÇ) = z :=
  rfl

@[simp]
theorem mk_coe (z : ùîª) (hz : ‚Äñ(z : ‚ÑÇ)‚Äñ < 1 := z.norm_lt_one) : mk z hz = z :=
  Subtype.eta _ _

@[simp]
theorem mk_neg (z : ‚ÑÇ) (hz : ‚Äñ-z‚Äñ < 1) : mk (-z) hz = -mk z (norm_neg z ‚ñ∏ hz) :=
  rfl

instance : SemigroupWithZero ùîª :=
  { instCommSemigroup with
    zero := mk 0 <| norm_zero.trans_lt one_pos
    zero_mul := fun _ => coe_injective <| zero_mul _
    mul_zero := fun _ => coe_injective <| mul_zero _ }

@[simp]
theorem coe_zero : ((0 : ùîª) : ‚ÑÇ) = 0 :=
  rfl

@[simp]
theorem coe_eq_zero {z : ùîª} : (z : ‚ÑÇ) = 0 ‚Üî z = 0 :=
  coe_injective.eq_iff' coe_zero

instance : Inhabited ùîª :=
  ‚ü®0‚ü©

instance circleAction : MulAction Circle ùîª :=
  mulActionSphereBall

instance isScalarTower_circle_circle : IsScalarTower Circle Circle ùîª :=
  isScalarTower_sphere_sphere_ball

instance isScalarTower_circle : IsScalarTower Circle ùîª ùîª :=
  isScalarTower_sphere_ball_ball

instance instSMulCommClass_circle : SMulCommClass Circle ùîª ùîª :=
  instSMulCommClass_sphere_ball_ball

instance instSMulCommClass_circle' : SMulCommClass ùîª Circle ùîª :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_smul_circle (z : Circle) (w : ùîª) : ‚Üë(z ‚Ä¢ w) = (z * w : ‚ÑÇ) :=
  rfl

instance closedBallAction : MulAction (closedBall (0 : ‚ÑÇ) 1) ùîª :=
  mulActionClosedBallBall

instance isScalarTower_closedBall_closedBall :
    IsScalarTower (closedBall (0 : ‚ÑÇ) 1) (closedBall (0 : ‚ÑÇ) 1) ùîª :=
  isScalarTower_closedBall_closedBall_ball

instance isScalarTower_closedBall : IsScalarTower (closedBall (0 : ‚ÑÇ) 1) ùîª ùîª :=
  isScalarTower_closedBall_ball_ball

instance instSMulCommClass_closedBall : SMulCommClass (closedBall (0 : ‚ÑÇ) 1) ùîª ùîª :=
  ‚ü®fun _ _ _ => Subtype.ext <| mul_left_comm _ _ _‚ü©

instance instSMulCommClass_closedBall' : SMulCommClass ùîª (closedBall (0 : ‚ÑÇ) 1) ùîª :=
  SMulCommClass.symm _ _ _

instance instSMulCommClass_circle_closedBall : SMulCommClass Circle (closedBall (0 : ‚ÑÇ) 1) ùîª :=
  instSMulCommClass_sphere_closedBall_ball

instance instSMulCommClass_closedBall_circle : SMulCommClass (closedBall (0 : ‚ÑÇ) 1) Circle ùîª :=
  SMulCommClass.symm _ _ _

@[simp, norm_cast]
theorem coe_smul_closedBall (z : closedBall (0 : ‚ÑÇ) 1) (w : ùîª) : ‚Üë(z ‚Ä¢ w) = (z * w : ‚ÑÇ) :=
  rfl

/-- Real part of a point of the unit disc. -/
def re (z : ùîª) : ‚Ñù :=
  Complex.re z

/-- Imaginary part of a point of the unit disc. -/
def im (z : ùîª) : ‚Ñù :=
  Complex.im z

@[simp, norm_cast]
theorem re_coe (z : ùîª) : (z : ‚ÑÇ).re = z.re :=
  rfl

@[simp, norm_cast]
theorem im_coe (z : ùîª) : (z : ‚ÑÇ).im = z.im :=
  rfl

@[simp]
theorem re_neg (z : ùîª) : (-z).re = -z.re :=
  rfl

@[simp]
theorem im_neg (z : ùîª) : (-z).im = -z.im :=
  rfl

/-- Conjugate point of the unit disc. -/
def conj (z : ùîª) : ùîª :=
  mk (conj' ‚Üëz) <| (norm_conj z).symm ‚ñ∏ z.norm_lt_one

@[simp]
theorem coe_conj (z : ùîª) : (z.conj : ‚ÑÇ) = conj' ‚Üëz :=
  rfl

@[simp]
theorem conj_zero : conj 0 = 0 :=
  coe_injective (map_zero conj')

@[simp]
theorem conj_conj (z : ùîª) : conj (conj z) = z :=
  coe_injective <| Complex.conj_conj (z : ‚ÑÇ)

@[simp]
theorem conj_neg (z : ùîª) : (-z).conj = -z.conj :=
  rfl

@[simp]
theorem re_conj (z : ùîª) : z.conj.re = z.re :=
  rfl

@[simp]
theorem im_conj (z : ùîª) : z.conj.im = -z.im :=
  rfl

@[simp]
theorem conj_mul (z w : ùîª) : (z * w).conj = z.conj * w.conj :=
  Subtype.ext <| map_mul _ _ _

end UnitDisc

end Complex
