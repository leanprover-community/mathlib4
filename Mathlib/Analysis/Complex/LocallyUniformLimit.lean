/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Analysis.Calculus.Series

#align_import analysis.complex.locally_uniform_limit from "leanprover-community/mathlib"@"fe44cd36149e675eb5dec87acc7e8f1d6568e081"

/-!
# Locally uniform limits of holomorphic functions

This file gathers some results about locally uniform limits of holomorphic functions on an open
subset of the complex plane.

## Main results

* `TendstoLocallyUniformlyOn.differentiableOn`: A locally uniform limit of holomorphic functions
  is holomorphic.
* `TendstoLocallyUniformlyOn.deriv`: Locally uniform convergence implies locally uniform
  convergence of the derivatives to the derivative of the limit.
-/


open Set Metric MeasureTheory Filter Complex intervalIntegral

open scoped Real Topology

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

variable {E Î¹ : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [CompleteSpace E] {U K : Set â„‚}
  {z : â„‚} {M r Î´ : â„} {Ï† : Filter Î¹} {F : Î¹ â†’ â„‚ â†’ E} {f g : â„‚ â†’ E}

namespace Complex

section Cderiv

/-- A circle integral which coincides with `deriv f z` whenever one can apply the Cauchy formula for
the derivative. It is useful in the proof that locally uniform limits of holomorphic functions are
holomorphic, because it depends continuously on `f` for the uniform topology. -/
noncomputable def cderiv (r : â„) (f : â„‚ â†’ E) (z : â„‚) : E :=
  (2 * Ï€ * I : â„‚)â»Â¹ â€¢ âˆ® w in C(z, r), ((w - z) ^ 2)â»Â¹ â€¢ f w
#align complex.cderiv Complex.cderiv

theorem cderiv_eq_deriv (hU : IsOpen U) (hf : DifferentiableOn â„‚ f U) (hr : 0 < r)
    (hzr : closedBall z r âŠ† U) : cderiv r f z = deriv f z :=
  two_pi_I_inv_smul_circleIntegral_sub_sq_inv_smul_of_differentiable hU hzr hf (mem_ball_self hr)
#align complex.cderiv_eq_deriv Complex.cderiv_eq_deriv

theorem norm_cderiv_le (hr : 0 < r) (hf : âˆ€ w âˆˆ sphere z r, â€–f wâ€– â‰¤ M) :
    â€–cderiv r f zâ€– â‰¤ M / r := by
  have hM : 0 â‰¤ M := by
    obtain âŸ¨w, hwâŸ© : (sphere z r).Nonempty := NormedSpace.sphere_nonempty.mpr hr.le
    exact (norm_nonneg _).trans (hf w hw)
  have h1 : âˆ€ w âˆˆ sphere z r, â€–((w - z) ^ 2)â»Â¹ â€¢ f wâ€– â‰¤ M / r ^ 2 := by
    intro w hw
    simp only [mem_sphere_iff_norm, norm_eq_abs] at hw
    simp only [norm_smul, inv_mul_eq_div, hw, norm_eq_abs, map_invâ‚€, Complex.abs_pow]
    exact div_le_div hM (hf w hw) (sq_pos_of_pos hr) le_rfl
  have h2 := circleIntegral.norm_integral_le_of_norm_le_const hr.le h1
  -- âŠ¢ â€–cderiv r f zâ€– â‰¤ M / r
  simp only [cderiv, norm_smul]
  -- âŠ¢ â€–(2 * â†‘Ï€ * I)â»Â¹â€– * â€–âˆ® (w : â„‚) in C(z, r), ((w - z) ^ 2)â»Â¹ â€¢ f wâ€– â‰¤ M / r
  refine' (mul_le_mul le_rfl h2 (norm_nonneg _) (norm_nonneg _)).trans (le_of_eq _)
  -- âŠ¢ â€–(2 * â†‘Ï€ * I)â»Â¹â€– * (2 * Ï€ * r * (M / r ^ 2)) = M / r
  field_simp [_root_.abs_of_nonneg Real.pi_pos.le]
  -- âŠ¢ 2 * Ï€ * r * M * r = M * (2 * Ï€ * r ^ 2)
  ring
  -- ğŸ‰ no goals
#align complex.norm_cderiv_le Complex.norm_cderiv_le

theorem cderiv_sub (hr : 0 < r) (hf : ContinuousOn f (sphere z r))
    (hg : ContinuousOn g (sphere z r)) : cderiv r (f - g) z = cderiv r f z - cderiv r g z := by
  have h1 : ContinuousOn (fun w : â„‚ => ((w - z) ^ 2)â»Â¹) (sphere z r) := by
    refine' ((continuous_id'.sub continuous_const).pow 2).continuousOn.invâ‚€ fun w hw h => hr.ne _
    rwa [mem_sphere_iff_norm, sq_eq_zero_iff.mp h, norm_zero] at hw
  simp_rw [cderiv, â† smul_sub]
  -- âŠ¢ ((2 * â†‘Ï€ * I)â»Â¹ â€¢ âˆ® (w : â„‚) in C(z, r), ((w - z) ^ 2)â»Â¹ â€¢ (f - g) w) = (2 *  â€¦
  congr 1
  -- âŠ¢ (âˆ® (w : â„‚) in C(z, r), ((w - z) ^ 2)â»Â¹ â€¢ (f - g) w) = (âˆ® (w : â„‚) in C(z, r), â€¦
  simpa only [Pi.sub_apply, smul_sub] using
    circleIntegral.integral_sub ((h1.smul hf).circleIntegrable hr.le)
      ((h1.smul hg).circleIntegrable hr.le)
#align complex.cderiv_sub Complex.cderiv_sub

theorem norm_cderiv_lt (hr : 0 < r) (hfM : âˆ€ w âˆˆ sphere z r, â€–f wâ€– < M)
    (hf : ContinuousOn f (sphere z r)) : â€–cderiv r f zâ€– < M / r := by
  obtain âŸ¨L, hL1, hL2âŸ© : âˆƒ L < M, âˆ€ w âˆˆ sphere z r, â€–f wâ€– â‰¤ L := by
    have e1 : (sphere z r).Nonempty := NormedSpace.sphere_nonempty.mpr hr.le
    have e2 : ContinuousOn (fun w => â€–f wâ€–) (sphere z r) := continuous_norm.comp_continuousOn hf
    obtain âŸ¨x, hx, hx'âŸ© := (isCompact_sphere z r).exists_forall_ge e1 e2
    exact âŸ¨â€–f xâ€–, hfM x hx, hx'âŸ©
  exact (norm_cderiv_le hr hL2).trans_lt ((div_lt_div_right hr).mpr hL1)
  -- ğŸ‰ no goals
#align complex.norm_cderiv_lt Complex.norm_cderiv_lt

theorem norm_cderiv_sub_lt (hr : 0 < r) (hfg : âˆ€ w âˆˆ sphere z r, â€–f w - g wâ€– < M)
    (hf : ContinuousOn f (sphere z r)) (hg : ContinuousOn g (sphere z r)) :
    â€–cderiv r f z - cderiv r g zâ€– < M / r :=
  cderiv_sub hr hf hg â–¸ norm_cderiv_lt hr hfg (hf.sub hg)
#align complex.norm_cderiv_sub_lt Complex.norm_cderiv_sub_lt

theorem _root_.TendstoUniformlyOn.cderiv (hF : TendstoUniformlyOn F f Ï† (cthickening Î´ K))
    (hÎ´ : 0 < Î´) (hFn : âˆ€á¶  n in Ï†, ContinuousOn (F n) (cthickening Î´ K)) :
    TendstoUniformlyOn (cderiv Î´ âˆ˜ F) (cderiv Î´ f) Ï† K := by
  rcases Ï†.eq_or_neBot with rfl | hne
  -- âŠ¢ TendstoUniformlyOn (cderiv Î´ âˆ˜ F) (cderiv Î´ f) âŠ¥ K
  Â· simp only [TendstoUniformlyOn, eventually_bot, imp_true_iff]
    -- ğŸ‰ no goals
  have e1 : ContinuousOn f (cthickening Î´ K) := TendstoUniformlyOn.continuousOn hF hFn
  -- âŠ¢ TendstoUniformlyOn (Complex.cderiv Î´ âˆ˜ F) (Complex.cderiv Î´ f) Ï† K
  rw [tendstoUniformlyOn_iff] at hF âŠ¢
  -- âŠ¢ âˆ€ (Îµ : â„), Îµ > 0 â†’ âˆ€á¶  (n : Î¹) in Ï†, âˆ€ (x : â„‚), x âˆˆ K â†’ dist (Complex.cderiv  â€¦
  rintro Îµ hÎµ
  -- âŠ¢ âˆ€á¶  (n : Î¹) in Ï†, âˆ€ (x : â„‚), x âˆˆ K â†’ dist (Complex.cderiv Î´ f x) ((Complex.cd â€¦
  filter_upwards [hF (Îµ * Î´) (mul_pos hÎµ hÎ´), hFn] with n h h' z hz
  -- âŠ¢ dist (Complex.cderiv Î´ f z) ((Complex.cderiv Î´ âˆ˜ F) n z) < Îµ
  simp_rw [dist_eq_norm] at h âŠ¢
  -- âŠ¢ â€–Complex.cderiv Î´ f z - (Complex.cderiv Î´ âˆ˜ F) n zâ€– < Îµ
  have e2 : âˆ€ w âˆˆ sphere z Î´, â€–f w - F n wâ€– < Îµ * Î´ := fun w hw1 =>
    h w (closedBall_subset_cthickening hz Î´ (sphere_subset_closedBall hw1))
  have e3 := sphere_subset_closedBall.trans (closedBall_subset_cthickening hz Î´)
  -- âŠ¢ â€–Complex.cderiv Î´ f z - (Complex.cderiv Î´ âˆ˜ F) n zâ€– < Îµ
  have hf : ContinuousOn f (sphere z Î´) :=
    e1.mono (sphere_subset_closedBall.trans (closedBall_subset_cthickening hz Î´))
  simpa only [mul_div_cancel _ hÎ´.ne.symm] using norm_cderiv_sub_lt hÎ´ e2 hf (h'.mono e3)
  -- ğŸ‰ no goals
#align tendsto_uniformly_on.cderiv TendstoUniformlyOn.cderiv

end Cderiv

section Weierstrass

theorem tendstoUniformlyOn_deriv_of_cthickening_subset (hf : TendstoLocallyUniformlyOn F f Ï† U)
    (hF : âˆ€á¶  n in Ï†, DifferentiableOn â„‚ (F n) U) {Î´ : â„} (hÎ´ : 0 < Î´) (hK : IsCompact K)
    (hU : IsOpen U) (hKU : cthickening Î´ K âŠ† U) :
    TendstoUniformlyOn (deriv âˆ˜ F) (cderiv Î´ f) Ï† K := by
  have h1 : âˆ€á¶  n in Ï†, ContinuousOn (F n) (cthickening Î´ K) := by
    filter_upwards [hF] with n h using h.continuousOn.mono hKU
  have h2 : IsCompact (cthickening Î´ K) :=
    isCompact_of_isClosed_bounded isClosed_cthickening hK.bounded.cthickening
  have h3 : TendstoUniformlyOn F f Ï† (cthickening Î´ K) :=
    (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).mp hf (cthickening Î´ K) hKU h2
  apply (h3.cderiv hÎ´ h1).congr
  -- âŠ¢ âˆ€á¶  (n : Î¹) in Ï†, EqOn ((cderiv Î´ âˆ˜ F) n) ((deriv âˆ˜ F) n) K
  filter_upwards [hF] with n h z hz
  -- âŠ¢ (cderiv Î´ âˆ˜ F) n z = (deriv âˆ˜ F) n z
  exact cderiv_eq_deriv hU h hÎ´ ((closedBall_subset_cthickening hz Î´).trans hKU)
  -- ğŸ‰ no goals
#align complex.tendsto_uniformly_on_deriv_of_cthickening_subset Complex.tendstoUniformlyOn_deriv_of_cthickening_subset

theorem exists_cthickening_tendstoUniformlyOn (hf : TendstoLocallyUniformlyOn F f Ï† U)
    (hF : âˆ€á¶  n in Ï†, DifferentiableOn â„‚ (F n) U) (hK : IsCompact K) (hU : IsOpen U) (hKU : K âŠ† U) :
    âˆƒ Î´ > 0, cthickening Î´ K âŠ† U âˆ§ TendstoUniformlyOn (deriv âˆ˜ F) (cderiv Î´ f) Ï† K := by
  obtain âŸ¨Î´, hÎ´, hKÎ´âŸ© := hK.exists_cthickening_subset_open hU hKU
  -- âŠ¢ âˆƒ Î´, Î´ > 0 âˆ§ cthickening Î´ K âŠ† U âˆ§ TendstoUniformlyOn (deriv âˆ˜ F) (cderiv Î´  â€¦
  exact âŸ¨Î´, hÎ´, hKÎ´, tendstoUniformlyOn_deriv_of_cthickening_subset hf hF hÎ´ hK hU hKÎ´âŸ©
  -- ğŸ‰ no goals
#align complex.exists_cthickening_tendsto_uniformly_on Complex.exists_cthickening_tendstoUniformlyOn

/-- A locally uniform limit of holomorphic functions on an open domain of the complex plane is
holomorphic (the derivatives converge locally uniformly to that of the limit, which is proved
as `TendstoLocallyUniformlyOn.deriv`). -/
theorem _root_.TendstoLocallyUniformlyOn.differentiableOn [Ï†.NeBot]
    (hf : TendstoLocallyUniformlyOn F f Ï† U) (hF : âˆ€á¶  n in Ï†, DifferentiableOn â„‚ (F n) U)
    (hU : IsOpen U) : DifferentiableOn â„‚ f U := by
  rintro x hx
  -- âŠ¢ DifferentiableWithinAt â„‚ f U x
  obtain âŸ¨K, âŸ¨hKx, hKâŸ©, hKUâŸ© := (compact_basis_nhds x).mem_iff.mp (hU.mem_nhds hx)
  -- âŠ¢ DifferentiableWithinAt â„‚ f U x
  obtain âŸ¨Î´, _, _, h1âŸ© := exists_cthickening_tendstoUniformlyOn hf hF hK hU hKU
  -- âŠ¢ DifferentiableWithinAt â„‚ f U x
  have h2 : interior K âŠ† U := interior_subset.trans hKU
  -- âŠ¢ DifferentiableWithinAt â„‚ f U x
  have h3 : âˆ€á¶  n in Ï†, DifferentiableOn â„‚ (F n) (interior K)
  -- âŠ¢ âˆ€á¶  (n : Î¹) in Ï†, DifferentiableOn â„‚ (F n) (interior K)
  filter_upwards [hF] with n h using h.mono h2
  -- âŠ¢ DifferentiableWithinAt â„‚ f U x
  have h4 : TendstoLocallyUniformlyOn F f Ï† (interior K) := hf.mono h2
  -- âŠ¢ DifferentiableWithinAt â„‚ f U x
  have h5 : TendstoLocallyUniformlyOn (deriv âˆ˜ F) (cderiv Î´ f) Ï† (interior K) :=
    h1.tendstoLocallyUniformlyOn.mono interior_subset
  have h6 : âˆ€ x âˆˆ interior K, HasDerivAt f (cderiv Î´ f x) x := fun x h =>
    hasDerivAt_of_tendsto_locally_uniformly_on' isOpen_interior h5 h3 (fun _ => h4.tendsto_at) h
  have h7 : DifferentiableOn â„‚ f (interior K) := fun x hx =>
    (h6 x hx).differentiableAt.differentiableWithinAt
  exact (h7.differentiableAt (interior_mem_nhds.mpr hKx)).differentiableWithinAt
  -- ğŸ‰ no goals
#align tendsto_locally_uniformly_on.differentiable_on TendstoLocallyUniformlyOn.differentiableOn

theorem _root_.TendstoLocallyUniformlyOn.deriv (hf : TendstoLocallyUniformlyOn F f Ï† U)
    (hF : âˆ€á¶  n in Ï†, DifferentiableOn â„‚ (F n) U) (hU : IsOpen U) :
    TendstoLocallyUniformlyOn (deriv âˆ˜ F) (deriv f) Ï† U := by
  rw [tendstoLocallyUniformlyOn_iff_forall_isCompact hU]
  -- âŠ¢ âˆ€ (K : Set â„‚), K âŠ† U â†’ IsCompact K â†’ TendstoUniformlyOn (_root_.deriv âˆ˜ F) ( â€¦
  rcases Ï†.eq_or_neBot with rfl | hne
  -- âŠ¢ âˆ€ (K : Set â„‚), K âŠ† U â†’ IsCompact K â†’ TendstoUniformlyOn (deriv âˆ˜ F) (deriv f â€¦
  Â· simp only [TendstoUniformlyOn, eventually_bot, imp_true_iff]
    -- ğŸ‰ no goals
  rintro K hKU hK
  -- âŠ¢ TendstoUniformlyOn (_root_.deriv âˆ˜ F) (_root_.deriv f) Ï† K
  obtain âŸ¨Î´, hÎ´, hK4, hâŸ© := exists_cthickening_tendstoUniformlyOn hf hF hK hU hKU
  -- âŠ¢ TendstoUniformlyOn (_root_.deriv âˆ˜ F) (_root_.deriv f) Ï† K
  refine' h.congr_right fun z hz => cderiv_eq_deriv hU (hf.differentiableOn hF hU) hÎ´ _
  -- âŠ¢ closedBall z Î´ âŠ† U
  exact (closedBall_subset_cthickening hz Î´).trans hK4
  -- ğŸ‰ no goals
#align tendsto_locally_uniformly_on.deriv TendstoLocallyUniformlyOn.deriv

end Weierstrass

section Tsums

/-- If the terms in the sum `âˆ‘' (i : Î¹), F i` are uniformly bounded on `U` by a
summable function, and each term in the sum is differentiable on `U`, then so is the sum. -/
theorem differentiableOn_tsum_of_summable_norm {u : Î¹ â†’ â„} (hu : Summable u)
    (hf : âˆ€ i : Î¹, DifferentiableOn â„‚ (F i) U) (hU : IsOpen U)
    (hF_le : âˆ€ (i : Î¹) (w : â„‚), w âˆˆ U â†’ â€–F i wâ€– â‰¤ u i) :
    DifferentiableOn â„‚ (fun w : â„‚ => âˆ‘' i : Î¹, F i w) U := by
  classical
  have hc := (tendstoUniformlyOn_tsum hu hF_le).tendstoLocallyUniformlyOn
  refine' hc.differentiableOn (eventually_of_forall fun s => _) hU
  exact DifferentiableOn.sum fun i _ => hf i
#align complex.differentiable_on_tsum_of_summable_norm Complex.differentiableOn_tsum_of_summable_norm

/-- If the terms in the sum `âˆ‘' (i : Î¹), F i` are uniformly bounded on `U` by a
summable function, then the sum of `deriv F i` at a point in `U` is the derivative of the
sum. -/
theorem hasSum_deriv_of_summable_norm {u : Î¹ â†’ â„} (hu : Summable u)
    (hf : âˆ€ i : Î¹, DifferentiableOn â„‚ (F i) U) (hU : IsOpen U)
    (hF_le : âˆ€ (i : Î¹) (w : â„‚), w âˆˆ U â†’ â€–F i wâ€– â‰¤ u i) (hz : z âˆˆ U) :
    HasSum (fun i : Î¹ => deriv (F i) z) (deriv (fun w : â„‚ => âˆ‘' i : Î¹, F i w) z) := by
  rw [HasSum]
  -- âŠ¢ Tendsto (fun s => Finset.sum s fun b => deriv (F b) z) atTop (ğ“ (deriv (fun  â€¦
  have hc := (tendstoUniformlyOn_tsum hu hF_le).tendstoLocallyUniformlyOn
  -- âŠ¢ Tendsto (fun s => Finset.sum s fun b => deriv (F b) z) atTop (ğ“ (deriv (fun  â€¦
  convert (hc.deriv (eventually_of_forall fun s =>
    DifferentiableOn.sum fun i _ => hf i) hU).tendsto_at hz using 1
  ext1 s
  -- âŠ¢ (Finset.sum s fun b => deriv (F b) z) = (deriv âˆ˜ fun t x => Finset.sum t fun â€¦
  exact (deriv_sum fun i _ => (hf i).differentiableAt (hU.mem_nhds hz)).symm
  -- ğŸ‰ no goals
#align complex.has_sum_deriv_of_summable_norm Complex.hasSum_deriv_of_summable_norm

end Tsums

end Complex
