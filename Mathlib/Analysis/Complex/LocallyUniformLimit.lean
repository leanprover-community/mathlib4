/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import Mathlib.Analysis.Complex.RemovableSingularity
import Mathlib.Analysis.Calculus.UniformLimitsDeriv
import Mathlib.Analysis.NormedSpace.FunctionSeries

/-!
# Locally uniform limits of holomorphic functions

This file gathers some results about locally uniform limits of holomorphic functions on an open
subset of the complex plane.

## Main results

* `TendstoLocallyUniformlyOn.differentiableOn`: A locally uniform limit of holomorphic functions
  is holomorphic.
* `TendstoLocallyUniformlyOn.deriv`: Locally uniform convergence implies locally uniform
  convergence of the derivatives to the derivative of the limit.
-/


open Set Metric MeasureTheory Filter Complex intervalIntegral

open scoped Real Topology

variable {E Œπ : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {U K : Set ‚ÑÇ}
  {z : ‚ÑÇ} {M r Œ¥ : ‚Ñù} {œÜ : Filter Œπ} {F : Œπ ‚Üí ‚ÑÇ ‚Üí E} {f g : ‚ÑÇ ‚Üí E}

namespace Complex

section Cderiv

/-- A circle integral which coincides with `deriv f z` whenever one can apply the Cauchy formula for
the derivative. It is useful in the proof that locally uniform limits of holomorphic functions are
holomorphic, because it depends continuously on `f` for the uniform topology. -/
noncomputable def cderiv (r : ‚Ñù) (f : ‚ÑÇ ‚Üí E) (z : ‚ÑÇ) : E :=
  (2 * œÄ * I : ‚ÑÇ)‚Åª¬π ‚Ä¢ ‚àÆ w in C(z, r), ((w - z) ^ 2)‚Åª¬π ‚Ä¢ f w

theorem cderiv_eq_deriv [CompleteSpace E] (hU : IsOpen U) (hf : DifferentiableOn ‚ÑÇ f U) (hr : 0 < r)
    (hzr : closedBall z r ‚äÜ U) : cderiv r f z = deriv f z :=
  two_pi_I_inv_smul_circleIntegral_sub_sq_inv_smul_of_differentiable hU hzr hf (mem_ball_self hr)

theorem norm_cderiv_le (hr : 0 < r) (hf : ‚àÄ w ‚àà sphere z r, ‚Äñf w‚Äñ ‚â§ M) :
    ‚Äñcderiv r f z‚Äñ ‚â§ M / r := by
  have hM : 0 ‚â§ M := by
    obtain ‚ü®w, hw‚ü© : (sphere z r).Nonempty := NormedSpace.sphere_nonempty.mpr hr.le
    exact (norm_nonneg _).trans (hf w hw)
  have h1 : ‚àÄ w ‚àà sphere z r, ‚Äñ((w - z) ^ 2)‚Åª¬π ‚Ä¢ f w‚Äñ ‚â§ M / r ^ 2 := by
    intro w hw
    simp only [mem_sphere_iff_norm] at hw
    simp only [norm_smul, inv_mul_eq_div, hw, norm_inv, norm_pow]
    exact div_le_div‚ÇÄ hM (hf w hw) (sq_pos_of_pos hr) le_rfl
  have h2 := circleIntegral.norm_integral_le_of_norm_le_const hr.le h1
  simp only [cderiv, norm_smul]
  refine (mul_le_mul le_rfl h2 (norm_nonneg _) (norm_nonneg _)).trans (le_of_eq ?_)
  simp [field, abs_of_nonneg Real.pi_pos.le]

theorem cderiv_sub (hr : 0 < r) (hf : ContinuousOn f (sphere z r))
    (hg : ContinuousOn g (sphere z r)) : cderiv r (f - g) z = cderiv r f z - cderiv r g z := by
  have h1 : ContinuousOn (fun w : ‚ÑÇ => ((w - z) ^ 2)‚Åª¬π) (sphere z r) := by
    refine ((continuous_id'.sub continuous_const).pow 2).continuousOn.inv‚ÇÄ fun w hw h => hr.ne ?_
    rwa [mem_sphere_iff_norm, sq_eq_zero_iff.mp h, norm_zero] at hw
  simp_rw [cderiv, ‚Üê smul_sub]
  congr 1
  simpa only [Pi.sub_apply, smul_sub] using
    circleIntegral.integral_sub ((h1.smul hf).circleIntegrable hr.le)
      ((h1.smul hg).circleIntegrable hr.le)

theorem norm_cderiv_lt (hr : 0 < r) (hfM : ‚àÄ w ‚àà sphere z r, ‚Äñf w‚Äñ < M)
    (hf : ContinuousOn f (sphere z r)) : ‚Äñcderiv r f z‚Äñ < M / r := by
  obtain ‚ü®L, hL1, hL2‚ü© : ‚àÉ L < M, ‚àÄ w ‚àà sphere z r, ‚Äñf w‚Äñ ‚â§ L := by
    have e1 : (sphere z r).Nonempty := NormedSpace.sphere_nonempty.mpr hr.le
    have e2 : ContinuousOn (fun w => ‚Äñf w‚Äñ) (sphere z r) := continuous_norm.comp_continuousOn hf
    obtain ‚ü®x, hx, hx'‚ü© := (isCompact_sphere z r).exists_isMaxOn e1 e2
    exact ‚ü®‚Äñf x‚Äñ, hfM x hx, hx'‚ü©
  exact (norm_cderiv_le hr hL2).trans_lt ((div_lt_div_iff_of_pos_right hr).mpr hL1)

theorem norm_cderiv_sub_lt (hr : 0 < r) (hfg : ‚àÄ w ‚àà sphere z r, ‚Äñf w - g w‚Äñ < M)
    (hf : ContinuousOn f (sphere z r)) (hg : ContinuousOn g (sphere z r)) :
    ‚Äñcderiv r f z - cderiv r g z‚Äñ < M / r :=
  cderiv_sub hr hf hg ‚ñ∏ norm_cderiv_lt hr hfg (hf.sub hg)

theorem _root_.TendstoUniformlyOn.cderiv (hF : TendstoUniformlyOn F f œÜ (cthickening Œ¥ K))
    (hŒ¥ : 0 < Œ¥) (hFn : ‚àÄ·∂† n in œÜ, ContinuousOn (F n) (cthickening Œ¥ K)) :
    TendstoUniformlyOn (cderiv Œ¥ ‚àò F) (cderiv Œ¥ f) œÜ K := by
  rcases œÜ.eq_or_neBot with rfl | hne
  ¬∑ simp only [TendstoUniformlyOn, eventually_bot, imp_true_iff]
  have e1 : ContinuousOn f (cthickening Œ¥ K) := TendstoUniformlyOn.continuousOn hF hFn
  rw [tendstoUniformlyOn_iff] at hF ‚ä¢
  rintro Œµ hŒµ
  filter_upwards [hF (Œµ * Œ¥) (mul_pos hŒµ hŒ¥), hFn] with n h h' z hz
  simp_rw [dist_eq_norm] at h ‚ä¢
  have e2 : ‚àÄ w ‚àà sphere z Œ¥, ‚Äñf w - F n w‚Äñ < Œµ * Œ¥ := fun w hw1 =>
    h w (closedBall_subset_cthickening hz Œ¥ (sphere_subset_closedBall hw1))
  have e3 := sphere_subset_closedBall.trans (closedBall_subset_cthickening hz Œ¥)
  have hf : ContinuousOn f (sphere z Œ¥) :=
    e1.mono (sphere_subset_closedBall.trans (closedBall_subset_cthickening hz Œ¥))
  simpa only [mul_div_cancel_right‚ÇÄ _ hŒ¥.ne.symm] using norm_cderiv_sub_lt hŒ¥ e2 hf (h'.mono e3)

end Cderiv

variable [CompleteSpace E]

section Weierstrass

theorem tendstoUniformlyOn_deriv_of_cthickening_subset (hf : TendstoLocallyUniformlyOn F f œÜ U)
    (hF : ‚àÄ·∂† n in œÜ, DifferentiableOn ‚ÑÇ (F n) U) {Œ¥ : ‚Ñù} (hŒ¥ : 0 < Œ¥) (hK : IsCompact K)
    (hU : IsOpen U) (hKU : cthickening Œ¥ K ‚äÜ U) :
    TendstoUniformlyOn (deriv ‚àò F) (cderiv Œ¥ f) œÜ K := by
  have h1 : ‚àÄ·∂† n in œÜ, ContinuousOn (F n) (cthickening Œ¥ K) := by
    filter_upwards [hF] with n h using h.continuousOn.mono hKU
  have h2 : IsCompact (cthickening Œ¥ K) := hK.cthickening
  have h3 : TendstoUniformlyOn F f œÜ (cthickening Œ¥ K) :=
    (tendstoLocallyUniformlyOn_iff_forall_isCompact hU).mp hf (cthickening Œ¥ K) hKU h2
  apply (h3.cderiv hŒ¥ h1).congr
  filter_upwards [hF] with n h z hz
  exact cderiv_eq_deriv hU h hŒ¥ ((closedBall_subset_cthickening hz Œ¥).trans hKU)

theorem exists_cthickening_tendstoUniformlyOn (hf : TendstoLocallyUniformlyOn F f œÜ U)
    (hF : ‚àÄ·∂† n in œÜ, DifferentiableOn ‚ÑÇ (F n) U) (hK : IsCompact K) (hU : IsOpen U) (hKU : K ‚äÜ U) :
    ‚àÉ Œ¥ > 0, cthickening Œ¥ K ‚äÜ U ‚àß TendstoUniformlyOn (deriv ‚àò F) (cderiv Œ¥ f) œÜ K := by
  obtain ‚ü®Œ¥, hŒ¥, hKŒ¥‚ü© := hK.exists_cthickening_subset_open hU hKU
  exact ‚ü®Œ¥, hŒ¥, hKŒ¥, tendstoUniformlyOn_deriv_of_cthickening_subset hf hF hŒ¥ hK hU hKŒ¥‚ü©

/-- A locally uniform limit of holomorphic functions on an open domain of the complex plane is
holomorphic (the derivatives converge locally uniformly to that of the limit, which is proved
as `TendstoLocallyUniformlyOn.deriv`). -/
theorem _root_.TendstoLocallyUniformlyOn.differentiableOn [œÜ.NeBot]
    (hf : TendstoLocallyUniformlyOn F f œÜ U) (hF : ‚àÄ·∂† n in œÜ, DifferentiableOn ‚ÑÇ (F n) U)
    (hU : IsOpen U) : DifferentiableOn ‚ÑÇ f U := by
  rintro x hx
  obtain ‚ü®K, ‚ü®hKx, hK‚ü©, hKU‚ü© := (compact_basis_nhds x).mem_iff.mp (hU.mem_nhds hx)
  obtain ‚ü®Œ¥, _, _, h1‚ü© := exists_cthickening_tendstoUniformlyOn hf hF hK hU hKU
  have h2 : interior K ‚äÜ U := interior_subset.trans hKU
  have h3 : ‚àÄ·∂† n in œÜ, DifferentiableOn ‚ÑÇ (F n) (interior K) := by
    filter_upwards [hF] with n h using h.mono h2
  have h4 : TendstoLocallyUniformlyOn F f œÜ (interior K) := hf.mono h2
  have h5 : TendstoLocallyUniformlyOn (deriv ‚àò F) (cderiv Œ¥ f) œÜ (interior K) :=
    h1.tendstoLocallyUniformlyOn.mono interior_subset
  have h6 : ‚àÄ x ‚àà interior K, HasDerivAt f (cderiv Œ¥ f x) x := fun x h =>
    hasDerivAt_of_tendsto_locally_uniformly_on' isOpen_interior h5 h3 (fun _ => h4.tendsto_at) h
  have h7 : DifferentiableOn ‚ÑÇ f (interior K) := fun x hx =>
    (h6 x hx).differentiableAt.differentiableWithinAt
  exact (h7.differentiableAt (interior_mem_nhds.mpr hKx)).differentiableWithinAt

theorem _root_.TendstoLocallyUniformlyOn.deriv (hf : TendstoLocallyUniformlyOn F f œÜ U)
    (hF : ‚àÄ·∂† n in œÜ, DifferentiableOn ‚ÑÇ (F n) U) (hU : IsOpen U) :
    TendstoLocallyUniformlyOn (deriv ‚àò F) (deriv f) œÜ U := by
  rw [tendstoLocallyUniformlyOn_iff_forall_isCompact hU]
  rcases œÜ.eq_or_neBot with rfl | hne
  ¬∑ simp only [TendstoUniformlyOn, eventually_bot, imp_true_iff]
  rintro K hKU hK
  obtain ‚ü®Œ¥, hŒ¥, hK4, h‚ü© := exists_cthickening_tendstoUniformlyOn hf hF hK hU hKU
  refine h.congr_right fun z hz => cderiv_eq_deriv hU (hf.differentiableOn hF hU) hŒ¥ ?_
  exact (closedBall_subset_cthickening hz Œ¥).trans hK4

end Weierstrass

section Tsums

/-- If the terms in the sum `‚àë' (i : Œπ), F i` are uniformly bounded on `U` by a
summable function, and each term in the sum is differentiable on `U`, then so is the sum. -/
theorem differentiableOn_tsum_of_summable_norm {u : Œπ ‚Üí ‚Ñù} (hu : Summable u)
    (hf : ‚àÄ i : Œπ, DifferentiableOn ‚ÑÇ (F i) U) (hU : IsOpen U)
    (hF_le : ‚àÄ (i : Œπ) (w : ‚ÑÇ), w ‚àà U ‚Üí ‚ÄñF i w‚Äñ ‚â§ u i) :
    DifferentiableOn ‚ÑÇ (fun w : ‚ÑÇ => ‚àë' i : Œπ, F i w) U := by
  classical
  have hc := (tendstoUniformlyOn_tsum hu hF_le).tendstoLocallyUniformlyOn
  refine hc.differentiableOn (Eventually.of_forall fun s => ?_) hU
  exact DifferentiableOn.fun_sum fun i _ => hf i

/-- If the terms in the sum `‚àë' (i : Œπ), F i` are uniformly bounded on `U` by a
summable function, then the sum of `deriv F i` at a point in `U` is the derivative of the
sum. -/
theorem hasSum_deriv_of_summable_norm {u : Œπ ‚Üí ‚Ñù} (hu : Summable u)
    (hf : ‚àÄ i : Œπ, DifferentiableOn ‚ÑÇ (F i) U) (hU : IsOpen U)
    (hF_le : ‚àÄ (i : Œπ) (w : ‚ÑÇ), w ‚àà U ‚Üí ‚ÄñF i w‚Äñ ‚â§ u i) (hz : z ‚àà U) :
    HasSum (fun i : Œπ => deriv (F i) z) (deriv (fun w : ‚ÑÇ => ‚àë' i : Œπ, F i w) z) := by
  rw [HasSum]
  have hc := (tendstoUniformlyOn_tsum hu hF_le).tendstoLocallyUniformlyOn
  convert (hc.deriv (Eventually.of_forall fun s =>
    DifferentiableOn.fun_sum fun i _ => hf i) hU).tendsto_at hz using 1
  ext1 s
  exact (deriv_fun_sum fun i _ => (hf i).differentiableAt (hU.mem_nhds hz)).symm

end Tsums

section LogDeriv

/-- The logarithmic derivative of a sequence of functions converging locally uniformly to a
function is the logarithmic derivative of the limit function. -/
theorem logDeriv_tendsto {Œπ : Type*} {p : Filter Œπ} {f : Œπ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ} {g : ‚ÑÇ ‚Üí ‚ÑÇ}
    {s : Set ‚ÑÇ} (hs : IsOpen s) (x : s) (hF : TendstoLocallyUniformlyOn f g p s)
    (hf : ‚àÄ·∂† n : Œπ in p, DifferentiableOn ‚ÑÇ (f n) s) (hg : g x ‚â† 0) :
    Tendsto (fun n : Œπ => logDeriv (f n) x) p (ùìù ((logDeriv g) x)) := by
  simp_rw [logDeriv]
  apply Tendsto.div ((hF.deriv hf hs).tendsto_at x.2) (hF.tendsto_at x.2) hg

end LogDeriv

end Complex
