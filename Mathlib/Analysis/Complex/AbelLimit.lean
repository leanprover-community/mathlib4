/-
Copyright (c) 2024 Jeremy Tan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Tan
-/
import Mathlib.Analysis.Complex.Basic
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Tactic.Peel
import Mathlib.Tactic.Positivity

/-!
# Abel's limit theorem

If a real or complex power series for a function has radius of convergence 1 and the series is only
known to converge conditionally at 1, Abel's limit theorem gives the value at 1 as the limit of the
function at 1 from the left. "Left" for complex numbers means within a fixed cone opening to the
left with angle less than `œÄ`.

## Main theorems

* `Complex.tendsto_tsum_powerSeries_nhdsWithin_stolzCone`:
  Abel's limit theorem for complex power series.
* `Real.tendsto_tsum_powerSeries_nhdsWithin_lt`: Abel's limit theorem for real power series.

## References

* https://planetmath.org/proofofabelslimittheorem
* https://en.wikipedia.org/wiki/Abel%27s_theorem
-/


open Filter Finset

open scoped Topology

namespace Complex

section StolzSet

open Real

/-- The Stolz set for a given `M`, roughly teardrop-shaped with the tip at 1 but tending to the
open unit disc as `M` tends to infinity. -/
def stolzSet (M : ‚Ñù) : Set ‚ÑÇ := {z | ‚Äñz‚Äñ < 1 ‚àß ‚Äñ1 - z‚Äñ < M * (1 - ‚Äñz‚Äñ)}

/-- The cone to the left of `1` with angle `2Œ∏` such that `tan Œ∏ = s`. -/
def stolzCone (s : ‚Ñù) : Set ‚ÑÇ := {z | |z.im| < s * (1 - z.re)}

theorem stolzSet_empty {M : ‚Ñù} (hM : M ‚â§ 1) : stolzSet M = ‚àÖ := by
  ext z
  rw [stolzSet, Set.mem_setOf, Set.mem_empty_iff_false, iff_false, not_and, not_lt, ‚Üê sub_pos]
  intro zn
  calc
    _ ‚â§ 1 * (1 - ‚Äñz‚Äñ) := mul_le_mul_of_nonneg_right hM zn.le
    _ = ‚Äñ(1 : ‚ÑÇ)‚Äñ - ‚Äñz‚Äñ := by rw [one_mul, norm_one]
    _ ‚â§ _ := norm_sub_norm_le _ _

theorem nhdsWithin_lt_le_nhdsWithin_stolzSet {M : ‚Ñù} (hM : 1 < M) :
    (ùìù[<] 1).map ofReal ‚â§ ùìù[stolzSet M] 1 := by
  rw [‚Üê tendsto_id']
  refine tendsto_map' <| tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within ofReal
    (tendsto_nhdsWithin_of_tendsto_nhds <| ofRealCLM.continuous.tendsto' 1 1 rfl) ?_
  simp only [eventually_iff, mem_nhdsWithin]
  refine ‚ü®Set.Ioo 0 2, isOpen_Ioo, by simp, fun x hx ‚Ü¶ ?_‚ü©
  simp only [Set.mem_inter_iff, Set.mem_Ioo, Set.mem_Iio] at hx
  simp only [Set.mem_setOf_eq, stolzSet, ‚Üê ofReal_one, ‚Üê ofReal_sub, norm_real,
    norm_of_nonneg hx.1.1.le, norm_of_nonneg <| (sub_pos.mpr hx.2).le]
  exact ‚ü®hx.2, lt_mul_left (sub_pos.mpr hx.2) hM‚ü©

-- An ugly technical lemma
private lemma stolzCone_subset_stolzSet_aux' (s : ‚Ñù) :
    ‚àÉ M Œµ, 0 < M ‚àß 0 < Œµ ‚àß ‚àÄ x y, 0 < x ‚Üí x < Œµ ‚Üí |y| < s * x ‚Üí
      ‚àö(x ^ 2 + y ^ 2) < M * (1 - ‚àö((1 - x) ^ 2 + y ^ 2)) := by
  refine ‚ü®2 * ‚àö(1 + s ^ 2) + 1, 1 / (1 + s ^ 2), by positivity, by positivity,
    fun x y hx‚ÇÄ hx‚ÇÅ hy ‚Ü¶ ?_‚ü©
  have H : ‚àö((1 - x) ^ 2 + y ^ 2) ‚â§ 1 - x / 2 := by
    calc ‚àö((1 - x) ^ 2 + y ^ 2)
      _ ‚â§ ‚àö((1 - x) ^ 2 + (s * x) ^ 2) := sqrt_le_sqrt <| by rw [‚Üê sq_abs y]; gcongr
      _ = ‚àö(1 - 2 * x + (1 + s ^ 2) * x * x) := by congr 1; ring
      _ ‚â§ ‚àö(1 - 2 * x + (1 + s ^ 2) * (1 / (1 + s ^ 2)) * x) := by gcongr
      _ = ‚àö(1 - x) := by congr 1; field_simp; ring
      _ ‚â§ 1 - x / 2 := by
        simp_rw [sub_eq_add_neg, ‚Üê neg_div]
        refine sqrt_one_add_le <| neg_le_neg_iff.mpr (hx‚ÇÅ.trans_le ?_).le
        rw [div_le_one (by positivity)]
        exact le_add_of_nonneg_right <| sq_nonneg s
  calc ‚àö(x ^ 2 + y ^ 2)
    _ ‚â§ ‚àö(x ^ 2 + (s * x) ^ 2) := by rw [‚Üê sq_abs y]; gcongr
    _ = ‚àö((1 + s ^ 2) * x ^ 2) := by congr; ring
    _ = ‚àö(1 + s ^ 2) * x := by rw [sqrt_mul' _ (sq_nonneg x), sqrt_sq hx‚ÇÄ.le]
    _ = 2 * ‚àö(1 + s ^ 2) * (x / 2) := by ring
    _ < (2 * ‚àö(1 + s ^ 2) + 1) * (x / 2) := by gcongr; exact lt_add_one _
    _ ‚â§ _ := by gcongr; exact le_sub_comm.mpr H

lemma stolzCone_subset_stolzSet_aux {s : ‚Ñù} (hs : 0 < s) :
    ‚àÉ M Œµ, 0 < M ‚àß 0 < Œµ ‚àß {z : ‚ÑÇ | 1 - Œµ < z.re} ‚à© stolzCone s ‚äÜ stolzSet M := by
  peel stolzCone_subset_stolzSet_aux' s with M Œµ hM hŒµ H
  rintro z ‚ü®hzl, hzr‚ü©
  rw [Set.mem_setOf_eq, sub_lt_comm, ‚Üê one_re, ‚Üê sub_re] at hzl
  rw [stolzCone, Set.mem_setOf_eq, ‚Üê one_re, ‚Üê sub_re] at hzr
  replace H :=
    H (1 - z).re z.im ((mul_pos_iff_of_pos_left hs).mp <| (abs_nonneg z.im).trans_lt hzr) hzl hzr
  have h : z.im ^ 2 = (1 - z).im ^ 2 := by
    simp only [sub_im, one_im, zero_sub, neg_sq]
  rw [h, ‚Üê norm_eq_sqrt_sq_add_sq, ‚Üê h, sub_re, one_re, sub_sub_cancel,
    ‚Üê norm_eq_sqrt_sq_add_sq] at H
  exact ‚ü®sub_pos.mp <| (mul_pos_iff_of_pos_left hM).mp <| (norm_nonneg _).trans_lt H, H‚ü©

lemma nhdsWithin_stolzCone_le_nhdsWithin_stolzSet {s : ‚Ñù} (hs : 0 < s) :
    ‚àÉ M, ùìù[stolzCone s] 1 ‚â§ ùìù[stolzSet M] 1 := by
  obtain ‚ü®M, Œµ, _, hŒµ, H‚ü© := stolzCone_subset_stolzSet_aux hs
  use M
  rw [nhdsWithin_le_iff, mem_nhdsWithin]
  refine ‚ü®{w | 1 - Œµ < w.re}, isOpen_lt continuous_const continuous_re, ?_, H‚ü©
  simp only [Set.mem_setOf_eq, one_re, sub_lt_self_iff, hŒµ]

end StolzSet

variable {f : ‚Ñï ‚Üí ‚ÑÇ} {l : ‚ÑÇ}

/-- Auxiliary lemma for Abel's limit theorem. The difference between the sum `l` at 1 and the
power series's value at a point `z` away from 1 can be rewritten as `1 - z` times a power series
whose coefficients are tail sums of `l`. -/
lemma abel_aux (h : Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, f i) atTop (ùìù l)) {z : ‚ÑÇ} (hz : ‚Äñz‚Äñ < 1) :
    Tendsto (fun n ‚Ü¶ (1 - z) * ‚àë i ‚àà range n, (l - ‚àë j ‚àà range (i + 1), f j) * z ^ i)
      atTop (ùìù (l - ‚àë' n, f n * z ^ n)) := by
  let s := fun n ‚Ü¶ ‚àë i ‚àà range n, f i
  have k := h.sub (summable_powerSeries_of_norm_lt_one h.cauchySeq hz).hasSum.tendsto_sum_nat
  simp_rw [‚Üê sum_sub_distrib, ‚Üê mul_one_sub, ‚Üê geom_sum_mul_neg, ‚Üê mul_assoc, ‚Üê sum_mul,
    mul_comm, mul_sum _ _ (f _), range_eq_Ico, ‚Üê sum_Ico_Ico_comm', ‚Üê range_eq_Ico,
    ‚Üê sum_mul] at k
  conv at k =>
    enter [1, n]
    rw [sum_congr (g := fun j ‚Ü¶ (‚àë k ‚àà range n, f k - ‚àë k ‚àà range (j + 1), f k) * z ^ j)
      rfl (fun j hj ‚Ü¶ by congr 1; exact sum_Ico_eq_sub _ (mem_range.mp hj))]
  suffices Tendsto (fun n ‚Ü¶ (l - s n) * ‚àë i ‚àà range n, z ^ i) atTop (ùìù 0) by
    simp_rw [mul_sum] at this
    replace this := (this.const_mul (1 - z)).add k
    conv at this =>
      enter [1, n]
      rw [‚Üê mul_add, ‚Üê sum_add_distrib]
      enter [2, 2, i]
      rw [‚Üê add_mul, sub_add_sub_cancel]
    rwa [mul_zero, zero_add] at this
  rw [‚Üê zero_mul (-1 / (z - 1))]
  apply Tendsto.mul
  ¬∑ simpa only [neg_zero, neg_sub] using (tendsto_sub_nhds_zero_iff.mpr h).neg
  ¬∑ conv =>
      enter [1, n]
      rw [geom_sum_eq (by contrapose! hz; simp [hz]), sub_div, sub_eq_add_neg, ‚Üê neg_div]
    rw [‚Üê zero_add (-1 / (z - 1)), ‚Üê zero_div (z - 1)]
    apply Tendsto.add (Tendsto.div_const (tendsto_pow_atTop_nhds_zero_of_norm_lt_one hz) (z - 1))
    simp only [zero_div, zero_add, tendsto_const_nhds_iff]

/-- **Abel's limit theorem**. Given a power series converging at 1, the corresponding function
is continuous at 1 when approaching 1 within a fixed Stolz set. -/
theorem tendsto_tsum_powerSeries_nhdsWithin_stolzSet
    (h : Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, f i) atTop (ùìù l)) {M : ‚Ñù} :
    Tendsto (fun z ‚Ü¶ ‚àë' n, f n * z ^ n) (ùìù[stolzSet M] 1) (ùìù l) := by
  -- If `M ‚â§ 1` the Stolz set is empty and the statement is trivial
  rcases le_or_gt M 1 with hM | hM
  ¬∑ simp_rw [stolzSet_empty hM, nhdsWithin_empty, tendsto_bot]
  -- Abbreviations
  let s := fun n ‚Ü¶ ‚àë i ‚àà range n, f i
  let g := fun z ‚Ü¶ ‚àë' n, f n * z ^ n
  have hm := Metric.tendsto_atTop.mp h
  rw [Metric.tendsto_nhdsWithin_nhds]
  simp only [dist_eq_norm] at hm ‚ä¢
  -- Introduce the "challenge" `Œµ`
  intro Œµ Œµpos
  -- First bound, handles the tail
  obtain ‚ü®B‚ÇÅ, hB‚ÇÅ‚ü© := hm (Œµ / 4 / M) (by positivity)
  -- Second bound, handles the head
  let F := ‚àë i ‚àà range B‚ÇÅ, ‚Äñl - s (i + 1)‚Äñ
  use Œµ / 4 / (F + 1), by positivity
  intro z ‚ü®zn, zm‚ü© zd
  have p := abel_aux h zn
  simp_rw [Metric.tendsto_atTop, dist_eq_norm, norm_sub_rev] at p
  -- Third bound, regarding the distance between `l - g z` and the rearranged sum
  obtain ‚ü®B‚ÇÇ, hB‚ÇÇ‚ü© := p (Œµ / 2) (by positivity)
  clear hm p
  replace hB‚ÇÇ := hB‚ÇÇ (max B‚ÇÅ B‚ÇÇ) (by simp)
  suffices ‚Äñ(1 - z) * ‚àë i ‚àà range (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ < Œµ / 2 by
    calc
      _ = ‚Äñl - g z‚Äñ := by rw [norm_sub_rev]
      _ = ‚Äñl - g z - (1 - z) * ‚àë i ‚àà range (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i +
          (1 - z) * ‚àë i ‚àà range (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ := by rw [sub_add_cancel _]
      _ ‚â§ ‚Äñl - g z - (1 - z) * ‚àë i ‚àà range (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ +
          ‚Äñ(1 - z) * ‚àë i ‚àà range (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ := norm_add_le _ _
      _ < Œµ / 2 + Œµ / 2 := add_lt_add hB‚ÇÇ this
      _ = _ := add_halves Œµ
  -- We break the rearranged sum along `B‚ÇÅ`
  calc
    _ = ‚Äñ(1 - z) * ‚àë i ‚àà range B‚ÇÅ, (l - s (i + 1)) * z ^ i +
        (1 - z) * ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ := by
      rw [‚Üê mul_add, sum_range_add_sum_Ico _ (le_max_left B‚ÇÅ B‚ÇÇ)]
    _ ‚â§ ‚Äñ(1 - z) * ‚àë i ‚àà range B‚ÇÅ, (l - s (i + 1)) * z ^ i‚Äñ +
        ‚Äñ(1 - z) * ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ := norm_add_le _ _
    _ = ‚Äñ1 - z‚Äñ * ‚Äñ‚àë i ‚àà range B‚ÇÅ, (l - s (i + 1)) * z ^ i‚Äñ +
        ‚Äñ1 - z‚Äñ * ‚Äñ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), (l - s (i + 1)) * z ^ i‚Äñ := by
      rw [norm_mul, norm_mul]
    _ ‚â§ ‚Äñ1 - z‚Äñ * ‚àë i ‚àà range B‚ÇÅ, ‚Äñl - s (i + 1)‚Äñ * ‚Äñz‚Äñ ^ i +
        ‚Äñ1 - z‚Äñ * ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), ‚Äñl - s (i + 1)‚Äñ * ‚Äñz‚Äñ ^ i := by
      gcongr <;> simp_rw [‚Üê norm_pow, ‚Üê norm_mul, norm_sum_le]
  -- then prove that the two pieces are each less than `Œµ / 4`
  have S‚ÇÅ : ‚Äñ1 - z‚Äñ * ‚àë i ‚àà range B‚ÇÅ, ‚Äñl - s (i + 1)‚Äñ * ‚Äñz‚Äñ ^ i < Œµ / 4 :=
    calc
      _ ‚â§ ‚Äñ1 - z‚Äñ * ‚àë i ‚àà range B‚ÇÅ, ‚Äñl - s (i + 1)‚Äñ := by
        gcongr; nth_rw 3 [‚Üê mul_one ‚Äñ_‚Äñ]
        gcongr; exact pow_le_one‚ÇÄ (norm_nonneg _) zn.le
      _ ‚â§ ‚Äñ1 - z‚Äñ * (F + 1) := by gcongr; linarith only
      _ < _ := by rwa [norm_sub_rev, lt_div_iff‚ÇÄ (by positivity)] at zd
  have S‚ÇÇ : ‚Äñ1 - z‚Äñ * ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), ‚Äñl - s (i + 1)‚Äñ * ‚Äñz‚Äñ ^ i < Œµ / 4 :=
    calc
      _ ‚â§ ‚Äñ1 - z‚Äñ * ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), Œµ / 4 / M * ‚Äñz‚Äñ ^ i := by
        gcongr with i hi
        have := hB‚ÇÅ (i + 1) (by linarith only [(mem_Ico.mp hi).1])
        rw [norm_sub_rev] at this
        exact this.le
      _ = ‚Äñ1 - z‚Äñ * (Œµ / 4 / M) * ‚àë i ‚àà Ico B‚ÇÅ (max B‚ÇÅ B‚ÇÇ), ‚Äñz‚Äñ ^ i := by
        rw [‚Üê mul_sum, ‚Üê mul_assoc]
      _ ‚â§ ‚Äñ1 - z‚Äñ * (Œµ / 4 / M) * ‚àë' i, ‚Äñz‚Äñ ^ i := by
        gcongr
        exact Summable.sum_le_tsum _ (fun _ _ ‚Ü¶ by positivity)
          (summable_geometric_of_lt_one (by positivity) zn)
      _ = ‚Äñ1 - z‚Äñ * (Œµ / 4 / M) / (1 - ‚Äñz‚Äñ) := by
        rw [tsum_geometric_of_lt_one (by positivity) zn, ‚Üê div_eq_mul_inv]
      _ < M * (1 - ‚Äñz‚Äñ) * (Œµ / 4 / M) / (1 - ‚Äñz‚Äñ) := by gcongr; linarith only [zn]
      _ = _ := by
        rw [‚Üê mul_rotate, mul_div_cancel_right‚ÇÄ _ (by linarith only [zn]),
          div_mul_cancel‚ÇÄ _ (by linarith only [hM])]
  convert add_lt_add S‚ÇÅ S‚ÇÇ using 1
  linarith only

/-- **Abel's limit theorem**. Given a power series converging at 1, the corresponding function
is continuous at 1 when approaching 1 within any fixed Stolz cone. -/
theorem tendsto_tsum_powerSeries_nhdsWithin_stolzCone
    (h : Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, f i) atTop (ùìù l)) {s : ‚Ñù} (hs : 0 < s) :
    Tendsto (fun z ‚Ü¶ ‚àë' n, f n * z ^ n) (ùìù[stolzCone s] 1) (ùìù l) :=
  (tendsto_tsum_powerSeries_nhdsWithin_stolzSet h).mono_left
    (nhdsWithin_stolzCone_le_nhdsWithin_stolzSet hs).choose_spec

theorem tendsto_tsum_powerSeries_nhdsWithin_lt
    (h : Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, f i) atTop (ùìù l)) :
    Tendsto (fun z ‚Ü¶ ‚àë' n, f n * z ^ n) ((ùìù[<] 1).map ofReal) (ùìù l) :=
  (tendsto_tsum_powerSeries_nhdsWithin_stolzSet (M := 2) h).mono_left
    (nhdsWithin_lt_le_nhdsWithin_stolzSet one_lt_two)

end Complex

namespace Real

open Complex

variable {f : ‚Ñï ‚Üí ‚Ñù} {l : ‚Ñù}

/-- **Abel's limit theorem**. Given a real power series converging at 1, the corresponding function
is continuous at 1 when approaching 1 from the left. -/
theorem tendsto_tsum_powerSeries_nhdsWithin_lt
    (h : Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, f i) atTop (ùìù l)) :
    Tendsto (fun x ‚Ü¶ ‚àë' n, f n * x ^ n) (ùìù[<] 1) (ùìù l) := by
  have m : (ùìù l).map ofReal ‚â§ ùìù ‚Üël := ofRealCLM.continuous.tendsto l
  replace h := (tendsto_map.comp h).mono_right m
  rw [Function.comp_def] at h
  push_cast at h
  replace h := Complex.tendsto_tsum_powerSeries_nhdsWithin_lt h
  rw [tendsto_map'_iff] at h
  rw [Metric.tendsto_nhdsWithin_nhds] at h ‚ä¢
  convert h
  simp_rw [Function.comp_apply, dist_eq_norm]
  norm_cast

end Real
