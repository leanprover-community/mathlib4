/-
Copyright (c) 2025 Antoine Chambert-Loir. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Antoine Chambert-Loir
-/

import Mathlib.Algebra.CharP.Quotient
import Mathlib.Algebra.Field.ZMod
import Mathlib.Algebra.Polynomial.SpecificDegree
import Mathlib.RingTheory.Ideal.Quotient.Operations
import Mathlib.RingTheory.Polynomial.Eisenstein.Basic
import Mathlib.RingTheory.Polynomial.Eisenstein.Generalized
import Mathlib.Tactic.ComputeDegree

/-! # Example of an application of the generalized Eisenstein criterion

We show here how `Polynomial.generalizedEisenstein` can be applied
to establish the irreducibility of the explicit polynomial of degree 4
  `X ^ 4 - 10 * X ^ 2 + 1 : â„¤[X]`.
(to which the standard criterion) wouldn't apply.
One argues modulo `3`, with `q := X ^ 2 + 1`.

-/

namespace Polynomial

open Ideal.Quotient Ideal RingHom

/-- The classical Eisenstein theorem follows from the generalized one -/
example {R : Type*} [CommRing R] [IsDomain R]
    {ğ“Ÿ : Ideal R} (hprime : ğ“Ÿ.IsPrime) {f : Polynomial R}
    (hf : f.IsEisensteinAt ğ“Ÿ) (hu : f.IsPrimitive) (hfd0 : 0 < f.natDegree) :
    Irreducible f := by
  apply generalizedEisenstein (K := FractionRing (R â§¸ ğ“Ÿ)) (q := X) (p := f.natDegree)
    (by simp [map_X, irreducible_X]) monic_X hu hfd0
  Â· simp only [IsScalarTower.algebraMap_eq R (R â§¸ ğ“Ÿ) (FractionRing (R â§¸ ğ“Ÿ)),
      Quotient.algebraMap_eq, coe_comp, Function.comp_apply, ne_eq,
      FaithfulSMul.algebraMap_eq_zero_iff]
    rw [Ideal.Quotient.eq_zero_iff_mem]
    exact hf.leading
  Â· rw [â† map_C, â† Polynomial.map_pow, â† Polynomial.map_mul]
    simp only [IsScalarTower.algebraMap_eq R (R â§¸ ğ“Ÿ) (FractionRing (R â§¸ ğ“Ÿ)),
      Quotient.algebraMap_eq, coe_comp, Function.comp_apply, â† map_map]
    congr 1
    ext n
    simp only [coeff_map, Ideal.Quotient.mk_eq_mk_iff_sub_mem]
    simp only [coeff_C_mul, coeff_X_pow, mul_ite, mul_one, mul_zero, sub_ite, sub_zero]
    split_ifs with hn
    Â· rw [hn, leadingCoeff, sub_self]
      exact zero_mem _
    Â· exact IsEisensteinAt.coeff_mem hf hn
  Â· rw [modByMonic_X, map_C, ne_eq, C_eq_zero, Ideal.Quotient.eq_zero_iff_mem,
      â† coeff_zero_eq_eval_zero]
    convert hf.not_mem
    Â· rw [IsScalarTower.algebraMap_eq R (R â§¸ ğ“Ÿ) (FractionRing (R â§¸ ğ“Ÿ))]
      rw [ker_comp_of_injective]
      Â· ext a; simp
      Â· exact FaithfulSMul.algebraMap_injective (R â§¸ ğ“Ÿ) (FractionRing (R â§¸ ğ“Ÿ))


example : Irreducible (X ^ 4 - 10 * X ^ 2 + 1 : â„¤[X]) := by
  -- We will apply the generalized Eisenstein criterion with `q = X ^ 2 + 1` and `K = ZMod 3`.
  set f : â„¤[X] := X ^ 4 - 10 * X ^ 2 + 1 with hf_eq
  have hdeg_f : f.natDegree = 4 := by unfold f; compute_degree!
  have hf_lC : f.leadingCoeff = 1 := by
    simp only [f, leadingCoeff, hdeg_f]; compute_degree!
  set q : â„¤ [X] := X ^ 2 + 1 with hq_eq
  have hq_deg : q.natDegree = 2 := by unfold q; compute_degree!
  have hq_monic : q.Monic := by unfold q; monicity!
  have hfq : f = q ^ 2 - 12 * q + 12 := by ring
   -- On the other hand, `f %â‚˜ q = 12`, which is not a multiple of `9`.
  apply generalizedEisenstein (K := ZMod 3) (q := q) (p := 2)
  Â· set qâ‚ƒ : (ZMod 3)[X] := X ^ 2 + 1
    have hdeg_qâ‚ƒ : qâ‚ƒ.natDegree = 2 := by unfold qâ‚ƒ; compute_degree!
    suffices Irreducible qâ‚ƒ by simpa [q] using this
    apply irreducible_of_degree_le_three_of_not_isRoot
      (by simp_all) (by simp_all [qâ‚ƒ]; decide)
  Â· unfold q; monicity!
  Â· exact Monic.isPrimitive hf_lC
  Â· simp_all
  Â· suffices f.leadingCoeff = 1 by
      simp [this, map_one, one_ne_zero]
    simp only [leadingCoeff, hdeg_f]
    unfold f; compute_degree!
  Â· nth_rewrite 1 [hfq]
    rw [hf_lC, â† map_C, C_1, Polynomial.map_one, one_mul, â† sub_eq_zero]
    have : (12 : (ZMod 3)[X]) = 0 := by apply CharP.ofNat_eq_zero' _ 3 12; norm_num
    simp [this]
  Â· suffices f %â‚˜ q = 12 by
      rw [this, â† map_ofNat C, Polynomial.map_C, ne_eq, C_eq_zero, eq_zero_iff_mem,
      CharP.ker_intAlgebraMap_eq_span 3, span_singleton_pow, mem_span_singleton]
      norm_num
    rw [hfq, â† modByMonicHom_apply, LinearMap.map_add]
    convert zero_add _
    Â· rw [â† LinearMap.mem_ker, mem_ker_modByMonic hq_monic]
      rw [pow_two, â† sub_mul]
      apply dvd_mul_left
    Â· symm
      simp only [modByMonicHom_apply, Polynomial.modByMonic_eq_self_iff hq_monic, f]
      rw [show q.degree = 2 by unfold q; compute_degree!]
      rw [show degree _ = 0 by compute_degree!]
      norm_num

end Polynomial
