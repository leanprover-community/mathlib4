/-
Copyright (c) 2023 Floris Van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, SÃ©bastien GouÃ«zel, Patrick Massot, Ruben Van de Velde, Floris Van Doorn,
Junyan Xu
-/
import Mathlib.Analysis.Distribution.AEEqOfIntegralContDiff
import Mathlib.RingTheory.MvPolynomial.Basic
import Mathlib.Analysis.Analytic.Uniqueness
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.FieldTheory.Finite.Polynomial
import Mathlib.Data.MvPolynomial.Funext

/-!
# Smooth functions whose integral calculates the values of polynomials

In any space `â„áµˆ` and given any `N`, we construct a smooth function supported in the unit ball
whose integral against a multivariate polynomial `P` of total degree at most `N` is `P 0`.

This is a test of the state of the library suggested by Martin Hairer.
-/

noncomputable section

open Metric Set MeasureTheory
open MvPolynomial hiding support
open Function hiding eval

section normed
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E E' F  : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (ğ•œ E F) in
/-- The set of smooth functions supported in a set `s`, as a submodule of the space of functions. -/
def SmoothSupportedOn (n : â„•âˆ) (s : Set E) : Submodule ğ•œ (E â†’ F) where
  carrier := { f : E â†’ F | tsupport f âŠ† s âˆ§ ContDiff ğ•œ n f }
  add_mem' hf hg := âŸ¨tsupport_add.trans <| union_subset hf.1 hg.1, hf.2.add hg.2âŸ©
  zero_mem' :=
    âŸ¨(tsupport_eq_empty_iff.mpr rfl).subset.trans (empty_subset _), contDiff_const (c := 0)âŸ©
  smul_mem' r f hf :=
    âŸ¨(closure_mono <| support_smul_subset_right r f).trans hf.1, contDiff_const.smul hf.2âŸ©

namespace SmoothSupportedOn

variable {n : â„•âˆ} {s : Set E}

instance : FunLike (SmoothSupportedOn ğ•œ E F n s) E (fun _ â†¦ F) where
  coe := Subtype.val
  coe_injective' := Subtype.coe_injective

@[simp]
lemma coe_mk (f : E â†’ F) (h) : (âŸ¨f, hâŸ© : SmoothSupportedOn ğ•œ E F n s) = f := rfl

lemma tsupport_subset (f : SmoothSupportedOn ğ•œ E F n s) : tsupport f âŠ† s := f.2.1

lemma support_subset (f : SmoothSupportedOn ğ•œ E F n s) :
    support f âŠ† s := subset_tsupport _ |>.trans (tsupport_subset f)

lemma contDiff (f : SmoothSupportedOn ğ•œ E F n s) :
    ContDiff ğ•œ n f := f.2.2

theorem continuous (f : SmoothSupportedOn ğ•œ E F n s) : Continuous f :=
  ContDiff.continuous <| SmoothSupportedOn.contDiff _

lemma hasCompactSupport [ProperSpace E] (f : SmoothSupportedOn ğ•œ E F n (closedBall 0 1)) :
    HasCompactSupport f :=
  HasCompactSupport.of_support_subset_isCompact (isCompact_closedBall 0 1) (support_subset f)

end SmoothSupportedOn

end normed
open SmoothSupportedOn

section missing_polynomial
open MvPolynomial Submodule

variable {R Ïƒ : Type*} [CommSemiring R] (n : â„•)

lemma restrictTotalDegree_eq_span {n : â„•} :
    restrictTotalDegree Ïƒ R n =
    span R ((fun c : Ïƒ â†’â‚€ â„• â†¦ monomial c (1 : R)) '' {s : Ïƒ â†’â‚€ â„• | s.sum (fun _ e â†¦ e) â‰¤ n}) := by
  ext P; constructor <;> intro h
  Â· rw [â† P.support_sum_monomial_coeff]
    refine sum_mem fun c hc â†¦ ?_
    rw [â† mul_one (coeff c P), â† smul_eq_mul, â† smul_monomial]
    rw [mem_restrictTotalDegree] at h
    exact smul_mem _ _ (subset_span <| mem_image_of_mem _ <| (le_totalDegree hc).trans h)
  Â· refine span_le.mpr ?_ h
    rintro x âŸ¨c, hc, rflâŸ©
    rw [SetLike.mem_coe, mem_restrictTotalDegree]
    cases subsingleton_or_nontrivial R
    Â· rw [Subsingleton.elim ((fun c â†¦ monomial c 1) c) 0, totalDegree_zero]; apply zero_le
    Â· rw [totalDegree_monomial _ one_ne_zero]; exact hc

/- Can be obtained by combining `LinearMap.proj` and `MvPolynomial.evalâ‚—`. -/
/-- The evaluation of a multivariate polynomial at a point, as a linear map. -/
def evalAtâ‚— {R Ïƒ : Type*} [CommSemiring R] (x : Ïƒ â†’ R) : MvPolynomial Ïƒ R â†’â‚—[R] R where
  toFun P := eval x P
  map_add' := by simp
  map_smul' := by simp

/- what we actually want -/
lemma analyticOn_eval {R Ïƒ : Type*} [Fintype Ïƒ] [IsROrC R] (P : MvPolynomial Ïƒ R) :
    AnalyticOn R (fun x â†¦ eval (EuclideanSpace.equiv Ïƒ R x) P) univ := fun x _ â†¦ by
  apply P.induction_on (fun r â†¦ ?_) (fun p q hp hq â†¦ ?_) fun p i hp â†¦ ?_ -- refine doesn't work
  Â· simp_rw [eval_C]; exact analyticAt_const
  Â· simp_rw [eval_add]; exact hp.add hq
  Â· simp_rw [eval_mul, eval_X]; exact hp.mul ((ContinuousLinearMap.proj (R := R) i).analyticAt x)

lemma finite_stuff' [Finite Ïƒ] (N : â„•) : {s : Multiset Ïƒ | Multiset.card s â‰¤ N}.Finite := by
  classical
  have := Fintype.ofFinite Ïƒ
  let S := N â€¢ (Finset.univ.val : Multiset Ïƒ)
  apply Finset.finite_toSet (Multiset.toFinset (Multiset.powerset S)) |>.subset
  intro s hs
  rw [Set.mem_setOf] at hs
  rw [Finset.mem_coe, Multiset.mem_toFinset, Multiset.mem_powerset, Multiset.le_iff_count]
  intro x
  simp only [S, Multiset.count_nsmul, Multiset.count_univ, mul_one]
  exact le_trans (s.count_le_card x) hs

lemma finite_stuff [Finite Ïƒ] (N : â„•) : {s : Ïƒ â†’â‚€ â„• | s.sum (fun _ e â†¦ e) â‰¤ N}.Finite := by
  classical
  change {s : Ïƒ â†’â‚€ â„• | s.sum (fun _ => id) â‰¤ N}.Finite
  simp only [â† Finsupp.card_toMultiset]
  refine Set.Finite.of_finite_image ?_ (Multiset.toFinsupp.symm.injective.injOn _)
  convert finite_stuff' (Ïƒ := Ïƒ) N
  ext x
  rw [â† AddEquiv.coe_toEquiv, Set.mem_image_equiv]
  simp

instance [Finite Ïƒ] : Module.Finite R (restrictTotalDegree Ïƒ R n) := by
  rw [Module.finite_def, fg_top, restrictTotalDegree_eq_span]
  exact Submodule.fg_span ((finite_stuff _).image _)

end missing_polynomial

open Metric Set MeasureTheory Module
open MvPolynomial hiding support
open Function hiding eval

variable {Î¹ : Type*} [Fintype Î¹]
lemma MvPolynomial.continuous_eval (p : MvPolynomial Î¹ â„) :
    Continuous fun x â†¦ (eval x) p := by
  continuity

theorem SmoothSupportedOn.integrable_eval_mul (p : MvPolynomial Î¹ â„)
    (f : SmoothSupportedOn â„ (EuclideanSpace â„ Î¹) â„ âŠ¤ (closedBall 0 1)) :
    Integrable fun (x : EuclideanSpace â„ Î¹) â†¦ (eval x) p â€¢ f x := by
  simp only [smul_eq_mul]
  apply Continuous.integrable_of_hasCompactSupport
  Â· apply Continuous.mul
    Â· apply p.continuous_eval
    Â· apply ContDiff.continuous <| SmoothSupportedOn.contDiff _
  exact (hasCompactSupport f).mul_left

/-- Interpreting a multivariate polynomial as an element of the dual of smooth functions supported
in the unit ball, via integration against Lebesgue measure. -/
def L : MvPolynomial Î¹ â„ â†’â‚—[â„]
    Dual â„ (SmoothSupportedOn â„ (EuclideanSpace â„ Î¹) â„ âŠ¤ (closedBall 0 1)) where
  toFun p :=
    { toFun := fun f â†¦ âˆ« x : EuclideanSpace â„ Î¹, eval x p â€¢ f x
      map_add' := fun f g â†¦ by
        rw [â† integral_add]
        Â· simp only [â† smul_add]; rfl
        all_goals apply SmoothSupportedOn.integrable_eval_mul
      map_smul' := fun r f â†¦ by
        rw [â† integral_smul]
        dsimp only [id_eq, RingHom.id_apply]
        simp only [smul_comm r]
        rfl }
  map_add' := fun pâ‚ pâ‚‚ â†¦ by
    ext f
    dsimp only [id_eq, eq_mpr_eq_cast, AddHom.toFun_eq_coe, AddHom.coe_mk,
      RingHom.id_apply, LinearMap.coe_mk, LinearMap.add_apply]
    rw [â† integral_add]
    Â· simp only [â† add_smul, eval_add]
    all_goals apply SmoothSupportedOn.integrable_eval_mul
  map_smul' := fun r p â†¦ by
    ext f
    dsimp only [id_eq, eq_mpr_eq_cast, AddHom.toFun_eq_coe, AddHom.coe_mk,
      RingHom.id_apply, LinearMap.coe_mk, LinearMap.smul_apply]
    rw [â† integral_smul]
    simp only [â† evalâ‚—_apply, SMulHomClass.map_smul, â† smul_assoc]
    rfl

lemma inj_L (Î¹ : Type*) [Fintype Î¹] : Injective (L (Î¹ := Î¹)) := by
  rw [injective_iff_map_eq_zero]
  intro p hp
  suffices H : âˆ€áµ x : EuclideanSpace â„ Î¹, x âˆˆ ball 0 1 â†’ eval x p = 0 by
    simp_rw [MvPolynomial.funext_iff, map_zero]
    refine fun x â†¦ (analyticOn_eval p).eqOn_zero_of_preconnected_of_eventuallyEq_zero
      ?_ (zâ‚€ := 0) trivial ?_ trivial
    Â· rw [â† preconnectedSpace_iff_univ]; infer_instance
    Â· refine Filter.mem_of_superset (Metric.ball_mem_nhds 0 zero_lt_one) ?_
      rw [â† ae_restrict_iff'â‚€ measurableSet_ball.nullMeasurableSet] at H
      apply Measure.eqOn_of_ae_eq H
      Â· exact (analyticOn_eval p).continuous.continuousOn
      Â· exact continuousOn_const
      Â· rw [isOpen_ball.interior_eq]; apply subset_closure
  have h2p : LocallyIntegrableOn (fun x : EuclideanSpace â„ Î¹ â†¦ eval x p) (ball 0 1) :=
    continuous_eval p |>.locallyIntegrable |>.locallyIntegrableOn _
  apply isOpen_ball.ae_eq_zero_of_integral_contDiff_smul_eq_zero h2p
  intro g hg _h2g g_supp
  let Ï• : SmoothSupportedOn â„ (EuclideanSpace â„ Î¹) â„ âŠ¤ (closedBall 0 1) :=
    âŸ¨g, g_supp.trans ball_subset_closedBall, hgâŸ©
  apply_fun (Â· Ï•) at hp
  simpa [mul_comm (g _), L] using hp

lemma hairer (N : â„•) (Î¹ : Type*) [Fintype Î¹] :
    âˆƒ (Ï : EuclideanSpace â„ Î¹ â†’ â„), tsupport Ï âŠ† closedBall 0 1 âˆ§ ContDiff â„ âŠ¤ Ï âˆ§
    âˆ€ (p : MvPolynomial Î¹ â„), p.totalDegree â‰¤ N â†’
    âˆ« x : EuclideanSpace â„ Î¹, eval x p â€¢ Ï x = eval 0 p := by
  have := (inj_L Î¹).comp (restrictTotalDegree Î¹ â„ N).injective_subtype
  rw [â† LinearMap.coe_comp] at this
  obtain âŸ¨âŸ¨Ï†, supÏ†, difÏ†âŸ©, hÏ†âŸ© :=
    LinearMap.flip_surjective_iffâ‚.2 this ((evalAtâ‚— 0).comp <| Submodule.subtype _)
  refine âŸ¨Ï†, supÏ†, difÏ†, fun P hP â†¦ ?_âŸ©
  exact FunLike.congr_fun hÏ† âŸ¨P, (mem_restrictTotalDegree Î¹ N P).mpr hPâŸ©
