/-
Copyright (c) 2023 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, SÃ©bastien GouÃ«zel, Patrick Massot, Ruben Van de Velde, Floris van Doorn,
Junyan Xu
-/
import Mathlib.Algebra.MvPolynomial.Funext
import Mathlib.Analysis.Analytic.Polynomial
import Mathlib.Analysis.Analytic.Uniqueness
import Mathlib.Analysis.Distribution.AEEqOfIntegralContDiff
import Mathlib.LinearAlgebra.Dual.Lemmas
import Mathlib.RingTheory.MvPolynomial.Basic
import Mathlib.Topology.Algebra.MvPolynomial

/-!
# Smooth functions whose integral calculates the values of polynomials

In any space `â„áµˆ` and given any `N`, we construct a smooth function supported in the unit ball
whose integral against a multivariate polynomial `P` of total degree at most `N` is `P 0`.

This is a test of the state of the library suggested by Martin Hairer.
-/

noncomputable section

open Metric Set MeasureTheory
open MvPolynomial hiding support
open Function hiding eval
open scoped ContDiff

variable {Î¹ : Type*} [Fintype Î¹]

section normed
variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ]
variable [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (ğ•œ E F) in
/-- The set of `C^n` functions supported in a set `s`, as a submodule of the space of functions. -/
def ContDiffSupportedOn (n : â„•âˆ) (s : Set E) : Submodule ğ•œ (E â†’ F) where
  carrier := { f : E â†’ F | tsupport f âŠ† s âˆ§ ContDiff ğ•œ n f }
  add_mem' hf hg := âŸ¨tsupport_add.trans <| union_subset hf.1 hg.1, hf.2.add hg.2âŸ©
  zero_mem' :=
    âŸ¨(tsupport_eq_empty_iff.mpr rfl).subset.trans (empty_subset _), contDiff_const (c := 0)âŸ©
  smul_mem' r f hf :=
    âŸ¨(closure_mono <| support_const_smul_subset r f).trans hf.1, contDiff_const.smul hf.2âŸ©

namespace ContDiffSupportedOn

variable {n : â„•âˆ} {s : Set E}

instance : FunLike (ContDiffSupportedOn ğ•œ E F n s) E F where
  coe := Subtype.val
  coe_injective' := Subtype.coe_injective

@[simp]
lemma coe_mk (f : E â†’ F) (h) : (âŸ¨f, hâŸ© : ContDiffSupportedOn ğ•œ E F n s) = f := rfl

lemma tsupport_subset (f : ContDiffSupportedOn ğ•œ E F n s) : tsupport f âŠ† s := f.2.1

lemma support_subset (f : ContDiffSupportedOn ğ•œ E F n s) :
    support f âŠ† s := subset_tsupport _ |>.trans (tsupport_subset f)

lemma contDiff (f : ContDiffSupportedOn ğ•œ E F n s) :
    ContDiff ğ•œ n f := f.2.2

theorem continuous (f : ContDiffSupportedOn ğ•œ E F n s) : Continuous f :=
  (ContDiffSupportedOn.contDiff _).continuous

lemma hasCompactSupport [ProperSpace E] (f : ContDiffSupportedOn ğ•œ E F n (closedBall 0 1)) :
    HasCompactSupport f :=
  HasCompactSupport.of_support_subset_isCompact (isCompact_closedBall 0 1) (support_subset f)

theorem integrable_eval_mul (p : MvPolynomial Î¹ â„)
    (f : ContDiffSupportedOn â„ (EuclideanSpace â„ Î¹) â„ âŠ¤ (closedBall 0 1)) :
    Integrable fun (x : EuclideanSpace â„ Î¹) â†¦ eval x p * f x :=
  p.continuous_eval.mul (ContDiffSupportedOn.contDiff f).continuous
    |>.integrable_of_hasCompactSupport (hasCompactSupport f).mul_left

end ContDiffSupportedOn

end normed
open ContDiffSupportedOn

variable (Î¹)
/-- Interpreting a multivariate polynomial as an element of the dual of smooth functions supported
in the unit ball, via integration against Lebesgue measure. -/
def L : MvPolynomial Î¹ â„ â†’â‚—[â„]
    Module.Dual â„ (ContDiffSupportedOn â„ (EuclideanSpace â„ Î¹) â„ âŠ¤ (closedBall 0 1)) :=
  have int := ContDiffSupportedOn.integrable_eval_mul (Î¹ := Î¹)
  .mkâ‚‚ â„ (fun p f â†¦ âˆ« x : EuclideanSpace â„ Î¹, eval x p â€¢ f x)
    (fun pâ‚ pâ‚‚ f â†¦ by simp [add_mul, integral_add (int pâ‚ f) (int pâ‚‚ f)])
    (fun r p f â†¦ by simp [mul_assoc, integral_const_mul])
    (fun p fâ‚ fâ‚‚ â†¦ by simp_rw [smul_eq_mul, â† integral_add (int p _) (int p _), â† mul_add]; rfl)
    fun r p f â†¦ by simp_rw [â† integral_smul, smul_comm r]; rfl

lemma inj_L : Injective (L Î¹) :=
  (injective_iff_map_eq_zero _).mpr fun p hp â†¦ by
    have H : âˆ€áµ x : EuclideanSpace â„ Î¹, x âˆˆ ball 0 1 â†’ eval x p = 0 :=
      isOpen_ball.ae_eq_zero_of_integral_contDiff_smul_eq_zero
        (continuous_eval p |>.locallyIntegrable.locallyIntegrableOn _)
        fun g hg _h2g g_supp â†¦ by
          simpa [mul_comm (g _), L] using congr($hp âŸ¨g, g_supp.trans ball_subset_closedBall, hgâŸ©)
    simp_rw [MvPolynomial.funext_iff, map_zero]
    refine fun x â†¦ AnalyticOnNhd.eval_linearMap (EuclideanSpace.equiv Î¹ â„).toLinearMap p
      |>.eqOn_zero_of_preconnected_of_eventuallyEq_zero
      (preconnectedSpace_iff_univ.mp inferInstance) (zâ‚€ := 0) trivial
      (Filter.mem_of_superset (Metric.ball_mem_nhds 0 zero_lt_one) ?_) trivial
    rw [â† ae_restrict_iff'â‚€ measurableSet_ball.nullMeasurableSet] at H
    apply Measure.eqOn_of_ae_eq H p.continuous_eval.continuousOn continuousOn_const
    rw [isOpen_ball.interior_eq]
    apply subset_closure

lemma hairer (N : â„•) (Î¹ : Type*) [Fintype Î¹] :
    âˆƒ (Ï : EuclideanSpace â„ Î¹ â†’ â„), tsupport Ï âŠ† closedBall 0 1 âˆ§ ContDiff â„ âˆ Ï âˆ§
    âˆ€ (p : MvPolynomial Î¹ â„), p.totalDegree â‰¤ N â†’
    âˆ« x : EuclideanSpace â„ Î¹, eval x p â€¢ Ï x = eval 0 p := by
  have := (inj_L Î¹).comp (restrictTotalDegree Î¹ â„ N).injective_subtype
  rw [â† LinearMap.coe_comp] at this
  obtain âŸ¨âŸ¨Ï†, supÏ†, difÏ†âŸ©, hÏ†âŸ© :=
    LinearMap.flip_surjective_iffâ‚.2 this ((aeval 0).toLinearMap.comp <| Submodule.subtype _)
  exact âŸ¨Ï†, supÏ†, difÏ†, fun P hP â†¦ congr($hÏ† âŸ¨P, (mem_restrictTotalDegree Î¹ N P).mpr hPâŸ©)âŸ©
